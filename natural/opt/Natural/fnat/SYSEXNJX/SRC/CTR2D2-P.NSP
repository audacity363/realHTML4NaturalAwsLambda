0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050/** Main program generated for
0060/** adapter <:link P13370/A2DIM1-A>.
0070/*
0080/** :author jbb
0090/** :version Generated by Natural for Ajax.
0100/*
0110DEFINE DATA LOCAL
01201 INFOPAGENAME (A) DYNAMIC INIT <'html/ctrltwodimensionalarray2.html'>
0130
01401 LINES (1:*)
01502 FIRST (A) DYNAMIC
01602 ID (A) DYNAMIC
01702 LAST (A) DYNAMIC
01802 SELECTED (L)
0190
02001 EMPLOYEES_BONUSPROPS
02102 ROWS (1:*)
02203 COLS (1:*)
02304 STATUS (A) DYNAMIC
02404 TITLE (A) DYNAMIC
02503 SELECTED (L)
0260
02701 EMPLOYEES VIEW OF EMPLOYEES
0280  2 PERSONNEL-ID
0290  2 NAME
0300  2 REDEFINE NAME
0310    3 NAME1 (A1)
0320    3 NAME2 (A19)
0330  2 FIRST-NAME
0340  2 REDEFINE FIRST-NAME
0350   3 FIRST-NAME1 (A1)
0360    3 FIRST-NAME2 (A19)
0370  2 INCOME (1:20)
0380    3 BONUS (P9/1:5)
0390
0400/****************
0410* Extra variables
0420/****************
04301 #BONUSROWS (I4)
04401 #BONUSCOLS (I4)
04501 #ISN (B4)
04601 #I (I4)
04701 #J (I4)
04801 #SELECTED (I4)
04901 #NUMEMPL(I4)
0500END-DEFINE
0510*
0520#BONUSROWS := 20
0530#BONUSCOLS := 5
0540EXPAND ARRAY EMPLOYEES_BONUSPROPS.ROWS TO (1:#BONUSROWS)
0550EXPAND ARRAY EMPLOYEES_BONUSPROPS.COLS TO (*,1:#BONUSCOLS)
0560*
0570PERFORM INIT_TEMPOYEES_TABLE
0580LINES.SELECTED(1) := TRUE
0590*
0600PROCESS PAGE USING "CTR2D2-A"
0610*
0620DECIDE ON FIRST *PAGE-EVENT
0630 VALUE U'nat:page.end', U'nat:browser.end'
0640  /* Page closed.
0650  IGNORE
0660*
0670 VALUE U'onExit'
0680  /* TODO: Implement event code.
0690  PROCESS PAGE UPDATE FULL
0700*
0710 VALUE U'onFillTwoDimArray'
0720   PERFORM RESET_TWO_DIM_ARRAY
0730
0740   FOR #I := 1 TO #NUMEMPL
0750    IF LINES.SELECTED(#I) = TRUE
0760      #SELECTED := #I
0770      PERFORM READ_BONUS_DATA
0780    END-IF
0790   END-FOR
0800
0810   PERFORM SETTOOLTIPS
0820   PROCESS PAGE UPDATE FULL
0830*
0840 VALUE U'onResetTwoDimArray'
0850  PERFORM RESET_TWO_DIM_ARRAY
0860  PROCESS PAGE UPDATE FULL
0870*
0880 VALUE U'onValidateTwoDimArray'
0890   PERFORM VALIDATE_TWO_DIM_ARRAY
0900   PROCESS PAGE UPDATE FULL
0910*
0920 VALUE U'onStoreTwoDimArray'
0930   PERFORM STORE_BONUS_DATA
0940   END TRANSACTION
0950   PROCESS PAGE UPDATE FULL
0960*
0970  VALUE U'onBonusProposals'
0980    PERFORM RESETSTATUS
0990    PERFORM BONUSPROPS
1000   PROCESS PAGE UPDATE FULL
1010 NONE VALUE
1020   PROCESS PAGE UPDATE
1030END-DECIDE
1040*
1050/* ---------------------------------*/
1060DEFINE SUBROUTINE RESET_TWO_DIM_ARRAY
1070/* ---------------------------------*/
1080   FOR #I := 1 TO #BONUSROWS
1090     FOR #J := 1 TO #BONUSCOLS
1100       RESET EMPLOYEES.BONUS(#I,#J)
1110     END-FOR
1120   END-FOR
1130
1140   PERFORM RESETTITLE
1150
1160   PERFORM RESETSTATUS
1170END-SUBROUTINE
1180*
1190/* ------------------------------------*/
1200DEFINE SUBROUTINE VALIDATE_TWO_DIM_ARRAY
1210/* ------------------------------------*/
1220   FOR #I := 1 TO #BONUSROWS
1230     FOR #J := 1 TO #BONUSCOLS
1240       IF EMPLOYEES.BONUS(#I,#J) EQ 0
1250         COMPRESS 'ERROR' INTO EMPLOYEES_BONUSPROPS.STATUS(#I,#J)
1260       ELSE
1270         RESET EMPLOYEES_BONUSPROPS.STATUS(#I,#J)
1280       END-IF
1290     END-FOR
1300   END-FOR
1310END-SUBROUTINE
1320*
1330/* --------------------------*/
1340DEFINE SUBROUTINE SETTOOLTIPS
1350/* --------------------------*/
1360  FOR #I := 1 TO #BONUSROWS
1370      FOR #J := 1 TO #BONUSCOLS
1380        COMPRESS "Bonus value" #J INTO EMPLOYEES_BONUSPROPS.TITLE(#I,#J)
1390      END-FOR
1400  END-FOR
1410END-SUBROUTINE
1420*
1430/* --------------------------*/
1440DEFINE SUBROUTINE RESETSTATUS
1450/* --------------------------*/
1460   FOR #I := 1 TO #BONUSROWS
1470     FOR #J := 1 TO #BONUSCOLS
1480       RESET EMPLOYEES_BONUSPROPS.STATUS(#I,#J)
1490     END-FOR
1500   END-FOR
1510END-SUBROUTINE
1520*
1530/* --------------------------*/
1540DEFINE SUBROUTINE RESETTITLE
1550/* --------------------------*/
1560   FOR #I := 1 TO #BONUSROWS
1570     FOR #J := 1 TO #BONUSCOLS
1580       RESET EMPLOYEES_BONUSPROPS.TITLE(#I,#J)
1590     END-FOR
1600   END-FOR
1610END-SUBROUTINE
1620*
1630/* --------------------------*/
1640DEFINE SUBROUTINE BONUSPROPS
1650/* --------------------------*/
1660  FOR #I := 1 TO #BONUSROWS
1670      IF EMPLOYEES.BONUS(#I,1) EQ 0
1680          EMPLOYEES.BONUS(#I,1) := 200
1690      END-IF
1700  END-FOR
1710
1720  EMPLOYEES.BONUS(1,2) := 345
1730  EMPLOYEES.BONUS(4,2) := 345
1740  EMPLOYEES.BONUS(5,2) := 266
1750  EMPLOYEES.BONUS(6,2) := 345
1760  EMPLOYEES.BONUS(9,2) := 445
1770
1780  EMPLOYEES.BONUS(1,3) := 099
1790  EMPLOYEES.BONUS(4,3) := 123
1800  EMPLOYEES.BONUS(5,3) := 066
1810  EMPLOYEES.BONUS(6,3) := 098
1820
1830  EMPLOYEES.BONUS(1,4) := 345
1840  EMPLOYEES.BONUS(4,4) := 721
1850  EMPLOYEES.BONUS(5,4) := 066
1860
1870  EMPLOYEES.BONUS(4,5) := 176
1880  EMPLOYEES.BONUS(5,5) := 298
1890END-SUBROUTINE
1900*
1910/* -----------------------------------*/
1920DEFINE SUBROUTINE INIT_TEMPOYEES_TABLE
1930/* -----------------------------------*/
1940FIND NUMBER EMPLOYEES WITH PERSONNEL-ID NE ' '
1950#NUMEMPL :=  *NUMBER
1960EXPAND ARRAY LINES TO (1:#NUMEMPL)
1970*
1980RESET #J
1990READ EMPLOYEES
2000  ADD 1 TO #J
2010  IF #J GT #NUMEMPL
2020    ESCAPE BOTTOM
2030  END-IF
2040  COMPRESS EMPLOYEES.PERSONNEL-ID INTO LINES.ID(#J) LEAVING NO
2050  COMPRESS EMPLOYEES.NAME         INTO LINES.FIRST(#J) LEAVING NO
2060  COMPRESS EMPLOYEES.FIRST-NAME   INTO LINES.LAST(#J) LEAVING NO
2070END-READ
2080PERFORM RESET_TWO_DIM_ARRAY
2090END-SUBROUTINE
2100*
2110/* ------------------------------*/
2120DEFINE SUBROUTINE READ_BONUS_DATA
2130/* ------------------------------*/
2140#I := #NUMEMPL
2150RESET #J
2160R1. READ EMPLOYEES
2170    #ISN := *ISN
2180    ADD 1 TO #J
2190
2200    IF #J EQ #SELECTED
2210      ESCAPE BOTTOM
2220    END-IF
2230    IF #J GT #I
2240      ESCAPE BOTTOM
2250    END-IF
2260
2270 END-READ
2280G1. GET EMPLOYEES #ISN
2290END-SUBROUTINE
2300*
2310/* ------------------------------*/
2320DEFINE SUBROUTINE STORE_BONUS_DATA
2330/* ------------------------------*/
2340  UPDATE (G1.)
2350END-SUBROUTINE
2360*
2370END
