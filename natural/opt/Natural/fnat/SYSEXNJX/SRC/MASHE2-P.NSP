0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050/** Main program generated for
0060/** adapter <:link MASHNG/MASHEX-A>.
0070/*
0080/** :author zim
0090/** :version Generated by Natural for Ajax.
0100/*
0110DEFINE DATA LOCAL
01201 ENTRY (1:*)
01302 CAPITAL (A) DYNAMIC
01402 CONTINENT (A) DYNAMIC
01502 NAME (A) DYNAMIC
01602 POPULATION (A) DYNAMIC
01702 SELECTED (L)
01801 MASHZONESERVER (A) DYNAMIC INIT <'http://localhost:8080/mashzone'>
01901 MYDASHBOARD
02002 DATASOURCE (1:*)
02103 FILENAME (A) DYNAMIC
02203 OBJECTID (A) DYNAMIC
02303 REFRESH (L)
02402 URL (A) DYNAMIC
02501 MYDATA (A) DYNAMIC
02601 XCIOBJECTS (1:*)
02702 CONTENT (B) DYNAMIC
02802 CONTENTID (A) DYNAMIC
02902 CONTENTTYPE (A) DYNAMIC
03001 XCIREQUESTCONTEXT
03102 CISSESSIONID (A) DYNAMIC
03202 CISVERSION (A) DYNAMIC
03302 CLIENTHOSTNAME (A) DYNAMIC
03402 CLIENTIPADDR (A) DYNAMIC
03502 CLIENTLOCALE (A) DYNAMIC
03602 CLIENTLOCALES (A/1:*) DYNAMIC
03702 CLIENTPORT (I4)
03802 HTTPSESSIONID (A) DYNAMIC
03902 NJXVERSION (A) DYNAMIC
04002 SERVERHOSTNAME (A) DYNAMIC
04102 SERVERPORT (I4)
04201 XCITRIGGEREVENT (A) DYNAMIC
04301 #II (I2)
04401 #DUMMY (A) DYNAMIC
04501 #MYBLOB1 (B) DYNAMIC
04601 #MYBLOB2 (B) DYNAMIC
04701 #MYBLOB3 (B) DYNAMIC
04801 #MYURL (A) DYNAMIC
04901 #MYDATA (A) DYNAMIC
05001 #ENTRYLENGTH (I2)
05101 #COUNTER (I4)
05201 #MASHURL (A) DYNAMIC INIT <'##MASHSERVER/hub/dashboard/dashboard.jsp?x-p-anonymous=true&guid=1500a2db-f7bc-4978-a0d6-27513b0a33f0&appheader=false&mynjxapp.input=##MYNJXAPP&mysessionid.input=##SESSION#mydatasource.input=##NAME'>
0530END-DEFINE
0540*
0550EXPAND ARRAY ENTRY TO (1:4)
0560ENTRY.CAPITAL(1):="Kabul"
0570ENTRY.CONTINENT(1) :="Asia"
0580ENTRY.NAME(1) :="Afghanistan"
0590ENTRY.POPULATION(1) :="31108076"
0600ENTRY.NAME(2) :="Albania"
0610ENTRY.CONTINENT(2) :="Europe"
0620ENTRY.POPULATION(2):="3011405"
0630ENTRY.CAPITAL(2):="Tirana"
0640ENTRY.NAME(3):="Algeria"
0650ENTRY.CONTINENT(3):="Africa"
0660ENTRY.POPULATION(3):="35818924"
0670ENTRY.CAPITAL(3):="Algiers"
0680ENTRY.NAME(4):="American Samoa"
0690ENTRY.CONTINENT(4):="Australia"
0700ENTRY.POPULATION(4):="68891"
0710ENTRY.CAPITAL(4):="Pago Pago"
0720*
0730MYDATA:='world.xml'
0740#COUNTER := 0
0750*
0760XCITRIGGEREVENT:='onInitializeSessionData'
0770*
0780*
0790PROCESS PAGE USING "MASHE2-A"
0800*
0810DECIDE ON FIRST *PAGE-EVENT
0820 VALUE U'nat:page.end', U'nat:browser.end'
0830  /* Page closed.
0840  IGNORE
0850 VALUE U'onInitializeSessionData'
0860   RESET XCITRIGGEREVENT
0870   PERFORM INITIALIZESESSIONDATA
0880   PROCESS PAGE UPDATE FULL
0890 VALUE U'onRefresh'
0900   MYDASHBOARD.URL:= #MASHURL
0910   PERFORM PREPAREDATA
0920  PROCESS PAGE UPDATE FULL
0930 NONE VALUE
0940  PROCESS PAGE UPDATE FULL
0950END-DECIDE
0960*
0970*
0980DEFINE SUBROUTINE INITIALIZESESSIONDATA
0990    MYDASHBOARD.URL:= #MASHURL
1000    EXPAND ARRAY XCIOBJECTS TO (1:3)
1010    EXPAND ARRAY MYDASHBOARD.DATASOURCE TO (1:3)
1020    CALLNAT "LOADFILE" "world.xml" #MYBLOB1
1030    CALLNAT "MAKEURL" XCIOBJECTS(*) #MYBLOB1 #MYURL 'world.xml'
1040    MYDASHBOARD.FILENAME(2) :='world.xml'
1050    MYDASHBOARD.OBJECTID(2):=#MYURL
1060    MYDASHBOARD.REFRESH(2):=TRUE
1070    CALLNAT "LOADFILE" 'world2.xml' #MYBLOB2
1080    CALLNAT "MAKEURL" XCIOBJECTS(*) #MYBLOB2 #MYURL 'world2.xml'
1090    MYDASHBOARD.FILENAME(3) :='world2.xml'
1100    MYDASHBOARD.OBJECTID(3):=#MYURL
1110    MYDASHBOARD.REFRESH(3):=TRUE
1120    PERFORM PREPAREDATA
1130END-SUBROUTINE
1140*
1150DEFINE SUBROUTINE PREPAREDATA
1160  IF MYDATA = 'mydata'
1170      #COUNTER := #COUNTER + 1
1180      REDUCE ARRAY XCIOBJECTS TO 0
1190      EXPAND ARRAY XCIOBJECTS TO (1:1)
1200      PERFORM SERIALIZEMYDATA
1210   END-IF
1220   EXAMINE MYDASHBOARD.URL FOR '##MASHSERVER' REPLACE MASHZONESERVER
1230   EXAMINE MYDASHBOARD.URL FOR '##SESSION' REPLACE XCIREQUESTCONTEXT.CISSESSIONID
1240   COMPRESS 'http://' XCIREQUESTCONTEXT.SERVERHOSTNAME ':' XCIREQUESTCONTEXT.SERVERPORT '/cisnatural' INTO #DUMMY LEAVING NO
1250   EXAMINE MYDASHBOARD.URL FOR '##MYNJXAPP' REPLACE #DUMMY
1260   COMPRESS 'mydata' #COUNTER INTO #DUMMY LEAVING NO
1270   IF MYDATA = 'mydata'
1280     EXAMINE MYDASHBOARD.URL FOR '##NAME' REPLACE #DUMMY
1290   ELSE
1300     EXAMINE MYDASHBOARD.URL FOR '##NAME' REPLACE MYDATA
1310   END-IF
1320END-SUBROUTINE
1330*
1340DEFINE SUBROUTINE SERIALIZEMYDATA
1350      COMPRESS '<DataTable>' INTO #MYDATA LEAVING NO
1360      #ENTRYLENGTH := *OCC(ENTRY.CONTINENT)
1370      FOR #II := 1 TO #ENTRYLENGTH
1380          COMPRESS #MYDATA '<Entry>' INTO #MYDATA LEAVING NO
1390          COMPRESS #MYDATA '<continent>' ENTRY.CONTINENT(#II) '</continent>' INTO #MYDATA LEAVING NO
1400          COMPRESS #MYDATA '<name>' ENTRY.NAME(#II) '</name>' INTO #MYDATA LEAVING NO
1410          COMPRESS #MYDATA '<population>' ENTRY.POPULATION(#II)'</population>' INTO #MYDATA LEAVING NO
1420          COMPRESS #MYDATA '<capital>' ENTRY.CAPITAL(#II)'</capital>' INTO #MYDATA LEAVING NO
1430          COMPRESS #MYDATA '</Entry>' INTO #MYDATA LEAVING NO
1440      END-FOR
1450      COMPRESS #MYDATA '</DataTable>' INTO #MYDATA LEAVING NO
1460      #MYBLOB3:=#MYDATA
1470      RESET #MYURL
1480      COMPRESS 'mydata' #COUNTER INTO #DUMMY LEAVING NO
1490      CALLNAT "MAKEURL" XCIOBJECTS(*) #MYBLOB3 #MYURL #DUMMY
1500      MYDASHBOARD.FILENAME(1) :=#DUMMY
1510      MYDASHBOARD.OBJECTID(1):= #MYURL
1520      MYDASHBOARD.REFRESH(1):=TRUE
1530END-SUBROUTINE
1540*
1550*
1560END
