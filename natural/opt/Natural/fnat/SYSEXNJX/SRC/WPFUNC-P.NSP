0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* ----- GENERATED BY NATURAL FOR AJAX TOOLS
0060* Library .....: SYSEXNJX
0070* Source Name .: LOGIN
0080* -----
0090DEFINE DATA LOCAL
01001 FUNC
0110  2 FOLDER (U) DYNAMIC INIT <'MyFolder'>
0120  2 LIBRARY (U) DYNAMIC INIT <*LIBRARY-ID>
0130  2 NODE (U) DYNAMIC INIT <'MyNode'>
0140  2 PROGRAM (U) DYNAMIC INIT <'CALCUL-P'>
0150  2 TOPIC (U) DYNAMIC INIT <'MyTopic'>
0160  2 URL (U) DYNAMIC INIT <'http://www.softwareag.com'>
01701 STATUS
0180  2 FOLDER (U) DYNAMIC
0190  2 LIBRARY (U) DYNAMIC
0200  2 NODE (U) DYNAMIC
0210  2 PROGRAM (U) DYNAMIC
0220  2 TOPIC (U) DYNAMIC
0230  2 URL (U) DYNAMIC
02401 XCIWPFUNCTIONS (1:*)
0250  2 CMDADDFOLDER
0260    3 ADDFOLDER_ASFIRST (L)
0270    3 ADDFOLDER_FOLDERNAME (U) DYNAMIC
0280    3 ADDFOLDER_FOLDERTEXTID (U) DYNAMIC
0290    3 ADDFOLDER_OPENED (I4)
0300    3 ADDFOLDER_TOPICNAME (U) DYNAMIC
0310    3 ADDFOLDER_TOPICTEXTID (U) DYNAMIC
0320  2 CMDADDNODE
0330    3 ADDNODE_ACTIVITYID (U) DYNAMIC
0340    3 ADDNODE_ACTIVITYURL (U) DYNAMIC
0350    3 ADDNODE_ASFIRST (L)
0360    3 ADDNODE_FOLDERNAME (U) DYNAMIC
0370    3 ADDNODE_FOLDERTEXTID (U) DYNAMIC
0380    3 ADDNODE_HEIGHT (I4)
0390    3 ADDNODE_LEFT (I4)
0400    3 ADDNODE_NAME (U) DYNAMIC
0410    3 ADDNODE_TARGET (U) DYNAMIC
0420    3 ADDNODE_TEXTID (U) DYNAMIC
0430    3 ADDNODE_TOP (I4)
0440    3 ADDNODE_TOPICNAME (U) DYNAMIC
0450    3 ADDNODE_TOPICTEXTID (U) DYNAMIC
0460    3 ADDNODE_TYPE (U) DYNAMIC
0470    3 ADDNODE_WIDTH (I4)
0480  2 CMDADDTOPIC
0490    3 ADDTOPIC_SWITCHTOTOPIC (L)
0500    3 ADDTOPIC_TOPICNAME (U) DYNAMIC
0510    3 ADDTOPIC_TOPICTEXTID (U) DYNAMIC
0520    3 ADDTOPIC_TREECLASS (U) DYNAMIC
0530  2 CMDREMFOLDER
0540    3 REMFOLDER_FOLDERNAME (U) DYNAMIC
0550    3 REMFOLDER_FOLDERTEXTID (U) DYNAMIC
0560    3 REMFOLDER_TOPICNAME (U) DYNAMIC
0570    3 REMFOLDER_TOPICTEXTID (U) DYNAMIC
0580  2 CMDREMNODE
0590    3 REMNODE_FOLDERNAME (U) DYNAMIC
0600    3 REMNODE_FOLDERTEXTID (U) DYNAMIC
0610    3 REMNODE_NAME (U) DYNAMIC
0620    3 REMNODE_TEXTID (U) DYNAMIC
0630    3 REMNODE_TOPICNAME (U) DYNAMIC
0640    3 REMNODE_TOPICTEXTID (U) DYNAMIC
0650  2 CMDREMTOPIC
0660    3 REMTOPIC_TOPICNAME (U) DYNAMIC
0670    3 REMTOPIC_TOPICTEXTID (U) DYNAMIC
06801 XCIWPINFO_CHANGEINDEX (I4)
06901 XCIWPINFO_NODE (1:*)
0700  2 ACTIVITYID (U) DYNAMIC
0710  2 ACTIVITYURL (U) DYNAMIC
0720  2 BUTTONSTYLE (U) DYNAMIC
0730  2 DRAGINFO (U) DYNAMIC
0740  2 FOLLOWPAGESWITCHES (L)
0750  2 HEIGHT (I4)
0760  2 ICONURL (U) DYNAMIC
0770  2 LEFT (I4)
0780  2 LEVEL (I4)
0790  2 NAME (U) DYNAMIC
0800  2 ONLYONEINSTANCE (L)
0810  2 OPENED (I4)
0820  2 TARGET (U) DYNAMIC
0830  2 TEXTID (U) DYNAMIC
0840  2 TOOLTIP (U) DYNAMIC
0850  2 TOOLTIPID (U) DYNAMIC
0860  2 TOP (I4)
0870  2 TREECLASS (U) DYNAMIC
0880  2 TREESTYLE (U) DYNAMIC
0890  2 TYPE (U) DYNAMIC
0900  2 WIDTH (I4)
0910*
09201 URLPATTERN (U) DYNAMIC INIT <'/cisnatural/NatLogon.html&xciParameters.natsession=Workplace&xciParameters.natparam=stack%3D%28LOGON+##LIBRARY%3B##PROGRAM%29'>
09301 ##TOPICNAME (U) DYNAMIC
09401 ##TOPICNAMECURR (U) DYNAMIC
09501 ##FOLDERNAME (U) DYNAMIC
09601 ##FOLDERNAMECURR (U) DYNAMIC
09701 ##NODENAME (U) DYNAMIC
09801 ##MESSAGE (U) DYNAMIC
09901 ##INDEX (I2)
10001 ##CNT (I2)
10101 ##I (I2)
1020END-DEFINE
1030*
1040PROCESS PAGE USING 'WPFUNC'
1050RESET STATUS
1060*
1070DECIDE ON FIRST *PAGE-EVENT
1080  VALUE U'nat:page.end'
1090    IGNORE
1100*
1110  VALUE U'onAddTopic'
1120    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
1130    ADDTOPIC_TOPICNAME(1) := FUNC.TOPIC
1140    ADDTOPIC_SWITCHTOTOPIC(1) := TRUE
1150    PROCESS PAGE UPDATE FULL
1160*
1170  VALUE U'onAddFolder'
1180    ##TOPICNAME := FUNC.TOPIC
1190    PERFORM FINDTOPIC
1200    IF ##INDEX = 0
1210      STATUS.TOPIC := 'ERROR'
1220      ##MESSAGE := 'Topic does not exist.'
1230      PERFORM DISPMESSAGE
1240    END-IF
1250*
1260    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
1270    ADDFOLDER_TOPICNAME(1) := FUNC.TOPIC
1280    ADDFOLDER_FOLDERNAME(1) := FUNC.FOLDER
1290    ADDFOLDER_OPENED(1) := 1
1300    PROCESS PAGE UPDATE FULL
1310*
1320  VALUE U'onAddProgramNode'
1330    ##TOPICNAME := FUNC.TOPIC
1340    ##FOLDERNAME := FUNC.FOLDER
1350    PERFORM FINDFOLDER
1360    IF ##INDEX = 0
1370      STATUS.FOLDER := 'ERROR'
1380      ##MESSAGE := 'Folder does not exist.'
1390      PERFORM DISPMESSAGE
1400    END-IF
1410*
1420    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
1430    ADDNODE_TOPICNAME(1) := FUNC.TOPIC
1440    ADDNODE_FOLDERNAME(1) := FUNC.FOLDER
1450    COMPRESS FUNC.LIBRARY FUNC.PROGRAM TO ADDNODE_NAME(1)
1460    ADDNODE_ACTIVITYURL(1) := URLPATTERN
1470    EXAMINE ADDNODE_ACTIVITYURL(1) FOR '##LIBRARY' REPLACE FUNC.LIBRARY
1480    EXAMINE ADDNODE_ACTIVITYURL(1) FOR '##PROGRAM' REPLACE FUNC.PROGRAM
1490    ADDNODE_TYPE(1) := 'cis'
1500    PROCESS PAGE UPDATE FULL
1510*
1520  VALUE U'onAddProgramNodeAsPopup'
1530    ##TOPICNAME := FUNC.TOPIC
1540    ##FOLDERNAME := FUNC.FOLDER
1550    PERFORM FINDFOLDER
1560    IF ##INDEX = 0
1570      STATUS.FOLDER := 'ERROR'
1580      ##MESSAGE := 'Folder does not exist.'
1590      PERFORM DISPMESSAGE
1600    END-IF
1610*
1620    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
1630    ADDNODE_TOPICNAME(1) := FUNC.TOPIC
1640    ADDNODE_FOLDERNAME(1) := FUNC.FOLDER
1650    COMPRESS 'Pop-up:' FUNC.LIBRARY FUNC.PROGRAM TO ADDNODE_NAME(1)
1660    ADDNODE_ACTIVITYURL(1) := URLPATTERN
1670    EXAMINE ADDNODE_ACTIVITYURL(1) FOR '##LIBRARY' REPLACE FUNC.LIBRARY
1680    EXAMINE ADDNODE_ACTIVITYURL(1) FOR '##PROGRAM' REPLACE FUNC.PROGRAM
1690    ADDNODE_TYPE(1) := 'cispopup'
1700    ADDNODE_LEFT (1) := 250
1710    ADDNODE_TOP (1) := 150
1720    ADDNODE_HEIGHT (1) := 400
1730    ADDNODE_WIDTH (1) := 600
1740    PROCESS PAGE UPDATE FULL
1750*
1760  VALUE U'onAddProgramNodeInFrame'
1770    ##TOPICNAME := FUNC.TOPIC
1780    ##FOLDERNAME := FUNC.FOLDER
1790    PERFORM FINDFOLDER
1800    IF ##INDEX = 0
1810      STATUS.FOLDER := 'ERROR'
1820      ##MESSAGE := 'Folder does not exist.'
1830      PERFORM DISPMESSAGE
1840    END-IF
1850*
1860    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
1870    ADDNODE_TOPICNAME(1) := FUNC.TOPIC
1880    ADDNODE_FOLDERNAME(1) := FUNC.FOLDER
1890    COMPRESS 'Frame:' FUNC.LIBRARY FUNC.PROGRAM TO ADDNODE_NAME(1)
1900    ADDNODE_ACTIVITYURL(1) := URLPATTERN
1910    EXAMINE ADDNODE_ACTIVITYURL(1) FOR '##LIBRARY' REPLACE FUNC.LIBRARY
1920    EXAMINE ADDNODE_ACTIVITYURL(1) FOR '##PROGRAM' REPLACE FUNC.PROGRAM
1930    ADDNODE_TYPE(1) := 'cistarget'
1940    ADDNODE_TARGET(1) := 'NEWS'
1950    PROCESS PAGE UPDATE FULL
1960*
1970  VALUE U'onAddURLNode'
1980    ##TOPICNAME := FUNC.TOPIC
1990    ##FOLDERNAME := FUNC.FOLDER
2000    PERFORM FINDFOLDER
2010    IF ##INDEX = 0
2020      STATUS.FOLDER := 'ERROR'
2030      ##MESSAGE := 'Folder does not exist.'
2040      PERFORM DISPMESSAGE
2050    END-IF
2060*
2070    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
2080    ADDNODE_TOPICNAME(1) := FUNC.TOPIC
2090    ADDNODE_FOLDERNAME(1) := FUNC.FOLDER
2100    ADDNODE_NAME(1) := FUNC.URL
2110    ADDNODE_ACTIVITYURL(1) := FUNC.URL
2120    ADDNODE_TYPE(1) := 'html'
2130    PROCESS PAGE UPDATE FULL
2140*
2150  VALUE U'onAddURLNodeAsPopup'
2160    ##TOPICNAME := FUNC.TOPIC
2170    ##FOLDERNAME := FUNC.FOLDER
2180    PERFORM FINDFOLDER
2190    IF ##INDEX = 0
2200      STATUS.FOLDER := 'ERROR'
2210      ##MESSAGE := 'Folder does not exist.'
2220      PERFORM DISPMESSAGE
2230    END-IF
2240*
2250    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
2260    ADDNODE_TOPICNAME(1) := FUNC.TOPIC
2270    ADDNODE_FOLDERNAME(1) := FUNC.FOLDER
2280    COMPRESS 'Pop-up:' FUNC.URL TO ADDNODE_NAME(1)
2290    ADDNODE_ACTIVITYURL(1) := FUNC.URL
2300    ADDNODE_TYPE(1) := 'htmlpopup'
2310    ADDNODE_LEFT (1) := 250
2320    ADDNODE_TOP (1) := 150
2330    ADDNODE_HEIGHT (1) := 400
2340    ADDNODE_WIDTH (1) := 600
2350    PROCESS PAGE UPDATE FULL
2360*
2370  VALUE U'onAddURLNodeInFrame'
2380    ##TOPICNAME := FUNC.TOPIC
2390    ##FOLDERNAME := FUNC.FOLDER
2400    PERFORM FINDFOLDER
2410    IF ##INDEX = 0
2420      STATUS.FOLDER := 'ERROR'
2430      ##MESSAGE := 'Folder does not exist.'
2440      PERFORM DISPMESSAGE
2450    END-IF
2460*
2470    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
2480    ADDNODE_TOPICNAME(1) := FUNC.TOPIC
2490    ADDNODE_FOLDERNAME(1) := FUNC.FOLDER
2500    COMPRESS 'Frame:' FUNC.URL TO ADDNODE_NAME(1)
2510    ADDNODE_ACTIVITYURL(1) := FUNC.URL
2520    ADDNODE_TYPE(1) := 'htmltarget'
2530    ADDNODE_TARGET(1) := 'NEWS'
2540    PROCESS PAGE UPDATE FULL
2550*
2560  VALUE U'onRemoveTopic'
2570    ##TOPICNAME := FUNC.TOPIC
2580    PERFORM FINDTOPIC
2590    IF ##INDEX = 0
2600      STATUS.TOPIC := 'ERROR'
2610      ##MESSAGE := 'Topic does not exist.'
2620      PERFORM DISPMESSAGE
2630    END-IF
2640*
2650    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
2660    REMTOPIC_TOPICNAME(1) := FUNC.TOPIC
2670    PROCESS PAGE UPDATE FULL
2680*
2690  VALUE U'onRemoveFolder'
2700    ##TOPICNAME := FUNC.TOPIC
2710    ##FOLDERNAME := FUNC.FOLDER
2720    PERFORM FINDFOLDER
2730    IF ##INDEX = 0
2740      STATUS.FOLDER := 'ERROR'
2750      ##MESSAGE := 'Folder does not exist.'
2760      PERFORM DISPMESSAGE
2770    END-IF
2780*
2790    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
2800    REMFOLDER_TOPICNAME(1) := FUNC.TOPIC
2810    REMFOLDER_FOLDERNAME(1) := FUNC.FOLDER
2820    PROCESS PAGE UPDATE FULL
2830*
2840  VALUE U'onRemoveNode'
2850    ##TOPICNAME := FUNC.TOPIC
2860    ##FOLDERNAME := FUNC.FOLDER
2870    ##NODENAME := FUNC.NODE
2880    PERFORM FINDNODE
2890    IF ##INDEX = 0
2900      STATUS.NODE := 'ERROR'
2910      ##MESSAGE := 'Node does not exist.'
2920      PERFORM DISPMESSAGE
2930    END-IF
2940*
2950    RESIZE ARRAY XCIWPFUNCTIONS TO (1:1)
2960    REMNODE_TOPICNAME(1) := FUNC.TOPIC
2970    REMNODE_FOLDERNAME(1) := FUNC.FOLDER
2980    REMNODE_NAME(1) := FUNC.NODE
2990    PROCESS PAGE UPDATE FULL
3000*
3010  NONE VALUE
3020    PROCESS PAGE UPDATE FULL
3030END-DECIDE
3040*
3050DEFINE SUBROUTINE FINDTOPIC
3060* In:  ##TOPICNAME
3070* Out: ##INDEX
3080RESET ##INDEX
3090##CNT := *OCCURRENCE(XCIWPINFO_NODE.NAME)
3100FOR ##I := 1 TO ##CNT
3110  IF XCIWPINFO_NODE.NAME(##I) = ##TOPICNAME AND
3120      XCIWPINFO_NODE.LEVEL(##I) = 1
3130    ##INDEX := ##I
3140    ESCAPE BOTTOM
3150  END-IF
3160END-FOR
3170END-SUBROUTINE
3180*
3190DEFINE SUBROUTINE FINDFOLDER
3200* In:  ##TOPICNAME
3210* In:  ##FOLDERNAME
3220* Out: ##INDEX
3230RESET ##INDEX ##TOPICNAMECURR
3240##CNT := *OCCURRENCE(XCIWPINFO_NODE.NAME)
3250FOR ##I := 1 TO ##CNT
3260  IF XCIWPINFO_NODE.LEVEL(##I) = 1
3270    ##TOPICNAMECURR := XCIWPINFO_NODE.NAME(##I)
3280    ESCAPE TOP
3290  END-IF
3300  IF ##TOPICNAMECURR = ##TOPICNAME AND
3310      XCIWPINFO_NODE.NAME(##I) = ##FOLDERNAME AND
3320      XCIWPINFO_NODE.LEVEL(##I) = 2
3330    ##INDEX := ##I
3340    ESCAPE BOTTOM
3350  END-IF
3360END-FOR
3370END-SUBROUTINE
3380*
3390DEFINE SUBROUTINE FINDNODE
3400* In:  ##TOPICNAME
3410* In:  ##FOLDERNAME
3420* In:  ##NODENAME
3430* Out: ##INDEX
3440RESET ##INDEX ##TOPICNAMECURR ##FOLDERNAMECURR
3450##CNT := *OCCURRENCE(XCIWPINFO_NODE.NAME)
3460FOR ##I := 1 TO ##CNT
3470  IF XCIWPINFO_NODE.LEVEL(##I) = 1
3480    ##TOPICNAMECURR := XCIWPINFO_NODE.NAME(##I)
3490    ESCAPE TOP
3500  END-IF
3510  IF XCIWPINFO_NODE.LEVEL(##I) = 2
3520    ##FOLDERNAMECURR := XCIWPINFO_NODE.NAME(##I)
3530    ESCAPE TOP
3540  END-IF
3550  IF ##TOPICNAMECURR = ##TOPICNAME AND
3560      ##FOLDERNAMECURR = ##FOLDERNAME AND
3570      XCIWPINFO_NODE.NAME(##I) = ##NODENAME AND
3580      XCIWPINFO_NODE.LEVEL(##I) = 3
3590    ##INDEX := ##I
3600    ESCAPE BOTTOM
3610  END-IF
3620END-FOR
3630END-SUBROUTINE
3640*
3650DEFINE SUBROUTINE DISPMESSAGE
3660* In:  ##MESSAGE
3670PROCESS PAGE UPDATE FULL
3680  AND SEND EVENT 'nat:page.message'
3690  WITH PARAMETERS
3700    NAME 'type' VALUE 'E'
3710    NAME 'short' VALUE ##MESSAGE
3720  END-PARAMETERS
3730END-SUBROUTINE
3740*
3750END
