0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050DEFINE DATA PARAMETER
00601 NAME (A) DYNAMIC BY VALUE
00701 LOADFILE (B) DYNAMIC
0080LOCAL
0090* Get path of resource (Open Systems)
01001 USR6203L
0110  2 INPUTS
0120    3 P-FUNCTION      (A1)
0130    3 P-RESOURCE-NAME (A253)
0140    3 P-LIBRARY-NAME  (A8)
0150  2 OUTPUTS
0160    3 P-PATH-NAME     (A253)
0170    3 P-RESPONSE      (I4)
0180* Load resource (Mainframe)
01901 USR4208L
0200  2 RESOURCE-FUNCTION                 (A1)
0210  2 OBJECT-KEY
0220    3 LIBRARY-NAME                    (A8)
0230    3 RESOURCE-NAME                   (A250)
0240    3 OBJECT-NAME                     (A8)
0250  2 OBJECT-LOCATION
0260    3 LIBRARY-DBID                    (N5)
0270    3 LIBRARY-FNR                     (N5)
0280    3 LIBRARY-PASSWORD                (A8)
0290    3 LIBRARY-CIPHER                  (N8)
0300  2 WRITE-OPTIONS
0310    3 OPTION-REPLACE                  (A1)
0320  2 INPUT-OUTPUTS
0330    3 RESOURCE-DATA                   (A) DYNAMIC
0340    3 RESOURCE-DIRECTORY-DATA         (A389)
0350    3 REDEFINE RESOURCE-DIRECTORY-DATA
0360      4 RD-RESOURCE-LIBRARY           (A8)
0370      4 RD-RESOURCE-OBJECT-NAME       (A8)
0380      4 RD-RESOURCE-NAME              (A245)
0390      4 RD-RESOURCE-TYPE              (A4)
0400      4 RD-RESOURCE-FORMAT            (A1)
0410      4 RD-RESOURCE-DATE              (A8)
0420      4 RD-RESOURCE-TIME              (A7)
0430      4 RD-RESOURCE-USER              (A8)
0440      4 RD-RESOURCE-INIT-USER         (A8)
0450      4 RD-RESOURCE-SIZE              (I4)
0460      4 RD-RESOURCE-VERSION           (A8)
0470      4 RD-RESOURCE-OPSYS             (A8)
0480      4 RD-RESOURCE-TPSYS             (A8)
0490      4 RD-RESOURCE-CODE-PAGE         (A64)
0500        /*
05101 USR4208N
0520  2 VERSION                           (I1) INIT <0>
05301 REDEFINE USR4208N
0540  2 EXTENDED-PARMS
0550    3 EXTENDED-DATA                   (A1/1:1)
0560LOCAL USING USR-MSG
0570LOCAL USING USR-FLD
0580END-DEFINE
0590*
0600IF *MACHINE-CLASS = "MAINFRAME"
0610* Load the resource.
0620  RESOURCE-FUNCTION := 'R'
0630  RESOURCE-NAME := NAME
0640  LIBRARY-NAME := *LIBRARY-ID
0650  CALLNAT 'USR4208N' USR4208L
0660    USR4208N.EXTENDED-DATA(*) NAD-MSG NAD-FLD
0670  LOADFILE := RESOURCE-DATA
0680*
0690ELSE
0700* Get the absolute path from the file name.
0710  P-FUNCTION := "L"
0720  P-RESOURCE-NAME := NAME
0730  P-LIBRARY-NAME := *LIBRARY-ID
0740  CALLNAT 'USR6203N' USR6203L
0750* Load the file.
0760  DEFINE WORK FILE 1 P-PATH-NAME TYPE "UNFORMATTED"
0770  READ WORK FILE 1 ONCE LOADFILE
0780  CLOSE WORK FILE 1
0790END-IF
0800*
0810END
