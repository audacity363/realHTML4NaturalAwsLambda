0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050DEFINE DATA LOCAL
00601 INFOPAGENAME (U) DYNAMIC INIT <'html/ctrlimageviewer.html'>
00701 DISP
00802 IMAGEHEIGHT (I4)
00902 IMAGENAME (U) DYNAMIC
01002 IMAGETITLE (U) DYNAMIC
01102 IMAGEURL (U) DYNAMIC
01202 IMAGEWIDTH (I4)
01302 ROTATION (I4)
01402 XCENTER (I4)
01502 YCENTER (I4)
01602 ZOOMFACTOR (N5.2)
01701 IMAGE
01802 IMAGEHEIGHT (I4)
01902 IMAGEURL (U) DYNAMIC
02002 IMAGEWIDTH (I4)
02102 ROTATION (I4)
02202 XCENTER (I4)
02302 YCENTER (I4)
02402 ZOOMFACTOR (F4)
02501 SLIDERHORZ
02602 DISPLAYONLY (L)
02702 FROM (F4)
02802 SLIDERVALUE (F4)
02902 STEP (F4)
03002 TO (F4)
03101 SLIDERROTA
03202 DISPLAYONLY (L)
03302 FROM (F4)
03402 SLIDERVALUE (F4)
03502 STEP (F4)
03602 TO (F4)
03701 SLIDERVERT
03802 DISPLAYONLY (L)
03902 FROM (F4)
04002 SLIDERVALUE (F4)
04102 STEP (F4)
04202 TO (F4)
04301 SLIDERZOOM
04402 DISPLAYONLY (L)
04502 FROM (F4)
04602 SLIDERVALUE (F4)
04702 STEP (F4)
04802 TO (F4)
04901 #CANVASWIDTH (I4) INIT <540>
05001 #CANVASHEIGHT (I4) INIT <360>
05101 #TEMP (F4)
0520END-DEFINE
0530*
0540DISP.IMAGENAME := DISP.IMAGETITLE := DISP.IMAGEURL := 'images/sagair3.jpg'
0550IMAGE.IMAGEURL := DISP.IMAGEURL
0560IMAGE.ZOOMFACTOR := 0
0570IMAGE.ROTATION := 0
0580*
0590SLIDERHORZ.FROM := 0
0600SLIDERHORZ.TO := 100
0610SLIDERHORZ.STEP := 1
0620SLIDERHORZ.SLIDERVALUE := 50
0630SLIDERVERT.FROM := 0
0640SLIDERVERT.TO := 100
0650SLIDERVERT.STEP := 1
0660SLIDERVERT.SLIDERVALUE := 50
0670SLIDERROTA.FROM := 0
0680SLIDERROTA.TO := 270
0690SLIDERROTA.STEP := 90
0700SLIDERROTA.SLIDERVALUE := 0
0710SLIDERZOOM.FROM := 0
0720SLIDERZOOM.TO := 400
0730SLIDERZOOM.STEP := 0.05
0740PERFORM DISPLAYINFO
0750*
0760PROCESS PAGE USING "CTRIMV-A"
0770*
0780DECIDE ON FIRST *PAGE-EVENT
0790  VALUE U'nat:page.end'
0800    IGNORE
0810  VALUE U'onFitIn', U'onLoad'
0820    IMAGE.IMAGEURL := DISP.IMAGEURL
0830    DISP.IMAGETITLE := DISP.IMAGENAME := DISP.IMAGEURL
0840    IMAGE.ZOOMFACTOR := 0
0850    SLIDERZOOM.SLIDERVALUE := IMAGE.ZOOMFACTOR * 100
0860    SLIDERHORZ.SLIDERVALUE := 50
0870    SLIDERVERT.SLIDERVALUE := 50
0880    PERFORM DISPLAYINFO
0890    PROCESS PAGE UPDATE FULL
0900  VALUE U'onReset'
0910    IMAGE.ZOOMFACTOR := 1
0920    IMAGE.ROTATION := 0
0930    IMAGE.XCENTER := IMAGE.IMAGEWIDTH / 2
0940    IMAGE.YCENTER := IMAGE.IMAGEHEIGHT / 2
0950    SLIDERZOOM.SLIDERVALUE := IMAGE.ZOOMFACTOR * 100
0960    SLIDERROTA.SLIDERVALUE := IMAGE.ROTATION
0970    SLIDERHORZ.SLIDERVALUE := 50
0980    SLIDERVERT.SLIDERVALUE := 50
0990    PERFORM DISPLAYINFO
1000    PROCESS PAGE UPDATE FULL
1010  VALUE U'onMove'
1020    SLIDERHORZ.SLIDERVALUE := IMAGE.XCENTER * 100 / IMAGE.IMAGEWIDTH
1030    SLIDERVERT.SLIDERVALUE := IMAGE.YCENTER * 100 / IMAGE.IMAGEHEIGHT
1040    PERFORM DISPLAYINFO
1050    PROCESS PAGE UPDATE FULL
1060  VALUE U'onZoomIn'
1070    IMAGE.ZOOMFACTOR := IMAGE.ZOOMFACTOR * 1.2
1080    SLIDERZOOM.SLIDERVALUE := IMAGE.ZOOMFACTOR * 100
1090    PERFORM DISPLAYINFO
1100    PROCESS PAGE UPDATE FULL
1110  VALUE U'onZoomOut'
1120    IMAGE.ZOOMFACTOR := IMAGE.ZOOMFACTOR / 1.2
1130    SLIDERZOOM.SLIDERVALUE := IMAGE.ZOOMFACTOR * 100
1140    PERFORM DISPLAYINFO
1150    PROCESS PAGE UPDATE FULL
1160  VALUE U'onRotateRight'
1170    ADD 90 TO IMAGE.ROTATION
1180    IF IMAGE.ROTATION = 360
1190      IMAGE.ROTATION := 0
1200    END-IF
1210    SLIDERROTA.SLIDERVALUE := IMAGE.ROTATION
1220    PERFORM DISPLAYINFO
1230    PROCESS PAGE UPDATE FULL
1240  VALUE U'onRotateLeft'
1250    IF IMAGE.ROTATION = 0
1260      IMAGE.ROTATION := 360
1270    END-IF
1280    SUBTRACT 90 FROM IMAGE.ROTATION
1290    SLIDERROTA.SLIDERVALUE := IMAGE.ROTATION
1300    PERFORM DISPLAYINFO
1310    PROCESS PAGE UPDATE FULL
1320  VALUE U'nat:page.default'
1330    IMAGE.XCENTER := SLIDERHORZ.SLIDERVALUE * IMAGE.IMAGEWIDTH / 100
1340    IMAGE.YCENTER := SLIDERVERT.SLIDERVALUE * IMAGE.IMAGEHEIGHT / 100
1350    IMAGE.ROTATION := SLIDERROTA.SLIDERVALUE
1360    IMAGE.ZOOMFACTOR := SLIDERZOOM.SLIDERVALUE / 100
1370    PERFORM DISPLAYINFO
1380    PROCESS PAGE UPDATE FULL
1390  NONE VALUE
1400    PROCESS PAGE UPDATE
1410END-DECIDE
1420*
1430DEFINE SUBROUTINE DISPLAYINFO
1440MOVE BY NAME IMAGE TO DISP
1450#TEMP := IMAGE.ZOOMFACTOR * 100
1460MOVE ROUNDED #TEMP TO DISP.ZOOMFACTOR
1470END-SUBROUTINE
1480*
1490END
