0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* ----- GENERATED BY NATURAL FOR AJAX TOOLS
0060* Library .....: SYSEXNJX
0070* Source Name .: CTRCTX-A
0080* -----
0090DEFINE DATA LOCAL
01001 INFOPAGENAME (A) DYNAMIC INIT <'html/ctrlcontextmenu.html'>
01101 LINES (1:*)
0120  2 DEPT (A) DYNAMIC
0130  2 FIRST (A) DYNAMIC
0140  2 NAME (A) DYNAMIC
0150  2 SELECTED (L)
01601 XCIPOPUPMENU
0170  2 MENUNODE (1:*)
0180    3 ID (A) DYNAMIC
0190    3 IMAGE (A) DYNAMIC
0200    3 LEVEL (I4)
0210    3 REFERENCE (A) DYNAMIC
0220    3 TEXT (A) DYNAMIC
0230  2 ORIGINATORQUERY (A) DYNAMIC
0240  2 SELECTEDREFERENCE (A) DYNAMIC
0250*
02601 EVENT (A64)
02701 MESSAGE (A64)
02801 SELCMD (A) DYNAMIC
02901 SELITEM (A) DYNAMIC
03001 I (I2)
0310END-DEFINE
0320*
0330EXPAND ARRAY LINES TO (1:3)
0340LINES.NAME(1) := 'Carter'
0350LINES.NAME(2) := 'Miller'
0360LINES.NAME(3) := 'Smith'
0370LINES.FIRST(1) := 'James'
0380LINES.FIRST(2) := 'John'
0390LINES.FIRST(3) := 'Hugh'
0400PROCESS PAGE USING 'CTRCTX-A'
0410EVENT := *PAGE-EVENT
0420*
0430DECIDE ON FIRST *PAGE-EVENT
0440  VALUE U'nat:page.end', 'onExit'
0450    IGNORE
0460  VALUE U'lines.reactOnContextMenuRequest'
0470    RESIZE ARRAY MENUNODE TO (1:3)
0480    XCIPOPUPMENU.ID(1) := 'CMD1'
0490    XCIPOPUPMENU.REFERENCE(1) := 'Show'
0500    XCIPOPUPMENU.TEXT(1) := 'Show'
0510    XCIPOPUPMENU.LEVEL(1) := 1
0520    XCIPOPUPMENU.ID(2) := 'CMD2'
0530    XCIPOPUPMENU.REFERENCE(2) := 'Modify'
0540    XCIPOPUPMENU.TEXT(2) := 'Modify'
0550    XCIPOPUPMENU.LEVEL(2) := 1
0560    XCIPOPUPMENU.ID(3) := 'CMD3'
0570    XCIPOPUPMENU.REFERENCE(3) := 'Delete'
0580    XCIPOPUPMENU.TEXT(3) := 'Delete'
0590    XCIPOPUPMENU.LEVEL(3) := 1
0600    PROCESS PAGE UPDATE FULL
0610  VALUE U'onGridContextMenu'
0620    RESIZE ARRAY MENUNODE TO (1:2)
0630    XCIPOPUPMENU.ID(1) := 'CMD1'
0640    XCIPOPUPMENU.REFERENCE(1) := 'Grid function 1'
0650    XCIPOPUPMENU.TEXT(1) := 'Grid function 1'
0660    XCIPOPUPMENU.LEVEL(1) := 1
0670    XCIPOPUPMENU.ID(2) := 'CMD2'
0680    XCIPOPUPMENU.REFERENCE(2) := 'Grid function 2'
0690    XCIPOPUPMENU.TEXT(2) := 'Grid function 2'
0700    XCIPOPUPMENU.LEVEL(2) := 1
0710    PROCESS PAGE UPDATE FULL
0720  VALUE U'onPageContextMenu'
0730    RESIZE ARRAY MENUNODE TO (1:2)
0740    XCIPOPUPMENU.ID(1) := 'CMD1'
0750    XCIPOPUPMENU.REFERENCE(1) := 'Page function 1'
0760    XCIPOPUPMENU.TEXT(1) := 'Page function 1'
0770    XCIPOPUPMENU.LEVEL(1) := 1
0780    XCIPOPUPMENU.ID(2) := 'CMD2'
0790    XCIPOPUPMENU.REFERENCE(2) := 'Page function 2'
0800    XCIPOPUPMENU.TEXT(2) := 'Page function 2'
0810    XCIPOPUPMENU.LEVEL(2) := 1
0820    PROCESS PAGE UPDATE FULL
0830  VALUE U'xcipopupmenu.reactOnSelect'
0840    PERFORM GETSELCMD
0850    PERFORM GETSELITEM
0860    PERFORM GETMESSAGE
0870    PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message'
0880      WITH PARAMETERS
0890        NAME 'type'  VALUE 'S'
0900        NAME 'short' VALUE MESSAGE
0910        NAME 'long'  VALUE MESSAGE
0920      END-PARAMETERS
0930  NONE VALUE
0940    PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message'
0950      WITH PARAMETERS
0960        NAME 'type'  VALUE 'S'
0970        NAME 'short' VALUE EVENT
0980        NAME 'long'  VALUE EVENT
0990      END-PARAMETERS
1000END-DECIDE
1010*
1020DEFINE SUBROUTINE GETSELCMD
1030SELCMD := XCIPOPUPMENU.SELECTEDREFERENCE
1040END-SUBROUTINE
1050*
1060DEFINE SUBROUTINE GETSELITEM
1070RESET SELITEM
1080FOR I := 1 TO *OCC(LINES.SELECTED)
1090  IF LINES.SELECTED(I)
1100    SELITEM := LINES.NAME(I)
1110    ESCAPE BOTTOM
1120  END-IF
1130END-FOR
1140END-SUBROUTINE
1150*
1160DEFINE SUBROUTINE GETMESSAGE
1170COMPRESS SELCMD SELITEM TO MESSAGE
1180IF SELCMD = SCAN('Grid') OR = SCAN('Page')
1190  MESSAGE := SELCMD
1200END-IF
1210END-SUBROUTINE
1220*
1230END
