0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* Name: not used/ActiveX Control /*<RO>
0060*
0070DEFINE DATA LOCAL
00801 ADAPTERDAY (U) DYNAMIC
00901 ADAPTERMONTH (U) DYNAMIC
01001 ADAPTERYEAR (U) DYNAMIC
01101 ADAPTERDATE (U) DYNAMIC
01201 ADAPTERDAYN (N2)
01301 ADAPTERMONTHN (N2)
01401 DATE (D)
01501 INFOPAGENAME (U) DYNAMIC INIT <'html/ctrlactivex.html'>
0160LOCAL USING NJX-MSGL
0170END-DEFINE
0180*
0190DATE := *DATX
0200*
0210PROCESS PAGE USING "CTRACX-A"
0220*
0230/*( DEFINE EVENT HANDLER
0240DECIDE ON FIRST *PAGE-EVENT
0250  VALUE U'nat:page.end'
0260    /* Page closed.
0270    IGNORE
0280  VALUE U'onGetDate'
0290    /* Need leading zeroes in day and month.
0300    ADAPTERDAYN := VAL(ADAPTERDAY)
0310    ADAPTERMONTHN := VAL(ADAPTERMONTH)
0320    MOVE EDITED ADAPTERDAYN (EM=99) TO ADAPTERDAY
0330    MOVE EDITED ADAPTERMONTHN (EM=99) TO ADAPTERMONTH
0340    /* Build the date string for international date format.
0350    COMPRESS ADAPTERYEAR '-' ADAPTERMONTH '-' ADAPTERDAY TO
0360      ADAPTERDATE LEAVING NO
0370    MOVE EDITED ADAPTERDATE TO DATE (EM=YYYY-MM-DD)
0380    OUTPUTMESSAGE.SHORTTEXT := DATE
0390    PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message' WITH
0400      PARAMETERS
0410        NAME 'type'  VALUE OUTPUTMESSAGE_SUCCESS
0420        NAME 'short' VALUE OUTPUTMESSAGE.SHORTTEXT
0430        NAME 'long'  VALUE OUTPUTMESSAGE.LONGTEXT
0440      END-PARAMETERS
0450  NONE VALUE
0460    /* Unhandled events.
0470    PROCESS PAGE UPDATE
0480END-DECIDE
0490/*) END-HANDLER
0500END
