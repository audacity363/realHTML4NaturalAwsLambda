0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* Name: Operatios on Grid/Operatios on Textgrid /*<RO>
0060*
0070DEFINE DATA LOCAL
00801 AGEOUT (U) DYNAMIC
00901 GRID (1:*)
0100  2 GRIDAGE (U) DYNAMIC
0110  2 GRIDID (U) DYNAMIC
0120  2 GRIDNAME (U) DYNAMIC
0130  2 SELECTED (L)
01401 GRIDINFO
0150  2 ROWCOUNT (I4)
0160  2 SIZE (I4)
0170  2 SORTPROPS (1:*)
0180    3 ASCENDING (L)
0190    3 PROPNAME (U) DYNAMIC
0200  2 TOPINDEX (I4)
02101 IDOUT (U) DYNAMIC
02201 INFOPAGENAME (U) DYNAMIC INIT <'html/ctrlgridoperations.html'>
02301 LINESINGRID (U) DYNAMIC
02401 NAMEOUT (U) DYNAMIC
02501 SELECTEDLINEINGRID (U) DYNAMIC
0260*
0270LOCAL USING NJX-MSGL
0280LOCAL
02901 #II (I2)
03001 #JJ (I2)
03101 #KK (I2)
0320END-DEFINE
0330*
0340PROCESS PAGE USING 'CTRGOP-A'
0350*
0360/*( DEFINE EVENT HANDLER
0370DECIDE ON FIRST *PAGE-EVENT
0380  VALUE U'nat:page.end'
0390    /* Page closed.
0400    IGNORE
0410*  VALUE U'grid.onSort'
0420*   /* TODO: Implement event code.
0430*   PROCESS PAGE UPDATE FULL
0440*  VALUE U'grid.onTopindexChanged'
0450*   /* TODO: Implement event code.
0460*   PROCESS PAGE UPDATE FULL
0470  VALUE 'onAppendValues'
0480    #II := *OCC(GRID.GRIDID) + 1
0490    EXPAND ARRAY GRID TO (1:#II)
0500    GRID.GRIDID(#II)   := IDOUT
0510    GRID.GRIDNAME(#II) := NAMEOUT
0520    GRIDAGE(#II)       := AGEOUT
0530    /*
0540    PROCESS PAGE UPDATE FULL
0550  VALUE 'onCountLines'
0560    LINESINGRID := *OCC(GRID.GRIDID)
0570    /*
0580    PROCESS PAGE UPDATE FULL
0590  VALUE 'onDeleteGridValues'
0600    REDUCE ARRAY GRID TO 0
0610    /*
0620    PROCESS PAGE UPDATE FULL
0630  VALUE 'onDeleteSelectedValues'
0640    FOR #II = 1 TO *OCC(GRID.GRIDID)
0650      IF GRID.SELECTED(#II) = TRUE
0660        PERFORM REMOVE_GRID_ENTRY
0670        ESCAPE BOTTOM
0680      END-IF
0690    END-FOR
0700    /*
0710    IF #II > *OCC(GRID.GRIDID)
0720      PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message' WITH
0730        PARAMETERS
0740          NAME U'type'  VALUE OUTPUTMESSAGE_SUCCESS
0750          NAME U'short' VALUE 'Nothing selected!'
0760        END-PARAMETERS
0770    ELSE
0780      IF #II > 0
0790        GRID.SELECTED(#II) := TRUE
0800      END-IF
0810      PROCESS PAGE UPDATE FULL
0820    END-IF
0830  VALUE 'onFillGrid'
0840    RESET INITIAL GRID(*)
0850    EXPAND ARRAY GRID TO (1:11)
0860    FOR #II=1 TO *OCC(GRID.GRIDID)
0870      COMPRESS #II INTO GRID.GRIDID(#II) LEAVING NO
0880      COMPRESS 'Name' #II INTO GRID.GRIDNAME(#II)
0890      GRID.GRIDAGE(#II) :=  40 + #II
0900    END-FOR
0910    /*
0920    PROCESS PAGE UPDATE FULL
0930  VALUE 'onGetSelectedLine'
0940    FOR #II = 1 TO *OCC(GRID.GRIDID)
0950      IF GRID.SELECTED(#II) = TRUE
0960        SELECTEDLINEINGRID := #II
0970        ESCAPE BOTTOM
0980      END-IF
0990    END-FOR
1000    /*
1010    IF #II > *OCC(GRID.GRIDID)
1020      PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message' WITH
1030        PARAMETERS
1040          NAME U'type' VALUE OUTPUTMESSAGE_SUCCESS
1050          NAME U'short'   VALUE 'Nothing selected!'
1060        END-PARAMETERS
1070    ELSE
1080      PROCESS PAGE UPDATE FULL
1090    END-IF
1100  VALUE 'onInsertValues'
1110    FOR #II = 1 TO *OCC(GRID.GRIDID)
1120      IF GRID.SELECTED(#II) = TRUE
1130        PERFORM DUPLICATE_GRID_ENTRY
1140        GRID.GRIDID(#II)   := IDOUT
1150        GRID.GRIDNAME(#II) := NAMEOUT
1160        GRIDAGE(#II)       := AGEOUT
1170        GRID.SELECTED(#II) := FALSE
1180        ESCAPE BOTTOM
1190      END-IF
1200    END-FOR
1210    /*
1220    IF #II > *OCC(GRID.GRIDID)
1230      PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message' WITH
1240        PARAMETERS
1250          NAME U'type'  VALUE OUTPUTMESSAGE_SUCCESS
1260          NAME U'short' VALUE 'Nothing selected!'
1270        END-PARAMETERS
1280    ELSE
1290      PROCESS PAGE UPDATE FULL
1300    END-IF
1310  VALUE 'onReplaceValues'
1320    FOR #II = 1 TO *OCC(GRID.GRIDID)
1330      IF GRID.SELECTED(#II) = TRUE
1340        GRID.GRIDID(#II)   := IDOUT
1350        GRID.GRIDNAME(#II) := NAMEOUT
1360        GRIDAGE(#II)       := AGEOUT
1370        ESCAPE BOTTOM
1380      END-IF
1390    END-FOR
1400    /*
1410    IF #II > *OCC(GRID.GRIDID)
1420      PROCESS PAGE UPDATE FULL AND SEND EVENT 'nat:page.message' WITH
1430        PARAMETERS
1440          NAME U'type'  VALUE OUTPUTMESSAGE_SUCCESS
1450          NAME U'short' VALUE 'Nothing selected!'
1460        END-PARAMETERS
1470    ELSE
1480      PROCESS PAGE UPDATE FULL
1490    END-IF
1500  VALUE 'onSelectGrid'
1510    FOR #II=1 TO *OCC(GRID.GRIDID)
1520      IF GRID.SELECTED(#II) = TRUE
1530        IDOUT   := GRID.GRIDID(#II)
1540        NAMEOUT := GRID.GRIDNAME(#II)
1550        AGEOUT  := GRID.GRIDAGE(#II)
1560        ESCAPE BOTTOM
1570      END-IF
1580    END-FOR
1590    /*
1600    PROCESS PAGE UPDATE FULL
1610  NONE VALUE
1620    /* Unhandled events.
1630    PROCESS PAGE UPDATE
1640END-DECIDE
1650/*) END-HANDLER
1660*
1670DEFINE SUBROUTINE REMOVE_GRID_ENTRY
1680IF #II > 0 AND *OCC(GRID.GRIDID) > 0
1690  #KK := #II + 1
1700  FOR #JJ = #KK TO *OCC(GRID.GRIDID)
1710    #KK := #JJ - 1
1720    MOVE BY NAME GRID(#JJ) TO GRID(#KK)
1730  END-FOR
1740  #KK := *OCC(GRID.GRIDID) - 1
1750  IF #KK > 0
1760    REDUCE ARRAY GRID TO (1:#KK)
1770    IF #II > *OCC(GRID.GRIDID)
1780      #II := *OCC(GRID.GRIDID)
1790    END-IF
1800  ELSE
1810    RESET INITIAL GRID(*)
1820  END-IF
1830ELSE
1840  #II := 0
1850END-IF
1860END-SUBROUTINE
1870*
1880DEFINE SUBROUTINE DUPLICATE_GRID_ENTRY
1890IF *OCC(GRID.GRIDID) > 0
1900  #KK := *OCC(GRID.GRIDID) + 1
1910  EXPAND ARRAY GRID TO (1:#KK)
1920  ADD 1 TO #II
1930  FOR #JJ = *OCC(GRID.GRIDID) TO #II STEP -1
1940    #KK := #JJ - 1
1950    MOVE BY NAME GRID(#KK) TO GRID(#JJ)
1960  END-FOR
1970ELSE
1980  EXPAND ARRAY GRID TO (1:1)
1990  #II := 1
2000END-IF
2010*
2020END-SUBROUTINE
2030/*
2040END
