0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* ----- GENERATED BY NATURAL FOR AJAX TOOLS
0060* Library .....: SYSEXNJX
0070* Source Name .: CTRFIL-A
0080* -----
0090DEFINE DATA LOCAL
01001 COLUMNS (1:*)
0110  2 FIRST (A) DYNAMIC
0120  2 ID (A) DYNAMIC
0130  2 LAST (A) DYNAMIC
0140  2 STATUS (A) DYNAMIC
01501 INFOPAGENAME (A) DYNAMIC INIT <'html/ctrlfieldlist.html'>
01601 SIMPLE (A/1:*) DYNAMIC
0170*
01801 EMPL VIEW OF EMPLOYEES
0190  2 PERSONNEL-ID
0200  2 NAME
0210  2 REDEFINE NAME
0220    3 NAME1 (A1)
0230    3 NAME2 (A19)
0240  2 FIRST-NAME
0250  2 REDEFINE FIRST-NAME
0260    3 FIRST-NAME1 (A1)
0270    3 FIRST-NAME2 (A19)
0280*
02901 #II (I2)
03001 #MSG1 (A72) INIT <'This example needs the EMPLOYEES file in the demo database'>
03101 #MSG2 (A24)
0320END-DEFINE
0330*
0340* Just to make sure in advance that the Employees file is there.
0350READ (1) EMPL BY PERSONNEL-ID
0360END-READ
0370*
0380PROCESS PAGE USING "CTRFIL-A"
0390*
0400DECIDE ON FIRST *PAGE-EVENT
0410  VALUE U'nat:page.end'
0420    IGNORE
0430*
0440  VALUE U'onInitialize'
0450    EXPAND ARRAY COLUMNS TO (1:5)
0460    RESET #II
0470    READ (5) EMPL BY PERSONNEL-ID
0480      ADD 1 TO #II
0490      COLUMNS.ID(#II) := EMPL.PERSONNEL-ID
0500      EXAMINE EMPL.NAME2 TRANSLATE INTO LOWER
0510      EXAMINE EMPL.FIRST-NAME2 TRANSLATE INTO LOWER
0520      COLUMNS.LAST(#II) := EMPL.NAME
0530      COLUMNS.FIRST(#II) := EMPL.FIRST-NAME
0540      COLUMNS.STATUS(#II) := 'VISIBLE'
0550    END-READ
0560*
0570    EXPAND ARRAY SIMPLE TO (1:10)
0580    RESET #II
0590    READ (10) EMPL BY PERSONNEL-ID
0600      ADD 1 TO #II
0610      EXAMINE EMPL.NAME2 TRANSLATE INTO LOWER
0620      SIMPLE(#II) := EMPL.NAME
0630    END-READ
0640    PROCESS PAGE UPDATE FULL
0650*
0660  VALUE U'onToggle'
0670    IF *OCC(COLUMNS.STATUS) GT 0
0680        AND COLUMNS.STATUS(1) EQ 'VISIBLE'
0690      COLUMNS.STATUS(1) := 'INVISIBLE'
0700    ELSE
0710      COLUMNS.STATUS(1) := 'VISIBLE'
0720    END-IF
0730    PROCESS PAGE UPDATE FULL
0740*
0750  NONE VALUE
0760    PROCESS PAGE UPDATE
0770END-DECIDE
0780*
0790ON ERROR
0800  IF *LEVEL > 1
0810    COMPRESS '(NAT' *ERROR-NR ').' TO #MSG2 LEAVING NO
0820    COMPRESS #MSG1 #MSG2 TO #MSG1
0830    STACK TOP DATA #MSG1
0840    RESET *ERROR-NR
0850    ESCAPE ROUTINE
0860  END-IF
0870END-ERROR
0880END
