0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* Name: Grid Demos/Tree /*<RO>
0060*
0070DEFINE DATA LOCAL
00801 INFOPAGENAME (U) DYNAMIC INIT <'html/ctrltree.html'>
00901 METHODCALLED (U) DYNAMIC
01001 NODEPOSITION (U) DYNAMIC
01101 SELECTEDVALUE (U) DYNAMIC
01201 TREEGRIDINFO (1:*)
0130  2 DRAGINFO (U) DYNAMIC
0140  2 DROPINFO (U) DYNAMIC
0150  2 FIELD (I4)
0160  2 LEVEL (I4)
0170  2 OPENED (I4) /* <== toggle state; 0: closed, 1: opened, 2: end node
0180  2 SELECTED (L)
0190  2 STR (L)
0200  2 TEXT (U) DYNAMIC
0210LOCAL
02201 #I1 (I1)
02301 #I2 (I1)
02401 #I3 (I1)
0250*
02601 #IK (I4)
02701 #LEVEL1 (I2) INIT <6>
02801 #LEVEL2 (I2) INIT <3>
02901 #LEVEL3 (I2) INIT <3>
0300END-DEFINE
0310*
0320/*( INIT
0330#IK := #LEVEL1 * ( (#LEVEL2 * #LEVEL3) + #LEVEL2 ) + #LEVEL1
0340EXPAND ARRAY TREEGRIDINFO TO (1:#IK)
0350*
0360#IK := 0
0370FOR #I1=1 TO #LEVEL1
0380  ADD 1 TO #IK
0390  COMPRESS 'Node' #I1 INTO TREEGRIDINFO.TEXT(#IK)
0400  TREEGRIDINFO.LEVEL(#IK)  := 1
0410  TREEGRIDINFO.OPENED(#IK) := 0 /* closed
0420  TREEGRIDINFO.FIELD(#IK)  := #IK
0430  FOR #I2=1 TO #LEVEL2
0440    ADD 1 TO #IK
0450    COMPRESS 'SubNode' #I1 '.' #I2 INTO TREEGRIDINFO.TEXT(#IK)
0460    TREEGRIDINFO.LEVEL(#IK)  := 2
0470    TREEGRIDINFO.OPENED(#IK) := 0 /* closed
0480    TREEGRIDINFO.FIELD(#IK)  := #IK
0490    FOR #I3=1 TO #LEVEL3
0500      ADD 1 TO #IK
0510      COMPRESS 'SubSubNode' #I1 '.' #I2 '.' #I3
0520        INTO TREEGRIDINFO.TEXT(#IK)
0530      TREEGRIDINFO.LEVEL(#IK)  := 3
0540      TREEGRIDINFO.OPENED(#IK) := 2 /* end node
0550      TREEGRIDINFO.FIELD(#IK)  := #IK
0560    END-FOR
0570  END-FOR
0580END-FOR
0590/*)
0600*
0610PROCESS PAGE USING "CTRTRE-A"
0620*
0630/*( DEFINE EVENT HANDLER
0640DECIDE ON FIRST *PAGE-EVENT
0650  VALUE U'nat:page.end'
0660    /* Page closed.
0670    IGNORE
0680  VALUE U'treeGridInfo.reactOnSelect'
0690    METHODCALLED := *PAGE-EVENT
0700    /*
0710    PERFORM GETNODENUMBER
0720    /*
0730    PROCESS PAGE UPDATE FULL
0740  VALUE U'treeGridInfo.reactOnToggle'
0750    METHODCALLED := *PAGE-EVENT
0760    /*
0770    PERFORM GETNODENUMBER
0780    /*
0790    PROCESS PAGE UPDATE  FULL
0800  NONE VALUE
0810    /* Unhandled events.
0820    PROCESS PAGE UPDATE
0830END-DECIDE
0840/*) END-HANDLER
0850
0860*
0870DEFINE SUBROUTINE GETNODENUMBER
0880*
0890COMPRESS ' ' INTO NODEPOSITION  LEAVING NO
0900COMPRESS ' ' INTO SELECTEDVALUE LEAVING NO
0910FOR #I1=1 TO *OCC(TREEGRIDINFO.SELECTED)
0920  IF TREEGRIDINFO.SELECTED(#I1) = TRUE
0930    COMPRESS #I1 INTO NODEPOSITION LEAVING NO
0940    SELECTEDVALUE := TREEGRIDINFO.TEXT(#I1)
0950    ESCAPE ROUTINE
0960  END-IF
0970END-FOR
0980*
0990END-SUBROUTINE
1000*
1010END
