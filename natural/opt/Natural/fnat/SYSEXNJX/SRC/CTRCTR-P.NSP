0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050* Name: Grid Demos/Client Tree/*<RO>
0060*
0070DEFINE DATA LOCAL
00801 CT (1:*)
0090  2 LEVEL (I4) /* starts with level 1
0100  2 OPENED (I4) /* toggle state; 0: closed, 1: opened, 2: end node
0110  2 SELECTED (L)
0120  2 TEXT (U) DYNAMIC
01301 INFOPAGENAME (U) DYNAMIC INIT <'html/ctrlclienttree.html'>
01401 METHODCALLED (U) DYNAMIC
01501 SELECTEDVALUE (U) DYNAMIC
0160LOCAL
01701 #I1 (I1)
01801 #I2 (I1)
01901 #IK (I1)
0200*
02101 #LEVEL1 (I1) INIT <4>
02201 #LEVEL2 (I1) INIT <3>
0230END-DEFINE
0240*
0250*/( INIT
0260#IK := #LEVEL1 * ( #LEVEL2 ) + #LEVEL1
0270EXPAND ARRAY CT TO (1:#IK)
0280*
0290CT.OPENED(*) := 2 /* all nodes first end noded
0300CT.LEVEL(*)  := 2 /* all nodes first part of level 2
0310*
0320#IK := 0
0330FOR #I1=1 TO #LEVEL1
0340  ADD 1 TO #IK
0350  COMPRESS 'node' #I1 INTO CT.TEXT(#IK)
0360  CT.OPENED(#IK):= 1
0370  CT.LEVEL(#IK) := 1
0380  FOR #I2=1 TO #LEVEL2
0390    ADD 1 TO #IK
0400    COMPRESS FULL 'subnode ' #I1 '.' #I2 INTO CT.TEXT(#IK) LEAVING NO
0410  END-FOR
0420END-FOR
0430/*)
0440*
0450PROCESS PAGE USING "CTRCTR-A"
0460*
0470/*( DEFINE EVENT HANDLER
0480DECIDE ON FIRST *PAGE-EVENT
0490  VALUE U'nat:page.end'
0500    /* Page closed.
0510    IGNORE
0520*  VALUE U'ct.reactOnContextMenuRequest'
0530*   /* TODO: Implement event code.
0540*   PROCESS PAGE UPDATE FULL
0550  VALUE U'ct.reactOnSelect'
0560    METHODCALLED := *PAGE-EVENT
0570    PERFORM GETSELECTED
0580    PROCESS PAGE UPDATE FULL
0590  VALUE U'ct.reactOnToggle'
0600    METHODCALLED  := *PAGE-EVENT
0610    PERFORM GETSELECTED
0620    PROCESS PAGE UPDATE FULL
0630  NONE VALUE
0640    /* Unhandled events.
0650    PROCESS PAGE UPDATE
0660END-DECIDE
0670/*) END-HANDLER
0680*
0690DEFINE SUBROUTINE GETSELECTED
0700COMPRESS ' ' INTO SELECTEDVALUE LEAVING NO
0710*
0720FOR #I1=1 TO *OCC(CT.TEXT)
0730  IF CT.SELECTED(#I1) = TRUE
0740    COMPRESS TEXT( #I1 ) INTO SELECTEDVALUE LEAVING NO
0750    ESCAPE ROUTINE
0760  END-IF
0770END-FOR
0780*
0790END-SUBROUTINE
0800END
