0010* >Natural Source Header 000000
0020* :Mode S
0030* :CP
0040* <Natural Source Header
0050DEFINE DATA LOCAL
00601 DECREMENTALLOWED (L)
00701 INFOPAGENAME (A) DYNAMIC INIT <'html/ctrltimer.html'>
00801 INTERVAL (I4)
00901 SERVERTIME (A) DYNAMIC
01001 XCITRIGGEREVENT (A) DYNAMIC
0110END-DEFINE
0120*
0130* Start with 2 seconds.
0140INTERVAL := 2000
0150MOVE EDITED *TIMX (EM=DD/MM/YYYY-HH:II:SS:T) TO SERVERTIME
0160DECREMENTALLOWED := TRUE
0170*
0180PROCESS PAGE USING "CTRTIM-A"
0190*
0200DECIDE ON FIRST *PAGE-EVENT
0210  VALUE U'nat:page.end'
0220* Before ending the program, make sure the timer is stopped.
0230* We do this by adding an additional roundtrip that stops the
0240* timer, then we end the program.
0250    IF INTERVAL GT 0
0260      RESET INTERVAL
0270      XCITRIGGEREVENT := 'nat:page.end'
0280      PROCESS PAGE UPDATE FULL
0290    END-IF
0300  VALUE U'nat:browser.end'
0310    IGNORE
0320  VALUE U'decrementTimer'
0330    SUBTRACT 500 FROM INTERVAL
0340* The interval should not be so small that the user
0350* has no chance to press the button anymore.
0360    IF INTERVAL LT 1000
0370      DECREMENTALLOWED := FALSE
0380    END-IF
0390    PROCESS PAGE UPDATE FULL
0400  VALUE U'incrementTimer'
0410    ADD 500 TO INTERVAL
0420    IF INTERVAL GE 1000
0430      DECREMENTALLOWED := TRUE
0440    END-IF
0450    PROCESS PAGE UPDATE FULL
0460  VALUE U'stopTimer'
0470    RESET INTERVAL
0480    DECREMENTALLOWED := FALSE
0490    PROCESS PAGE UPDATE FULL
0500  VALUE U'onTimer'
0510    MOVE EDITED *TIMX (EM=DD/MM/YYYY-HH:II:SS:T) TO SERVERTIME
0520    PROCESS PAGE UPDATE FULL
0530  NONE VALUE
0540    PROCESS PAGE UPDATE
0550END-DECIDE
0560*
0570END
