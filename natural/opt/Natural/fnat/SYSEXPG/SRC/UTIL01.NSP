0010** Example 'UTIL01': USE THE ACTIVEX COMPONENT 'SoftwareAG.NaturalX.Utilities'
0020******************************************************************************
0030DEFINE DATA
0040LOCAL
00501 #CLASS (A) DYNAMIC CONST <'SoftwareAG.NaturalX.Utilities.3'>
00601 #UTIL HANDLE OF OBJECT
00701 #STUDIO HANDLE OF OBJECT
00801 #OBJECTS HANDLE OF OBJECT
00901 #PROGS HANDLE OF OBJECT
01001 #PROG HANDLE OF OBJECT
01101 #PROGIND HANDLE OF OBJECT
01201 #CANCELED (L)
01301 #I (I4)
01401 #TEXT (A) DYNAMIC
01501 #MSGTEXT (A) DYNAMIC
01601 #MSGCAPTION (A) DYNAMIC
01701 #MSGRESULT (I4)
01801 #MSG (A72)
0190END-DEFINE
0200*
0210* Create an instance of the Utilities class.
0220CREATE OBJECT #UTIL OF #CLASS
0230IF #UTIL EQ NULL-HANDLE
0240  COMPRESS 'Object of class' #CLASS 'could not be created.' TO #MSG
0250  WRITE #MSG
0260  ESCAPE ROUTINE
0270END-IF
0280*
0290* Retrieve the Natural Studio Automation Interface.
0300SEND 'GetThisNaturalStudio' TO #UTIL RETURN #STUDIO
0310IF #STUDIO EQ NULL-HANDLE
0320  #MSG := 'Please execute this example program in a Natural Studio session.'
0330  WRITE #MSG
0340  ESCAPE ROUTINE
0350END-IF
0360*
0370* Use the Automation Interface.
0380*
0390* Example 1: Show a Message Box.
0400#MSGTEXT := 'Hello, world !'
0410#MSGCAPTION := 'Natural Studio Automation Interface'
0420SEND 'MessageBox' TO #STUDIO
0430WITH #MSGTEXT #MSGCAPTION 33
0440*
0450* Example 2: Open a Program Editor.
0460#OBJECTS := #STUDIO.OBJECTS
0470#PROGS := #OBJECTS.PROGRAMS
0480SEND 'Open' TO #PROGS
0490WITH 1009 'UTIL01' 'SYSEXPG'
0500RETURN #PROG
0510SEND 'Open' TO #PROGS
0520WITH 1009 'UTIL02' 'SYSEXPG'
0530RETURN #PROG
0540SEND 'Open' TO #PROGS
0550WITH 1009 'UTIL03' 'SYSEXPG'
0560RETURN #PROG
0570*
0580* Example 3: Run a Progress Indicator.
0590SEND 'ProgressIndicator' TO #STUDIO
0600WITH 3000 1 100
0610RETURN #PROGIND
0620*
0630SEND 'Start' TO #PROGIND
0640FOR #I := 1 TO 3000
0650  IF #I EQ ( #I / 100 * 100 )
0660    COMPRESS #I 'items processed.' TO #TEXT
0670    #PROGIND.STATUSBARTEXT := #TEXT
0680  END-IF
0690  #PROGIND.GRADIENTBARTEXT := #I
0700  SEND 'Step' TO #PROGIND WITH 1
0710  #CANCELED := #PROGIND.CANCELED
0720  IF #CANCELED
0730    SEND 'Terminate' TO #PROGIND
0740    ESCAPE BOTTOM
0750  END-IF
0760END-FOR
0770IF NOT #CANCELED
0780  SEND 'Terminate' TO #PROGIND
0790END-IF
0800*
0810END
