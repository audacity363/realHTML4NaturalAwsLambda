0010** Example 'ATBREX05': AT BREAK OF (with system functions)
0020************************************************************************
0030DEFINE DATA LOCAL
00401 MYVIEW VIEW OF EMPLOYEES
0050  2 NAME
0060  2 CITY
0070  2 SALARY    (1:1)
0080  2 CURR-CODE (1:1)
0090END-DEFINE
0100*
0110LIMIT 3
0120READ MYVIEW BY CITY = 'SALT LAKE CITY'
0130  DISPLAY NOTITLE CITY NAME 'SALARY' SALARY(1) 'CURRENCY' CURR-CODE(1)
0140  /*
0150  AT BREAK OF CITY
0160    WRITE /  OLD(CITY) (EM=X^X^X^X^X^X^X^X^X^X^X^X^X^X^X)
0170      31T ' - MINIMUM:' MIN(SALARY(1))  CURR-CODE(1) /
0180      31T ' - AVERAGE:' AVER(SALARY(1)) CURR-CODE(1) /
0190      31T ' - MAXIMUM:' MAX(SALARY(1))  CURR-CODE(1) /
0200      31T ' -     SUM:' SUM(SALARY(1))  CURR-CODE(1) /
0210      33T COUNT(SALARY(1)) 'RECORDS FOUND' /
0220  END-BREAK
0230  /*
0240  AT END OF DATA
0250    WRITE 22T 'TOTAL (ALL RECORDS):'
0260               T*SALARY TOTAL(SALARY(1)) CURR-CODE(1)
0270  END-ENDDATA
0280END-READ
0290END
