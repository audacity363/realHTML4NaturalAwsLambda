0010**********************************************************************
0020*
0030* FILE   : XARR.NSP
0040* PURPOSE: SAMPLE PROGRAM CALLING USER EXITS (XARR)
0050*
0060* (C) COPYRIGHT 1991-2004 BY SOFTWARE AG
0070*
0080**********************************************************************
0090*
0100DEFINE DATA
0110LOCAL
01201 #ARRAY-OF-DYNAMIC(A/1:6) DYNAMIC
01301 #X-ARRAY(I4/1:*)
01401 #X-ARRAY-OF-DYNAMIC(A/1:*) DYNAMIC
01501 #RES(I4)
01601 #I(I4)
0170END-DEFINE
0180*
0190FOR #I := 1 TO 6
0200  COMPRESS 'INPUT' #I TO #ARRAY-OF-DYNAMIC(#I)
0210END-FOR
0220*
0230RESIZE ARRAY #X-ARRAY TO (1:3)
0240FOR #I := 1 TO 3
0250  MOVE #I TO #X-ARRAY(#I)
0260END-FOR
0270*
0280RESIZE ARRAY #X-ARRAY-OF-DYNAMIC TO (1:3)
0290FOR #I := 1 TO 3
0300  COMPRESS 'INPUT' #I TO #X-ARRAY-OF-DYNAMIC(#I)
0310END-FOR
0320*
0330WRITE 'BEFORE CALL TO XARR'
0340FOR #I := 1 TO 6
0350  WRITE '=' #ARRAY-OF-DYNAMIC(#I) (AL=32)
0360END-FOR
0370FOR #I := 1 TO 3
0380  WRITE '=' #X-ARRAY(#I)
0390END-FOR
0400FOR #I := 1 TO 3
0410  WRITE '=' #X-ARRAY-OF-DYNAMIC(#I) (AL=32)
0420END-FOR
0430*
0440CALL INTERFACE4 'XARR'
0450  #ARRAY-OF-DYNAMIC(*) #X-ARRAY(*) #X-ARRAY-OF-DYNAMIC(*)
0460COMPUTE #RES = RET('XARR')
0470*
0480* CALLNAT 'XARRSUB'
0490*   #ARRAY-OF-DYNAMIC(*) #X-ARRAY(*) #X-ARRAY-OF-DYNAMIC(*)
0500*
0510NEWPAGE
0520WRITE 'AFTER CALL TO XARR'
0530IF #RES NE 0
0540  WRITE 'ERROR ' #RES ' OCCURRED DURING CALL OF XARR'
0550ELSE
0560* The array of dynamic alphas has been filled
0570* with new strings of different length.
0580  FOR #I := 1 TO 6
0590    WRITE '=' #ARRAY-OF-DYNAMIC(#I) (AL=32)
0600  END-FOR
0610* The x-array has been resized and the new
0620* occurrences have been filled.
0630  FOR #I := 1 TO 6
0640    WRITE '=' #X-ARRAY(#I)
0650  END-FOR
0660* The x-array of dynamic alphas has been resized
0670* and the new occurrences have been filled.
0680  FOR #I := 1 TO 6
0690    WRITE '=' #X-ARRAY-OF-DYNAMIC(#I) (AL=32)
0700  END-FOR
0710END-IF
0720*
0730END
