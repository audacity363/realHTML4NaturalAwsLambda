0010*
0020* Program .. V3-YIS
0030* Library .. SYSEXV
0040* Version .. 3.1
0050*
0060* Functionality: For Year 2000, the functions IS(D) and VAL
0070*
0080* Note: Set profile parameter DTFORM=I for this example.
0090************************************************************************
0100*
0110DEFINE DATA
0120LOCAL
01301 #ALPHA1  (A08)
01401 #ALPHA2  (A10)
01501 #DATE1   (D)
01601 #DATE2   (D)
01701 #TEXT1   (A50)
01801 #TEXT2   (A50)
0190END-DEFINE
0200*
0210FORMAT KD=ON
0220SET KEY PF3  NAMED 'Exit'
0230  PF12 NAMED 'Canc'
0240*
0250#ALPHA1 := '97-12-20'
0260#ALPHA2 := '1997-12-20'
0270*
0280REPEAT
0290*
0300  RESET #DATE1 #TEXT1
0310    #DATE2 #TEXT2
0320*
0330  IF #ALPHA1 IS(D) THEN
0340    #DATE1 := VAL(#ALPHA1)
0350  ELSE
0360    COMPRESS #ALPHA1 'is not a valid date format.' TO #TEXT1
0370  END-IF
0380*
0390*
0400  IF #ALPHA2 IS(D) THEN
0410    #DATE2 := VAL(#ALPHA2)
0420  ELSE
0430    COMPRESS #ALPHA2 'is not a valid date format.' TO #TEXT2
0440  END-IF
0450*
0460*
0470  INPUT (IP=OFF)
0480    01T *TIMX  (AD=OTD  )
0490    21T '***** NATURAL 3.1 ENHANCEMENTS *****'
0500    70T *DATX  (AD=OTD DF=L )
0510    /
0520    01T 'User'
0530    06T *USER  (AD=OILT )
0540    28T '- Year 2000 Features -'(I)
0550    64T 'Program'
0560    72T *PROGRAM (AD=OI)
0570    //
0580    02T 'IS(D) option and VAL function'(I)
0590    //
0600    02T 'IF #ALPHA'  'IS(D)'(I)
0610    'THEN  #DATE :=' 'VAL'(I) '(#ALPHA)'
0620    /
0630    02T '                ELSE  WRITE ''Error text'' '
0640    //
0650    02T 'Check value of #ALPHA'  '(A8)'(I)  'for date value:'
0660    #ALPHA1 (AD=MIT CD=NE)
0670    /
0680    02T '(valid is YY-MM-DD or YYYYMMDD)'
0690    /
0700    02T 'Value of #DATE:'
0710    #DATE1 (AD=OI  EM=YYYY-MM-DD)
0720    /
0730    18T #TEXT1 (AD=OI)
0740    //
0750    02T 'Check value of #ALPHA'  '(A10)' (I) 'for date value:'
0760    #ALPHA2 (AD=MIT CD=NE)
0770    /
0780    02T '(valid is YY-MM-DD or YYYYMMDD or YYYY-MM-DD)'
0790    /
0800    02T 'Value of #DATE:'
0810    #DATE2 (AD=OI  EM=YYYY-MM-DD)
0820    /
0830    18T #TEXT2 (AD=OI)
0840    /
0850    /
0860    02T 'Note: If no century is specified, either the current'
0870    'century is used'
0880    /
0890    02T '      or the YSLW parameter determines the century.'
0900*
0910  IF *PF-KEY = 'PF3'
0920    ESCAPE BOTTOM
0930  END-IF
0940  IF (*PF-KEY = 'PF12' OR= 'CLR')
0950    CALLNAT 'UTILSTOP'
0960  END-IF
0970*
0980END-REPEAT
0990*
1000END
