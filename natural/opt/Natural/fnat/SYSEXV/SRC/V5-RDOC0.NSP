0005*
0010* Program .. V5-RDOC0
0015* Library .. SYSEXV
0020* Version .. 5.1
0025*
0030* Functionality: Show REQUEST DOCUMENT statement
0035************************************************************************
0040*
0045DEFINE DATA
0050LOCAL
00551 #IN-URL              (A78)
00601 #OUTSRC-ARRAY        (A74/1:5)
00651 #OUTHDR-ARRAY        (A74/1:5)
00701 #WINDOW-TITLE        (A40)
00751 #WIN-TYPE            (A1)
00801 #WINDOW-LINES        (A74/1:10)
00851 #WINDOW-LINK         (A78/1:10)
00901 #CMD                 (A1/1:10)
00951 #CMD-CV              (C/1:10)
0100
01051 #WINDOW-INDEX        (I4)
01101 IY                   (I4)
01151 #INDEX               (I4)
0120
01251 #URL                 (A) DYNAMIC
01301 #URL-START           (A) DYNAMIC       /* 'http' or 'https'
01351 #URL-FULL            (L)               /* start with http or https
01401 #USER                (A253)
01451 #PASSWORD            (A253)
01501 #HEADER_STATUS       (A253)
01551 #HEADER_CONTENT_TYPE (A253)
01601 #HEADER_LOCATION     (A253)
01651 #HEADER_AUTHENTICATE (A) DYNAMIC
01701 #RETURN_HEADER       (A) DYNAMIC
01751 #RETURN_PAGE         (A) DYNAMIC
01801 #RESPONSE            (I4)
01851 #ERROR-NUM           (I4)
01901 #ANSWER              (A1)
01951 #TEXT-REALM          (A77)
0200
02051 #OUTSRC              (A) DYNAMIC
02101 #OUTSRC-UPPER        (A) DYNAMIC
02151 #LINE-OUT            (A80)
02201 REDEFINE #LINE-OUT
0225  2 #LINE-OUT1         (A1/1:80)
02301 OBJLEN               (I4)
02351 OBJEND               (I4)
02401 OBJSTART             (I4)
02451 RECORD_END           (L)
02501 RECORD_NEW           (L)
02551 RECORD_LINK          (A1)
0260
02651 ATTSTART             (I4)
02701 ATTEND               (I4)
02751 NEXTSTART            (I4)
0280
02851 #HEADERS             (A) DYNAMIC
02901 #SOURCES             (A) DYNAMIC
02951 #LINKS               (A) DYNAMIC
03001 #LINKS-UPPER         (A) DYNAMIC
03051 GETATTRIBUTE         (A) DYNAMIC
03101 GETATTRIBUTE-UPPER   (A) DYNAMIC
03151 GETENTITY            (A) DYNAMIC
03201 GETMETA              (A) DYNAMIC
03251 GETMETA-UPPER        (A) DYNAMIC
0330
03351 #QUOTE               (A1) INIT <H'27'>
03401 #DQUOTE              (A1) INIT <H'22'>
03451 II                   (I4)
0350
03551 GETHREF              (A78)
03601 #BUTTON              (A1)
03651 #TEXT-RELOC          (A77)
0370
03751 NEXT-ATTSTART        (I4)
03801 OLDSTART             (I4)
03851 ATTLEN               (I4)
03901 ENDCHAR              (A1)
0395
0400END-DEFINE
0405*
0410FORMAT KD=ON
0415SET CONTROL 'MB'
0420SET KEY PF3  NAMED 'Exit'
0425  PF5  NAMED 'Load'
0430  PF12 NAMED 'Canc'
0435*
0440DEFINE WINDOW NORMAL-WINDOW
0445  SIZE 13 * 79 BASE 04 / 01
0450  TITLE #WINDOW-TITLE
0455  CONTROL SCREEN
0460  FRAMED POSITION OFF
0465DEFINE WINDOW SMALL-WINDOW
0470  SIZE 10 * 79 BASE 04 / 01
0475  TITLE #WINDOW-TITLE
0480  CONTROL SCREEN
0485  FRAMED POSITION OFF
0490*
0495#IN-URL := 'http://empower.softwareag.com'  /* Start URL
0500* #IN-URL := 'http://www.softwareag.com'  /* Start URL
0505*
0510REPEAT
0515*
0520  SET KEY PF5 NAMED 'Load'
0525*
0530  INPUT (IP=OFF )
0535    01T *TIMX  (AD=OTD)
0540    21T '***** NATURAL 5.1 ENHANCEMENTS *****'
0545    70T *DATX  (AD=OTD DF=L)
0550    /
0555    01T 'User'
0560    06T *USER  (AD=OILT)
0565    22T '- New Statement REQUEST DOCUMENT -'(I)
0570    64T 'Program'
0575    72T *PROGRAM (AD=OI)
0580    ///
0585    02T 'Url:' (I)
0590    /
0595    02T #IN-URL (AD=MI'_')
0600    ///
0605    02T 'Header:' (I)
0610    /
0615    02T #OUTHDR-ARRAY(1) (AD=OD)
0620    /
0625    02T #OUTHDR-ARRAY(2) (AD=OD)
0630    /
0635    02T #OUTHDR-ARRAY(3) (AD=OD)
0640    /
0645    02T #OUTHDR-ARRAY(4) (AD=OD)
0650    /
0655    02T #OUTHDR-ARRAY(5) (AD=OD)
0660    //
0665    02T 'Source:' (I)
0670    /
0675    02T #OUTSRC-ARRAY(1) (AD=OD)
0680    /
0685    02T #OUTSRC-ARRAY(2) (AD=OD)
0690    /
0695    02T #OUTSRC-ARRAY(3) (AD=OD)
0700    /
0705    02T #OUTSRC-ARRAY(4) (AD=OD)
0710    /
0715    02T #OUTSRC-ARRAY(5) (AD=OD)
0720    /
0725
0730*
0735  IF *PF-KEY = 'PF3'
0740    ESCAPE ROUTINE
0745  END-IF
0750  IF (*PF-KEY = 'PF12' OR= 'CLR')
0755    CALLNAT 'UTILSTOP'
0760  END-IF
0765
0770  IF *PF-KEY = 'PF5' OR= 'ENTR' /* Load html page
0775    PERFORM LOAD_PAGE
0780    SET KEY PF5 NAMED 'Load'
0785    SET KEY PF7 NAMED 'Headr'
0790    SET KEY PF8 NAMED 'Src'
0795    SET KEY PF9 NAMED 'Links'
0800    ESCAPE TOP
0805  END-IF
0810
0815  IF *PF-KEY = 'PF7'  /* Show Header records
0820    /*
0825    #OUTSRC := #HEADERS
0830    OBJSTART := 1    /* start of header records
0835    #WIN-TYPE := 'H'
0840    /*
0845    PERFORM SHOW-WINDOW
0850  END-IF
0855  IF *PF-KEY = 'PF8'  /* Show Source records
0860    /*
0865    #OUTSRC := #SOURCES
0870    OBJSTART := 1    /* start of source records
0875    #WIN-TYPE := 'S'
0880    /*
0885    PERFORM SHOW-WINDOW
0890  END-IF
0895  IF *PF-KEY = 'PF9'  /* Show Links
0900    /*
0905    #OUTSRC := #LINKS
0910    #OUTSRC-UPPER := #LINKS-UPPER
0915    NEXTSTART := 1    /* start of link records
0920    #WIN-TYPE := 'L'
0925    /*
0930    PERFORM SHOW-WINDOW2
0935  END-IF
0940  /*
0945END-REPEAT
0950*
0955* *********************************************************************
0960*
0965*
0970*
0975DEFINE SUBROUTINE SHOW-WINDOW
0980/*
0985SET KEY PF5 NAMED ' '
0990SET KEY PF7 NAMED '--'
0995SET KEY PF8 NAMED ' +'
1000SET KEY PF9 NAMED ' '
1005*
1010
1015RECORD_END := FALSE
1020RECORD_NEW := TRUE
1025
1030/*
1035REPEAT
1040  /*
1045  IF #WIN-TYPE = 'H'                  /* Show header records
1050    IF RECORD_NEW
1055      RESET #WINDOW-LINES(*)
1060        #WINDOW-LINK(*)
1065      PERFORM FILL_HEADER/SOURCE
1070    END-IF
1075    #WINDOW-TITLE := ' Show header records'
1080  END-IF
1085  IF #WIN-TYPE = 'S'                  /* Show source records
1090    IF RECORD_NEW
1095      RESET #WINDOW-LINES(*)
1100        #WINDOW-LINK(*)
1105      PERFORM FILL_HEADER/SOURCE
1110    END-IF
1115    #WINDOW-TITLE := ' Show source records'
1120  END-IF
1125  /*
1130  INPUT WINDOW = 'NORMAL-WINDOW' (IP=OFF)
1135    / 02T #WINDOW-LINES(1) (AD=OD)
1140    / 02T #WINDOW-LINES(2) (AD=OD)
1145    / 02T #WINDOW-LINES(3) (AD=OD)
1150    / 02T #WINDOW-LINES(4) (AD=OD)
1155    / 02T #WINDOW-LINES(5) (AD=OD)
1160    / 02T #WINDOW-LINES(6) (AD=OD)
1165    / 02T #WINDOW-LINES(7) (AD=OD)
1170    / 02T #WINDOW-LINES(8) (AD=OD)
1175    / 02T #WINDOW-LINES(9) (AD=OD)
1180    / 02T #WINDOW-LINES(10) (AD=OD)
1185  /*
1190  IF *PF-KEY = 'PF3' OR= 'PF12'
1195    SET KEY PF7 NAMED 'Headr'
1200    SET KEY PF8 NAMED 'Src'
1205    SET KEY PF9 NAMED 'Links'
1210    ESCAPE ROUTINE
1215  END-IF
1220  /*
1225  /*
1230  RESET RECORD_NEW
1235  /*
1240  IF *PF-KEY = 'PF7'   /* '--'
1245    RECORD_NEW := TRUE
1250    RECORD_END := FALSE
1255    OBJSTART := 1          /* start of header source
1260  END-IF
1265  IF *PF-KEY = 'PF8'   /* '+'
1270    IF #WINDOW-INDEX = 11 OR NOT RECORD_END
1275      OBJSTART := OLDSTART /* go forward
1280      RECORD_NEW := TRUE
1285    END-IF
1290  END-IF
1295
1300END-REPEAT
1305/*
1310END-SUBROUTINE
1315*
1320DEFINE SUBROUTINE SHOW-WINDOW2
1325/*
1330SET KEY PF5 NAMED ' '
1335SET KEY PF7 NAMED '--'
1340SET KEY PF8 NAMED ' +'
1345SET KEY PF9 NAMED ' '
1350/*
1355RECORD_NEW := TRUE
1360RECORD_LINK := 'A'
1365/*
1370REPEAT
1375  /*
1380  IF RECORD_NEW
1385    RESET #CMD(*)
1390    MOVE (AD=PN) TO #CMD-CV(*)
1395    RESET #WINDOW-LINES(*)
1400      #WINDOW-LINK(*)
1405    /*
1410    /* If page contains an auto-refresh like
1415    /* <meta http-equiv="Refresh" content="0;URL=/corp/default.htm ">,
1420    /* add URL to HTML link list.
1425    /* And find all anchors and add URLs to HTML link list
1430    /*
1435    PERFORM FILL_LINKS
1440    /*
1445  END-IF
1450  #WINDOW-TITLE := ' Show link records and select link'
1455  /*
1460  INPUT WINDOW = 'NORMAL-WINDOW' (IP=OFF)
1465    / 02T #CMD(1) (AD=MY'_' CV=#CMD-CV(1))
1470    #WINDOW-LINES(1) (AD=OD)
1475    / 02T #CMD(2) (AD=MY'_' CV=#CMD-CV(2))
1480    #WINDOW-LINES(2) (AD=OD)
1485    / 02T #CMD(3) (AD=MY'_' CV=#CMD-CV(3))
1490    #WINDOW-LINES(3) (AD=OD)
1495    / 02T #CMD(4) (AD=MY'_' CV=#CMD-CV(4))
1500    #WINDOW-LINES(4) (AD=OD)
1505    / 02T #CMD(5) (AD=MY'_' CV=#CMD-CV(5))
1510    #WINDOW-LINES(5) (AD=OD)
1515    / 02T #CMD(6) (AD=MY'_' CV=#CMD-CV(6))
1520    #WINDOW-LINES(6) (AD=OD)
1525    / 02T #CMD(7) (AD=MY'_' CV=#CMD-CV(7))
1530    #WINDOW-LINES(7) (AD=OD)
1535    / 02T #CMD(8) (AD=MY'_' CV=#CMD-CV(8))
1540    #WINDOW-LINES(8) (AD=OD)
1545    / 02T #CMD(9) (AD=MY'_' CV=#CMD-CV(9))
1550    #WINDOW-LINES(9) (AD=OD)
1555    / 02T #CMD(10)(AD=MY'_' CV=#CMD-CV(10))
1560    #WINDOW-LINES(10) (AD=OD)
1565  /*
1570  IF *PF-KEY = 'PF3' OR= 'PF12'
1575    SET KEY PF7 NAMED 'Headr'
1580    SET KEY PF8 NAMED 'Src'
1585    SET KEY PF9 NAMED 'Links'
1590    ESCAPE ROUTINE
1595  END-IF
1600  /*
1605  IF (#CMD(*) NE ' ')        /* Link selected
1610    FOR IY 1 TO 10
1615      IF #CMD (IY) NE ' '
1620        RESET #OUTSRC-ARRAY(*) #OUTHDR-ARRAY (*)
1625        /*
1630        PERFORM GET_NEW_URL /* read url and show header/source records
1635        /*
1640        SET KEY PF7 NAMED 'Headr'
1645        SET KEY PF8 NAMED 'Src'
1650        SET KEY PF9 NAMED 'Links'
1655        /*
1660        ESCAPE ROUTINE
1665      END-IF
1670    END-FOR
1675  END-IF
1680  /*
1685  RESET RECORD_NEW
1690  /*
1695  IF *PF-KEY = 'PF7'   /* '--'
1700    RECORD_NEW := TRUE
1705    NEXTSTART := 1          /* start of links
1710    RECORD_LINK := 'A'
1715  END-IF
1720  IF *PF-KEY = 'PF8'   /* '+'
1725    IF #WINDOW-INDEX = 11
1730      NEXTSTART := OLDSTART /* go forward
1735      RECORD_NEW := TRUE
1740    END-IF
1745  END-IF
1750
1755END-REPEAT
1760/*
1765END-SUBROUTINE
1770*
1775DEFINE SUBROUTINE LOAD_PAGE
1780/* Read internet page by given URL
1785/*
1790#URL := #IN-URL
1795PERFORM HTTP_REQUEST
1800/*
1805RESET #HEADERS #SOURCES #LINKS #LINKS-UPPER
1810/*
1815/* Fill in header of received page
1820/*
1825#OUTSRC := #RETURN_HEADER
1830/* Change to Unix file format
1835EXAMINE #OUTSRC FOR H'0D' DELETE
1840#HEADERS := #OUTSRC
1845OBJSTART := 1  /* Start of Header Source
1850/*
1855PERFORM FILL_HEADER/SOURCE
1860#OUTHDR-ARRAY(1:5) := #WINDOW-LINES(1:5)
1865/*
1870/* User is not authorized or wrong Url found
1875IF #RESPONSE = 401 OR #ERROR-NUM = 8301
1880  ESCAPE ROUTINE
1885END-IF
1890/*
1895/* Fill in the received page
1900/*
1905#OUTSRC := #RETURN_PAGE
1910/* Change to Unix file format
1915EXAMINE #OUTSRC FOR H'0D' DELETE
1920#SOURCES := #OUTSRC
1925/*
1930OBJSTART := 1
1935PERFORM FILL_HEADER/SOURCE
1940#OUTSRC-ARRAY(1:5) := #WINDOW-LINES(1:5)
1945/*
1950/* Find all HTML links to other pages ( <A HREF=' '> </A> )
1955/* First remove all comments (not relevant for anchors)
1960/*
1965PERFORM REMOVE_COMMENT
1970/*
1975/* Translate page ( HTML tags may be m#Window-indexed case)
1980/*
1985#OUTSRC-UPPER := #OUTSRC
1990EXAMINE #OUTSRC-UPPER TRANSLATE INTO UPPER
1995/*
2000#LINKS := #OUTSRC                     /* prepared link records
2005#LINKS-UPPER := #OUTSRC-UPPER
2010/*
2015/***********************************************************************
2020END-SUBROUTINE
2025*
2030DEFINE SUBROUTINE HTTP_REQUEST
2035SET KEY PF5 NAMED ' '
2040SET KEY PF7 NAMED ' '
2045SET KEY PF8 NAMED ' '
2050SET KEY PF9 NAMED ' '
2055/*
2060/* Search for URL
2065/*
2070RESET #RETURN_HEADER #RETURN_PAGE
2075REPEAT
2080  /*
2085  RESET #RESPONSE #ERROR-NUM
2090  /*
2095  REQUEST DOCUMENT FROM #URL
2100    WITH USER #USER
2105      PASSWORD #PASSWORD
2110      HEADER NAME 'Accept'      VALUE   'text/*'
2115        NAME 'User-Agent'       VALUE   'Natural'
2120    RETURN HEADER ALL  #RETURN_HEADER
2125        NAME ' '                VALUE #HEADER_STATUS
2130        NAME 'Content-Type'     VALUE #HEADER_CONTENT_TYPE
2135        NAME 'Location'         VALUE #HEADER_LOCATION
2140        NAME 'WWW-authenticate' VALUE #HEADER_AUTHENTICATE
2145      PAGE #RETURN_PAGE
2150  RESPONSE #RESPONSE
2155    GIVING #ERROR-NUM
2160  /*
2165  IF #ERROR-NUM GT 0             /* Natural error occurred
2170    IF #ERROR-NUM = 8301        /* Wrong Url was entered
2175      MOVE 'NAT8301 Wrong URL syntax.' TO #RETURN_HEADER
2180    ELSE
2185      COMPRESS 'Natural error NAT' #ERROR-NUM ' occurred.'
2190        INTO #RETURN_HEADER LEAVING NO
2195    END-IF
2200    ESCAPE ROUTINE
2205  END-IF
2210  IF #RESPONSE = 401    /* User is not authorized
2215    PERFORM GET_USERID_PASSWORD
2220    IF #ANSWER = 'Y'    /* try again with userID and password
2225      IGNORE
2230    ELSE
2235      ESCAPE ROUTINE
2240    END-IF
2245  ELSE
2250    ESCAPE BOTTOM
2255  END-IF
2260END-REPEAT
2265/*
2270/*
2275REPEAT WHILE (#RESPONSE = 301 /* Object permanently moved
2280    OR #RESPONSE = 302 /* Object temporarily moved
2285    OR #RESPONSE = 303)/* Redirection w/o new access method
2290    AND #HEADER_LOCATION NE ' '  /* no location header found
2295
2300  /* Reload internet page with new URL
2305  /*
2310  /* fully-qualified URL
2315  IF SUBSTRING(#HEADER_LOCATION,1,7) = 'http://'
2320      OR SUBSTRING(#HEADER_LOCATION,1,8) = 'https://'
2325    #URL := #HEADER_LOCATION
2330  ELSE
2335    /* relative URL
2340    DECIDE FOR FIRST CONDITION
2345      WHEN SUBSTRING(#URL,1,7) = 'http://'
2350        #URL-START := 'http://'
2355        #URL := SUBSTRING(#URL,8)
2360      WHEN SUBSTRING(#URL,1,8) = 'https://'
2365        #URL-START := 'https://'
2370        #URL := SUBSTRING(#URL,9)
2375      WHEN ANY
2380        RESET #URL-FULL #URL-START
2385      WHEN NONE
2390        IGNORE
2395    END-DECIDE
2400    /* relative URL to root of last page
2405    IF SUBSTRING(#HEADER_LOCATION,1,1) = '/'
2410      IF #URL-FULL
2415        EXAMINE #URL FOR '/' GIVING POSITION II
2420        IF II  > 0
2425          ADD -1 TO II
2430          #URL := SUBSTRING(#URL,1,II )
2435        END-IF
2440        COMPRESS #URL-START #URL #HEADER_LOCATION INTO #URL LEAVING NO
2445      END-IF
2450    ELSE
2455      /* relative URL to last page
2460      FOR II  = *LENGTH(#URL) TO 1 STEP -1
2465        IF SUBSTRING(#URL,II ,1) = '/'
2470          ESCAPE BOTTOM
2475        END-IF
2480      END-FOR
2485      /*
2490      IF II  > 1
2495        #URL := SUBSTRING(#URL,1,II )
2500      ELSE
2505        COMPRESS #URL '/' INTO #URL LEAVING NO
2510      END-IF
2515      COMPRESS #URL-START #URL #HEADER_LOCATION INTO #URL LEAVING NO
2520    END-IF
2525  END-IF
2530  /*
2535  /* Show user the new URL
2540  /*
2545  MOVE #URL TO #TEXT-RELOC
2550  #BUTTON := 'Y'
2555  #WINDOW-TITLE := ' V5-RDOC0  - Message'
2560  /*
2565  INPUT WINDOW = 'SMALL-WINDOW' (IP=OFF)
2570    /  02T 'Follow relocation to:'
2575    /  02T #TEXT-RELOC (AD=OI)
2580    // 02T 'Do you want to follow?'
2585    #BUTTON (AD=MIT) '(Y/N)'
2590  /*
2595  IF *PF-KEY = 'PF3'
2600    ESCAPE BOTTOM
2605  END-IF
2610  IF (*PF-KEY = 'PF12' OR = 'CLR')
2615    CALLNAT 'UTILSTOP'
2620  END-IF
2625  /*
2630  IF NOT (#BUTTON = 'Y' OR= 'N')
2635    REINPUT 'Enter ''Y'' or ''N''.'
2640  END-IF
2645  IF #BUTTON = 'N'
2650    ESCAPE BOTTOM
2655  END-IF
2660  /*
2665  /*
2670  #IN-URL := #URL
2675  /*
2680  /* Read new URL (relocation)
2685  /*
2690  RESET #RESPONSE #ERROR-NUM
2695  /*
2700  REQUEST DOCUMENT FROM #URL
2705    WITH USER #USER
2710      PASSWORD #PASSWORD
2715      HEADER NAME 'Accept'      VALUE   'text/*'
2720        NAME 'User-Agent'       VALUE   'Natural'
2725    RETURN HEADER ALL  #RETURN_HEADER
2730        NAME ' '                VALUE #HEADER_STATUS
2735        NAME 'Content-Type'     VALUE #HEADER_CONTENT_TYPE
2740        NAME 'Location'         VALUE #HEADER_LOCATION
2745        NAME 'WWW-authenticate' VALUE #HEADER_AUTHENTICATE
2750      PAGE #RETURN_PAGE
2755  RESPONSE #RESPONSE
2760    GIVING #ERROR-NUM
2765  /*
2770  IF #ERROR-NUM GT 0             /* Natural error occurred
2775    IF #ERROR-NUM = 8301        /* Wrong Url was entered
2780      MOVE 'NAT8301 Wrong URL syntax.' TO #RETURN_HEADER
2785    ELSE
2790      COMPRESS 'Natural error NAT' #ERROR-NUM ' occurred.'
2795        INTO #RETURN_HEADER LEAVING NO
2800    END-IF
2805    ESCAPE ROUTINE
2810  END-IF
2815  IF #RESPONSE = 401    /* User is not authorized
2820    PERFORM GET_USERID_PASSWORD
2825    IF #ANSWER = 'Y'    /* try again with userID and password
2830      ESCAPE TOP
2835    ELSE
2840      ESCAPE ROUTINE
2845    END-IF
2850  END-IF
2855END-REPEAT
2860END-SUBROUTINE
2865*
2870DEFINE SUBROUTINE GET_USERID_PASSWORD
2875
2880#ANSWER := 'N'
2885RESET #USER #PASSWORD
2890#TEXT-REALM := #HEADER_AUTHENTICATE
2895#WINDOW-TITLE := ' V5-RDOC0'
2900/*
2905INPUT WINDOW = 'SMALL-WINDOW' (IP=OFF)
2910  /   02T 'User is not authorized to open this internet page.'
2915  /   02T #TEXT-REALM (AD=OI)
2920  //  02T 'Please enter your user ID and password.'
2925  //  02T 'User ID ...' #USER (AD=MI'_' AL=30)
2930  /   02T 'Password ..' #PASSWORD (AD=MN AL=30)
2935/*
2940IF *PF-KEY = 'PF3'
2945  #ANSWER := 'N'
2950  ESCAPE ROUTINE
2955END-IF
2960IF (*PF-KEY = 'PF12' OR = 'CLR')
2965  CALLNAT 'UTILSTOP'
2970END-IF
2975IF #USER = ' '
2980  REINPUT 'Please enter your user ID.' MARK 1
2985END-IF
2990IF #PASSWORD = ' '
2995  REINPUT 'Please enter your password.' MARK 2
3000END-IF
3005#ANSWER := 'Y'
3010END-SUBROUTINE
3015*
3020DEFINE SUBROUTINE FILL_HEADER/SOURCE
3025/*
3030/* Separate header into single lines
3035/* and add to header array
3040/*
3045RESET #WINDOW-INDEX
3050/*
3055/*
3060RESET OLDSTART
3065IF *LENGTH(#OUTSRC) = 0
3070  ESCAPE ROUTINE
3075END-IF
3080RESET #WINDOW-LINES(*)
3085EXAMINE SUBSTRING(#OUTSRC,OBJSTART) FOR H'0A' GIVING POSITION OBJEND
3090/*
3095REPEAT WHILE OBJEND NE 0
3100  /*
3105  ADD OBJSTART TO OBJEND
3110  /*
3115  OBJLEN := OBJEND - OBJSTART - 1
3120  /*
3125  IF OBJLEN GE 75
3130    #LINE-OUT := SUBSTRING(#OUTSRC, OBJSTART, 74)
3135    /*
3140    FOR IY = 74  TO 1 STEP -1
3145      IF #LINE-OUT1(IY) = ' '
3150        ESCAPE BOTTOM
3155      END-IF
3160    END-FOR
3165    IF IY LE 1
3170      IY := 74
3175    END-IF
3180    OBJLEN := IY
3185    OBJEND := OBJSTART + IY
3190  END-IF
3195  ADD 1 TO #WINDOW-INDEX
3200  /*
3205  IF #WINDOW-INDEX GE 11
3210    ESCAPE ROUTINE
3215  END-IF
3220
3225  /*
3230  IF OBJLEN = 0
3235    #WINDOW-LINES(#WINDOW-INDEX) := ' '
3240  ELSE
3245    #WINDOW-LINES(#WINDOW-INDEX) := SUBSTRING(#OUTSRC,OBJSTART,OBJLEN)
3250  END-IF
3255  /*
3260  EXAMINE #WINDOW-LINES(#WINDOW-INDEX) FOR H'0A' REPLACE WITH ' '
3265  /*
3270  OLDSTART := OBJEND  /* remeber last object end
3275  OBJSTART := OBJEND
3280  /*
3285  IF OBJSTART GE *LENGTH(#OUTSRC)
3290    RECORD_END := TRUE
3295    ESCAPE ROUTINE
3300  END-IF
3305  /*
3310  EXAMINE SUBSTRING(#OUTSRC,OBJSTART) FOR H'0A' GIVING POSITION OBJEND
3315END-REPEAT
3320/*
3325/*
3330IF OBJSTART LT *LENGTH(#OUTSRC)
3335  REPEAT
3340    OBJLEN := *LENGTH(#OUTSRC) - OBJSTART + 1
3345    /*
3350    IF OBJLEN GE 75
3355      #LINE-OUT := SUBSTRING(#OUTSRC, OBJSTART, 74)
3360      FOR IY = 74  TO 1 STEP -1
3365        IF #LINE-OUT1(IY) = ' '
3370          ESCAPE BOTTOM
3375        END-IF
3380      END-FOR
3385      IF IY LE 1
3390        IY := 74
3395      END-IF
3400      /*
3405      OBJLEN := IY
3410    END-IF
3415    /*
3420    ADD 1 TO #WINDOW-INDEX
3425    IF #WINDOW-INDEX GE 11
3430      ESCAPE ROUTINE
3435    END-IF
3440    OLDSTART := OBJSTART + OBJLEN
3445    /*
3450    #WINDOW-LINES(#WINDOW-INDEX) := SUBSTRING(#OUTSRC,OBJSTART,OBJLEN)
3455    /*
3460    OBJSTART := OBJSTART + OBJLEN
3465    IF OBJSTART GT *LENGTH(#OUTSRC)
3470      RECORD_END := TRUE
3475      ESCAPE ROUTINE
3480    END-IF
3485  END-REPEAT
3490END-IF
3495END-SUBROUTINE
3500*
3505*
3510DEFINE SUBROUTINE REMOVE_COMMENT
3515OBJSTART := 1
3520/* Remove all comments in source ( <!--   --> )
3525REPEAT
3530  IF *LENGTH(#OUTSRC) > 0
3535    EXAMINE SUBSTRING(#OUTSRC,OBJSTART) FOR '<!--'
3540      GIVING POSITION OBJEND
3545    IF OBJEND GT 0
3550      ADD OBJEND TO OBJSTART
3555      /*
3560      EXAMINE SUBSTRING(#OUTSRC,OBJSTART) FOR '-->'
3565        GIVING POSITION OBJEND
3570      /*
3575      OBJLEN := OBJEND + 3
3580      IF OBJLEN > 0
3585        ATTSTART := OBJSTART - 1
3590        GETENTITY := SUBSTRING(#OUTSRC,ATTSTART,OBJLEN)
3595        EXAMINE #OUTSRC FOR GETENTITY DELETE
3600        OBJSTART := 1
3605      END-IF
3610    ELSE
3615      ESCAPE BOTTOM
3620    END-IF
3625  ELSE
3630    ESCAPE BOTTOM
3635  END-IF
3640END-REPEAT
3645/*
3650END-SUBROUTINE
3655*
3660DEFINE SUBROUTINE FILL_LINKS
3665/*
3670/* Find <META HTTP-EQUIV="REFRESH" CONTENT='0;URL=/corp/default.htm'>
3675/*
3680RESET #WINDOW-INDEX  /* Start new page
3685/*
3690/*
3695RESET OLDSTART
3700IF RECORD_LINK = 'A'
3705  REPEAT
3710    IF *LENGTH(#OUTSRC) > 0
3715      EXAMINE SUBSTRING(#OUTSRC-UPPER,NEXTSTART) FOR '<META'
3720        GIVING POSITION OBJSTART
3725      /*
3730      IF OBJSTART GT 0
3735        IF OBJSTART GT 1
3740          ADD OBJSTART TO NEXTSTART
3745        END-IF
3750        /*
3755        EXAMINE SUBSTRING(#OUTSRC,NEXTSTART) FOR '>'
3760          GIVING POSITION OBJSTART
3765        /*
3770        OBJLEN := OBJSTART + 1
3775        IF OBJLEN > 0
3780          ATTSTART := NEXTSTART - 1
3785          /* complete meta-tag (m#Window-indexed case)
3790          GETENTITY := SUBSTRING(#OUTSRC,ATTSTART,OBJLEN)
3795          EXAMINE GETENTITY FOR H'0A' DELETE
3800          EXAMINE GETENTITY FOR H'0D' DELETE
3805          EXAMINE GETENTITY FOR H'09' REPLACE WITH FULL ' '
3810          REPEAT
3815            EXAMINE GETENTITY FOR FULL '  ' REPLACE WITH FULL ' '
3820              GIVING POSITION II
3825            UNTIL II = 0
3830          END-REPEAT
3835
3840          GETMETA       := SUBSTRING(#OUTSRC,ATTSTART,OBJLEN)
3845          GETMETA-UPPER := SUBSTRING(#OUTSRC-UPPER,ATTSTART,OBJLEN)
3850          /* Found meta-tag relevant?
3855          IF GETMETA-UPPER = SCAN 'HTTP-EQUIV' AND
3860              GETMETA-UPPER = SCAN 'REFRESH' AND
3865              GETMETA-UPPER = SCAN 'CONTENT' AND
3870              GETMETA-UPPER = SCAN 'URL=      '
3875
3880            EXAMINE GETMETA-UPPER FOR 'URL=' GIVING POSITION II
3885            IF II  > 0
3890              ADD 4 TO II
3895              GETMETA := SUBSTRING(GETMETA,II )
3900              /*
3905              EXAMINE GETMETA FOR #QUOTE GIVING POSITION II
3910              IF II  = 0
3915                EXAMINE GETMETA FOR #DQUOTE GIVING POSITION II
3920              END-IF
3925              IF II  > 0
3930                ADD 1 TO #WINDOW-INDEX
3935                /*
3940                IF #WINDOW-INDEX GE 11
3945                  ESCAPE ROUTINE
3950                END-IF
3955                OLDSTART := NEXTSTART
3960                /*
3965                ADD -1 TO II
3970                GETMETA := SUBSTRING(GETMETA,1,II ) /* URL of meta-tag
3975                /*
3980                #WINDOW-LINES (#WINDOW-INDEX) := GETENTITY
3985                #WINDOW-LINK(#WINDOW-INDEX) := GETMETA
3990                #CMD-CV (#WINDOW-INDEX) := (AD=I)
3995                ADD 1 TO #WINDOW-INDEX
4000                COMPRESS  '[' GETMETA ']' INTO GETMETA LEAVING NO
4005                #WINDOW-LINES (#WINDOW-INDEX) := GETMETA
4010
4015              END-IF
4020            END-IF
4025          END-IF
4030
4035        END-IF
4040      ELSE
4045        NEXTSTART := 1
4050        ESCAPE BOTTOM
4055      END-IF
4060    ELSE
4065      NEXTSTART := 1
4070      ESCAPE BOTTOM
4075    END-IF
4080  END-REPEAT
4085END-IF
4090/*
4095/* Find all anchors
4100/* and separate HREF and anchor text
4105/*
4110REPEAT
4115  IF *LENGTH(#OUTSRC-UPPER) > 0
4120    EXAMINE SUBSTRING(#OUTSRC-UPPER,NEXTSTART) FOR '<A'
4125      GIVING POSITION OBJSTART
4130    IF OBJSTART GT 0
4135      IF OBJSTART GT 1
4140        ADD OBJSTART TO NEXTSTART
4145      END-IF
4150      /*
4155      EXAMINE SUBSTRING(#OUTSRC-UPPER,NEXTSTART) FOR '>'
4160        GIVING POSITION OBJSTART
4165      /*
4170      OBJLEN := OBJSTART + 1
4175      IF OBJLEN > 0
4180        ATTSTART := NEXTSTART + 2
4185        GETATTRIBUTE := SUBSTRING(#OUTSRC,ATTSTART,OBJLEN)
4190        GETATTRIBUTE-UPPER := GETATTRIBUTE
4195        EXAMINE GETATTRIBUTE-UPPER TRANSLATE INTO UPPER
4200      END-IF
4205      /*
4210      ATTEND := NEXTSTART + OBJSTART - 2
4215      IF SUBSTRING(#OUTSRC,ATTEND,1) = '/'
4220        ADD OBJSTART TO NEXTSTART
4225      ELSE
4230        /*
4235        OBJSTART := OBJSTART + NEXTSTART
4240        /*
4245        EXAMINE SUBSTRING(#OUTSRC-UPPER,OBJSTART) FOR '</A'
4250          GIVING POSITION OBJEND
4255        /*
4260        IF OBJEND GT 0
4265          IF OBJSTART GT 1
4270            ADD OBJSTART TO OBJEND
4275          END-IF
4280          /*
4285          OBJLEN := OBJEND - OBJSTART - 1
4290          IF OBJLEN GT 0
4295            GETENTITY := SUBSTRING(#OUTSRC, OBJSTART, OBJLEN)
4300            EXAMINE GETENTITY FOR H'0A' DELETE
4305            EXAMINE GETENTITY FOR H'0D' DELETE
4310            EXAMINE GETENTITY FOR H'09' REPLACE WITH FULL ' '
4315            REPEAT
4320              EXAMINE GETENTITY FOR FULL '  ' REPLACE WITH FULL ' '
4325                GIVING POSITION II
4330              UNTIL II = 0
4335            END-REPEAT
4340            /*
4345            /* Get contents of HREF
4350            /*
4355            PERFORM GET_HREF
4360            /*
4365            IF GETENTITY NE ' ' AND GETHREF NE ' ' AND
4370                SUBSTRING(GETHREF,1,11) NE 'javascript:' AND
4375                SUBSTRING(GETHREF,1,1) NE '#' AND
4380                SUBSTRING(GETHREF,1,7) NE 'mailto:'
4385              ADD 1 TO #WINDOW-INDEX
4390              /*
4395              IF #WINDOW-INDEX GE 11
4400                ESCAPE ROUTINE
4405              END-IF
4410              RECORD_LINK := 'B'
4415              OLDSTART := NEXTSTART
4420              /* Find only real links
4425              /*
4430              #WINDOW-LINES (#WINDOW-INDEX) := GETENTITY
4435              #WINDOW-LINK(#WINDOW-INDEX) := GETHREF
4440              #CMD-CV (#WINDOW-INDEX) := (AD=I)
4445              ADD 1 TO #WINDOW-INDEX
4450              COMPRESS  '[' GETHREF ']' INTO GETHREF LEAVING NO
4455              #WINDOW-LINES (#WINDOW-INDEX) := GETHREF
4460
4465            END-IF
4470          ELSE
4475            GETENTITY := ' '
4480          END-IF
4485          NEXTSTART := OBJEND
4490        END-IF
4495      END-IF
4500    ELSE
4505      ESCAPE BOTTOM
4510    END-IF
4515  ELSE
4520    ESCAPE BOTTOM
4525  END-IF
4530
4535END-REPEAT
4540/*
4545END-SUBROUTINE
4550*
4555DEFINE SUBROUTINE GET_HREF
4560/*
4565RESET GETHREF
4570/*
4575MOVE 1 TO NEXT-ATTSTART II
4580/*
4585/* Delete leading blanks
4590/*
4595REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4600  IF SUBSTRING(GETATTRIBUTE,II ,1) NE ' '
4605    NEXT-ATTSTART := II
4610    ADD 1 TO II
4615    ESCAPE BOTTOM
4620  END-IF
4625  ADD 1 TO II
4630END-REPEAT
4635/*
4640/* Find attribute name of HREF ( HREF='  ' )
4645REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4650  ATTLEN := II  - NEXT-ATTSTART
4655  IF SUBSTRING(GETATTRIBUTE-UPPER,NEXT-ATTSTART,ATTLEN) = 'HREF'
4660    NEXT-ATTSTART := II  + NEXT-ATTSTART
4665    /*
4670    REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4675      IF SUBSTRING(GETATTRIBUTE,II ,1) = '='
4680        ADD 1 TO II
4685        /* Next ' or " enclose value
4690        /*
4695        REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4700          IF SUBSTRING(GETATTRIBUTE,II ,1) = #QUOTE OR
4705              SUBSTRING(GETATTRIBUTE,II ,1) = #DQUOTE
4710            ENDCHAR := SUBSTRING(GETATTRIBUTE,II ,1)
4715            ADD 1 TO II
4720            ATTSTART := II
4725            ESCAPE BOTTOM
4730          END-IF
4735          ADD 1 TO II
4740        END-REPEAT
4745        /*
4750        REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4755          IF SUBSTRING(GETATTRIBUTE,II ,1) = ENDCHAR
4760            ATTLEN := II  - ATTSTART
4765            IF ATTLEN > 0
4770              GETHREF:= SUBSTRING(GETATTRIBUTE,ATTSTART,ATTLEN)
4775            ELSE
4780              COMPRESS ' ' INTO GETHREF LEAVING NO
4785            END-IF
4790            NEXT-ATTSTART := II  + 1
4795            /*
4800            ESCAPE ROUTINE
4805          END-IF
4810          ADD 1 TO II
4815        END-REPEAT
4820        /* Attribute found, but error occurred
4825        NEXT-ATTSTART := -1
4830        ESCAPE ROUTINE
4835        /*
4840      END-IF
4845      ADD 1 TO II
4850    END-REPEAT
4855  ELSE
4860    /*
4865    IF SUBSTRING(GETATTRIBUTE,II ,1) = '='
4870      NOA.    REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4875        IF SUBSTRING(GETATTRIBUTE,II ,1) = '='
4880          ADD 1 TO II
4885          /* Next ' or " enclose value
4890          REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4895            IF SUBSTRING(GETATTRIBUTE,II ,1) = #QUOTE OR
4900                SUBSTRING(GETATTRIBUTE,II ,1) = #DQUOTE
4905              ENDCHAR := SUBSTRING(GETATTRIBUTE,II ,1)
4910              ADD 1 TO II
4915              ATTSTART := II
4920              ESCAPE BOTTOM
4925            END-IF
4930            ADD 1 TO II
4935          END-REPEAT
4940          /*
4945          REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4950            IF SUBSTRING(GETATTRIBUTE,II ,1) = ENDCHAR
4955              ADD 1 TO II
4960              REPEAT WHILE II  <= *LENGTH(GETATTRIBUTE)
4965                IF SUBSTRING(GETATTRIBUTE,II ,1) <> ' '
4970                  NEXT-ATTSTART := II
4975                  ADD 1 TO II
4980                  ESCAPE BOTTOM
4985                END-IF
4990                ADD 1 TO II
4995              END-REPEAT
5000              /*
5005              ESCAPE BOTTOM (NOA.)
5010            END-IF
5015            ADD 1 TO II
5020          END-REPEAT
5025          /* Attribute found, but error occurred
5030          ESCAPE BOTTOM
5035          /*
5040        END-IF
5045        ADD 1 TO II
5050      END-REPEAT
5055    END-IF
5060  END-IF
5065  ADD 1 TO II
5070END-REPEAT
5075/*
5080END-SUBROUTINE
5085/*
5090DEFINE SUBROUTINE GET_NEW_URL
5095/*
5100COMPRESS #IN-URL INTO #URL LEAVING NO
5105/*
5110/* fully-qualified URL
5115IF SUBSTRING(#WINDOW-LINK(IY),1,7) = 'http://'
5120    OR SUBSTRING(#WINDOW-LINK(IY),1,8) = 'https://'
5125  #IN-URL :=#WINDOW-LINK(IY)
5130ELSE
5135  /* relative URL
5140  DECIDE FOR FIRST CONDITION
5145    WHEN SUBSTRING(#URL,1,7) = 'http://'
5150      #URL-START := 'http://'
5155      #URL := SUBSTRING(#URL,8)
5160    WHEN SUBSTRING(#URL,1,8) = 'https://'
5165      #URL-START := 'https://'
5170      #URL := SUBSTRING(#URL,9)
5175    WHEN ANY
5180      #URL-FULL := TRUE
5185    WHEN NONE
5190      RESET #URL-FULL #URL-START
5195  END-DECIDE
5200  /* relative URL to root of last page
5205  IF SUBSTRING(#WINDOW-LINK(IY),1,1) = '/'
5210    IF #URL-FULL
5215      EXAMINE #URL FOR '/' GIVING POSITION II
5220      IF II  > 0
5225        ADD -1 TO II
5230        #URL := SUBSTRING(#URL,1,II )
5235      END-IF
5240      COMPRESS #URL-START #URL #WINDOW-LINK(IY) INTO #IN-URL LEAVING NO
5245    END-IF
5250  ELSE                                     /* relative URL to last page
5255    FOR II  = *LENGTH(#URL) TO 1 STEP -1
5260      IF SUBSTRING(#URL,II ,1) = '/'
5265        ESCAPE BOTTOM
5270      END-IF
5275    END-FOR
5280    /*
5285    IF II  > 1
5290      #URL := SUBSTRING(#URL,1,II )
5295    ELSE
5300      COMPRESS #URL '/' INTO #URL LEAVING NO
5305    END-IF
5310    COMPRESS #URL-START #URL #WINDOW-LINK(IY) INTO #IN-URL LEAVING NO
5315  END-IF
5320END-IF
5325/* Load page with new URL
5330PERFORM LOAD_PAGE
5335/*
5340END-SUBROUTINE
5345*
5350END
