0010/*  EXAM-GLN - GET location Subprogram
0020/**********************************************************************
0030DEFINE DATA
0040PARAMETER
0050  1 LOC-NAME (A50)
0060  1 LOC-NR   (A10)
0070  1 REDEFINE LOC-NR
0080    2 LOC-N10 (N10)
0090LOCAL
0100  1 COMMAND VIEW OF COMMAND
0110    2 PROCESSOR-NAME
0120    2 COMMAND-LINE (1:8)
0130    2 RESULT-FIELD (1:5)
0140    2 GETSET-FIELD-NAME
0150    2 GETSET-FIELD-VALUE
0160    2 NATURAL-ERROR
0170    2 RETURN-CODE
0180  1 MSG (A72)
0190  1 PROC-NAME (A08)
0200END-DEFINE
0210RESET LOC-NAME LOC-NR
0220/**********************************************************************
0230/* Read Current Processor Name:
0240PROCESS COMMAND ACTION GET USING
0250  PROCESSOR-NAME = ' '
0260  GETSET-FIELD-NAME = 'NAME&'
0270MOVE GETSET-FIELD-VALUE TO PROC-NAME
0280/**********************************************************************
0290IF NATURAL-ERROR NE 0
0300  CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0310  WRITE 'Read Current Processor Name' (I)
0320  WRITE *PROGRAM (AD=I) MSG (AL=68)
0330END-IF
0340/**********************************************************************
0350/* Read Current Location Number:
0360PROCESS COMMAND ACTION GET USING
0370  PROCESSOR-NAME = PROC-NAME
0380  GETSET-FIELD-NAME = 'CURR-LOCATION'
0390MOVE GETSET-FIELD-VALUE TO LOC-NR
0400IF NATURAL-ERROR NE 0
0410  CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0420  WRITE 'Read Current Location Number' (I)
0430  WRITE *PROGRAM (AD=I) MSG (AL=68)
0440END-IF
0450/**********************************************************************
0460IF LOC-NR = '0000000000'
0470  MOVE '< Initial Location >' TO LOC-NAME
0480  ESCAPE ROUTINE
0490END-IF
0500/*
0510MOVE 'IFN' TO COMMAND-LINE (1)
0520MOVE LOC-NR TO COMMAND-LINE (2)
0530/*
0540PROCESS COMMAND ACTION HELP USING
0550  PROCESSOR-NAME = PROC-NAME
0560  COMMAND-LINE (1:2) = COMMAND-LINE (1:2)
0570/*
0580IF NATURAL-ERROR NE 0
0590  IF NATURAL-ERROR = 6931
0600    IGNORE
0610  ELSE
0620    CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0630    WRITE 'Find location' (I) LOC-NR
0640    WRITE *PROGRAM (AD=I) MSG (AL=68)
0650  END-IF
0660END-IF
0670/*
0680COMPRESS RESULT-FIELD (1:3) TO LOC-NAME
0690/**********************************************************************
0700/* WRITE / PROC-NAME / LOC-NR /  LOC-NAME
0710/**********************************************************************
0720END
