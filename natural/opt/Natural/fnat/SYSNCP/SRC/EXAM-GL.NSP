0010/*  EXAM-GL - GET location
0020/**********************************************************************
0030DEFINE DATA LOCAL
0040  1 COMMAND VIEW OF COMMAND
0050    2 PROCESSOR-NAME
0060    2 COMMAND-LINE (1:8)
0070    2 RESULT-FIELD (1:5)
0080    2 GETSET-FIELD-NAME
0090    2 GETSET-FIELD-VALUE
0100    2 NATURAL-ERROR
0110    2 RETURN-CODE
0120  1 MSG (A72)
0130  1 PROC-NAME (A08)
0140  1 CURR-LOC  (A10)
0150  1 REDEFINE CURR-LOC
0160    2 CURR-LOC-N10 (N10)
0170  1 EVAL-LOC  (A10)
0180  1 REDEFINE EVAL-LOC
0190    2 EVAL-LOC-N10 (N10)
0200  1 FIND-LOC  (A10)
0210  1 REDEFINE FIND-LOC
0220    2 FIND-LOC-N10 (N10)
0230  1 A10  (A10)
0240  1 REDEFINE A10
0250    2 FILLER 6X
0260    2 A10-A04  (A04)
0270    2 REDEFINE A10-A04
0280      3 A10-N04  (N04)
0290  1 CURR-LOC-REM (A44)
0300  1 EVAL-LOC-REM (A44)
0310  1 FIND-LOC-REM (A44)
0320  1 CURR-IKN   (A04/1:3)
0330  1 EVAL-IKN   (N04/1:3)
0340  1 CURR-KEY   (A16/1:3)
0350  1 EVAL-KEY   (A16/1:3)
0360  1 MOD        (C)
0370  1 KILO       (I04)  CONST <1024>
0380  1 MEGA       (I04)  CONST <1048576>
0390  1 W          (N01)
0400  1 X          (N04/1:3)
0410  1 Y          (I04)
0420  1 Z          (I04)
0430END-DEFINE
0440/**********************************************************************
0450DEFINE WINDOW EXAM
0460  SIZE 14 * 78
0470  BASE 03 / 01
0480  TITLE ' (GL) Display Current Location '
0490  CONTROL SCREEN
0500  FRAMED POSITION OFF
0510DEFINE WINDOW ERRW
0520  SIZE 04 * 72
0530  BASE 01 / 01
0540  TITLE ' (GL) Current Location '
0550  CONTROL SCREEN
0560  FRAMED POSITION OFF
0570SET KEY ALL
0580/**********************************************************************
0590/* Read Current Processor Name:
0600PROCESS COMMAND ACTION GET USING
0610  PROCESSOR-NAME = ' '
0620  GETSET-FIELD-NAME = 'NAME&'
0630MOVE GETSET-FIELD-VALUE TO PROC-NAME
0640/**********************************************************************
0650/* Not possible to read Current Processor Name:
0660IF NATURAL-ERROR NE 0
0670  CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0680  INPUT WINDOW = 'ERRW' (AD=OI IP=OFF) / MSG
0690  SET WINDOW OFF
0700  ESCAPE ROUTINE
0710END-IF
0720/**********************************************************************
0730/* Read Current Location Number:
0740PROCESS COMMAND ACTION GET USING
0750  PROCESSOR-NAME = PROC-NAME
0760  GETSET-FIELD-NAME = 'CURR-LOCATION'
0770MOVE GETSET-FIELD-VALUE TO CURR-LOC
0780IF NATURAL-ERROR NE 0
0790  CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0800END-IF
0810/**********************************************************************
0820/* Read Current IKNs:
0830FOR W 1 3
0840  COMPRESS 'CURR-IKN' W TO GETSET-FIELD-NAME LEAVING NO
0850  PROCESS COMMAND ACTION GET USING
0860    PROCESSOR-NAME = PROC-NAME
0870    GETSET-FIELD-NAME = GETSET-FIELD-NAME
0880  MOVE GETSET-FIELD-VALUE TO A10
0890  MOVE A10-A04 TO CURR-IKN (W)
0900  MOVE A10-N04 TO X (W)
0910  IF NATURAL-ERROR NE 0
0920    CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0930  END-IF
0940END-FOR
0950/**********************************************************************
0960/* Translate Current IKNs to Keywords:
0970FOR W 1 3
0980  IF CURR-IKN (W) = '1001' THRU '3999'
0990    MOVE 'IKN' TO COMMAND-LINE (1)
1000    MOVE CURR-IKN (W) TO COMMAND-LINE (2)
1010    /*
1020    PROCESS COMMAND ACTION HELP USING
1030      PROCESSOR-NAME = PROC-NAME
1040      COMMAND-LINE (1:2) = COMMAND-LINE (1:2)
1050    IF NATURAL-ERROR > 0
1060      CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
1070    END-IF
1080    MOVE RESULT-FIELD (1) TO CURR-KEY (W)
1090  ELSE
1100    RESET CURR-IKN (W)
1110  END-IF
1120END-FOR
1130/**********************************************************************
1140/* Cross-check: Compute IFN using CURR-IKN:
1150IF X (1) = 1000 THRU 1999
1160  SUBTRACT 1000 FROM X (1)
1170END-IF
1180IF X (2) = 2000 THRU 2999
1190  SUBTRACT 2000 FROM X (2)
1200END-IF
1210IF X (3) = 3000 THRU 3999
1220  SUBTRACT 3000 FROM X (3)
1230END-IF
1240/*
1250EVAL-LOC-N10 := ( MEGA * X (1) ) + ( KILO * X (2) ) + X (3)
1260/**********************************************************************
1270/* Cross-check: Compute IKN using CURR-LOCATION:
1280Y := CURR-LOC-N10
1290DIVIDE MEGA INTO Y GIVING EVAL-IKN (1) REMAINDER Y
1300DIVIDE KILO INTO Y GIVING EVAL-IKN (2) REMAINDER EVAL-IKN (3)
1310/*
1320IF EVAL-IKN (1) = 001 THRU 999
1330  ADD 1000 TO EVAL-IKN (1)
1340END-IF
1350IF EVAL-IKN (2) = 001 THRU 999
1360  ADD 2000 TO EVAL-IKN (2)
1370END-IF
1380IF EVAL-IKN (3) = 001 THRU 999
1390  ADD 3000 TO EVAL-IKN (3)
1400END-IF
1410/**********************************************************************
1420/* Cross-check: Find KEYWORD belonging to computed IKN:
1430FOR W 1 3
1440  IF EVAL-IKN (W) = 1001 THRU 3999
1450    MOVE 'IKN' TO COMMAND-LINE (1)
1460    MOVE EVAL-IKN (W) TO COMMAND-LINE (2)
1470    /*
1480    PROCESS COMMAND ACTION HELP USING
1490      PROCESSOR-NAME = PROC-NAME
1500      COMMAND-LINE (1:2) = COMMAND-LINE (1:2)
1510    IF NATURAL-ERROR > 0
1520      CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
1530    END-IF
1540    MOVE RESULT-FIELD (1) TO EVAL-KEY (W)
1550  ELSE
1560    RESET EVAL-IKN (W)
1570  END-IF
1580END-FOR
1590/**********************************************************************
1600MOVE CURR-LOC TO FIND-LOC
1610PERFORM FIND-LOC-DEF
1620MOVE FIND-LOC-REM TO CURR-LOC-REM
1630/**********************************************************************
1640MOVE EVAL-LOC TO FIND-LOC
1650PERFORM FIND-LOC-DEF
1660MOVE FIND-LOC-REM TO EVAL-LOC-REM
1670/**********************************************************************
1680INPUT WINDOW = 'EXAM' (AD=OIL IP=OFF CV=MOD ) WITH TEXT MSG
1690  / 'Processor Name ......' PROC-NAME
1700  / ' '
1710  / 'Current   Location ..' CURR-LOC         2X CURR-LOC-REM
1720  / 'Computed  Location ..' EVAL-LOC (AD=D)  2X EVAL-LOC-REM
1730  / ' '
1740  / 'Current   IKNs ......' CURR-IKN (*)
1750  / 'Computed  IKNs ......' EVAL-IKN (*) (AD=D ZP=F SG=F)
1760  / ' '
1770  / 'Current   Keywords ..' CURR-KEY (*)
1780  / 'Computed  Keywords ..' EVAL-KEY (*) (AD=D)
1790/********************************************************************
1800SET WINDOW OFF
1810ESCAPE ROUTINE
1820/********************************************************************
1830DEFINE FIND-LOC-DEF
1840  RESET FIND-LOC-REM
1850  /*
1860  IF FIND-LOC = '0000000000'
1870    MOVE '< Initial Location >' TO FIND-LOC-REM
1880    ESCAPE ROUTINE
1890  END-IF
1900  /*
1910  MOVE 'IFN' TO COMMAND-LINE (1)
1920  MOVE FIND-LOC TO COMMAND-LINE (2)
1930  /*
1940  PROCESS COMMAND ACTION HELP USING
1950    PROCESSOR-NAME = PROC-NAME
1960    COMMAND-LINE (1:2) = COMMAND-LINE (1:2)
1970  /*
1980  IF NATURAL-ERROR NE 0
1990    CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
2000    MOVE MSG TO FIND-LOC-REM
2010    RESET MSG
2020    ESCAPE ROUTINE
2030  END-IF
2040  /*
2050  COMPRESS RESULT-FIELD (1:3) TO FIND-LOC-REM
2060  /*
2070END-SUBROUTINE
2080/********************************************************************
2090END
