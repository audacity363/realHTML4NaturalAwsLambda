0010/*  EXAM-HKL - HELP Keywords
0020***********************************************************************
0030DEFINE DATA
0040LOCAL
0050  01 CMD VIEW OF COMMAND
0060     02 PROCESSOR-NAME
0070     02 COMMAND-LINE (1:8)
0080     02 NATURAL-ERROR
0090     02 RETURN-CODE
0100     02 RESULT-FIELD (01:18)
0110     02 GETSET-FIELD-NAME
0120     02 GETSET-FIELD-VALUE
0130LOCAL
0140  01 KW VIEW OF COMMAND
0150     02 PROCESSOR-NAME
0160     02 COMMAND-LINE (1:8)
0170     02 NATURAL-ERROR
0180     02 RETURN-CODE
0190     02 RESULT-FIELD (1:9)
0200     02 GETSET-FIELD-NAME
0210     02 GETSET-FIELD-VALUE
0220  01 TXT     (A18/1:9)
0230  01 MSG     (A72)
0240  01 MODE    (A24)
0250  01 MAX-ARR (I1) CONST <18>
0260  01 IY (I1)
0270  01 NO (N3)
0280  01 PROC-NAME (A08)
0290  01 INTERNAL (A03)
0300  01 PREFIX1 (A01)
0310  01 PREFIX2 (A01)
0320  01 HEX1    (A01)
0330  01 HEX2    (A01)
0340  01 KEYW    (A16)
0350  01 REDEFINE KEYW
0360     02 KEYW-B  (A1/1:16)
0370  01 LINE (A72) /* (A56)
0380  01 REDEFINE LINE
0390     02 L-KEYWORD (A16)
0400     02 FILLER  16X
0410     02 L-IKN   (N04)
0420     02 FILLER  1X
0430     02 L-MIN   (A03)
0440     02 FILLER  1X
0450     02 L-MAX   (A03)
0460     02 FILLER  1X
0470     02 L-TYPE  (A02)
0480     02 FILLER  1X
0490     02 L-TEXT  (A16)
0500END-DEFINE
0510/**********************************************************************
0520/* Ask for currently used name:
0530/*
0540PROCESS CMD ACTION GET USING
0550  CMD.PROCESSOR-NAME = ' '
0560  CMD.GETSET-FIELD-NAME = 'NAME&'
0570/*
0580MOVE CMD.GETSET-FIELD-VALUE TO PROC-NAME
0590/*
0600IF CMD.NATURAL-ERROR NE 0
0610  CALLNAT 'EXAM-ERR' CMD.NATURAL-ERROR MSG
0620  WRITE MSG (AD=I)
0630  RESET CMD.NATURAL-ERROR MSG
0640  ESCAPE ROUTINE
0650END-IF
0660/**********************************************************************
0670PROCESS CMD ACTION GET USING
0680  CMD.PROCESSOR-NAME = ' '
0690  CMD.GETSET-FIELD-NAME = 'PREFIX1'
0700MOVE CMD.GETSET-FIELD-VALUE TO PREFIX1
0710/**********************************************************************
0720PROCESS CMD ACTION GET USING
0730  CMD.PROCESSOR-NAME = ' '
0740  CMD.GETSET-FIELD-NAME = 'PREFIX2'
0750MOVE CMD.GETSET-FIELD-VALUE TO PREFIX2
0760/**********************************************************************
0770PROCESS CMD ACTION GET USING
0780  CMD.PROCESSOR-NAME = ' '
0790  CMD.GETSET-FIELD-NAME = 'HEX1'
0800MOVE CMD.GETSET-FIELD-VALUE TO HEX1
0810/**********************************************************************
0820PROCESS CMD ACTION GET USING
0830  CMD.PROCESSOR-NAME = ' '
0840  CMD.GETSET-FIELD-NAME = 'HEX2'
0850MOVE CMD.GETSET-FIELD-VALUE TO HEX2
0860/**********************************************************************
0870/* Options for HELP:
0880/*
0890COMPRESS /*  Create a command
0900  'K'    /*  Keywords
0910  '*'    /*  all types
0920  'S'    /*  Synonyms, too
0930  'T'    /*  Translate abbreviation to upper case
0940  'I'    /*  IKN, too
0950  '+'    /*  Start BEHIND given start value
0960  'A'    /*  Internal Keywords, too
0970         /*
0980TO CMD.COMMAND-LINE (1) LEAVING NO   /*  Options
0990MOVE ALL H'00' TO COMMAND-LINE (2)   /*  Start value
1000/*
1010SET KEY ALL
1020/********************************************************************
1030REP. REPEAT
1040  /*
1050  RESET MSG
1060  /*
1070  PROCESS CMD ACTION HELP USING
1080    CMD.PROCESSOR-NAME = PROC-NAME
1090    CMD.COMMAND-LINE (*) = CMD.COMMAND-LINE (*)
1100    /*
1110  RESET CMD.COMMAND-LINE (2)           /*  Old start value
1120  MOVE ALL CMD.RESULT-FIELD (MAX-ARR)  /*  Take the last line
1130    TO CMD.COMMAND-LINE (2)            /*  Make it the start value
1140    UNTIL 16                           /*  do not take the IKN
1150  EXAMINE CMD.COMMAND-LINE (2)
1160    TRANSLATE INTO UPPER
1170  /*
1180  IF CMD.NATURAL-ERROR > 0
1190    CALLNAT 'EXAM-ERR' CMD.NATURAL-ERROR MSG
1200    WRITE MSG (AD=I)
1210    RESET CMD.NATURAL-ERROR MSG
1220  END-IF
1230  /*
1240  FOR IY 1 MAX-ARR
1250    IF CMD.RESULT-FIELD (IY) = ' '
1260      ESCAPE BOTTOM (REP.)
1270    END-IF
1280    MOVE CMD.RESULT-FIELD (IY) TO LINE
1290    PERFORM KEYWORD-INFO
1300    ADD 1 TO NO
1310    WRITE NO (SG=F)  L-KEYWORD L-IKN (SG=F)
1320      L-MIN L-MAX L-TYPE ' ' INTERNAL MSG (AL=7) L-TEXT
1330    RESET MSG
1340  END-FOR
1350  /*
1360END-REPEAT
1370ESCAPE ROUTINE
1380/******************************************************************
1390DEFINE KEYWORD-INFO
1400  RESET INTERNAL
1410  KEYW := L-KEYWORD
1420  EXAMINE KEYW TRANSLATE INTO UPPER
1430  IF KEYW-B (1) = PREFIX1
1440    MOVE HEX1 TO KEYW-B (1)
1450    INTERNAL := 'Yes'
1460  ELSE
1470    IF KEYW-B (1) = PREFIX2
1480      MOVE HEX2 TO KEYW-B (1)
1490      INTERNAL := 'Yes'
1500    END-IF
1510  END-IF
1520  KW.COMMAND-LINE (1) := KEYW
1530  DECIDE ON FIRST L-IKN
1540    VALUE 1000 : 1999   KW.COMMAND-LINE (2) := '1'
1550    VALUE 2000 : 2999   KW.COMMAND-LINE (2) := '2'
1560    VALUE 3000 : 3999   KW.COMMAND-LINE (2) := '3'
1570    NONE                KW.COMMAND-LINE (2) := ' '
1580  END-DECIDE
1590  PROCESS KW ACTION TEXT USING
1600    KW.PROCESSOR-NAME = PROC-NAME
1610    KW.COMMAND-LINE (*) = KW.COMMAND-LINE (*)
1620  /*************************************
1630  IF KW.NATURAL-ERROR > 0
1640    CALLNAT 'EXAM-ERR' KW.NATURAL-ERROR MSG
1650  END-IF
1660  MOVE KW.RESULT-FIELD (1) TO L-TEXT
1670  MOVE KW.RESULT-FIELD (6) TO L-MIN
1680  MOVE KW.RESULT-FIELD (7) TO L-MAX
1690  MOVE KW.RESULT-FIELD (8) TO L-TYPE
1700  /*************************************
1710END-SUBROUTINE
1720/******************************************************************
1730WRITE TITLE LEFT
1740    'List all Keywords in Processor' CMD.PROCESSOR-NAME (AD=I)
1750    'with options =' CMD.COMMAND-LINE (1) (AD=I AL=08)
1760  / ' '
1770  / 'No. Keyword          IKN  Min Max Type Int Error   Text'
1780  / '--- ---------------- ---- --- --- ---- --- -------' '-' (16)
1790/******************************************************************
1800END
