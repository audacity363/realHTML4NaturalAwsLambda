0010/* EXAM-HKI - HELP Keywords sorted by IKN
0020***********************************************************************
0030DEFINE DATA LOCAL
0040  01 COMMAND VIEW OF COMMAND
0050     02 PROCESSOR-NAME
0060     02 COMMAND-LINE (1:8)
0070     02 NATURAL-ERROR
0080     02 RETURN-CODE
0090     02 RESULT-FIELD (1)
0100     02 GETSET-FIELD-NAME
0110     02 GETSET-FIELD-VALUE
0120     02 REDEFINE GETSET-FIELD-VALUE
0130        03 FILLER 6X
0140        03 GFV-N4 (N4)
0150     /*********************
0160  01 PROC-NAME   (A08)
0170  01 IKN         (N04)
0180  01 MSG         (A72)
0190  01 LST         (I01)
0200  01 IKN-BEG     (N04)
0210  01 IKN-END     (N04)
0220  01 #FIELD      (A08/1:3) CONST <'TOP-IKN1','TOP-IKN2','TOP-IKN3'>
0230END-DEFINE
0240/**********************************************************************
0250/* Ask for currently used name:
0260PROCESS COMMAND ACTION GET USING
0270    PROCESSOR-NAME = ' '
0280    GETSET-FIELD-NAME = 'NAME&'
0290/*
0300IF NATURAL-ERROR NE 0
0310  CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0320  WRITE MSG (AD=I)
0330  ESCAPE ROUTINE
0340END-IF
0350/*
0360MOVE GETSET-FIELD-VALUE TO PROC-NAME
0370/**********************************************************************
0380FOR LST 1 4                            /* All Keyword types
0390  IF LST NE 4
0400    PROCESS COMMAND ACTION GET USING
0410      PROCESSOR-NAME = PROC-NAME
0420      GETSET-FIELD-NAME = #FIELD (LST)
0430    /*
0440    IF NATURAL-ERROR NE 0
0450      CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0460      WRITE MSG (AD=I)
0470      ESCAPE ROUTINE
0480    END-IF
0490    /*
0500    IKN-BEG := LST * 1000 + 1
0510    IKN-END := GFV-N4
0520  ELSE
0530    IKN-BEG := 4001
0540    IKN-END := 4050  /* Try up to this number
0550  END-IF
0560  /*
0570  /*******************************************************************
0580  FOR IKN IKN-BEG IKN-END             /*  Keywords for a given type
0590    MOVE 'IKNAT' TO COMMAND-LINE (1)
0600    MOVE  IKN  TO COMMAND-LINE (2)
0610    RESET MSG
0620    /*
0630    PROCESS COMMAND ACTION HELP USING
0640      PROCESSOR-NAME = PROC-NAME
0650      COMMAND-LINE (*) = COMMAND-LINE (*)
0660    /*
0670    IF NATURAL-ERROR NE 0
0680      CALLNAT 'EXAM-ERR' NATURAL-ERROR MSG
0690    END-IF
0700    /*
0710    IF MSG = ' ' AND RESULT-FIELD (1) = ' '
0720      COMPRESS 'No Keyword exists for IKN' IKN TO MSG
0730    END-IF
0740    /*
0750    IF IKN < 4000
0760    OR ( IKN > 4000 AND RESULT-FIELD (1) NE ' ' )
0770      WRITE IKN (SG=F) RESULT-FIELD (1) (AL=16) MSG (AL=56)
0780    END-IF
0790    /*
0800  END-FOR
0810  WRITE '----' '-' (16) '-' (56)
0820END-FOR
0830ESCAPE ROUTINE
0840/*
0850WRITE TITLE LEFT
0860  'IKN  Keyword          Comment' / '----' '-' (16) '-' (56)
0870END
