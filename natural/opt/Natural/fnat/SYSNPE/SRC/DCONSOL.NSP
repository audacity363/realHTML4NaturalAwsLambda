0010* Operator-Console
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL  USING CONSOLEL
0060END-DEFINE
0070*
0080SET KEY PF12 NAMED 'Node'
0090*
0100REPEAT
0110   RESET #LINE (*)
0120   RESET #CV-LINE (*)
0130   FIND CONSOLE WITH NODE     = ##NODE
0140                 AND FUNCTION = 'DISPLAY'
0150      PERFORM CHECK-ERROR
0160      COMPRESS TIME-STAMP TEXT TO #LINE (20)
0170      ASSIGN #LINE (1:19) = #LINE(2:20)
0180    RESET #LINE (20)
0190   END-FIND
0200*
0210   FIND CONSOLE WITH NODE     = ##NODE
0220                 AND FUNCTION = 'DIS-WTOR'
0230      PERFORM CHECK-ERROR
0240      ASSIGN #I = *COUNTER
0250      COMPRESS REPLY-ID TEXT INTO #LINE (#I)
0260      ASSIGN #CV-LINE (#I) = (AD=I)
0270   END-FIND
0280*
0290   FOR #I 1 19
0300      FOR #J = #I 20
0310         IF #LINE (#I) = ' '
0320            ASSIGN #LINE (#I:19) = #LINE (#I+1:20)
0330            ASSIGN #CV-LINE (#I:19) = #CV-LINE (#I+1:20)
0340            RESET  #LINE (20)
0350                   #CV-LINE (20)
0360         ELSE
0370            ESCAPE BOTTOM
0380         END-IF
0390      END-FOR
0400   END-FOR
0410*
0420   PERFORM SCREEN-IO
0430*
0440END-REPEAT
0450*
0460DEFINE SUBROUTINE SCREEN-IO
0470   INPUT WITH TEXT *##MSG-NR, ##MSG-TXT1, ##MSG-TXT2
0480              USING MAP 'CONSOLE1'
0490   RESET ##MSG
0500   IF #COMMAND-LINE NE ' '
0510      ASSIGN #FUNCTION = 'OP-CMD'
0520      PERFORM ISSUE-OPERATOR-COMMAND
0530      RESET #COMMAND-LINE
0540   END-IF
0550   IF *PF-KEY = 'PF12'
0560      CALLNAT 'TUTODB' ##NODE ##MSG ##TUTO
0570   END-IF
0580END-SUBROUTINE
0590*
0600DEFINE SUBROUTINE ISSUE-OPERATOR-COMMAND
0610   PROCESS CONSOLE USING NODE       = ##NODE
0620                     ,   FUNCTION   = #FUNCTION
0630                     ,   TEXT       = #COMMAND-LINE
0640                  GIVING ERROR-CODE ERROR-TEXT
0650   PERFORM CHECK-ERROR
0660END-SUBROUTINE
0670*
0680DEFINE SUBROUTINE CHECK-ERROR
0690   IF CONSOLE.ERROR-CODE > 0
0700      ASSIGN ##MSG-NR   = 1000
0710      ASSIGN ##MSG-TXT1 = CONSOLE.ERROR-TEXT
0720   END-IF
0730END-SUBROUTINE
0740*
0750END
