0010* Program   DVTOC
0020* View      VTOC
0030*
0040* Function  List VTOC of a specific volume
0050*
0060* ----------------------------------------------------------------------
0070* Maintenance..:
0080* hka 97-12-03   NPE213:   make it y2000 compliant
0090* ======================================================================
0100*
0110DEFINE DATA GLOBAL USING TUTO
0120            LOCAL  USING DVTOCL
0130            LOCAL
014001  #VOLSER                 (A6)
015001  #EXTENT-TYPE            (A4)   INIT <'USED'>
016001  #DSNAME                 (A54)  INIT <'*'>
0170*
018001  #DISP-1 (15)
0190    02 DSNAME                 (A43)
020001  #DISP-2-HEAD-HALF         (A35)
021001  REDEFINE #DISP-2-HEAD-HALF
0220    02  #DISP-2-HEAD-HALF-A   (A1/1:35)
023001  #DISP-2-HEAD-FULL-A       (A1/1:70)
024001  REDEFINE #DISP-2-HEAD-FULL-A
0250    02 #DISP-2-HEADER         (A70)
026001  #DISP-2-HEADER-TEXT-1     (A70) INIT              /* english
0270* ----1...5....10...15...20...25...30...35...40...45...50...55...60----*
0280    <'Trks Dsorg Extent                  Cyls Creation   Expiration'>
0290*     NNNN XXXX  HHHHHHHHHHHHHHHHHHHHHHHHNNNN YYYY-MM-DD YYYY-MM-DD
030001  #DISP-2-HEADER-TEXT-2     (A60) INIT              /* german
0310* ----1...5....10...15...20...25...30...35...40...45...50...55...60----*
0320    <'Trks Dsorg Extent                  Cyls Angelegt   Verf{llt'>
033001  #DISP-2 (15)
0340    02 #DISP-2-LINE-HALF      (A35)
0350    02 REDEFINE #DISP-2-LINE-HALF
0360       03 #DISP-2-LINE-HALF-A (A1/1:35)
0370    02 #DISP-2-LINE-FULL-A    (A1/1:70)
0380    02 REDEFINE #DISP-2-LINE-FULL-A
0390       03 FILE-SIZE           (N4)
0400       03 #FILLER-1           (A1)
0410       03 DSORG               (A4)
0420       03 #FILLER-2           (A2)
0430       03 #EXTENTS            (A24)
0440       03 CYLINDERS-ALLOCATED (N4)
0450       03 #FILLER-3           (A1)
0460       03 #CREATION-DATE      (A10)
0470       03 #FILLER-4           (A1)
0480       03 #EXPIRATION-DATE    (A10)
0490*
050001  #I1        (I2)
051001  #I2        (I2)  /* gives start position within #DISP-2-LINE-FULL-A
052001  #COUNT     (I4)
053001  #CVAR      (C)
0540*
0550END-DEFINE
0560#CVAR := (AD=I)
0570#I2   := 1
0580DECIDE ON FIRST VALUE OF *LANGUAGE
0590  VALUE 1       #DISP-2-HEADER := #DISP-2-HEADER-TEXT-1
0600  VALUE 2       #DISP-2-HEADER := #DISP-2-HEADER-TEXT-2
0610  NONE VALUE IGNORE
0620END-DECIDE
0630PERFORM PF-KEY
0640PERFORM SCREEN-IO
0650*
0660* ------------------------------ MAIN LOOP ----------------------------
0670*
0680REPEAT
0690FND.
0700FIND VTOC
0710     WITH NODE         =  ##NODE
0720      AND  VOLSER      =  #VOLSER
0730      AND  DSNAME      =  #DSNAME
0740      AND  EXTENT-TYPE =  #EXTENT-TYPE
0750*
0760  IF ERROR-CODE NE 0
0770     ASSIGN ##MSG-NR   = 1000
0780     ASSIGN ##MSG-TXT1 = ERROR-TEXT
0790     ESCAPE BOTTOM (FND.)
0800  END-IF
0810*
0820  IF #I1 = 15
0830     PERFORM SCREEN-IO
0840     IF #CVAR MODIFIED
0850      ESCAPE BOTTOM (FND.)
0860     END-IF
0870  END-IF
0880  ADD 1 TO #I1
0890  ADD 1 TO #COUNT
0900  MOVE BY NAME VTOC TO #DISP-1 (#I1)
0910  MOVE BY NAME VTOC TO #DISP-2 (#I1)
0920  MOVE EDITED VTOC.EXTENTS (EM=H(12)) TO #DISP-2.#EXTENTS (#I1)
0930  DECIDE ON FIRST VALUE OF *LANGUAGE
0940    VALUE 1
0950      MOVE EDITED VTOC.CREATION-DATX (EM=YYYY-MM-DD)
0960           TO #DISP-2.#CREATION-DATE (#I1)
0970      MOVE EDITED VTOC.EXPIRATION-DATX (EM=YYYY-MM-DD)
0980           TO #DISP-2.#EXPIRATION-DATE (#I1)
0990    VALUE 2
1000      MOVE EDITED VTOC.CREATION-DATX (EM=DD.MM.YYYY)
1010           TO #DISP-2.#CREATION-DATE (#I1)
1020      MOVE EDITED VTOC.EXPIRATION-DATX (EM=DD.MM.YYYY)
1030           TO #DISP-2.#EXPIRATION-DATE (#I1)
1040    NONE VALUE IGNORE
1050  END-DECIDE
1060*
1070END-FIND
1080DECIDE FOR FIRST CONDITION
1090  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
1100       IGNORE
1110  WHEN #I1 > 0
1120       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
1130  WHEN #COUNT = 0
1140       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
1150  WHEN ANY
1160       PERFORM SCREEN-IO
1170  WHEN NONE
1180       IGNORE
1190END-DECIDE
1200RESET #COUNT
1210END-REPEAT
1220*
1230* ------------------------------ SCREEN-IO ----------------------------
1240*
1250DEFINE SUBROUTINE SCREEN-IO
1260  #DISP-2-HEAD-HALF-A(1:35) := #DISP-2-HEAD-FULL-A(#I2:#I2+34)
1270  #DISP-2-LINE-HALF-A(*,1:35) := #DISP-2-LINE-FULL-A(*,#I2:#I2+34)
1280  INPUT WITH TEXT *##MSG-NR,
1290                   ##MSG-TXT1,
1300                   ##MSG-TXT2
1310        USING MAP 'DVTOC--&'
1320  /*
1330  DECIDE ON FIRST VALUE *PF-KEY
1340     VALUE 'PF10' #I2 := 1
1350     VALUE 'PF11' #I2 := 36
1360     NONE VALUE   IGNORE
1370  END-DECIDE
1380  /*
1390  RESET ##MSG-NR ##MSG-TXT1 ##MSG-TXT2
1400  IF #DSNAME = ' '
1410     ASSIGN #DSNAME = '*'
1420  END-IF
1430  IF #EXTENT-TYPE = ' '
1440     ASSIGN #EXTENT-TYPE = '*'
1450  END-IF
1460  RESET #DISP-1 (*) #DISP-2 (*) #I1
1470END-SUBROUTINE
1480*
1490* ------------------------------ PF-KEY -------------------------------
1500*
1510DEFINE SUBROUTINE PF-KEY
1520SET KEY PF1  = PGM NAMED OFF
1530        PF2  = PGM NAMED OFF
1540        PF4  = PGM NAMED OFF
1550        PF5  = PGM NAMED OFF
1560        PF6  = PGM NAMED OFF
1570        PF7  = PGM NAMED OFF
1580        PF8  = PGM NAMED OFF
1590        PF9  = PGM NAMED OFF
1600        PF10 = PGM NAMED 'Left'
1610        PF11 = PGM NAMED 'Right'
1620        PF12 = PGM NAMED OFF
1630END-SUBROUTINE
1640END
