0010* Program   DFATTR
0020* View      FILE-ATTRIBUTES
0030*
0040* Function  Display attributes of a specific data set
0050*
0060* ----------------------------------------------------------------------
0070* Maintenance..:
0080* hka 97-12-03   NPE213:   make it y2000 compliant
0090* ======================================================================
0100*
0110DEFINE DATA
0120GLOBAL USING TUTO
0130LOCAL  USING FATTRIBL
0140END-DEFINE
0150*
0160REPEAT
0170  UNTIL *PF-KEY = 'PF3'
0180*
0190  INPUT (AD=MI'_')
0200    ##TITLE (AD=OI IP=OFF)
0210    // '  Data set..:' FILE-ATTRIBUTES.DSNAME
0220     / '  Volser....:' FILE-ATTRIBUTES.VOLSER
0230*
0240  FIND FILE-ATTRIBUTES WITH DSNAME = FILE-ATTRIBUTES.DSNAME
0250      AND VOLSER = FILE-ATTRIBUTES.VOLSER
0260      AND NODE   = ##NODE
0270*
0280    IF ERROR-CODE > 0
0290      REINPUT ERROR-TEXT
0300    END-IF
0310*
0320    WRITE NOTITLE (AD=IL)
0330      /  'Data Set Info For' (I) DSNAME
0340      // 'Volser...........:' VOLSER
0350    45T  'Creation date....:' CREATION-DATX       (EM=YYYY-MM-DD)
0360      /  'Unit.............:' UNIT
0370    45T  'Expiration date..:' EXPIRATION-DATX     (EM=YYYY-MM-DD)
0380      /  'Series...........:' SERIES
0390      /  'Dsorg............:' DSORG
0400      /  'Lrecl............:' LRECL
0410      /  'Blksize..........:' BLKSIZE
0420      /  'Recfm............:' RECFM
0430      /  'Number of extents:' NUMBER-OF-EXTENTS
0440      /  'Security.........:' SECURITY
0450      // 'Allocation type..:' ALLOCATION-TYPE
0460      /  'Cylinders........:' CYLINDERS-ALLOCATED
0470      /  'Percent used.....:' PERCENT-USED
0480  END-FIND
0490*
0500END-REPEAT
0510*
0520END
