0010* Program   MCONSOL
0020* View      CONSOLE
0030*
0040* Function  Operator Console
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA
0090   GLOBAL USING TUTO
0100   LOCAL  USING CONSOLEL
0110END-DEFINE
0120*
0130SET KEY PF12 NAMED 'Node'
0140*
0150REPEAT
0160   RESET #LINE (*)
0170   RESET #CV-LINE (*)
0180   FIND CONSOLE WITH NODE     = ##NODE
0190                 AND FUNCTION = 'DISPLAY'
0200      PERFORM CHECK-ERROR
0210      ASSIGN #LINE (20) = TEXT
0220      ASSIGN #LINE (1:19) = #LINE(2:20)
0230   END-FIND
0240*
0250   FIND CONSOLE WITH NODE     = ##NODE
0260                 AND FUNCTION = 'DIS-WTOR'
0270      PERFORM CHECK-ERROR
0280      ASSIGN #I = *COUNTER
0290      ASSIGN #LINE (#I) = TEXT
0300      ASSIGN #CV-LINE (#I) = (AD=I)
0310   END-FIND
0320*
0330   RESET #LINE (20)
0340         #CV-LINE (20)
0350   FOR #I 1 19
0360      FOR #J = #I 20
0370         IF #LINE (#I) = ' '
0380            ASSIGN #LINE (#I:19) = #LINE (#I+1:20)
0390            ASSIGN #CV-LINE (#I:19) = #CV-LINE (#I+1:20)
0400            RESET  #LINE (20)
0410                   #CV-LINE (20)
0420         ELSE
0430            ESCAPE BOTTOM
0440         END-IF
0450      END-FOR
0460   END-FOR
0470*
0480   PERFORM SCREEN-IO
0490*
0500END-REPEAT
0510*
0520DEFINE SUBROUTINE SCREEN-IO
0530   INPUT WITH TEXT *##MSG-NR,##MSG-TXT1, ##MSG-TXT2
0540         USING MAP 'CONSOLE&'
0550   RESET ##MSG-NR ##MSG-TXT1 ##MSG-TXT2
0560   IF #COMMAND-LINE NE ' '
0570      ASSIGN #FUNCTION = 'OP-CMD'
0580      PERFORM ISSUE-OPERATOR-COMMAND
0590      RESET #COMMAND-LINE
0600   END-IF
0610   IF *PF-KEY = 'PF12'
0620      CALLNAT 'TUTODB' ##NODE ##MSG ##TUTO
0630   END-IF
0640END-SUBROUTINE
0650*
0660DEFINE SUBROUTINE ISSUE-OPERATOR-COMMAND
0670   PROCESS CONSOLE USING NODE       = ##NODE
0680                     ,   FUNCTION   = #FUNCTION
0690                     ,   TEXT       = #COMMAND-LINE
0700                  GIVING ERROR-CODE ERROR-TEXT
0710   PERFORM CHECK-ERROR
0720END-SUBROUTINE
0730*
0740DEFINE SUBROUTINE CHECK-ERROR
0750   IF CONSOLE.ERROR-CODE > 0
0760      ASSIGN ##MSG-TXT1 = CONSOLE.ERROR-TEXT
0770      ASSIGN ##MSG-NR   = 1000
0780   END-IF
0790END-SUBROUTINE
0800*
0810END
