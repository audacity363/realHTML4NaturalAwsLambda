0010* List standard labels
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL  USING DCATLGL
0060END-DEFINE
0070*
0080WRITE TITLE ##TITLE (AD=I) /
0090*
0100REPEAT
0110   INPUT // ##TITLE (AD=OI IP=OFF)
0120         // 'List contents of Entire System Server ' -
0130            'Internal File Table'
0140         // 'File name.....:' (I) CATALOG.FILE-NAME
0150         // 'List entire contents of VSAM catalog'
0160         /  '(this may take a while)'
0170         // 'Catalog DLBL..:' (I) CATALOG.VSAM-CATALOG
0180         // 'Catalog name..:' (I) CATALOG.USER-CATALOG
0190*
0200   IF CATALOG.FILE-NAME NE ' ' AND
0210        (CATALOG.VSAM-CATALOG NE ' ' OR CATALOG.USER-CATALOG NE ' ')
0220      REINPUT 'Please select only one of Internal or VSAM'
0230   END-IF
0240   IF CATALOG.VSAM-CATALOG NE ' ' AND CATALOG.USER-CATALOG NE ' '
0250      REINPUT 'Please select only one of the VSAM options'
0260   END-IF
0270*
0280   IF CATALOG.FILE-NAME NE ' ' OR
0290      (CATALOG.VSAM-CATALOG = ' ' AND CATALOG.USER-CATALOG = ' ')
0300      PERFORM DISPLAY-INTERNAL
0310   ELSE
0320      PERFORM DISPLAY-VSAM
0330   END-IF
0340*
0350END-REPEAT
0360*
0370*
0380DEFINE SUBROUTINE DISPLAY-INTERNAL
0390   IF CATALOG.FILE-NAME = ' '
0400      ASSIGN CATALOG.FILE-NAME = '*'
0410   END-IF
0420*
0430   FIND CATALOG WITH FILE-NAME = CATALOG.FILE-NAME
0440                 AND NODE      = ##NODE
0450      IF ERROR-CODE > 0
0460         REINPUT ERROR-TEXT
0470      END-IF
0480*
0490      DISPLAY 'FName'  FILE-NAME
0500              'Dsname' DSNAME (AL=42)
0510              'Type' TYPE (AL=4)
0520              'Volser' VOLSER
0530              'Libr'   LIBRARIAN
0540              'Label/Area' LABEL-AREA
0550*
0560   END-FIND
0570END-SUBROUTINE
0580*
0590*
0600DEFINE SUBROUTINE DISPLAY-VSAM
0610   IF CATALOG.VSAM-CATALOG NE ' '
0620      FIND CATALOG WITH VSAM-CATALOG = CATALOG.VSAM-CATALOG
0630                    AND NODE      = ##NODE
0640         IF ERROR-CODE > 0
0650            REINPUT ERROR-TEXT
0660         END-IF
0670*
0680         PERFORM WRITE-VSAM
0690      END-FIND
0700   ELSE
0710      FIND CATALOG WITH USER-CATALOG = CATALOG.USER-CATALOG
0720                    AND NODE      = ##NODE
0730         IF ERROR-CODE > 0
0740            REINPUT ERROR-TEXT
0750         END-IF
0760*
0770         PERFORM WRITE-VSAM
0780      END-FIND
0790   END-IF
0800END-SUBROUTINE
0810*
0820*
0830DEFINE SUBROUTINE WRITE-VSAM
0840   DISPLAY 8X
0850           DSNAME (AL=42)
0860           TYPE (AL=4)
0870           VOLSER
0880END-SUBROUTINE
0890END
