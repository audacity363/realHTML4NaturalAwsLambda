0010*
0020* DISPLAY LIB DIRECTORY
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BLIB---L
0060            LOCAL
007001  #DISPLAY (15)
0080    02 ELEMENT-TYPE    (A8)
0090    02 ELEMENT         (A64)
0100    02 ELEMENT-VERSION (A24)
0110    02 MODIF-DATE      (A14)
0120*
013001  #DSNAME            (A54)
014001  #PASSWORD          (A8)
015001  #ELEMENT           (A64)  INIT <'*'>
016001  #ELEMENT-TYPE      (A8)   INIT <'*'>
017001  #ELEMENT-VERSION   (A24)  INIT <'*'>
0180*
019001  #I                 (P2)
020001  #COUNT             (P8)
021001  #CVAR              (C)
0220*
0230END-DEFINE
0240ASSIGN #CVAR = (AD=I)
0250PERFORM PF-KEY
0260PERFORM SCREEN-IO
0270*
0280* ------------------------------ MAIN LOOP ----------------------------
0290*
0300REPEAT
0310FND.
0320FIND LIB-DIRECTORY WITH NODE            =  ##NODE
0330                   AND  DSNAME          =  #DSNAME
0340                   AND  PASSWORD        =  #PASSWORD
0350                   AND  ELEMENT         =  #ELEMENT
0360                   AND  ELEMENT-TYPE    =  #ELEMENT-TYPE
0370                   AND  ELEMENT-VERSION =  #ELEMENT-VERSION
0380  IF ERROR-CODE NE 0
0390     INCLUDE BERR---C
0400     ESCAPE BOTTOM (FND.)
0410  END-IF
0420  IF #I = 15
0430     PERFORM SCREEN-IO
0440     IF #CVAR MODIFIED
0450        ESCAPE BOTTOM (FND.)
0460     END-IF
0470  END-IF
0480  ADD 1 TO #I
0490  ADD 1 TO #COUNT
0500  MOVE BY NAME LIB-DIRECTORY TO #DISPLAY (#I)
0510END-FIND
0520DECIDE FOR FIRST CONDITION
0530  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0540       IGNORE
0550  WHEN #I > 0
0560       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0570  WHEN #COUNT = 0
0580       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0590  WHEN ANY
0600       PERFORM SCREEN-IO
0610  WHEN NONE
0620       IGNORE
0630END-DECIDE
0640RESET #COUNT
0650END-REPEAT
0660*
0670* ------------------------------ SCREEN-IO ----------------------------
0680*
0690DEFINE SUBROUTINE SCREEN-IO
0700INPUT WITH TEXT *##MSG-NR,
0710                 ##MSG-TXT1,
0720                 ##MSG-TXT2
0730      USING MAP 'BLIB---&'
0740RESET ##MSG
0750DECIDE FOR EVERY CONDITION
0760  WHEN #DSNAME = ' '
0770       REINPUT *1034
0780  WHEN #ELEMENT = ' '
0790       ASSIGN #ELEMENT = '*'
0800  WHEN #ELEMENT-TYPE = ' '
0810       ASSIGN #ELEMENT-TYPE = '*'
0820  WHEN NONE
0830       IGNORE
0840END-DECIDE
0850RESET #DISPLAY (*)
0860RESET #I
0870END-SUBROUTINE
0880*
0890* ------------------------------ PF-KEY -------------------------------
0900*
0910INCLUDE C-PFKEY
0920END
