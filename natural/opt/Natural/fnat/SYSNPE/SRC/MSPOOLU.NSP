0010* Program   MSPOOLU
0020* View      SPOOL-UPDATE
0030*
0040* Function  Alter status of a spool job
0050*
0060* ----------------------------------------------------------------------
0070* Maintenance..:
0080* sju 02-02-18   NPE321:   7 digit job-number
0090* ======================================================================
0100*
0110DEFINE DATA
0120   GLOBAL USING TUTO
0130   LOCAL USING SPOOLU-L
0140   LOCAL USING SPOOLQ-L
0150   LOCAL 1 #MARK      (A1/1:4)
0160         1 #FUNCTIONS (A8/1:4) CONST <'RELEASE',
0170                                      'CANCEL',
0180                                      'HOLD',
0190                                      'CHANGE'>
0200         1 #I         (I1)
0210END-DEFINE
0220*
0230INPUT (AD=MIL'_' ZP=OFF IP=OFF)
0240    / ##TITLE (AD=OI IP=OFF)
0250    / '  please mark'
0260    / '  function ....:'
0270      18T #MARK (1) #FUNCTIONS (1) (AD=OD)
0280    / 18T #MARK (2) #FUNCTIONS (2) (AD=OD)
0290    / 18T #MARK (3) #FUNCTIONS (3) (AD=OD)
0300    / 18T #MARK (4) #FUNCTIONS (4) (AD=OD)
0310   // '  Job-Name ....:' SPOOL-UPDATE.JOB-NAME
0320    / '  Job-Number ..:' SPOOL-UPDATE.JOB-NUMBER (SG=OFF)
0330* sju 02-02-18 added SG=OFF on above line
0340    / '  New class ...:' SPOOL-UPDATE.CLASS
0350*
0360IF SELECTION NOT UNIQUE #MARK (*)
0370   REINPUT *1015
0380END-IF
0390*
0400FOR #I 1 4
0410   IF #MARK (#I) NE ' '
0420      ASSIGN SPOOL-UPDATE.FUNCTION = #FUNCTIONS (#I)
0430      ESCAPE BOTTOM
0440   END-IF
0450END-FOR
0460*
0470DECIDE FOR FIRST CONDITION
0480   WHEN SPOOL-UPDATE.JOB-NAME = ' '
0490      REINPUT *1016 MARK *SPOOL-UPDATE.JOB-NAME
0500   WHEN SPOOL-UPDATE.JOB-NUMBER = 0
0510      REINPUT *1017 MARK *SPOOL-UPDATE.JOB-NUMBER
0520   WHEN SPOOL-UPDATE.CLASS = ' ' AND SPOOL-UPDATE.FUNCTION = 'CHANGE'
0530      REINPUT *1018 MARK *SPOOL-UPDATE.CLASS
0540   WHEN NONE
0550      IGNORE
0560END-DECIDE
0570*
0580FIND SPOOL-QUEUE WITH JOB-NAME   = SPOOL-UPDATE.JOB-NAME
0590                  AND JOB-NUMBER = SPOOL-UPDATE.JOB-NUMBER
0600                  AND NODE       = ##NODE
0610IF ERROR-CODE > 0
0620   REINPUT ERROR-TEXT
0630END-IF
0640*
0650SPOOLU.
0660PROCESS SPOOL-UPDATE USING FUNCTION   = SPOOL-UPDATE.FUNCTION
0670                       ,   JOB-NAME   = SPOOL-UPDATE.JOB-NAME
0680                       ,   JOB-NUMBER = SPOOL-UPDATE.JOB-NUMBER
0690                       ,   CLASS      = SPOOL-UPDATE.CLASS
0700                       ,   GROUP-ID   = SPOOL-QUEUE.GROUP-ID
0710                       ,   NODE       = ##NODE
0720                    GIVING ERROR-CODE
0730                           ERROR-TEXT
0740IF ERROR-CODE (SPOOLU.) > 0
0750   REINPUT ERROR-TEXT (SPOOLU.)
0760ELSE
0770   REINPUT *1019
0780END-IF
0790END-FIND
0800END
