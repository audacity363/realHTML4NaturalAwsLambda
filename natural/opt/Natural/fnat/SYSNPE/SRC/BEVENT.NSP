0010* Demonstrate Task Communication via Eventing-View
0020*
0030DEFINE DATA GLOBAL USING TUTO
0040            LOCAL  USING BEVENT-L
0050            LOCAL
0060*
007001  #DISPLAY
0080    02  #FUNCTION             (A8)
0090    02  #EVENT-NAME           (A8)
0100    02  #MAXIMUM-WAIT-TIME    (N5)
011001  #MSG-TEXT                 (A250/1:2)
012001  REDEFINE #MSG-TEXT
0130    02  #MESSAGE-TEXT         (A50/1:10)
014001  #NUMBER                   (N5)
0150END-DEFINE
0160* ------------------------------------- MAIN ---------------------------
0170*
0180PERFORM PF-KEY
0190*
0200REPEAT
0210*
0220  PERFORM SCREEN-IO
0230*
0240FND.
0250  FIND (#NUMBER) EVENTING
0260       WITH FUNCTION      = #FUNCTION
0270        AND EVENT-NAME    = #EVENT-NAME
0280        AND MAX-TIME      = #MAXIMUM-WAIT-TIME
0290        AND MESSAGE-ARRAY = #MESSAGE-TEXT (1)
0300        AND MESSAGE-ARRAY = #MESSAGE-TEXT (2)
0310        AND NODE          = ##NODE
0320*
0330    IF NO RECORDS FOUND
0340       ASSIGN ##MSG-NR = 1031
0350       ESCAPE BOTTOM (FND.)
0360    END-NOREC
0370*
0380    IF ERROR-CODE NE 0
0390       INCLUDE BERR---C
0400       ESCAPE BOTTOM (FND.)
0410    END-IF
0420*
0430    DECIDE FOR FIRST CONDITION
0440      WHEN ##MSG-NR NE 0
0450           IGNORE
0460      WHEN #FUNCTION = 'RECEIVE'
0470             ASSIGN #MESSAGE-TEXT (1:2) = MESSAGE-ARRAY (1:2)
0480      WHEN #FUNCTION = 'LIST'
0490           DISPLAY 'Event'              EVENT-NAME
0500                   'Number of waiters'  NUMBER-OF-WAITERS
0510                   'Number of messages' NUMBER-OF-MESSAGES
0520      WHEN NONE
0530           IGNORE
0540    END-DECIDE
0550*
0560  END-FIND
0570*
0580  IF ##MSG-NR = 0
0590     ASSIGN ##MSG-NR = 1007
0600     IF #FUNCTION NE 'RECEIVE'
0610        RESET #MESSAGE-TEXT (*)
0620     END-IF
0630     RESET #DISPLAY
0640  END-IF
0650END-REPEAT
0660* ------------------------------------- SCREEN IO ----------------------
0670DEFINE SUBROUTINE SCREEN-IO
0680INPUT WITH TEXT *##MSG-NR,
0690                 ##MSG-TXT1,
0700                 ##MSG-TXT2
0710      USING MAP 'BEVENT-&'
0720*
0730RESET ##MSG
0740*
0750IF #FUNCTION = 'LIST' AND #EVENT-NAME = ' '
0760   MOVE '*' TO #EVENT-NAME
0770END-IF
0780*
0790IF #FUNCTION = 'RECEIVE' AND NOT (#MESSAGE-TEXT (*) NE ' ')
0800   ASSIGN #MESSAGE-TEXT (1) = '*'
0810END-IF
0820*
0830IF #FUNCTION = 'LIST'
0840   ASSIGN #NUMBER = 999
0850 ELSE
0860   ASSIGN #NUMBER = 1
0870END-IF
0880END-SUBROUTINE
0890* ------------------------------------- SCREEN IO ----------------------
0900INCLUDE C-PFKEY
0910END
