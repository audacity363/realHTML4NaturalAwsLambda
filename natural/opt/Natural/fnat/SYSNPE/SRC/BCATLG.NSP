0010*
0020* select files and show file-attributes
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BCATLG-L
0060            LOCAL  USING BFATTR-L
0070            LOCAL
008001  #DISPLAY (15)
0090    02 DSNAME                 (A54)
0100    02 CREATION-DATE          (A8)
0110    02 VOLSER                 (A6)
0120    02 DSORG                  (A4)
0130*
014001  #DSNAME         (A54)  INIT <'*'>
015001  #CREATION-DATE  (A9)
016001  REDEFINE #CREATION-DATE
0170    02  #CR-FIL     (A1)
0180    02  #CR-DATE    (A8)
019001  #CREATION-RANGE (A19)
0200*
021001  #I              (P2)
022001  #COUNT          (P8)
023001  #CVAR           (C)
0240*
0250END-DEFINE
0260ASSIGN #CVAR = (AD=I)
0270PERFORM PF-KEY
0280PERFORM SCREEN-IO
0290*
0300* ------------------------------ MAIN LOOP ----------------------------
0310*
0320REPEAT
0330FND.
0340FIND CATALOG WITH NODE           =  ##NODE
0350             AND  DSNAME         =  #DSNAME
0360             AND  CREATION-RANGE =  #CREATION-RANGE
0370  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0380     INCLUDE BERR---C
0390     ESCAPE BOTTOM (FND.)
0400  END-IF
0410FNDA.
0420  FIND FILE-ATTRIBUTES WITH NODE   =  ##NODE
0430                        AND DSNAME =  DSNAME (FND.)
0440    IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0450       INCLUDE BERR---C
0460       ESCAPE BOTTOM (FND.)
0470    END-IF
0480    IF #I = 15
0490       PERFORM SCREEN-IO
0500       IF #CVAR MODIFIED
0510        ESCAPE BOTTOM (FND.)
0520       END-IF
0530    END-IF
0540    ADD 1 TO #I
0550    ADD 1 TO #COUNT
0560    MOVE BY NAME FILE-ATTRIBUTES TO #DISPLAY (#I)
0570  END-FIND
0580END-FIND
0590DECIDE FOR FIRST CONDITION
0600  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0610       IGNORE
0620  WHEN #I > 0
0630       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0640  WHEN #COUNT = 0
0650       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0660  WHEN ANY
0670       PERFORM SCREEN-IO
0680  WHEN NONE
0690       IGNORE
0700END-DECIDE
0710RESET #COUNT
0720END-REPEAT
0730*
0740* ------------------------------ SCREEN-IO ----------------------------
0750*
0760DEFINE SUBROUTINE SCREEN-IO
0770INPUT WITH TEXT *##MSG-NR,
0780                 ##MSG-TXT1,
0790                 ##MSG-TXT2
0800      USING MAP 'BCATLGM&'
0810RESET ##MSG #CREATION-RANGE
0820IF #DSNAME = ' '
0830   ASSIGN #DSNAME = '*'
0840END-IF
0850DECIDE FOR FIRST CONDITION
0860  WHEN #CREATION-DATE = ' '
0870       IGNORE
0880  WHEN #CREATION-DATE = MASK('<'YY'-'MM'-'DD)
0890       COMPRESS '(,' #CR-DATE ')' INTO #CREATION-RANGE LEAVING NO SPACE
0900  WHEN #CREATION-DATE = MASK('>'YY'-'MM'-'DD)
0910       COMPRESS '(' #CR-DATE ',)' INTO #CREATION-RANGE LEAVING NO SPACE
0920  WHEN #CREATION-DATE = MASK('='YY'-'MM'-'DD)
0930       ASSIGN #CREATION-RANGE = #CR-DATE
0940  WHEN NONE
0950       REINPUT *1032 MARK *#CREATION-DATE
0960END-DECIDE
0970RESET #DISPLAY (*)
0980RESET #I
0990END-SUBROUTINE
1000*
1010* ------------------------------ PF-KEY -------------------------------
1020*
1030INCLUDE C-PFKEY
1040END
