0010* Program   MASCB
0020* View      ADDRESS-SPACE
0030*
0040* Function  Display address-space information
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA GLOBAL USING TUTO
0090            LOCAL  USING ASCB-L
0100            LOCAL
011001  #DISPLAY (15)
0120    02 JOB-NAME     (A8)
0130    02 ADR          (A8)
0140    02 ASID         (N4)
0150    02 TCB-ADR      (A8)
0160    02 DISP         (A3)
0170    02 TYPE         (A6)
0180    02 CPU          (N7.2)
0190    02 REGION       (N5)
0200    02 STATUS       (A8)
0210*
022001  #JOB-NAME       (A8)   INIT <'*'>
023001  #TYPE           (A6)   INIT <'*'>
024001  #CPU            (N7.2)
025001  #STATUS         (A8)   INIT <'*'>
0260*
027001  #I              (P2)
028001  #COUNT          (P8)
029001  #CVAR           (C)
0300END-DEFINE
0310ASSIGN #CVAR = (AD=I)
0320PERFORM PF-KEY
0330*
0340* ------------------------------ MAIN LOOP ----------------------------
0350*
0360REPEAT
0370FND.
0380FIND ADDRESS-SPACE WITH NODE   =  ##NODE
0390                 AND  JOB-NAME =  #JOB-NAME
0400                 AND  TYPE     =  #TYPE
0410                 AND  STATUS   =  #STATUS
0420  IF ERROR-CODE NE 0
0430     ASSIGN ##MSG-NR = 1000
0440     ASSIGN ##MSG-TXT1 = ERROR-TEXT
0450     ESCAPE BOTTOM (FND.)
0460  END-IF
0470  IF #I = 15
0480     PERFORM SCREEN-IO
0490     IF #CVAR MODIFIED
0500        ESCAPE BOTTOM (FND.)
0510     END-IF
0520  END-IF
0530  ADD 1 TO #I
0540  ADD 1 TO #COUNT
0550  MOVE BY NAME ADDRESS-SPACE TO #DISPLAY (#I)
0560  MOVE EDITED ADDRESS (EM=HHHHHHHH) TO #DISPLAY.ADR (#I)
0570  MOVE EDITED TCB     (EM=HHHHHHHH) TO #DISPLAY.TCB-ADR (#I)
0580END-FIND
0590DECIDE FOR FIRST CONDITION
0600  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0610       IGNORE
0620  WHEN #I > 0
0630       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0640  WHEN #COUNT = 0
0650       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0660  WHEN ANY
0670       PERFORM SCREEN-IO
0680  WHEN NONE
0690       IGNORE
0700END-DECIDE
0710RESET #COUNT
0720END-REPEAT
0730*
0740* ------------------------------ SCREEN-IO ----------------------------
0750*
0760DEFINE SUBROUTINE SCREEN-IO
0770INPUT WITH TEXT *##MSG-NR,
0780                 ##MSG-TXT1,
0790                 ##MSG-TXT2
0800      USING MAP 'MASCB-M&'
0810RESET ##MSG
0820DECIDE FOR EVERY CONDITION
0830  WHEN #JOB-NAME = ' '
0840       ASSIGN #JOB-NAME = '*'
0850  WHEN #TYPE = ' '
0860       ASSIGN #TYPE = '*'
0870  WHEN #STATUS = ' '
0880       ASSIGN #STATUS = '*'
0890  WHEN NONE
0900       IGNORE
0910END-DECIDE
0920RESET #DISPLAY (*)
0930RESET #I
0940END-SUBROUTINE
0950*
0960* ------------------------------ PF-KEY -------------------------------
0970*
0980INCLUDE C-PFKEY
0990END
