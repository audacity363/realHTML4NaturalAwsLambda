0010* Program   BSPOOLQ
0020* View      SPOOL-QUEUE
0030*
0040* Function  Display SPOOL Queue
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA GLOBAL USING TUTO
0090            LOCAL  USING BSPOOLQL
0100            LOCAL
011001  #DISPLAY (15)
0120    02 JOB-NAME          (A8)
0130    02 USER              (A8)
0140    02 JOB-ID            (A5)
0150    02 PRIORITY          (N3)
0160    02 ORIGINATOR-JOB-ID (A5)
0170    02 STATUS            (A9)
0180    02 TYPE-NUMBER       (N1)
0190    02 SIZE              (A6)
0200    02 CPU-USED          (N7.2)
0210    02 COPIES            (N3)
0220*
023001  #JOB-NAME       (A8)   INIT <'*'>
024001  #USER-ID        (A8)   INIT <'*'>
025001  #STATUS         (A9)   INIT <'*'>
0260*
027001  #I              (P2)
028001  #COUNT          (P8)
029001  #CVAR           (C)
0300*
0310END-DEFINE
0320ASSIGN #CVAR = (AD=I)
0330PERFORM PF-KEY
0340PERFORM SCREEN-IO
0350*
0360* ------------------------------ MAIN LOOP ----------------------------
0370*
0380REPEAT
0390FND.
0400FIND SPOOL-QUEUE WITH NODE     =  ##NODE
0410                 AND  JOB-NAME =  #JOB-NAME
0420                 AND  STATUS   =  #STATUS
0430                 AND  USER     =  #USER-ID
0440*
0450  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0460     INCLUDE BERR---C
0470     ESCAPE BOTTOM (FND.)
0480  END-IF
0490*
0500  IF #I = 15
0510     PERFORM SCREEN-IO
0520     IF #CVAR MODIFIED
0530        ESCAPE BOTTOM (FND.)
0540     END-IF
0550  END-IF
0560*
0570  ADD 1 TO #I
0580  ADD 1 TO #COUNT
0590  MOVE BY NAME SPOOL-QUEUE TO #DISPLAY (#I)
0600END-FIND
0610DECIDE FOR FIRST CONDITION
0620  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0630       IGNORE
0640  WHEN #I > 0
0650       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0660  WHEN #COUNT = 0
0670       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0680  WHEN ANY
0690       PERFORM SCREEN-IO
0700  WHEN NONE
0710       IGNORE
0720END-DECIDE
0730RESET #COUNT
0740END-REPEAT
0750*
0760* ------------------------------ SCREEN-IO ----------------------------
0770*
0780DEFINE SUBROUTINE SCREEN-IO
0790INPUT WITH TEXT *##MSG-NR,
0800                 ##MSG-TXT1,
0810                 ##MSG-TXT2
0820      USING MAP 'BSPOOLQ&'
0830RESET ##MSG
0840DECIDE FOR EVERY CONDITION
0850  WHEN #USER-ID = ' '
0860       ASSIGN #USER-ID = '*'
0870  WHEN #JOB-NAME = ' '
0880       ASSIGN #JOB-NAME = '*'
0890  WHEN #STATUS = ' '
0900       ASSIGN #STATUS = '*'
0910  WHEN NONE
0920       IGNORE
0930END-DECIDE
0940RESET #DISPLAY (*)
0950RESET #I
0960END-SUBROUTINE
0970*
0980* ------------------------------ PF-KEY -------------------------------
0990*
1000INCLUDE C-PFKEY
1010END
