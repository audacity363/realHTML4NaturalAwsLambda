0010* Program   DSPOOLQ
0020* View      SPOOL-QUEUE
0030*
0040* Function  Display spool queue entries
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA GLOBAL USING TUTO
0090            INDEPENDENT
010001  +G-JOB-NAME     (A8)
011001  +G-JOB-NUMBER   (N5)
0120            LOCAL  USING SPOOLQ-L
0130            LOCAL
014001  #DISPLAY (15)
0150    02 MARK         (A1)
0160    02 JOB-NAME     (A8)
0170    02 JOB-NUMBER   (N5)
0180    02 QUEUE        (A3)
0190    02 CLASS        (A8)
0200    02 DISP         (A1)
0210    02 PRIORITY     (N3)
0220    02 RECORD-COUNT (N7)
0230    02 DESTINATION  (A8)
0240    02 STATUS       (A9)
0250    02 HOLD         (A3)
0260*
027001  #JOB-NAME       (A8)   INIT <'*'>
028001  #QUEUE          (A3)
0290*
030001  #I              (P2)
031001  #COUNT          (P8)
032001  #CVAR           (C)
033001  #CV-MARK      (C/1:15)
0340END-DEFINE
0350ASSIGN #CVAR = (AD=I)
0360ASSIGN #CV-MARK (*) = (AD=DP)
0370PERFORM PF-KEY
0380*
0390* ------------------------------ MAIN LOOP ----------------------------
0400*
0410REPEAT
0420FND.
0430FIND SPOOL-QUEUE
0440     WITH NODE       =  ##NODE
0450      AND JOB-NAME   =  #JOB-NAME
0460      AND QUEUE      =  #QUEUE
0470*
0480  IF ERROR-CODE NE 0
0490     ASSIGN ##MSG-NR = 1000
0500     ASSIGN ##MSG-TXT1 = ERROR-TEXT
0510     ESCAPE BOTTOM (FND.)
0520  END-IF
0530  IF #I = 15
0540     PERFORM SCREEN-IO
0550     IF #CVAR MODIFIED
0560        ESCAPE BOTTOM (FND.)
0570     END-IF
0580  END-IF
0590  ADD 1 TO #I
0600  ADD 1 TO #COUNT
0610  MOVE BY NAME SPOOL-QUEUE TO #DISPLAY (#I)
0620  ASSIGN #CV-MARK (#I) = (AD=I)
0630END-FIND
0640DECIDE FOR FIRST CONDITION
0650  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0660       IGNORE
0670  WHEN #I > 0
0680       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0690  WHEN #COUNT = 0
0700       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0710  WHEN ANY
0720       PERFORM SCREEN-IO
0730  WHEN NONE
0740       IGNORE
0750END-DECIDE
0760RESET #COUNT
0770END-REPEAT
0780*
0790* ------------------------------ SCREEN-IO ----------------------------
0800*
0810DEFINE SUBROUTINE SCREEN-IO
0820INPUT WITH TEXT *##MSG-NR,
0830                 ##MSG-TXT1,
0840                 ##MSG-TXT2
0850      USING MAP 'DSPOOLQ&'
0860RESET ##MSG
0870ASSIGN #CV-MARK (*) = (AD=DP)
0880*
0890FOR #I = 1 TO 15
0900  IF #DISPLAY.MARK (#I) NE ' '
0910     ASSIGN +G-JOB-NAME   = #DISPLAY.JOB-NAME (#I)
0920     ASSIGN +G-JOB-NUMBER = #DISPLAY.JOB-NUMBER (#I)
0930     ESCAPE BOTTOM
0940  END-IF
0950END-FOR
0960*
0970DECIDE FOR EVERY CONDITION
0980  WHEN #JOB-NAME = ' '
0990       ASSIGN #JOB-NAME = '*'
1000  WHEN #QUEUE = ' '
1010       ASSIGN #QUEUE = '*'
1020  WHEN NONE
1030       IGNORE
1040END-DECIDE
1050RESET #DISPLAY (*)
1060RESET #I
1070END-SUBROUTINE
1080*
1090* ------------------------------ PF-KEY -------------------------------
1100*
1110INCLUDE C-PFKEY
1120END
