0010* Program   MREADSC
0020* View      READ-FILE
0030*
0040* Function  Scan a PDS-member
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA
0090GLOBAL USING TUTO
0100LOCAL USING READ-L
0110LOCAL
01201 #DSN          (A44)
01301 #TITLE        (A78)
01401 #SCAN-VALUE   (A40)
01501 #SCAN-VALUE-1 (A40)
01601 #RECORDS-FOUND(L)
0170END-DEFINE
0180*
0190INPUT (AD=MI'_') / ##TITLE (AD=OI IP=OFF)
0200  // '  Data set ....:' READ-FILE.DSNAME
0210  / '  Member ......:' READ-FILE.MEMBER
0220  // '  Search for ..:' #SCAN-VALUE
0230*
0240COMPRESS READ-FILE.DSNAME '(' READ-FILE.MEMBER ')' TO #DSN LEAVING NO
0250COMPRESS '''' #SCAN-VALUE '''' TO #SCAN-VALUE-1 LEAVING NO
0260COMPRESS 'Scan' #DSN 'for' #SCAN-VALUE-1 TO #TITLE
0270*
0280WRITE TITLE / #TITLE (AD=I) /
0290*
0300COMPRESS '*' #SCAN-VALUE '*' TO #SCAN-VALUE-1 LEAVING NO
0310*
0320RESET #RECORDS-FOUND
0330*
0340FIND READ-FILE WITH DSNAME = READ-FILE.DSNAME
0350    AND MEMBER = READ-FILE.MEMBER
0360    AND RECORD = #SCAN-VALUE-1
0370    AND NODE = ##NODE
0380*
0390  IF END-OF-FILE = 'YES'
0400    ESCAPE BOTTOM
0410  END-IF
0420*
0430  IF ERROR-CODE > 0
0440    IF #RECORDS-FOUND
0450      WRITE ERROR-CODE ERROR-TEXT
0460    ELSE
0470      REINPUT ERROR-TEXT
0480    END-IF
0490  ELSE
0500    WRITE RECORD-NUMBER (AD=I NL=5) RECORD (AL=72)
0510    #RECORDS-FOUND := TRUE
0520  END-IF
0530END-FIND
0540END
