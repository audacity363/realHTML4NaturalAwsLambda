0010* Program   MWRTF
0020* View      WRITE-FILE
0030*
0040* Function  Write catalog entries to OS file
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA
0090   GLOBAL USING TUTO
0100   LOCAL  USING WRITE-L
0110   LOCAL  USING CATALOGL
0120   LOCAL
0130*
014001  RESOURCE-CONTROL VIEW OF RESOURCE-CONTROL
0150    02  ERROR-CODE
0160    02  ERROR-TEXT
0170*
018001  WRITE-FILE-DONE            (L)
019001  #ERROR-CODE                (N4)
020001  #ERROR-TEXT                (A58)
021001  #RESOURCE                  (A52)
022001  REDEFINE #RESOURCE
0230    02  #RES-DSNAME            (A44)
0240    02  #RES-MEMBER            (A8)
0250*
0260END-DEFINE
0270*
0280ASSIGN WRITE-FILE.DISP = 'SHR'
0290*
0300REPEAT
0310*
0320   INPUT (AD=MI'_')
0330     / ##TITLE (AD=OI IP=OFF)
0340    // '  For Prefix....:' CATALOG.DSNAME
0350    // 'to' (I)
0360     / '  Data set......:' WRITE-FILE.DSNAME
0370     / '  Member........:' WRITE-FILE.MEMBER
0380     / '  DISP..........:' WRITE-FILE.DISP
0390*
0400ASSIGN #RES-DSNAME = WRITE-FILE.DSNAME
0410ASSIGN #RES-MEMBER = WRITE-FILE.MEMBER
0420*
0430RESET  #ERROR-CODE #ERROR-TEXT WRITE-FILE-DONE
0440*
0450ENQ.
0460FIND RESOURCE-CONTROL
0470     WITH NODE         = ##NODE
0480      AND QNAME        = 'SPFEDIT'
0490      AND RNAME        = #RESOURCE
0500      AND RNAME-LENGTH = 52
0510      AND FUNCTION     = 'ENQ-USE'
0520*
0530   IF ERROR-CODE (ENQ.) NE 0
0540      REINPUT  RESOURCE-CONTROL.ERROR-TEXT
0550   END-IF
0560*
0570END-FIND
0580*
0590CAT.
0600FIND CATALOG WITH NODE   = ##NODE
0610              AND DSNAME = CATALOG.DSNAME
0620*
0630   IF ERROR-CODE NE 0
0640      ASSIGN #ERROR-CODE = ERROR-CODE (CAT.)
0650      ASSIGN #ERROR-TEXT = ERROR-TEXT (CAT.)
0660      ESCAPE BOTTOM (CAT.)
0670   END-IF
0680*
0690WRT.
0700   PROCESS WRITE-FILE USING NODE   = ##NODE
0710                        ,   DSNAME = WRITE-FILE.DSNAME
0720                        ,   MEMBER = WRITE-FILE.MEMBER
0730                        ,   DISP   = WRITE-FILE.DISP
0740                        ,   RECORD = DSNAME
0750                     GIVING WRITE-FILE.ERROR-CODE
0760                            WRITE-FILE.ERROR-TEXT
0770*
0780   IF WRITE-FILE.ERROR-CODE NE 0
0790      ASSIGN #ERROR-CODE = ERROR-CODE (WRT.)
0800      ASSIGN #ERROR-TEXT = ERROR-TEXT (WRT.)
0810      ESCAPE BOTTOM (CAT.)
0820   END-IF
0830*
0840   ASSIGN WRITE-FILE-DONE = TRUE
0850*
0860END-FIND
0870*
0880IF WRITE-FILE-DONE
0890CLS.
0900   PROCESS WRITE-FILE USING NODE     = ##NODE
0910                        ,   FUNCTION = 'CLOSE'
0920                     GIVING ERROR-CODE
0930                            ERROR-TEXT
0940   IF ERROR-CODE NE 0
0950      ASSIGN #ERROR-CODE = ERROR-CODE (CLS.)
0960      ASSIGN #ERROR-TEXT = ERROR-TEXT (CLS.)
0970   END-IF
0980END-IF
0990*
1000DEQ.
1010FIND RESOURCE-CONTROL
1020     WITH NODE         = ##NODE
1030      AND QNAME        = 'SPFEDIT'
1040      AND RNAME        = #RESOURCE
1050      AND RNAME-LENGTH = 52
1060      AND FUNCTION     = 'DEQ'
1070*
1080   IF ERROR-CODE (DEQ.) NE 0
1090      ASSIGN #ERROR-CODE = ERROR-CODE (DEQ.)
1100      ASSIGN #ERROR-TEXT = ERROR-TEXT (DEQ.)
1110   END-IF
1120*
1130END-FIND
1140*
1150IF #ERROR-CODE NE 0
1160   REINPUT #ERROR-TEXT
1170 ELSE
1180   REINPUT *1022,*COUNTER (CAT.)
1190END-IF
1200*
1210END-REPEAT
1220*
1230END
