0010*
0020* select job variables and perform various operations
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BJV----L
0060            LOCAL
007001  #DISPLAY
0080    02 NAME                   (A54/1:15)
0090    02 SELECTION              (A1/1:16)
010001  CV                        (C/1:16)
0110*
012001  #NAME           (A54)  INIT <'*'>
0130*
014001  #J              (P2)
015001  #I              (P2)
016001  #COUNT          (P8)
017001  #CVAR           (C)
0180*
0190END-DEFINE
0200ASSIGN #CVAR = (AD=I)
0210ASSIGN CV (1:15) = (AD=DP)
0220PERFORM PF-KEY
0230PERFORM SCREEN-IO
0240*
0250* ------------------------------ MAIN LOOP ----------------------------
0260*
0270REPEAT
0280FND.
0290FIND JOB-VARIABLES
0300     WITH NODE     =  ##NODE
0310      AND FUNCTION = 'LIST'
0320      AND NAME     =  #NAME
0330*
0340  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0350     INCLUDE BERR---C
0360     ESCAPE BOTTOM (FND.)
0370  END-IF
0380*
0390  IF #I = 15
0400     PERFORM SCREEN-IO
0410     IF #CVAR MODIFIED
0420        ESCAPE BOTTOM (FND.)
0430     END-IF
0440  END-IF
0450  ADD 1 TO #I
0460  ADD 1 TO #COUNT
0470  ASSIGN CV (#I) = (AD=I)
0480  ASSIGN #DISPLAY.NAME (#I) = JOB-VARIABLES.NAME
0490END-FIND
0500DECIDE FOR FIRST CONDITION
0510  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0520       IGNORE
0530  WHEN #I > 0
0540       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0550  WHEN #COUNT = 0
0560       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0570  WHEN ANY
0580       PERFORM SCREEN-IO
0590  WHEN NONE
0600       IGNORE
0610END-DECIDE
0620RESET #COUNT
0630END-REPEAT
0640*
0650* ------------------------------ SCREEN-IO ----------------------------
0660*
0670DEFINE SUBROUTINE SCREEN-IO
0680INPUT WITH TEXT *##MSG-NR,
0690                 ##MSG-TXT1,
0700                 ##MSG-TXT2
0710      USING MAP 'BJV----&'
0720*
0730RESET ##MSG
0740*
0750FOR #J = 1 TO 16
0760  DECIDE ON FIRST #DISPLAY.SELECTION (#J)
0770    VALUE 'C' IF #J = 16
0780                 FETCH RETURN 'BJVC' #NAME
0790                 RESET #NAME
0800               ELSE
0810                 FETCH RETURN 'BJVC' #DISPLAY.NAME (#J)
0820              END-IF
0830    VALUE 'A' IF #J = 16
0840                 FETCH RETURN 'BJVA' #NAME
0850                 RESET #NAME
0860               ELSE
0870                 FETCH RETURN 'BJVA' #DISPLAY.NAME (#J)
0880              END-IF
0890    NONE      IGNORE
0900  END-DECIDE
0910  RESET #DISPLAY.SELECTION (#J)
0920END-FOR
0930*
0940IF #NAME = ' '
0950   ASSIGN #NAME = '*'
0960END-IF
0970*
0980RESET #DISPLAY
0990RESET #I
1000ASSIGN CV (1:15) = (AD=DP)
1010END-SUBROUTINE
1020*
1030* ------------------------------ PF-KEY -------------------------------
1040*
1050INCLUDE C-PFKEY
1060END
