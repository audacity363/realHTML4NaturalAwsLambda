0010* Alter status of a spool job
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL USING SPOOLU-L
0060   LOCAL 1 #MARK      (A1/1:3)
0070         1 #FUNCTIONS (A8/1:3) CONST <'RELEASE','CANCEL','HOLD'>
0080         1 #I         (I1)
0090END-DEFINE
0100*
0110INPUT (AD=MI'_' ZP=OFF IP=OFF)
0120    / ##TITLE (AD=OI IP=OFF)
0130   // '  Please mark function:'
0140    / 5T #MARK (1) #FUNCTIONS (1) (AD=OD)
0150    / 5T #MARK (2) #FUNCTIONS (2) (AD=OD)
0160    / 5T #MARK (3) #FUNCTIONS (3) (AD=OD)
0170   // '  Job-Name  :' SPOOL-UPDATE.JOB-NAME
0180    / '  Job-Number:' SPOOL-UPDATE.JOB-NUMBER
0190    / '  Queue     :' SPOOL-UPDATE.QUEUE  24T '(LST,PUN,RDR,XMT)'
0200*
0210IF SELECTION NOT UNIQUE #MARK (*)
0220   REINPUT *1015
0230END-IF
0240*
0250DECIDE FOR FIRST CONDITION
0260   WHEN SPOOL-UPDATE.JOB-NAME = ' '
0270      REINPUT *1016 MARK *SPOOL-UPDATE.JOB-NAME
0280   WHEN SPOOL-UPDATE.JOB-NUMBER = 0
0290      REINPUT *1017 MARK *SPOOL-UPDATE.JOB-NUMBER
0300   WHEN SPOOL-UPDATE.QUEUE = ' '
0310      REINPUT *1018 MARK *SPOOL-UPDATE.QUEUE
0320   WHEN NONE
0330      IGNORE
0340END-DECIDE
0350*
0360FOR #I 1 3
0370   IF #MARK (#I) NE ' '
0380      ASSIGN SPOOL-UPDATE.FUNCTION = #FUNCTIONS (#I)
0390      ESCAPE BOTTOM
0400   END-IF
0410END-FOR
0420*
0430PROCESS SPOOL-UPDATE USING FUNCTION   = SPOOL-UPDATE.FUNCTION
0440                       ,   JOB-NAME   = SPOOL-UPDATE.JOB-NAME
0450                       ,   JOB-NUMBER = SPOOL-UPDATE.JOB-NUMBER
0460                       ,   QUEUE      = SPOOL-UPDATE.QUEUE
0470                       ,   NODE       = ##NODE
0480                    GIVING ERROR-CODE
0490                           ERROR-TEXT
0500*
0510IF ERROR-CODE > 0
0520   REINPUT ERROR-TEXT
0530ELSE
0540   REINPUT *1019
0550END-IF
0560END
