0010* TUTO001P ENTIRE SYSTEM SERVER TUTORIAL                    GW 87-10-18
0020*
0030*     YY-MM-DD
0040* JWO 00-11-16 JWO01      Version number taken from control record
0050* JWO 00-12-13 JWO02      Reset error-ta upon exit
0060* JWO 02-11-27 JWO03      SP7 --> z/OS also in Modul TUTODB
0070* JWO 03-05-15 JWO04      Version set to 3.2.1
0080* STL 05-04-27 NPR33      To set version number, see copycode SATINFO
0090* WKK 08-09-30 WKK01      reset variables after error.
0100* WKK 13-09-20 WKK02      allow view AUTHORIZED-LIBRARIES (20 chars|)
0110DEFINE DATA GLOBAL USING TUTO
0120LOCAL
0130* JWO01 start
01401 #DOT                         (A10)  CONST <'.'>
01501 INT-PARAMETER
0160  2 INT-FUNCTION               (A01)
0170  2 INT-RESP                   (N04)
0180  2 INT-TEXT                   (A72)
0190  2 INT-PRODUCTNAME            (A03)
0200  2 INT-ACTIVE                 (A01)
0210  2 INT-EXTENT                 (I02)
0220  2 INT-VERSION                (I01)   INIT <3>
0230  2 INT-RELEASE                (I01)   INIT <5>   /* WKK02 for UNIX
0240  2 INT-SMLEVEL                (I01)   INIT <3>   /* compile
0250  2 INT-IUPDLEVEL              (I01)
0260  2 INT-DATE                   (N08)
0270  2 INT-TIME                   (N07)
0280  2 INT-USER                   (A08)
0290  2 INT-REM                    (A16)
0300  2 INT-SAG                    (I04/1:20)
0310  2 INT-PRODS                  (A03/1:100)
0320* JWO01 end
03301  #I  (P3)
03401  #J  (P3)
03501  #X  (P3)
03601  #Y  (P3)
03701  #Z  (P3)
03801  #SPFCMD (A30)
0390* JWO01 line below commented
0400* 1  #VERSION (A5)
0410*
04201  SYSTEM-INFO VIEW OF SYSTEM-INFO
0430  2 ERROR-CODE
0440  2 ERROR-TEXT
0450  2 SYSTEM-RELEASE
0460  2 SYSTEM-TYPE
0470  2 PRODUCT-NAME
0480*
04901  #TEMP-KEY   (A5)
0500*
05101  #MEMBER     (A8)
05201  #SAVE-MSG   (N4)
05301  #SAVE-TXT   (A65)
0540*
05501  LANGUAGE-CHANGED (L)
0560END-DEFINE
0570*
0580SET CONTROL 'WB'
0590SET CONTROL 'U'
0600RELEASE STACK
0610IF ##INIT
0620  ASSIGN ##PLAN  = *LANGUAGE
0630  PERFORM GET-DEFAULT-NODE-INFO
0640  IF ##MSG-NR NE 0
0650    ASSIGN #SAVE-MSG = ##MSG-NR
0660    ASSIGN #SAVE-TXT = ##MSG-TXT1
0670  END-IF
0680  IF ##OPSYSTEM NE ' '
0690    PERFORM READ-TUTORIAL
0700  END-IF
0710  IF ##MSG-NR = 0 AND #SAVE-MSG NE 0
0720    ASSIGN ##MSG-NR = #SAVE-MSG
0730    ASSIGN ##MSG-TXT1 = #SAVE-TXT
0740  END-IF
0750  IF ##MSG-NR = 0
0760    SET CONTROL 'N'
0770  ELSE
0780    RESET ##INIT
0790  END-IF
0800* JWO01 start
0810  ASSIGN INT-FUNCTION    = 'R'             /* Read Version Info
0820  ASSIGN INT-PRODUCTNAME = 'ESY'
0830*
0840  PERFORM  CALL-IT
0850*
0860  COMPRESS INT-VERSION #DOT INT-RELEASE #DOT INT-SMLEVEL
0870    INTO ##VERSION LEAVING NO
0880* JWO01 end
0890END-IF
0900*
0910ENTER.
0920REPEAT
0930*
0940  PERFORM PF-KEY
0950  RESET ##MARK (##ACT-LI:##ACT-LI + 14)
0960  INPUT TEXT *##MSG-NR,
0970    ##MSG-TXT1,
0980    ##MSG-TXT2
0990    MARK *##MARK (##SEL-LI)
1000    USING MAP 'TUTO001&'
1010*
1020  RESET ##MSG-NR ##MSG-TXT1 ##MSG-TXT2
1030*
1040  IF ##INIT
1050    RESET ##INIT
1060    CALLNAT 'TUTODB' ##NODE ##MSG ##TUTO
1070  END-IF
1080*
1090  DECIDE ON FIRST VALUE OF *PF-KEY
1100    VALUE 'PF3'
1110* JWO02      start
1120      RESET *ERROR-TA
1130      IF *APPLIC-ID EQ MASK ('SYS'*)
1140        STACK TOP COMMAND 'RETURN'
1150      END-IF
1160* JWO02      end
1170      STOP
1180    VALUE 'PF5'  IF ##PROGRAM = 'TUTO0001'
1190        ASSIGN ##PROGRAM  = ##SPROGRAM
1200        ASSIGN ##OPSYSTEM = ##SOP-SYSTEM
1210        PERFORM READ-TUTORIAL
1220      ELSE
1230        ASSIGN ##SPROGRAM = ##PROGRAM
1240        ASSIGN ##SOP-SYSTEM = ##OPSYSTEM
1250        ASSIGN ##PROGRAM = 'TUTO0001'
1260        ASSIGN ##OPSYSTEM = ##PROGRAM
1270        PERFORM READ-TUTORIAL
1280        ASSIGN #TEMP-KEY = ##PF-KEY (05)
1290        ASSIGN ##PF-KEY (05) = ##PF-KEY (06)
1300        ASSIGN ##PF-KEY (06) = #TEMP-KEY
1310        PERFORM PF-KEY
1320      END-IF
1330    VALUE 'PF7'  COMPUTE ##ACT-LI = ##ACT-LI - 15
1340      IF ##ACT-LI < 1
1350        ASSIGN ##ACT-LI = 1
1360      END-IF
1370    VALUE 'PF8'  COMPUTE ##ACT-LI = ##ACT-LI + 15
1380      IF ##ACT-LI + 15 > ##MAX-LI
1390        COMPUTE ##ACT-LI = ##MAX-LI - 14
1400        IF ##ACT-LI < 1
1410          ASSIGN ##ACT-LI = 1
1420        END-IF
1430      END-IF
1440    VALUE 'PF10' CALLNAT 'TUTOLN-N' LANGUAGE-CHANGED
1450      IF LANGUAGE-CHANGED
1460        ASSIGN ##PLAN  = *LANGUAGE
1470        PERFORM READ-TUTORIAL
1480      END-IF
1490    VALUE 'PF12' CALLNAT 'TUTODB' ##NODE ##MSG ##TUTO
1500    NONE         COMPUTE #J = ##ACT-LI + 14
1510      FOR #I = ##ACT-LI TO #J
1520        DECIDE FOR FIRST CONDITION
1530          WHEN ##MARK (#I) = ' '
1540            IGNORE
1550          WHEN ##MARK (#I) NE ' ' AND #I > ##MAX-LI
1560            REINPUT *1001 MARK *##MARK (#I)
1570          WHEN ##MARK (#I) = 'E' OR = 'X' OR = 'L' OR = 'V'
1580            OR = 'H'
1590            PERFORM LINE-COMMAND
1600            ESCAPE BOTTOM (ENTER.)
1610          WHEN NONE
1620            REINPUT *1002 MARK *##MARK (#I)
1630        END-DECIDE
1640      END-FOR
1650  END-DECIDE
1660END-REPEAT
1670*
1680* --------------------------------- PF-KEY ----------------------------
1690*
1700DEFINE SUBROUTINE PF-KEY
1710SET KEY PF1  = PGM  NAMED ##PF-KEY (01)
1720PF2  = PGM  NAMED ##PF-KEY (02)
1730PF3  = PGM  NAMED ##PF-KEY (03)
1740PF4  = PGM  NAMED ##PF-KEY (04)
1750PF5  = PGM  NAMED ##PF-KEY (05)
1760PF6  = PGM  NAMED OFF
1770PF7  = PGM  NAMED ##PF-KEY (07)
1780PF8  = PGM  NAMED ##PF-KEY (08)
1790PF9  = PGM  NAMED ##PF-KEY (09)
1800PF10 = PGM  NAMED ##PF-KEY (10)
1810PF11 = PGM  NAMED ##PF-KEY (11)
1820PF12 = PGM  NAMED ##PF-KEY (12)
1830END-SUBROUTINE
1840*
1850* --------------------------------- READ-TUTORIAL ---------------------
1860*
1870DEFINE SUBROUTINE READ-TUTORIAL
1880REPEAT
1890  RESET ##MSG
1900  CALLNAT 'TUTORD' ##MSG ##TUTO
1910* - WKK01 start ----
1920  IF ##MSG-NR > 0
1930  ##PROGRAM := ##SPROGRAM
1940  ##OPSYSTEM := ##SOP-SYSTEM
1950  END-IF
1960* - WKK01 end   ----
1970  DECIDE FOR FIRST CONDITION
1980    WHEN ##MSG-NR = 0
1990      IGNORE
2000    WHEN ##PLAN   = 1
2010      IGNORE
2020    WHEN ANY
2030      ESCAPE BOTTOM
2040    WHEN NONE
2050      ASSIGN ##PLAN = 1
2060      ASSIGN *LANGUAGE = 1
2070  END-DECIDE
2080END-REPEAT
2090END-SUBROUTINE
2100*
2110* --------------------------------- LINE-COMMAND ----------------------
2120*
2130DEFINE SUBROUTINE LINE-COMMAND
2140RELEASE STACK
2150STACK TOP COMMAND 'TUTO001P'
2160DECIDE ON FIRST VALUE OF ##MARK (#I)
2170  VALUE 'E'
2180    PERFORM CREATE-HEADER
2190    IF ##SPF
2200      RELEASE STACK
2210      STACK TOP COMMAND 'RETURN'
2220      COMPRESS 'SPF E' *APPLIC-ID TO #SPFCMD
2230      COMPRESS #SPFCMD '(' ##PGM (#I) ')'  TO #SPFCMD
2240        LEAVING NO SPACE
2250      STACK TOP COMMAND #SPFCMD
2260      STACK TOP COMMAND 'SETUP * TUTO001P'
2270    ELSE
2280      STACK TOP COMMAND 'E'   ##PGM (#I)
2290    END-IF
2300  VALUE 'X'
2310    STACK TOP COMMAND 'EX'  ##PGM (#I)
2320    PERFORM CREATE-HEADER
2330  VALUE 'L'
2340    STACK TOP COMMAND 'L'   ##PGM (#I)
2350  VALUE 'V'
2360* --- WKK02 start ----------------------------------
2370* handle case that view name is longer than 16 chars
2380    IF  ##VIEW (#I) = 'AUTHORIZED-LIBS'
2390      STACK TOP COMMAND 'L V AUTHORIZED-LIBRARIES '
2400    ELSE
2410      STACK TOP COMMAND 'L V' ##VIEW (#I)
2420    END-IF
2430*     STACK TOP COMMAND 'L V' ##VIEW (#I)
2440* --- WKK02 end   ----------------------------------
2450  VALUE 'H'
2460    CALLNAT 'TUTOHT-N' ##VIEW (#I)
2470      #MEMBER
2480      ##MSG
2490    STACK TOP COMMAND 'TUTOHLP' #MEMBER
2500  ANY VALUE
2510    SET KEY PF3 = 'TUTOINTR' NAMED ##PF-KEY (03)
2520    ASSIGN ##SEL-LI = #I
2530  NONE VALUE
2540    IGNORE
2550END-DECIDE
2560END-SUBROUTINE
2570* --------------------------------- get default node info -------------
2580DEFINE SUBROUTINE GET-DEFAULT-NODE-INFO
2590  FNDI.
2600FIND(1) SYSTEM-INFO
2610    WITH NODE = ##NODE
2620
2630  IF ERROR-CODE NE 0
2640    ASSIGN ##MSG-NR = 1000
2650    ASSIGN ##MSG-TXT1 = ERROR-TEXT
2660    ASSIGN ##OPSYS    = ##OPSYSTEM
2670    ESCAPE BOTTOM
2680  END-IF
2690  ASSIGN ##OPSYS = SYSTEM-TYPE
2700  IF   SUBSTR(SYSTEM-RELEASE, 1, 3) = 'SP6'
2710    THEN ASSIGN ##OPSYSTEM = 'OS/390'
2720  ELSE
2730    IF   SUBSTR(SYSTEM-RELEASE, 1, 3) = 'SP7'
2740      THEN ASSIGN ##OPSYSTEM = 'z/OS'
2750    ELSE ASSIGN ##OPSYSTEM = SYSTEM-TYPE
2760    END-IF
2770  END-IF
2780
2790* ASSIGN ##OPSYSTEM = 'OS/390'
2800END-FIND
2810END-SUBROUTINE
2820* --------------------------------- create header ---------------------
2830DEFINE SUBROUTINE CREATE-HEADER
2840ASSIGN #X = 49
2850REPEAT
2860  UNTIL #X = 0 OR ##D (#I,#X) NE ' '
2870  COMPUTE #X = #X - 1
2880END-REPEAT
2890RESET ##TITLE
2900IF #X NE 0
2910  COMPUTE #Y = (78 - #X) / 2 + 1
2920  COMPUTE #Z = #Y + #X - 1
2930  MOVE ##D(#I,1:#X) TO ##T (#Y:#Z)
2940END-IF
2950END-SUBROUTINE
2960*
2970* JWO01 start
2980* ---------------------------------------------- call it ---------------
2990DEFINE SUBROUTINE CALL-IT
3000CALLNAT 'INTSUB2' INT-FUNCTION
3010  INT-RESP
3020  INT-TEXT
3030  INT-PRODUCTNAME
3040  INT-ACTIVE
3050  INT-EXTENT
3060  INT-VERSION
3070  INT-RELEASE
3080  INT-SMLEVEL
3090  INT-IUPDLEVEL
3100  INT-DATE
3110  INT-TIME
3120  INT-USER
3130  INT-REM
3140  INT-SAG         (*)
3150  INT-PRODS       (*)
3160END-SUBROUTINE
3170* JWO01 end
3180END
