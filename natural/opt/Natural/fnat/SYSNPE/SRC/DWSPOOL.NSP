0010* Write directly to spool
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL  USING WRTSPL-L
0060   LOCAL  USING DCATLGL
0070   LOCAL
0080   01 #SPOOL-LINE         (A080)
0090   01 REDEFINE #SPOOL-LINE
0100      02 #SPOOL-DSNAME    (A054)
0110      02 #SPOOL-DUMMY1    (A002)
0120      02 #SPOOL-TYPE      (A004)
0130      02 #SPOOL-DUMMY2    (A002)
0140      02 #SPOOL-VOLSER    (A006)
0150      02 #SPOOL-DUMMY3    (A002)
0160      02 #SPOOL-LIBRARIAN (A006)
0170END-DEFINE
0180*
0190CAT.
0200FIND CATALOG  WITH NODE = ##NODE
0210*
0220   IF ERROR-CODE > 0
0230      REINPUT ERROR-TEXT
0240   END-IF
0250*
0260   ASSIGN #SPOOL-DSNAME      = CATALOG.DSNAME
0270   ASSIGN #SPOOL-VOLSER      = CATALOG.VOLSER
0280   ASSIGN #SPOOL-TYPE        = CATALOG.TYPE
0290   ASSIGN #SPOOL-LIBRARIAN   = CATALOG.LIBRARIAN
0300*
0310   ASSIGN WRITE-SPOOL.RECORD = #SPOOL-LINE
0320*
0330   PROCESS WRITE-SPOOL USING RECORD        = WRITE-SPOOL.RECORD
0340                         ,   RECORD-LENGTH = 80
0350                         ,   CLASS         = 'X'
0360                         ,   NODE          = ##NODE
0370*
0380   PERFORM CHECK-ERROR
0390*
0400END-FIND
0410*
0420PROCESS WRITE-SPOOL USING FUNCTION      = 'CLOSE'
0430                      ,   NODE          = ##NODE
0440*
0450PERFORM CHECK-ERROR
0460*
0470WRITE NOTITLE /// *COUNTER (CAT.) (AD=I) 'records written to spool' (I)
0480*
0490DEFINE SUBROUTINE CHECK-ERROR
0500   IF WRITE-SPOOL.ERROR-CODE NE 0
0510      ASSIGN ##MSG-NR = 1000
0520      ASSIGN ##MSG-TXT1 = WRITE-SPOOL.ERROR-TEXT
0530      STOP
0540   END-IF
0550END-SUBROUTINE
0560*
0570END
