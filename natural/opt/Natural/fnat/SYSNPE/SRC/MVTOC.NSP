0010* Program   MVTOC
0020* View      VTOC
0030*
0040* Function  List VTOC of a specific volume
0050*
0060* ----------------------------------------------------------------------
0070* Maintenance..:
0080* hka 97-12-03   NPE213:   make it y2000 compliant
0090* ======================================================================
0100*
0110DEFINE DATA GLOBAL USING TUTO
0120            LOCAL  USING VTOC-L
0130            LOCAL
014001  #VOLSER                 (A6)
015001  #EXTENT-TYPE            (A4)   INIT <'USED'>
016001  #DSNAME                 (A54)  INIT <'*'>
0170*
018001  #DISP-1 (15)
0190    02 DSNAME                 (A48)
020001  #DISP-2-HEAD-HALF         (A30)
021001  REDEFINE #DISP-2-HEAD-HALF
0220    02  #DISP-2-HEAD-HALF-A   (A1/1:30)
023001  #DISP-2-HEAD-FULL-A       (A1/1:60)
024001  REDEFINE #DISP-2-HEAD-FULL-A
0250    02 #DISP-2-HEADER         (A60)
026001  #DISP-2-HEADER-TEXT-1     (A60) INIT              /* english
0270* ----1...5....10...15...20...25...30...35...40...45...50...55...60----*
0280    <'Cyls Usage (percent)          Creation   Expiration LastRef'>
0290*                                   YYYY-MM-DD YYYY-MM-DD YYYYMMDD
030001  #DISP-2-HEADER-TEXT-2     (A60) INIT              /* german
0310* ----1...5....10...15...20...25...30...35...40...45...50...55...60----*
0320    <'Cyls Benutzt (Prozent)        Angelegt   Verf{llt   Zuletzt'>
033001  #DISP-2 (15)
0340    02 #DISP-2-LINE-HALF      (A30)
0350    02 REDEFINE #DISP-2-LINE-HALF
0360       03 #DISP-2-LINE-HALF-A (A1/1:30)
0370    02 #DISP-2-LINE-FULL-A    (A1/1:60)
0380    02 REDEFINE #DISP-2-LINE-FULL-A
0390       03 CYLINDERS-ALLOCATED (N4)
0400       03 #FILLER-1           (A1)
0410       03 PERCENT-USED        (N3)
0420       03 #FILLER-2           (A1)
0430       03 #BAR                (A20)
0440       03 #FILLER-3           (A1)
0450       03 #CREATION-DATE      (A10)
0460       03 #FILLER-4           (A1)
0470       03 #EXPIRATION-DATE    (A10)
0480       03 #FILLER-5           (A1)
0490       03 #LAST-REFERENCE     (A8)
0500*
051001  #I1        (I2)
052001  #I2        (I2)  /* gives start position within #DISP-2-LINE-FULL-A
053001  #LENGTH    (I2)
054001  #COUNT     (I4)
055001  #CVAR      (C)
0560*
0570END-DEFINE
0580#CVAR := (AD=I)
0590#I2   := 1
0600DECIDE ON FIRST VALUE OF *LANGUAGE
0610  VALUE 1       #DISP-2-HEADER := #DISP-2-HEADER-TEXT-1
0620  VALUE 2       #DISP-2-HEADER := #DISP-2-HEADER-TEXT-2
0630  NONE VALUE IGNORE
0640END-DECIDE
0650PERFORM PF-KEY
0660PERFORM SCREEN-IO
0670*
0680* ------------------------------ MAIN LOOP ----------------------------
0690*
0700REPEAT
0710FND.
0720FIND VTOC
0730     WITH NODE         =  ##NODE
0740      AND  VOLSER      =  #VOLSER
0750      AND  DSNAME      =  #DSNAME
0760      AND  EXTENT-TYPE =  #EXTENT-TYPE
0770*
0780  IF ERROR-CODE NE 0
0790     ASSIGN ##MSG-NR   = 1000
0800     ASSIGN ##MSG-TXT1 = ERROR-TEXT
0810     ESCAPE BOTTOM (FND.)
0820  END-IF
0830*
0840  IF #I1 = 15
0850     PERFORM SCREEN-IO
0860     IF #CVAR MODIFIED
0870      ESCAPE BOTTOM (FND.)
0880     END-IF
0890  END-IF
0900  ADD 1 TO #I1
0910  ADD 1 TO #COUNT
0920  MOVE BY NAME VTOC TO #DISP-1 (#I1)
0930  MOVE BY NAME VTOC TO #DISP-2 (#I1)
0940  COMPUTE #LENGTH = VTOC.PERCENT-USED / 5
0950  RESET #BAR (#I1)
0960  MOVE ALL '*' TO #BAR (#I1) UNTIL #LENGTH
0970  DECIDE ON FIRST VALUE OF *LANGUAGE
0980    VALUE 1
0990      MOVE EDITED VTOC.CREATION-DATX (EM=YYYY-MM-DD)
1000           TO #DISP-2.#CREATION-DATE (#I1)
1010      MOVE EDITED VTOC.EXPIRATION-DATX (EM=YYYY-MM-DD)
1020           TO #DISP-2.#EXPIRATION-DATE (#I1)
1030      MOVE EDITED VTOC.LAST-REFERENCE-DATX (EM=YYYYMMDD)
1040           TO #DISP-2.#LAST-REFERENCE (#I1)
1050    VALUE 2
1060      MOVE EDITED VTOC.CREATION-DATX (EM=DD.MM.YYYY)
1070           TO #DISP-2.#CREATION-DATE (#I1)
1080      MOVE EDITED VTOC.EXPIRATION-DATX (EM=DD.MM.YYYY)
1090           TO #DISP-2.#EXPIRATION-DATE (#I1)
1100      MOVE EDITED VTOC.LAST-REFERENCE-DATX (EM=DDMMYYYY)
1110           TO #DISP-2.#LAST-REFERENCE (#I1)
1120    NONE VALUE IGNORE
1130  END-DECIDE
1140*
1150END-FIND
1160DECIDE FOR FIRST CONDITION
1170  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
1180       IGNORE
1190  WHEN #I1 > 0
1200       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
1210  WHEN #COUNT = 0
1220       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
1230  WHEN ANY
1240       PERFORM SCREEN-IO
1250  WHEN NONE
1260       IGNORE
1270END-DECIDE
1280RESET #COUNT
1290END-REPEAT
1300*
1310* ------------------------------ SCREEN-IO ----------------------------
1320*
1330DEFINE SUBROUTINE SCREEN-IO
1340  #DISP-2-HEAD-HALF-A(1:30) := #DISP-2-HEAD-FULL-A(#I2:#I2+29)
1350  #DISP-2-LINE-HALF-A(*,1:30) := #DISP-2-LINE-FULL-A(*,#I2:#I2+29)
1360  INPUT WITH TEXT *##MSG-NR,
1370                   ##MSG-TXT1,
1380                   ##MSG-TXT2
1390        USING MAP 'MVTOC--&'
1400  /*
1410  DECIDE ON FIRST VALUE *PF-KEY
1420     VALUE 'PF10' #I2 := 1
1430     VALUE 'PF11' #I2 := 31
1440     NONE VALUE   IGNORE
1450  END-DECIDE
1460  /*
1470  RESET ##MSG-NR ##MSG-TXT1 ##MSG-TXT2
1480  IF #DSNAME = ' '
1490     ASSIGN #DSNAME = '*'
1500  END-IF
1510  IF #EXTENT-TYPE = ' '
1520     ASSIGN #EXTENT-TYPE = '*'
1530  END-IF
1540  RESET #DISP-1 (*) #DISP-2 (*) #I1
1550END-SUBROUTINE
1560*
1570* ------------------------------ PF-KEY -------------------------------
1580*
1590DEFINE SUBROUTINE PF-KEY
1600SET KEY PF1  = PGM NAMED OFF
1610        PF2  = PGM NAMED OFF
1620        PF4  = PGM NAMED OFF
1630        PF5  = PGM NAMED OFF
1640        PF6  = PGM NAMED OFF
1650        PF7  = PGM NAMED OFF
1660        PF8  = PGM NAMED OFF
1670        PF9  = PGM NAMED OFF
1680        PF10 = PGM NAMED 'Left'
1690        PF11 = PGM NAMED 'Right'
1700        PF12 = PGM NAMED OFF
1710END-SUBROUTINE
1720END
