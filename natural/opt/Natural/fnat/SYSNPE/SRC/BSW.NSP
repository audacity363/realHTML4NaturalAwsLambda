0010* BS2000 Job-switches maintenance
0020*
0030DEFINE DATA GLOBAL USING TUTO
0040            LOCAL  USING BSW----L
0050            LOCAL
006001  #DISPLAY     (32)
0070    02  MARK                (A1)
0080    02  SWITCH              (N2)
0090    02  REDEFINE SWITCH
0100        03 SWITCH-A         (A2)
0110    02  STATUS              (A3)
0120*
013001  #OPERATION              (A6)
014001  #LIST-OF-SWITCHES       (A64)
015001  #SWITCH-TYPE            (A7)
016001  #FUNCTION               (A8)
017001  #USERID                 (A8)
0180*
019001  #I                      (P2)
0200END-DEFINE
0210ASSIGN #USERID = *INIT-USER
0220FOR #I = 1 TO 32
0230  COMPUTE SWITCH (#I) = #I - 1
0240END-FOR
0250*
0260REPEAT
0270  PERFORM SCREEN-IO
0280FND.
0290  FIND JOB-SWITCHES
0300       WITH NODE           = ##NODE
0310        AND OPTION         = #OPERATION
0320        AND SWITCH-NUMBERS = #LIST-OF-SWITCHES
0330        AND SWITCH-TYPE    = #SWITCH-TYPE
0340        AND FUNCTION       = #FUNCTION
0350        AND USERID         = #USERID
0360*
0370    IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0380       INCLUDE BERR---C
0390       ESCAPE BOTTOM (FND.)
0400    END-IF
0410*
0420  END-FIND
0430END-REPEAT
0440*
0450* ------------------------------ SCREEN-IO ----------------------------
0460*
0470DEFINE SUBROUTINE SCREEN-IO
0480IF ##MSG-NR = 0
0490   FOR #I = 1 TO 32
0500     DECIDE ON FIRST JOB-SWITCHES.SW-VALUES (#I)
0510       VALUE 'Y' ASSIGN #DISPLAY.STATUS (#I) = 'on'
0520       VALUE 'N' ASSIGN #DISPLAY.STATUS (#I) = 'off'
0530       VALUE ' ' RESET  #DISPLAY.STATUS (#I)
0540       NONE      ASSIGN #DISPLAY.STATUS (#I) = '???'
0550     END-DECIDE
0560   END-FOR
0570END-IF
0580INPUT WITH TEXT *##MSG-NR,
0590                 ##MSG-TXT1,
0600                 ##MSG-TXT2
0610      USING MAP 'BSW----&'
0620RESET ##MSG #LIST-OF-SWITCHES
0630FOR #I = 1 TO 32
0640  IF MARK (#I) = 'X'
0650     COMPRESS #LIST-OF-SWITCHES SWITCH-A (#I) INTO #LIST-OF-SWITCHES
0660              LEAVING NO SPACE
0670  END-IF
0680  RESET MARK (#I)
0690END-FOR
0700END-SUBROUTINE
0710END
