0010*
0020* DISPLAY ACTIVE TASKS
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BACTIVEL
0060            LOCAL
007001  #DISPLAY (15)
0080    02 USER           (A8)
0090    02 JOB-NAME       (A8)
0100    02 TYPE           (A7)
0110    02 CPU-USED       (N7.2)
0120    02 JOB-ID         (A5)
0130    02 ACCOUNT-NUMBER (A8)
0140    02 CPU-M          (A10)
0150    02 TYPE-NUMBER    (N1)
0160*
017001  #USER           (A8)   INIT <'*'>
018001  #JOB-NAME       (A8)   INIT <'*'>
019001  #TYPE           (A6)   INIT <'*'>
020001  #CPU            (N7.2)
0210*
022001  #I              (P2)
023001  #COUNT          (P8)
024001  #CVAR           (C)
0250*
0260END-DEFINE
0270ASSIGN #CVAR = (AD=I)
0280PERFORM PF-KEY
0290PERFORM SCREEN-IO
0300* ------------------------------ MAIN LOOP ----------------------------
0310REPEAT
0320FND.
0330FIND ACTIVE-JOBS WITH NODE      =  ##NODE
0340                 AND  USER      =  #USER
0350                 AND  JOB-NAME  =  #JOB-NAME
0360                 AND  TYPE      =  #TYPE
0370                 AND  CPU-USED  >= #CPU
0380*
0390  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0400     INCLUDE BERR---C
0410     ESCAPE BOTTOM (FND.)
0420  END-IF
0430*
0440  IF #I = 15
0450     PERFORM SCREEN-IO
0460     IF #CVAR MODIFIED
0470        ESCAPE BOTTOM (FND.)
0480     END-IF
0490  END-IF
0500*
0510  ADD 1 TO #I
0520  ADD 1 TO #COUNT
0530  MOVE BY NAME ACTIVE-JOBS TO #DISPLAY (#I)
0540  IF CPU-MAX = 0
0550     ASSIGN #DISPLAY.CPU-M (#I) = 'NTL'
0560    ELSE
0570     MOVE EDITED CPU-MAX (EM=ZZZZZZ9.99) TO #DISPLAY.CPU-M (#I)
0580  END-IF
0590END-FIND
0600*
0610DECIDE FOR FIRST CONDITION
0620  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0630       IGNORE
0640  WHEN #I > 0
0650       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0660  WHEN #COUNT = 0
0670       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0680  WHEN ANY
0690       PERFORM SCREEN-IO
0700  WHEN NONE
0710       IGNORE
0720END-DECIDE
0730RESET #COUNT
0740END-REPEAT
0750*
0760* ------------------------------ SCREEN-IO ----------------------------
0770*
0780DEFINE SUBROUTINE SCREEN-IO
0790INPUT WITH TEXT *##MSG-NR,
0800                 ##MSG-TXT1,
0810                 ##MSG-TXT2
0820      USING MAP 'BACTIVM&'
0830RESET ##MSG
0840DECIDE FOR EVERY CONDITION
0850  WHEN #USER = ' '
0860       ASSIGN #USER = '*'
0870  WHEN #JOB-NAME = ' '
0880       ASSIGN #JOB-NAME = '*'
0890  WHEN #TYPE = ' '
0900       ASSIGN #TYPE = '*'
0910  WHEN NONE
0920       IGNORE
0930END-DECIDE
0940RESET #DISPLAY (*)
0950RESET #I
0960END-SUBROUTINE
0970*
0980* ------------------------------ PF-KEY -------------------------------
0990*
1000INCLUDE C-PFKEY
1010END
