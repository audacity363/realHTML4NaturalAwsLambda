0010* Submit Job via INTERNAL READER
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL  USING DSUBMITL
0060   LOCAL  USING DREADL
0070END-DEFINE
0080*
0090REPEAT
0100   INPUT (AD=MI'_')
0110        / ##TITLE (AD=OI IP=OFF)
0120           // '  Data set.......:' READ-FILE.DSNAME
0130            / '  Library........:' READ-FILE.LIBRARY
0140            / '  Sub library....:' READ-FILE.SUB-LIBRARY
0150            / '  Member.........:' READ-FILE.MEMBER
0160            / '  Member type....:' READ-FILE.MEMBER-TYPE
0170            / '  VSAM catalog...:' READ-FILE.VSAM-CATALOG
0180            / '  Volume.........:' READ-FILE.VOLSER
0190*
0200   FIND READ-FILE WITH DSNAME       = READ-FILE.DSNAME
0210                   AND LIBRARY      = READ-FILE.LIBRARY
0220                   AND SUB-LIBRARY  = READ-FILE.SUB-LIBRARY
0230                   AND MEMBER       = READ-FILE.MEMBER
0240                   AND MEMBER-TYPE  = READ-FILE.MEMBER-TYPE
0250                   AND VSAM-CATALOG = READ-FILE.VSAM-CATALOG
0260                   AND VOLSER       = READ-FILE.VOLSER
0270                   AND NODE         = ##NODE
0280      IF ERROR-CODE > 0
0290         REINPUT ERROR-TEXT
0300      END-IF
0310      ASSIGN SUBMIT.CARD = RECORD
0320      PERFORM SUBMIT
0330      IF *COUNTER = 1
0340         EXAMINE RECORD FOR 'JNM=' GIVING POSITION #I
0350         ASSIGN #JOB-NAME-BYTE (*) = #BYTE (#I+4:#I+11)
0360         SEPARATE #JOB-NAME INTO #TOKEN (*)
0370         ASSIGN #JOB-NAME = #TOKEN (1)
0380      END-IF
0390   END-FIND
0400*
0410   ASSIGN SUBMIT.FUNCTION = 'CLOSE'
0420   PERFORM SUBMIT
0430*
0440   REINPUT *1020,#JOB-NAME,SUBMIT.JOB-NUMBER
0450*
0460END-REPEAT
0470*
0480*
0490DEFINE SUBROUTINE SUBMIT
0500   PROCESS SUBMIT USING CARD     = SUBMIT.CARD
0510                    ,   FUNCTION = SUBMIT.FUNCTION
0520                    ,   NODE     = ##NODE
0530END-SUBROUTINE
0540*
0550END
