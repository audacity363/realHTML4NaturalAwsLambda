0010*
0020* Display ESY User
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BNPUSR-L
0060            LOCAL
007001  #DISPLAY (15)
0080    02 USER-ID             (A8)
0090    02 ADABAS-ID           (A8)
0100    02 NATPROC-ID          (A4)
0110    02 LAST-ACTIVITY       (N5)
0120    02 CPU                 (N5.2)
0130    02 IO-COUNT            (N7)
0140    02 ADABAS-EXTENDED-ID  (B28)
0150    02 TASK-ID             (A16)
0160    02 INTERNAL-TASK       (A03)
0170*
018001  #USER-ID        (A8)   INIT <'*'>
019001  #FULL-SCAN      (A3)
0200*
021001  #I              (P2)
022001  #COUNT          (P8)
023001  #CVAR           (C)
0240*
0250END-DEFINE
0260ASSIGN #CVAR = (AD=I)
0270PERFORM PF-KEY
0280PERFORM SCREEN-IO
0290* ------------------------------ MAIN LOOP ----------------------------
0300REPEAT
0310FND.
0320FIND NATPROC-USERS
0330     WITH NODE     =  ##NODE
0340      AND USER-ID  = #USER-ID
0350      AND FULL-SCAN = #FULL-SCAN
0360*
0370  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0380     INCLUDE BERR---C
0390     ESCAPE BOTTOM (FND.)
0400  END-IF
0410*
0420  IF #I = 15
0430     PERFORM SCREEN-IO
0440     IF #CVAR MODIFIED
0450        ESCAPE BOTTOM (FND.)
0460     END-IF
0470  END-IF
0480*
0490  ADD 1 TO #I
0500  ADD 1 TO #COUNT
0510  MOVE BY NAME NATPROC-USERS TO #DISPLAY (#I)
0520  MOVE EDITED NATPROC-USERS.ADABAS-ID (EM=HHHH) TO
0530       #DISPLAY.ADABAS-ID (#I)
0540END-FIND
0550*
0560DECIDE FOR FIRST CONDITION
0570  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0580       IGNORE
0590  WHEN #I > 0
0600       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0610  WHEN #COUNT = 0
0620       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0630  WHEN ANY
0640       PERFORM SCREEN-IO
0650  WHEN NONE
0660       IGNORE
0670END-DECIDE
0680RESET #COUNT
0690END-REPEAT
0700*
0710* ------------------------------ SCREEN-IO ----------------------------
0720*
0730DEFINE SUBROUTINE SCREEN-IO
0740INPUT WITH TEXT *##MSG-NR,
0750                 ##MSG-TXT1,
0760                 ##MSG-TXT2
0770      USING MAP 'BNPUSR-&'
0780RESET ##MSG
0790DECIDE FOR EVERY CONDITION
0800  WHEN #USER-ID = ' '
0810       ASSIGN #USER-ID = '*'
0820  WHEN NONE
0830       IGNORE
0840END-DECIDE
0850RESET #DISPLAY (*)
0860RESET #I
0870END-SUBROUTINE
0880*
0890* ------------------------------ PF-KEY -------------------------------
0900*
0910INCLUDE C-PFKEY
0920END
