0010* Program   MLOADMB
0020* View      LOAD-MODULE
0030*
0040* Function  Return Data on CSECT's
0050*           version
0060*
0070* Maintenance..:
0080* STL 2005-05-02   NPE331:   Field BLOCK-TTR removed from DDM
0090* ----------------------------------------------------------------------
0100DEFINE DATA GLOBAL USING TUTO
0110            LOCAL  USING LOADMODL
0120            LOCAL
013001  #DISPLAY (14)
0140    02 BLK-TTR      (A6)
0150    02 BLK-OFFSET   (A8)
0160    02 OFFSET       (A8)
0170    02 SEGMENT      (A20)
0180    02 HEX          (A40)
0190*
020001  #DSNAME         (A54)
021001  #VOLSER         (A6)
022001  #MEMBER         (A10)
023001  #PASSWORD       (A8)
024001  #ESD-NAME       (A8)
0250*
026001  END-OF-PROCESSING (L)
027001  #I              (P2)
028001  #COUNT          (P8)
029001  #CVAR           (C)
0300END-DEFINE
0310INPUT #DSNAME
0320    / #VOLSER
0330    / #MEMBER
0340    / #PASSWORD
0350    / #ESD-NAME
0360PERFORM PF-KEY
0370SET KEY PF3 = PGM
0380* --------------------------------------- main ------------------------
0390REPEAT
0400FND.
0410FIND LOAD-MODULE
0420     WITH NODE           =  ##NODE
0430     AND  FUNCTION       =  'DATA'
0440     AND  DSNAME         =  #DSNAME
0450     AND  VOLSER         =  #VOLSER
0460     AND  MEMBER         =  #MEMBER
0470     AND  PASSWORD       =  #PASSWORD
0480     AND  ESD-NAME       =  #ESD-NAME
0490     AND  SEGMENT-LENGTH =  20
0500*
0510  IF ERROR-CODE NE 0
0520     ASSIGN ##MSG-NR = 1000
0530     ASSIGN ##MSG-TXT1 = ERROR-TEXT
0540     ESCAPE BOTTOM (FND.)
0550  END-IF
0560*
0570  IF #I = 14
0580     PERFORM SCREEN-IO
0590     IF END-OF-PROCESSING
0600        ESCAPE BOTTOM (FND.)
0610     END-IF
0620  END-IF
0630*
0640  ADD 1 TO #I
0650  ADD 1 TO #COUNT
0660  MOVE SEGMENT (FND.)                        TO #DISPLAY.SEGMENT (#I)
0670* MOVE EDITED BLOCK-TTR    (FND.) (EM=H(3))  TO #DISPLAY.BLK-TTR (#I)
0680  MOVE EDITED H'000000'           (EM=H(3))  TO #DISPLAY.BLK-TTR (#I)
0690  MOVE EDITED BLOCK-OFFSET (FND.) (EM=H(4))  TO #DISPLAY.BLK-OFFSET (#I)
0700  MOVE EDITED CSECT-OFFSET (FND.) (EM=H(4))  TO #DISPLAY.OFFSET (#I)
0710  MOVE EDITED SEGMENT      (FND.) (EM=H(20)) TO #DISPLAY.HEX (#I)
0720END-FIND
0730*
0740DECIDE FOR FIRST CONDITION
0750  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0760       IGNORE
0770  WHEN #I > 0
0780       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0790  WHEN #COUNT = 0
0800       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0810  WHEN ANY
0820       PERFORM SCREEN-IO
0830  WHEN NONE
0840       IGNORE
0850END-DECIDE
0860RESET #COUNT
0870UNTIL END-OF-PROCESSING
0880END-REPEAT
0890* ------------------------------ screen io ----------------------------
0900DEFINE SUBROUTINE SCREEN-IO
0910INPUT WITH TEXT *##MSG-NR,
0920                 ##MSG-TXT1,
0930                 ##MSG-TXT2
0940      USING MAP 'MLOADMB&'
0950IF *PF-KEY = 'PF3'
0960   ASSIGN END-OF-PROCESSING = TRUE
0970END-IF
0980RESET ##MSG
0990RESET #DISPLAY (*)
1000RESET #I
1010END-SUBROUTINE
1020* ------------------------------ pf key -------------------------------
1030INCLUDE C-PFKEY
1040END
