0010* Program   BTASKI
0020* View      TASK-INFO
0030*
0040* Function  Display Task Information
0050*
0060* ----------------------------------------------------------------------
0070* Maintenance..:
0080* hka 97-12-03   NPE213:   make it y2000 compliant
0090* ======================================================================
0100*
0110DEFINE DATA GLOBAL USING TUTO
0120            LOCAL  USING BACTIVEL
0130            LOCAL  USING BTASKI-L
0140            LOCAL
015001  #DISPLAY (15)
0160    02 USER           (A8)
0170    02 JOB-NAME       (A8)
0180    02 JOB-ID         (A5)
0190    02 TYPE-NUMBER    (N1)
0200    02 LAST-COMMAND   (A12)
0210    02 LOADED-PROGRAM (A20)
0220    02 LOGON-DATX     (D)
0230    02 #LOGON-TIME    (A5)
0240*
025001  #USER           (A8)   INIT <'*'>
026001  #JOB-NAME       (A8)   INIT <'*'>
0270*
028001  #I              (P2)
029001  #COUNT          (P8)
030001  #CVAR           (C)
0310*
0320END-DEFINE
0330ASSIGN #CVAR = (AD=I)
0340PERFORM PF-KEY
0350PERFORM SCREEN-IO
0360* ------------------------------ MAIN LOOP ----------------------------
0370REPEAT
0380FNDAJ.
0390FIND ACTIVE-JOBS
0400     WITH NODE       =  ##NODE
0410      AND  USER      =  #USER
0420      AND  JOB-NAME  =  #JOB-NAME
0430*
0440  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0450     INCLUDE BERR---C
0460     ESCAPE BOTTOM (FNDAJ.)
0470  END-IF
0480*
0490FNDTI.
0500  FIND TASK-INFO
0510       WITH NODE       =  ##NODE
0520        AND  JOB-ID    =  JOB-ID (FNDAJ.)
0530*
0540    IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0550       INCLUDE BERR---C
0560       ESCAPE BOTTOM (FNDAJ.)
0570    END-IF
0580*
0590    IF #I = 15
0600       PERFORM SCREEN-IO
0610       IF #CVAR MODIFIED
0620          ESCAPE BOTTOM (FNDAJ.)
0630       END-IF
0640    END-IF
0650*
0660    ADD 1 TO #I
0670    ADD 1 TO #COUNT
0680    MOVE BY NAME ACTIVE-JOBS TO #DISPLAY (#I)
0690    MOVE BY NAME TASK-INFO TO #DISPLAY (#I)
0700    MOVE EDITED LOGON-TIMX (EM=HH':'II) TO #DISPLAY.#LOGON-TIME (#I)
0710  END-FIND
0720*
0730END-FIND
0740*
0750DECIDE FOR FIRST CONDITION
0760  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0770       IGNORE
0780  WHEN #I > 0
0790       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0800  WHEN #COUNT = 0
0810       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0820  WHEN ANY
0830       PERFORM SCREEN-IO
0840  WHEN NONE
0850       IGNORE
0860END-DECIDE
0870RESET #COUNT
0880END-REPEAT
0890*
0900* ------------------------------ SCREEN-IO ----------------------------
0910*
0920DEFINE SUBROUTINE SCREEN-IO
0930INPUT WITH TEXT *##MSG-NR,
0940                 ##MSG-TXT1,
0950                 ##MSG-TXT2
0960      USING MAP 'BTASKIM&'
0970RESET ##MSG
0980DECIDE FOR EVERY CONDITION
0990  WHEN #USER = ' '
1000       ASSIGN #USER = '*'
1010  WHEN #JOB-NAME = ' '
1020       ASSIGN #JOB-NAME = '*'
1030  WHEN NONE
1040       IGNORE
1050END-DECIDE
1060RESET #DISPLAY (*)
1070RESET #I
1080END-SUBROUTINE
1090*
1100* ------------------------------ PF-KEY -------------------------------
1110*
1120INCLUDE C-PFKEY
1130END
