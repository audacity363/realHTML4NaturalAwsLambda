0010* Operator-Console
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL  USING BCONSOLL
0060   LOCAL  1 #INDEX (I2)
0070          1 #LEFT  (L) INIT <TRUE>
008001  #SYSTEM-CODE    (A7) INIT  <'EXC'>
009001  REDEFINE #SYSTEM-CODE
0100    02  #SYS-PREF  (A3)
0110    02  #SYS-CODE  (A4)
0120*
013001  #DIRECTION       (A1) INIT <'B'>
014001  #CURRENT-RECORD  (N9)
015001  #MAX-RECORD      (N9)
0160*
017001  #PANNEL          (P1) INIT  <1>
018001  #ENTRIES         (P2) CONST <19>
019001  #PF-KEY          (A4)
0200*
021001  #DISPLAY (20)
0220    02  #LINE          (A78)
0230    02  REDEFINE #LINE
0240        03  TIME-STAMP (A8)
0250        03  FIL1       (A1)
0260        03  JOB-NAME   (A8)
0270        03  FIL2       (A1)
0280        03  TEXT       (A60)
0290END-DEFINE
0300*
0310SET KEY PF7  NAMED '-'
0320SET KEY PF8  NAMED '+'
0330SET KEY PF10 NAMED '<'
0340SET KEY PF11 NAMED '>'
0350*
0360REPEAT
0370FNDC.
0380   FIND (#ENTRIES) CONSOLE
0390        WITH NODE     = ##NODE
0400         AND FUNCTION = 'DISPLAY'
0410         AND READ-DIRECTION = #DIRECTION
0420         AND RECORD-NUMBER = #CURRENT-RECORD
0430*
0440    IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0450       INCLUDE BERR---C
0460       ESCAPE BOTTOM (FNDC.)
0470    END-IF
0480*
0490    COMPUTE #INDEX = 20 - *COUNTER
0500    MOVE BY NAME CONSOLE TO #DISPLAY (#INDEX)
0510*   ASSIGN #DISPLAY.TIME     (#INDEX) = TIME-STAMP
0520*   ASSIGN #DISPLAY.JOB-NAME (#INDEX) = JOB-NAME
0530    ASSIGN #DISPLAY.TEXT     (#INDEX) = NETTO-TXT (#PANNEL)
0540    ASSIGN #CURRENT-RECORD = RECORD-NUMBER
0550*
0560    IF #CURRENT-RECORD > #MAX-RECORD
0570       ASSIGN #MAX-RECORD = #CURRENT-RECORD
0580    END-IF
0590   END-FIND
0600*
0610   PERFORM SCREEN-IO
0620*
0630END-REPEAT
0640*
0650DEFINE SUBROUTINE SCREEN-IO
0660REPEAT
0670   INPUT WITH TEXT *##MSG-NR, ##MSG-TXT1, ##MSG-TXT2
0680         USING MAP 'BCONSOL&'
0690   RESET ##MSG
0700*
0710   ASSIGN #PF-KEY = *PF-KEY
0720*
0730   IF #COMMAND-LINE NE ' '
0740      ASSIGN #FUNCTION = 'OP-CMD'
0750      PERFORM ISSUE-OPERATOR-COMMAND
0760      RESET #COMMAND-LINE
0770      ASSIGN #PF-KEY = 'ENTR'
0780   END-IF
0790*
0800   DECIDE ON FIRST VALUE #PF-KEY
0810      VALUE 'ENTR' RESET #CURRENT-RECORD
0820      VALUE 'PF7'  COMPUTE #CURRENT-RECORD = #CURRENT-RECORD - 1
0830      VALUE 'PF8'  COMPUTE #CURRENT-RECORD = #CURRENT-RECORD
0840                                           + #ENTRIES * 2
0850                                           - 1
0860                   IF #CURRENT-RECORD > #MAX-RECORD
0870                      RESET #CURRENT-RECORD
0880                   END-IF
0890      VALUE 'PF10' COMPUTE #PANNEL = #PANNEL - 1
0900                   COMPUTE #CURRENT-RECORD = #CURRENT-RECORD
0910                                           + #ENTRIES - 1
0920                   IF #PANNEL < 1
0930                      ASSIGN #PANNEL = 1
0940                   END-IF
0950      VALUE 'PF11' COMPUTE #PANNEL = #PANNEL + 1
0960                   COMPUTE #CURRENT-RECORD = #CURRENT-RECORD
0970                                           + #ENTRIES - 1
0980                   IF #PANNEL > 4
0990                      ASSIGN #PANNEL = 4
1000                   END-IF
1010      NONE
1020         IGNORE
1030   END-DECIDE
1040UNTIL ##MSG-NR = 0
1050END-REPEAT
1060END-SUBROUTINE
1070*
1080DEFINE SUBROUTINE ISSUE-OPERATOR-COMMAND
1090   PROCESS CONSOLE USING NODE       = ##NODE
1100                     ,   FUNCTION   = #FUNCTION
1110                     ,   TEXT       = #COMMAND-LINE
1120                  GIVING ERROR-CODE ERROR-TEXT
1130   IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
1140      INCLUDE BERR---C
1150   END-IF
1160END-SUBROUTINE
1170*
1180END
