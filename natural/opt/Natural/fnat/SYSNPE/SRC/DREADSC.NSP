0010* Scan a PDS-member
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL USING READ-L
0060   LOCAL 1 #DSN          (A44)
0070         1 #SCAN-VALUE   (A40)
0080END-DEFINE
0090*
0100REPEAT
0110*
0120INPUT (AD=MI'_')
0130    / ##TITLE (AD=OI IP=OFF)
0140   // '  Data set.......:' READ-FILE.DSNAME
0150    / '  Library........:' READ-FILE.LIBRARY
0160    / '  Sub library....:' READ-FILE.SUB-LIBRARY
0170    / '  Member.........:' READ-FILE.MEMBER
0180    / '  Member type....:' READ-FILE.MEMBER-TYPE
0190    / '  VSAM catalog...:' READ-FILE.VSAM-CATALOG
0200    / '  Volser.........:' READ-FILE.VOLSER
0210   // '  Search string..:' #SCAN-VALUE
0220*
0230WRITE TITLE / ##TITLE (AD=I) /
0240*
0250IF #SCAN-VALUE = ' '
0260   ASSIGN #SCAN-VALUE = '*'
0270END-IF
0280*
0290FIND READ-FILE WITH DSNAME       = READ-FILE.DSNAME
0300                AND LIBRARY      = READ-FILE.LIBRARY
0310                AND SUB-LIBRARY  = READ-FILE.SUB-LIBRARY
0320                AND MEMBER       = READ-FILE.MEMBER
0330                AND MEMBER-TYPE  = READ-FILE.MEMBER-TYPE
0340                AND VSAM-CATALOG = READ-FILE.VSAM-CATALOG
0350                AND VOLSER       = READ-FILE.VOLSER
0360                AND RECORD       = #SCAN-VALUE
0370                AND NODE         = ##NODE
0380*
0390   IF END-OF-FILE = 'YES'
0400      ESCAPE BOTTOM
0410   END-IF
0420*
0430   IF ERROR-CODE > 0
0440      REINPUT ERROR-TEXT
0450   ELSE
0460      WRITE RECORD-NUMBER (AD=I NL=5) RECORD (AL=72)
0470   END-IF
0480END-FIND
0490*
0500END-REPEAT
0510*
0520END
