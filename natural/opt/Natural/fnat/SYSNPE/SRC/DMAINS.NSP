0010*
0020* DISPLAY MAIN STORAGE (VSE)
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050LOCAL  USING MAIN-L
0060LOCAL
0070*
008001 #ADDRESS-NEW             (B4)
009001 #ADDRESS                 (B4)
010001 #OFFSET                  (B4)
011001 #HEX-10                  (B4)    INIT <H'00000010'>
012001 #CV-ADDR                 (C)
013001 #CV-OFF                  (C)
0140*
015001 #DISPLAY                 (16)
0160  02  #AREA                 (A18)
0170  02  REDEFINE #AREA
0180    03  #AREA-F1            (A1)
0190    03  #AREA-CONT          (A16)
0200    03  #AREA-F2            (A1)
0210  02  #AREA-ADR             (B4)
0220  02  #AREA-HEX             (B4/1:4)
0230*
024001 #I                       (P2)
0250END-DEFINE
0260PERFORM PF-KEY
0270SET KEY PF7=PGM NAMED '-'
0280SET KEY PF8=PGM NAMED '+'
0290*
0300* ------------------------------ MAIN LOOP ----------------------------
0310*
0320REPEAT
0330  ASSIGN #ADDRESS-NEW = #ADDRESS
0340*
0350    FOR.
0360  FOR #I = 1 TO 16
0370      FND.
0380    FIND MAIN-STORAGE
0390        WITH NODE     = ##NODE
0400        AND  ADDRESS  = #ADDRESS-NEW
0410*       AND  JOB-NAME = #JOBNAME
0420        AND  OFFSET   = #OFFSET /* H'00000000'
0430        AND  LENGTH   = 16
0440*
0450      IF ERROR-CODE NE 0
0460        ASSIGN ##MSG-NR = 1000
0470        ASSIGN ##MSG-TXT1 = ERROR-TEXT
0480        RESET #AREA-CONT (#I:16) #AREA-ADR (#I:16) #AREA-HEX (#I:16,*)
0490        ESCAPE BOTTOM (FOR.)
0500      END-IF
0510*
0520      ASSIGN #AREA-ADR  (#I)   = #ADDRESS-NEW
0530      ASSIGN #AREA-F1   (#I)   = '*'
0540      ASSIGN #AREA-F2   (#I)   = '*'
0550      ASSIGN #AREA-CONT (#I)   = AREA-CONT (FND.)
0560      ASSIGN #AREA-HEX  (#I,*) = AREA-HEX (FND.,*)
0570      CALLNAT 'HEX-ADD' #ADDRESS-NEW #HEX-10
0580    END-FIND
0590  END-FOR
0600*
0610  PERFORM SCREEN-IO
0620END-REPEAT
0630*
0640* ------------------------------ SCREEN-IO ----------------------------
0650*
0660DEFINE SUBROUTINE SCREEN-IO
0670*
0680ASSIGN #CV-ADDR = (AD=I)
0690ASSIGN #CV-OFF  = (AD=I)
0700*
0710INPUT WITH TEXT *##MSG-NR,
0720  ##MSG-TXT1,
0730  ##MSG-TXT2
0740  USING MAP 'DMAINS-&'
0750*
0760RESET ##MSG
0770DECIDE FOR FIRST CONDITION
0780  WHEN #CV-ADDR MODIFIED
0790    CALLNAT 'HEX-ADD' #ADDRESS #OFFSET
0800    RESET #OFFSET
0810  WHEN #CV-OFF MODIFIED
0820    CALLNAT 'HEX-ADD' #ADDRESS #OFFSET
0830    RESET #OFFSET
0840  WHEN *PF-KEY = 'PF8'
0850    ASSIGN #ADDRESS = #ADDRESS-NEW
0860  WHEN *PF-KEY = 'PF7'
0870    IF #ADDRESS     >= H'00000100'
0880      ASSIGN #HEX-10 = H'00000100'
0890      CALLNAT 'HEX-SUB' #ADDRESS     #HEX-10
0900      RESET INITIAL #HEX-10
0910    ELSE
0920      RESET #ADDRESS #OFFSET
0930    END-IF
0940  WHEN NONE
0950    IGNORE
0960END-DECIDE
0970*
0980END-SUBROUTINE
0990*
1000* ------------------------------ PF-KEY -------------------------------
1010*
1020INCLUDE C-PFKEY
1030END
