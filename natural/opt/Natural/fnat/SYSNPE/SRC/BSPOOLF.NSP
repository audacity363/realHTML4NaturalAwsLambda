0010*
0020* display spool queue
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BSPOOLQL
0060            LOCAL  USING BSPOOLFL
0070            LOCAL
008001  #DISPLAY (15)
0090    02 JOB-NAME          (A8)
0100    02 USER              (A8)
0110    02 JOB-ID            (A5)
0120    02 TYPE-NUMBER       (N1)
0130    02 SIZE              (A6)
0140    02 DSNAME            (A54)
0150*
016001  #JOB-NAME       (A8)   INIT <'*'>
017001  #USER-ID        (A8)   INIT <'*'>
0180*
019001  #I              (P2)
020001  #COUNT          (P8)
021001  #CVAR           (C)
0220*
0230END-DEFINE
0240ASSIGN #CVAR = (AD=I)
0250PERFORM PF-KEY
0260PERFORM SCREEN-IO
0270*
0280* ------------------------------ MAIN LOOP ----------------------------
0290*
0300REPEAT
0310FNDSQ.
0320FIND SPOOL-QUEUE
0330     WITH NODE          =  ##NODE
0340      AND JOB-NAME      =  #JOB-NAME
0350      AND USER          =  #USER-ID
0360      WHERE TYPE-NUMBER (FNDSQ.) > 3
0370*
0380  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0390     INCLUDE BERR---C
0400     ESCAPE BOTTOM (FNDSQ.)
0410  END-IF
0420FNDSF.
0430  FIND SPOOL-FILES
0440       WITH NODE   =  ##NODE
0450       AND JOB-ID =  JOB-ID (FNDSQ.)
0460*
0470    IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0480       INCLUDE BERR---C
0490       ESCAPE BOTTOM (FNDSQ.)
0500    END-IF
0510*
0520    IF #I = 15
0530       PERFORM SCREEN-IO
0540       IF #CVAR MODIFIED
0550          ESCAPE BOTTOM (FNDSQ.)
0560       END-IF
0570    END-IF
0580*
0590    ADD 1 TO #I
0600    ADD 1 TO #COUNT
0610    MOVE BY NAME SPOOL-QUEUE TO #DISPLAY (#I)
0620    MOVE BY NAME SPOOL-FILES TO #DISPLAY (#I)
0630  END-FIND
0640*
0650END-FIND
0660*
0670DECIDE FOR FIRST CONDITION
0680  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0690       IGNORE
0700  WHEN #I > 0
0710       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0720  WHEN #COUNT = 0
0730       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0740  WHEN ANY
0750       PERFORM SCREEN-IO
0760  WHEN NONE
0770       IGNORE
0780END-DECIDE
0790RESET #COUNT
0800END-REPEAT
0810*
0820* ------------------------------ SCREEN-IO ----------------------------
0830*
0840DEFINE SUBROUTINE SCREEN-IO
0850INPUT WITH TEXT *##MSG-NR,
0860                 ##MSG-TXT1,
0870                 ##MSG-TXT2
0880      USING MAP 'BSPOOLF&'
0890RESET ##MSG
0900DECIDE FOR EVERY CONDITION
0910  WHEN #USER-ID = ' '
0920       ASSIGN #USER-ID = '*'
0930  WHEN #JOB-NAME = ' '
0940       ASSIGN #JOB-NAME = '*'
0950  WHEN NONE
0960       IGNORE
0970END-DECIDE
0980RESET #DISPLAY (*)
0990RESET #I
1000END-SUBROUTINE
1010*
1020* ------------------------------ PF-KEY -------------------------------
1030*
1040INCLUDE C-PFKEY
1050END
