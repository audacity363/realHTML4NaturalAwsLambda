0010* Program   MLIBDIR
0020* View      LIB-DIRECTORY
0030*
0040* Function  Display first n records of selected members in a PDS
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA GLOBAL USING TUTO
0090            LOCAL  USING LIBDIR-L
0100            LOCAL  USING READ-L
0110            LOCAL
012001  #DISPLAY (15)
0130    02 MEMBER       (A9)
0140    02 RECORD       (A65)
0150*
016001  #DSNAME         (A44)
017001  #VOLSER         (A8)
018001  #MEMBER         (A8)   INIT <'*'>
019001  #NOL            (N2)   INIT <1>
0200*
021001  NEW-MEMBER      (L)
022001  #I              (P2)
023001  #COUNT          (P8)
024001  #CVAR           (C)
0250END-DEFINE
0260ASSIGN #CVAR = (AD=I)
0270PERFORM PF-KEY
0280*
0290* ------------------------------ MAIN LOOP ----------------------------
0300*
0310PERFORM SCREEN-IO
0320REPEAT
0330DIR.
0340FIND LIB-DIRECTORY WITH NODE     =  ##NODE
0350                   AND  DSNAME   =  #DSNAME
0360                   AND  VOLSER   =  #VOLSER
0370                   AND  MEMBER   =  #MEMBER
0380  IF ERROR-CODE NE 0
0390     ASSIGN ##MSG-NR = 1000
0400     ASSIGN ##MSG-TXT1 = ERROR-TEXT
0410     ESCAPE BOTTOM (DIR.)
0420  END-IF
0430  ASSIGN NEW-MEMBER = TRUE
0440FNDR.
0450  FIND (#NOL) READ-FILE WITH DSNAME = DSNAME (DIR.)
0460                        AND  MEMBER = MEMBER (DIR.)
0470                        AND  NODE   = ##NODE
0480     IF #I = 15
0490        PERFORM SCREEN-IO
0500        IF #CVAR MODIFIED
0510           ESCAPE BOTTOM (DIR.)
0520        END-IF
0530     END-IF
0540     ADD 1 TO #I
0550     ADD 1 TO #COUNT
0560     ASSIGN #DISPLAY.RECORD (#I) = READ-FILE.RECORD
0570     IF #I = 1 OR NEW-MEMBER
0580        COMPRESS '>' MEMBER (FNDR.) INTO #DISPLAY.MEMBER (#I)
0590        LEAVING NO SPACE
0600        ASSIGN NEW-MEMBER = FALSE
0610     END-IF
0620  END-FIND
0630END-FIND
0640DECIDE FOR FIRST CONDITION
0650  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0660       IGNORE
0670  WHEN #I > 0
0680       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0690  WHEN #COUNT = 0
0700       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0710  WHEN ANY
0720       PERFORM SCREEN-IO
0730  WHEN NONE
0740       IGNORE
0750END-DECIDE
0760RESET #COUNT
0770END-REPEAT
0780*
0790* ------------------------------ SCREEN-IO ----------------------------
0800*
0810DEFINE SUBROUTINE SCREEN-IO
0820INPUT WITH TEXT *##MSG-NR,
0830                 ##MSG-TXT1,
0840                 ##MSG-TXT2
0850      USING MAP 'MLIBDIR&'
0860RESET ##MSG
0870DECIDE FOR EVERY CONDITION
0880  WHEN #MEMBER = ' '
0890       ASSIGN #MEMBER = '*'
0900  WHEN #NOL = 0
0910       ASSIGN #NOL = 1
0920  WHEN NONE
0930       IGNORE
0940END-DECIDE
0950RESET #DISPLAY (*)
0960RESET #I
0970END-SUBROUTINE
0980*
0990* ------------------------------ PF-KEY -------------------------------
1000*
1010INCLUDE C-PFKEY
1020END
