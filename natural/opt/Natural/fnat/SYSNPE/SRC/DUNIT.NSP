0010* STATUS DISPLAY DASD DEVICES
0020*
0030DEFINE DATA
0040   GLOBAL USING TUTO
0050   LOCAL  USING UNIT-L
0060   LOCAL  1 #DEV-STAT (A8)
0070          1 #SERIES   (A6)
0080          1 #NUMBER   (I4)
0090END-DEFINE
0100FORMAT LS=90
0110*
0120WRITE TITLE ##TITLE (AD=I) //
0130*
0140REPEAT
0150*
0160   INPUT (AD=MI'_')
0170      / ##TITLE (AD=OI IP=OFF)
0180     // '  Device status..:' #DEV-STAT '(ONLINE,OFFLINE)'
0190      / '  Unit type......:' #SERIES   '(3350,3380,...)'
0200*
0210   DECIDE FOR EVERY CONDITION
0220     WHEN #DEV-STAT = ' '
0230          ASSIGN #DEV-STAT = '*'
0240     WHEN #SERIES = ' '
0250          ASSIGN #SERIES = '*'
0260     WHEN NONE
0270          IGNORE
0280   END-DECIDE
0290*
0300   RESET #NUMBER
0310*
0320   FIND UNIT-ATTRIBUTES
0330        WITH CLASS = 'DASD'
0340         AND DEVICE-STATUS = #DEV-STAT
0350         AND SERIES = #SERIES
0360         AND NODE = ##NODE
0370*
0380      DISPLAY (ZP=OFF)
0390         'Unit/Adr'         UNIT
0400         'Unit/Type'        SERIES
0410         'Avail./Unit'      DEVICE-STATUS
0420         'Open/DCB''s'      DCB-COUNT
0430         5X
0440         'Mounted/Volume'   VOLSER
0450         'Mount/Attributes' MOUNT-STATUS
0460         'Avail./of Volume' VOLUME-STATUS
0470         'Free/Cyl'         FREE-CYLINDERS
0480         'Free/Tracks'      FREE-TRACKS
0490         'Free/Extents'     FREE-EXTENTS
0500       ADD 1 TO #NUMBER
0510   END-FIND
0520*
0530   IF #NUMBER = 0
0540      REINPUT *1021
0550   END-IF
0560*
0570END-REPEAT
0580*
0590END
