0010*
0020* Display VTOC of a specific volume
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BVTOC--L
0060            LOCAL
007001  #DISPLAY (15)
0080    02 DSNAME                 (A54)
0090    02 VOLSER                 (A6)
0100    02 FILE-SIZE              (N7)
0110*
012001  #DSNAME                 (A54)  INIT <'*'>
013001  #VOLSER                 (A6)
014001  #FILE-SIZE              (N7)
0150*
016001  #I              (P2)
017001  #COUNT          (P8)
018001  #CVAR           (C)
0190*
0200END-DEFINE
0210ASSIGN #CVAR = (AD=I)
0220PERFORM PF-KEY
0230PERFORM SCREEN-IO
0240*
0250* ------------------------------ MAIN LOOP ----------------------------
0260*
0270REPEAT
0280FND.
0290FIND VTOC
0300     WITH NODE        =  ##NODE
0310      AND  VOLSER     =  #VOLSER
0320      AND  DSNAME     =  #DSNAME
0330      AND  FILE-SIZE >=  #FILE-SIZE
0340*
0350  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0360     INCLUDE BERR---C
0370     ESCAPE BOTTOM (FND.)
0380  END-IF
0390*
0400  IF #I = 15
0410     PERFORM SCREEN-IO
0420     IF #CVAR MODIFIED
0430      ESCAPE BOTTOM (FND.)
0440     END-IF
0450  END-IF
0460  ADD 1 TO #I
0470  ADD 1 TO #COUNT
0480  MOVE BY NAME VTOC TO #DISPLAY (#I)
0490*
0500END-FIND
0510DECIDE FOR FIRST CONDITION
0520  WHEN ##MSG-NR NE 0                     /* ERROR OCCURED
0530       IGNORE
0540  WHEN #I > 0
0550       ASSIGN ##MSG-NR = 1030            /* BOTTOM OF DATA
0560  WHEN #COUNT = 0
0570       ASSIGN ##MSG-NR = 1031            /* NOTHING FOUND
0580  WHEN ANY
0590       PERFORM SCREEN-IO
0600  WHEN NONE
0610       IGNORE
0620END-DECIDE
0630RESET #COUNT
0640END-REPEAT
0650*
0660* ------------------------------ SCREEN-IO ----------------------------
0670*
0680DEFINE SUBROUTINE SCREEN-IO
0690INPUT WITH TEXT *##MSG-NR,
0700                 ##MSG-TXT1,
0710                 ##MSG-TXT2
0720      USING MAP 'BVTOC--&'
0730RESET ##MSG-NR ##MSG-TXT1 ##MSG-TXT2
0740IF #DSNAME = ' '
0750   ASSIGN #DSNAME = '*'
0760END-IF
0770RESET #DISPLAY (*)
0780RESET #I
0790END-SUBROUTINE
0800*
0810* ------------------------------ PF-KEY -------------------------------
0820*
0830INCLUDE C-PFKEY
0840END
