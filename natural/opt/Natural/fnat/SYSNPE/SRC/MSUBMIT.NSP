0010* Program   MSUBMIT
0020* View      SUBMIT
0030*
0040* Function  Submit Job via INTERNAL READER
0050*
0060* ----------------------------------------------------------------------
0070*
0080DEFINE DATA
0090   GLOBAL USING TUTO
0100   LOCAL  USING SUBMIT-L
0110   LOCAL  USING READ-L
0120END-DEFINE
0130*
0140ASSIGN #TARGET-NODE = ##NODE
0150*
0160REPEAT
0170   INPUT (AD=MIL'_' SG=OFF)
0180        / ##TITLE (AD=OI IP=OFF)
0190       // '  DSName .......:' READ-FILE.DSNAME
0200        / '  Member .......:' READ-FILE.MEMBER
0210        / '  Volume .......:' READ-FILE.VOLSER
0220       // '  Target-Node ..:' #TARGET-NODE
0230*
0240   FIND READ-FILE WITH DSNAME = READ-FILE.DSNAME
0250                   AND MEMBER = READ-FILE.MEMBER
0260                   AND VOLSER = READ-FILE.VOLSER
0270                   AND NODE   = ##NODE
0280      IF ERROR-CODE > 0
0290         REINPUT ERROR-TEXT
0300      END-IF
0310      IF END-OF-FILE = 'YES'
0320         ESCAPE BOTTOM
0330      END-IF
0340      ASSIGN SUBMIT.CARD = RECORD
0350      PERFORM SUBMIT
0360      IF *COUNTER = 1
0370         SEPARATE RECORD LEFT JUSTIFIED INTO #TOKEN (1:20)
0380         ASSIGN #JOB-NAME = #TOKEN (3)
0390      END-IF
0400   END-FIND
0410*
0420   ASSIGN SUBMIT.FUNCTION = 'CLOSE'
0430   PERFORM SUBMIT
0440   RESET  SUBMIT.FUNCTION
0450*
0460   REINPUT *1020,#JOB-NAME,SUBMIT.JOB-NUMBER
0470*
0480END-REPEAT
0490*
0500*
0510DEFINE SUBROUTINE SUBMIT
0520   PROCESS SUBMIT USING CARD     = SUBMIT.CARD
0530                    ,   FUNCTION = SUBMIT.FUNCTION
0540                    ,   NODE     = #TARGET-NODE
0550   IF SUBMIT.ERROR-CODE NE 0
0560      REINPUT SUBMIT.ERROR-TEXT
0570   END-IF
0580END-SUBROUTINE
0590*
0600END
