0010*
0020* SHARED MAIN STORAGE DATA
0030*
0040DEFINE DATA GLOBAL USING TUTO
0050            LOCAL  USING BCOMDTAL
0060            LOCAL
007001  #DISPLAY (8)
0080    02 MARK              (A1)
0090    02 DATA-ID           (A12)
0100    02 NUMBER-OF-ENTRIES (N5)
0110    02 ENTRY-LENGTH      (N3)
0120    02 CURRENT-ENTRIES   (N5)
0130*
014001  #FUNCTION            (A8)
015001  #DATA-ID             (A12)
016001  #NUMBER-OF-ENTRIES   (N5)
017001  #ENTRY-LENGTH        (N3)
018001  #DELETE-ENTRY        (A1) INIT <'N'>
0190*
020001  #ENTRY-NUMBER        (N5)
021001  #DATA                (A250)
0220*
023001  #I              (P2)
024001  #J              (P2)
025001  #COUNT          (P8)
026001  #CVAR           (C)
027001  #CV             (C)
0280*
029001  #SYSTEM-CODE    (A7) INIT  <'EXC'>
030001  REDEFINE #SYSTEM-CODE
0310    02  #SYS-PREF  (A3)
0320    02  #SYS-CODE  (A4)
0330END-DEFINE
0340ASSIGN #CVAR = (AD=I)
0350PERFORM PF-KEY
0360*
0370* ------------------------------ MAIN LOOP ----------------------------
0380*
0390REPEAT
0400FND.
0410FIND COMMON-DATA WITH NODE     =  ##NODE
0420                 AND  FUNCTION =  'LIST'
0430  IF ERROR-CODE NE 0 OR SYSTEM-CODE NE 0
0440     INCLUDE BERR---C
0450     ESCAPE BOTTOM (FND.)
0460  END-IF
0470  IF #I = 8
0480     PERFORM SCREEN-IO
0490     IF #CVAR MODIFIED
0500        ESCAPE BOTTOM (FND.)
0510     END-IF
0520  END-IF
0530  ADD 1 TO #I
0540  ADD 1 TO #COUNT
0550  MOVE BY NAME COMMON-DATA TO #DISPLAY (#I)
0560END-FIND
0570IF ##MSG-NR NE 0 OR #COUNT = 0 OR #I > 0
0580   PERFORM SCREEN-IO
0590END-IF
0600RESET #COUNT
0610END-REPEAT
0620*
0630* ------------------------------ SCREEN-IO ----------------------------
0640*
0650DEFINE SUBROUTINE SCREEN-IO
0660REPEAT
0670  INPUT WITH TEXT *##MSG-NR,
0680                   ##MSG-TXT1,
0690                   ##MSG-TXT2
0700        USING MAP 'BCOMDTA&'
0710  RESET  ##MSG
0720  IF #CVAR MODIFIED
0730     PERFORM PROCESS-DATA-AREA
0740  END-IF
0750UNTIL ##MSG-NR = 0
0760END-REPEAT
0770RESET  #DISPLAY (*)
0780RESET #I
0790END-SUBROUTINE
0800*
0810* ------------------------------ PROCESS-DATA-AREA --------------------
0820*
0830DEFINE SUBROUTINE PROCESS-DATA-AREA
0840DECIDE ON FIRST #FUNCTION
0850  VALUE 'CREATE','DELETE','CLOSE'
0860        PROCESS COMMON-DATA
0870          USING NODE               = ##NODE
0880            ,   FUNCTION           = #FUNCTION
0890            ,   DATA-ID            = #DATA-ID
0900            ,   NUMBER-OF-ENTRIES  = #NUMBER-OF-ENTRIES
0910            ,   ENTRY-LENGTH       = #ENTRY-LENGTH
0920         GIVING ERROR-CODE
0930                ERROR-TEXT
0940  VALUE 'GET'
0950        FIND COMMON-DATA WITH NODE = ##NODE
0960                          AND FUNCTION = #FUNCTION
0970                          AND DATA-ID  = #DATA-ID
0980                          AND ENTRY-NUMBER = #ENTRY-NUMBER
0990           MOVE DATA TO #DATA
1000        END-FIND
1010  VALUE 'MODIFY'
1020        PROCESS COMMON-DATA
1030          USING NODE               = ##NODE
1040            ,   FUNCTION           = #FUNCTION
1050            ,   DATA-ID            = #DATA-ID
1060            ,   ENTRY-NUMBER       = #ENTRY-NUMBER
1070            ,   DELETE-ENTRY       = #DELETE-ENTRY
1080            ,   DATA               = #DATA
1090         GIVING ERROR-CODE
1100                ERROR-TEXT
1110   ANY  IF ERROR-CODE NE 0
1120           INCLUDE BERR---C
1130        END-IF
1140   NONE ASSIGN ##MSG-NR = 1033
1150END-DECIDE
1160END-SUBROUTINE
1170*
1180* ------------------------------ PF-KEY -------------------------------
1190*
1200INCLUDE C-PFKEY
1210END
