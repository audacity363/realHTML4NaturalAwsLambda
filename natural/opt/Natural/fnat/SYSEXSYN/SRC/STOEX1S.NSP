0010** Example 'STOEX1S': STORE  (structured mode)
0020**
0030** CAUTION: Executing this example will modify the database records!
0040************************************************************************
0050DEFINE DATA LOCAL
00601 EMPL-VIEW VIEW OF EMPLOYEES
0070  2 PERSONNEL-ID
0080  2 NAME
0090  2 FIRST-NAME
0100  2 MAR-STAT
0110  2 BIRTH
0120  2 CITY
0130  2 COUNTRY
0140*
01501 #PERSONNEL-ID (A8)
01601 #NAME         (A20)
01701 #FIRST-NAME   (A15)
01801 #BIRTH-D      (D)
01901 #MAR-STAT     (A1)
02001 #BIRTH        (A8)
02101 #CITY         (A20)
02201 #COUNTRY      (A3)
02301 #CONF         (A1)
0240END-DEFINE
0250*
0260REPEAT
0270  INPUT 'ENTER A PERSONNEL ID AND NAME (OR ''END'' TO END)' //
0280        'PERSONNEL-ID : ' #PERSONNEL-ID  //
0290        'NAME         : ' #NAME          /
0300        'FIRST-NAME   : ' #FIRST-NAME
0310  /*
0320  /*  VALIDATE ENTERED DATA
0330  /*
0340  IF #PERSONNEL-ID = 'END' OR #NAME = 'END'
0350    STOP
0360  END-IF
0370  IF #NAME = ' '
0380    REINPUT WITH TEXT 'ENTER A LAST-NAME' MARK 2 AND SOUND ALARM
0390  END-IF
0400  IF #FIRST-NAME = ' '
0410    REINPUT WITH TEXT 'ENTER A FIRST-NAME' MARK 3 AND SOUND ALARM
0420  END-IF
0430  /*
0440  /*  ENSURE PERSON IS NOT ALREADY ON FILE
0450  /*
0460  FIND NUMBER EMPL-VIEW WITH PERSONNEL-ID =  #PERSONNEL-ID
0470  IF *NUMBER > 0
0480    REINPUT 'PERSON WITH SAME PERSONNEL-ID ALREADY EXISTS'
0490             MARK 1 AND SOUND ALARM
0500  END-IF
0510  MOVE 'N' TO #CONF
0520  /*
0530  /*  GET FURTHER INFORMATION
0540  /*
0550  INPUT
0560    'ADDITIONAL PERSONNEL DATA'                        ////
0570    'PERSONNEL-ID             :' #PERSONNEL-ID (AD=IO) /
0580    'NAME                     :' #NAME         (AD=IO) /
0590    'FIRST-NAME               :' #FIRST-NAME   (AD=IO) ///
0600    'MARITAL STATUS           :' #MAR-STAT             /
0610    'DATE OF BIRTH (YYYYMMDD) :' #BIRTH                /
0620    'CITY                     :' #CITY                 /
0630    'COUNTRY (3 CHARACTERS)   :' #COUNTRY              //
0640    'ADD THIS RECORD (Y/N)    :' #CONF          (AD=M)
0650  /*
0660  /*   ENSURE REQUIRED FIELDS CONTAIN VALID DATA
0670  /*
0680  IF NOT (#MAR-STAT = 'S'  OR = 'M' OR = 'D' OR = 'W')
0690    REINPUT TEXT 'ENTER VALID MARITAL STATUS  S=SINGLE ' -
0700                 'M=MARRIED D=DIVORCED W=WIDOWED' MARK 1
0710  END-IF
0720  IF NOT (#BIRTH = MASK(YYYYMMDD) AND #BIRTH = MASK(1582-2699))
0730    REINPUT TEXT 'ENTER CORRECT DATE' MARK 2
0740  END-IF
0750  IF #CITY  = ' '
0760    REINPUT TEXT 'ENTER A CITY NAME' MARK 3
0770  END-IF
0780  IF #COUNTRY = ' '
0790    REINPUT TEXT 'ENTER A COUNTRY CODE' MARK 4
0800  END-IF
0810  IF NOT (#CONF = 'N' OR= 'Y')
0820    REINPUT TEXT 'ENTER Y (YES) OR N (NO)' MARK 5
0830  END-IF
0840  IF #CONF = 'N'
0850    ESCAPE TOP
0860  END-IF
0870  /*
0880  /*  ADD THE RECORD
0890  /*
0900  MOVE EDITED #BIRTH TO #BIRTH-D (EM=YYYYMMDD)
0910  /*
0920  EMPL-VIEW.PERSONNEL-ID := #PERSONNEL-ID
0930  EMPL-VIEW.NAME         := #NAME
0940  EMPL-VIEW.FIRST-NAME   := #FIRST-NAME
0950  EMPL-VIEW.MAR-STAT     := #MAR-STAT
0960  EMPL-VIEW.BIRTH        := #BIRTH-D
0970  EMPL-VIEW.CITY         := #CITY
0980  EMPL-VIEW.COUNTRY      := #COUNTRY
0990  /*
1000  STORE RECORD IN EMPL-VIEW
1010  /*
1020  END OF TRANSACTION
1030  /*
1040  WRITE NOTITLE 'RECORD HAS BEEN ADDED'
1050  /*
1060END-REPEAT
1070END
