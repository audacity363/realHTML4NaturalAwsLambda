0010** Example 'HSTVSEQ': HISTOGRAM (with VARIABLE SEQUENCE)
0020***********************************************************************
0030DEFINE DATA LOCAL
00401 EMPL VIEW OF EMPLOYEES
0050  2 NAME
0060*
00701 #DIR      (A1)
00801 #STARTVAL (A20)
0090END-DEFINE
0100*
0110SET KEY PF3 PF7 PF8
0120*
0130MOVE 'ADKINSON' TO #STARTVAL
0140*
0150HISTOGRAM (9) EMPL FOR NAME FROM #STARTVAL
0160  WRITE NAME *NUMBER
0170  IF *COUNTER = 5
0180    MOVE NAME TO #STARTVAL
0190  END-IF
0200END-HISTOGRAM
0210*
0220#DIR := 'A'
0230*
0240REPEAT
0250  HISTOGRAM EMPL IN VARIABLE #DIR SEQUENCE
0260            FOR NAME FROM #STARTVAL
0270    MOVE NAME TO #STARTVAL
0280    INPUT NO ERASE  (IP=OFF AD=O)
0290          15/01 NAME *NUMBER
0300          //   'Direction:' #DIR
0310          //   'Press PF3 to stop'
0320          /    '      PF7 to go step back'
0330          /    '      PF8 to go step forward'
0340          /    '      ENTER to continue in that direction'
0350    /*
0360    IF *PF-KEY = 'PF7' AND #DIR = 'A'
0370      MOVE 'D' TO #DIR
0380      ESCAPE BOTTOM
0390    END-IF
0400    IF *PF-KEY = 'PF8' AND #DIR = 'D'
0410      MOVE 'A' TO #DIR
0420      ESCAPE BOTTOM
0430    END-IF
0440    IF *PF-KEY = 'PF3'
0450      STOP
0460    END-IF
0470  END-HISTOGRAM
0480  /*
0490  IF *COUNTER(0250) = 0
0500    STOP
0510  END-IF
0520END-REPEAT
0530END
