0010** Example 'EXMEX1': EXAMINE
0020************************************************************************
0030DEFINE DATA LOCAL
00401 #TEXT   (A45)
00501 #ARRAY  (A5/1:3)
00601 #A      (A3)
00701 #START  (N2)
00801 #NUM    (N2)
00901 #NUM1   (N2)
01001 #NUM2   (N2)
01101 #NUM3   (N2)
01201 #POS    (N2)
01301 #POS1   (N2)
01401 #LENG   (N2)
01501 #INDEX  (N2)
0160END-DEFINE
0170*
0180MOVE 'ABC   A B C   .A.  .B.  .C.    -A-  -B-  -C- ' TO #TEXT
0190*
0200WRITE / 'EXAMPLE 1  (DELIMITER, GIVING NUMBER)'
0210WRITE NOTITLE '#TEXT: ' #TEXT
0220EXAMINE #TEXT FOR 'A' GIVING NUMBER #NUM1
0230EXAMINE #TEXT FOR 'A' WITH DELIMITER GIVING NUMBER #NUM2
0240EXAMINE #TEXT FOR 'A' WITH DELIMITER '.' GIVING NUMBER #NUM3
0250WRITE 'EXAMINE #TEXT FOR "A" ' 57T 'Number found:' #NUM1
0260WRITE 'EXAMINE #TEXT FOR "A" WITH DELIMITER' 57T 'Number found:' #NUM2
0270WRITE 'EXAMINE #TEXT FOR "A" WITH DELIMITER "."'
0280  57T 'Number found:' #NUM3
0290*
0300WRITE / 'EXAMPLE 2  (DELIMITER, REPLACE, GIVING NUMBER)'
0310WRITE 'EXAMINE #TEXT FOR "A" WITH DELIMITER "-" REPLACE WITH "*"'
0320WRITE 'Before:' #TEXT
0330EXAMINE #TEXT FOR 'A' WITH DELIMITER '-' REPLACE WITH '*'
0340        GIVING NUMBER #NUM
0350WRITE 'After: ' #TEXT 57T 'Number found:' #NUM
0360*
0370*
0380NEWPAGE
0390*
0400WRITE / 'EXAMPLE 3  (REPLACE, GIVING NUMBER)'
0410WRITE 'EXAMINE      #TEXT FOR " " REPLACE WITH "+"'
0420WRITE 'Before:' #TEXT
0430EXAMINE #TEXT FOR ' ' REPLACE WITH '+' GIVING NUMBER #NUM
0440WRITE 'After: ' #TEXT 57T 'Number found:' #NUM
0450*
0460WRITE / 'EXAMPLE 4  (FULL, REPLACE, GIVING NUMBER)'
0470WRITE 'EXAMINE FULL #TEXT FOR " " REPLACE WITH "+"'
0480WRITE 'Before:' #TEXT
0490EXAMINE FULL #TEXT FOR ' ' REPLACE WITH '+' GIVING NUMBER #NUM
0500WRITE 'After: ' #TEXT 57T 'Number found:' #NUM
0510*
0520WRITE / 'EXAMPLE 5  (DELETE, GIVING POSITION)'
0530WRITE 'EXAMINE #TEXT FOR "+" DELETE GIVING POSITION #POS'
0540WRITE 'Before:' #TEXT
0550EXAMINE #TEXT FOR '+' DELETE GIVING POSITION #POS
0560WRITE 'After: ' #TEXT 57T 'Position found:' #POS
0570*
0580WRITE / 'EXAMPLE 6  (DELETE, GIVING LENGTH)'
0590WRITE 'EXAMINE #TEXT FOR "A" DELETE GIVING LENGTH #LENG'
0600WRITE 'Before:' #TEXT
0610EXAMINE #TEXT FOR 'A' DELETE GIVING LENGTH #LENG
0620WRITE 'After: ' #TEXT 57T 'Length found:' #LENG
0630*
0640*
0650NEWPAGE
0660*
0670MOVE 'ABC   A B C   .A.  .B.  .C.    -A-  -B-  -C- ' TO #TEXT
0680*
0690WRITE / 'EXAMPLE 7  (PATTERN, REPLACE, GIVING NUMBER)'
0700WRITE 'EXAMINE #TEXT FOR         ".A." AND REPLACE "***"'
0710WRITE 'Before:' #TEXT
0720EXAMINE #TEXT FOR         '.A.' AND REPLACE '***' GIVING NUMBER #NUM
0730WRITE 'After: ' #TEXT 57T 'Number found:' #NUM
0740*
0750MOVE 'ABC   A B C   .A.  .B.  .C.    -A-  -B-  -C- ' TO #TEXT
0760*
0770WRITE 'EXAMINE #TEXT FOR PATTERN ".A." AND REPLACE "***"'
0780WRITE 'Before:' #TEXT
0790EXAMINE #TEXT FOR PATTERN '.A.' AND REPLACE '***' GIVING NUMBER #NUM
0800WRITE 'After: ' #TEXT 57T 'Number found:' #NUM
0810*
0820MOVE 'ABC   A B C   .A.  .B.  .C.    -A-  -B-  -C- ' TO #TEXT
0830*
0840#A   := 'B C'
0850#POS := 6
0860#LENG:= 25
0870*
0880WRITE / 'EXAMPLE 8  (SUBSTRING, REPLACE, GIVING POSITION)'
0890WRITE '#A := "B C" ; #POS := 6 ; #LENG:= 25 '
0900WRITE 'EXAMINE SUBSTRING(#TEXT,#POS,#LENG) FOR #A AND REPLACE "***"'
0910WRITE 'Before:' #TEXT
0920EXAMINE SUBSTRING(#TEXT,#POS,#LENG) FOR #A AND REPLACE '***'
0930        GIVING POSITION #POS1
0940WRITE 'After: ' #TEXT 57T 'Position found:' #POS1
0950*
0960*
0970NEWPAGE
0980*
0990MOVE 'ABC   A B C   .A.  .B.  .C.    -A-  -B-  -C- ' TO #TEXT
1000*
1010WRITE / 'EXAMPLE 9  (DELETE, GIVING NUMBER, GIVING POSITION, '-
1020        'GIVING LENGTH)'
1030WRITE 'EXAMINE #TEXT FOR "." DELETE GIVING NUMBER   #NUM'
1040WRITE 30T 'GIVING POSITION #POS'
1050WRITE 30T 'GIVING LENGTH   #LENG'
1060WRITE 'Before:' #TEXT
1070EXAMINE #TEXT FOR '.' DELETE GIVING NUMBER   #NUM
1080                             GIVING POSITION #POS
1090                             GIVING LENGTH   #LENG
1100WRITE 'After: ' #TEXT
1110WRITE 'Number found:  ' #NUM
1120WRITE 'Position found:' #POS
1130WRITE 'Length found:  ' #LENG
1140*
1150*
1160*
1170MOVE 'ABC  ' TO #ARRAY (1)
1180MOVE '.A.B.' TO #ARRAY (2)
1190MOVE '-A-B-' TO #ARRAY (3)
1200*
1210WRITE / 'EXAMPLE 10 (GIVING NUMBER, GIVING POSITION, GIVING INDEX)'
1220WRITE '#ARRAY(1):' #ARRAY(1)
1230WRITE '#ARRAY(2):' #ARRAY(2)
1240WRITE '#ARRAY(3):' #ARRAY(3)
1250WRITE 'EXAMINE #ARRAY(*) FOR "B" GIVING NUMBER   #NUM'
1260WRITE 27T 'GIVING POSITION #POS'
1270WRITE 27T 'GIVING INDEX    #INDEX'
1280EXAMINE #ARRAY(*) FOR 'B' GIVING NUMBER   #NUM
1290                          GIVING POSITION #POS
1300                          GIVING INDEX    #INDEX
1310WRITE 'Number found:  ' #NUM
1320WRITE 'Position found:' #POS
1330WRITE 'Index found:   ' #INDEX
1340END
