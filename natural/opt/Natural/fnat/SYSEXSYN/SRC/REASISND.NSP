0010** Example 'REASISND': READ  (with STARTING WITH ISN)
0020************************************************************************
0030DEFINE DATA LOCAL
00401 EMPL VIEW OF EMPLOYEES
0050  2 NAME
0060  2 FIRST-NAME
0070  2 BIRTH
0080*
00901 #DIR      (A1)
01001 #STARTVAL (A20)
01101 #STARTISN (N8)
0120END-DEFINE
0130*
0140SET KEY PF3 PF7 PF8
0150*
0160MOVE 'ADKINSON' TO #STARTVAL
0170*
0180READ (9) EMPL BY NAME = #STARTVAL
0190  WRITE *ISN NAME FIRST-NAME BIRTH (EM=YYYY-MM-DD) *COUNTER
0200  IF *COUNTER = 5 THEN
0210    MOVE NAME TO #STARTVAL
0220    MOVE *ISN TO #STARTISN
0230  END-IF
0240END-READ
0250*
0260#DIR := 'A'
0270*
0280REPEAT
0290  READ EMPL IN VARIABLE #DIR  BY NAME = #STARTVAL
0300            STARTING WITH ISN = #STARTISN
0310    MOVE NAME TO #STARTVAL
0320    MOVE *ISN TO #STARTISN
0330    INPUT NO ERASE  (IP=OFF AD=O)
0340         15/01 *ISN  NAME  FIRST-NAME  BIRTH (EM=YYYY-MM-DD)
0350           //  'Direction:' #DIR
0360           //  'Press PF3 to stop'
0370           /   '      PF7 to go step back'
0380           /   '      PF8 to go step forward'
0390           /   '      ENTER to continue in that direction'
0400    /*
0410    IF *PF-KEY = 'PF7' AND #DIR = 'A'
0420      MOVE 'D' TO #DIR
0430      ESCAPE BOTTOM
0440    END-IF
0450    IF *PF-KEY = 'PF8' AND #DIR = 'D'
0460      MOVE 'A' TO #DIR
0470      ESCAPE BOTTOM
0480    END-IF
0490    IF *PF-KEY = 'PF3'
0500      STOP
0510    END-IF
0520  END-READ
0530  /*
0540  IF *COUNTER(0290) = 0
0550    STOP
0560  END-IF
0570END-REPEAT
0580END
