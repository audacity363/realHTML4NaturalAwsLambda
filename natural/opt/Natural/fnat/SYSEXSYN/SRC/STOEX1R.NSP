0010** Example 'STOEX1R': STORE  (reporting mode)
0020**
0030** CAUTION: Executing this example will modify the database records!
0040************************************************************************
0050RESET #PERSONNEL-ID (A8)
0060      #NAME         (A20)
0070      #FIRST-NAME   (A15)
0080      #BIRTH-D      (D)
0090      #MAR-STAT     (A1)
0100      #BIRTH        (A8)
0110      #CITY         (A20)
0120      #COUNTRY      (A3)
0130      #CONF         (A1)
0140*
0150REPEAT
0160  INPUT 'ENTER A PERSONNEL ID AND NAME (OR ''END'' TO END)' //
0170        'PERSONNEL-ID : ' #PERSONNEL-ID  //
0180        'NAME         : ' #NAME          /
0190        'FIRST-NAME   : ' #FIRST-NAME
0200  /*
0210  /*  VALIDATE ENTERED DATA
0220  /*
0230  IF #PERSONNEL-ID = 'END' OR #NAME = 'END'
0240    STOP
0250  IF #NAME = ' '
0260    REINPUT WITH TEXT 'ENTER A LAST-NAME' MARK 2 AND SOUND ALARM
0270  IF #FIRST-NAME = ' '
0280    REINPUT WITH TEXT 'ENTER A FIRST-NAME' MARK 3 AND SOUND ALARM
0290  /*
0300  /*  ENSURE PERSON IS NOT ALREADY ON FILE
0310  /*
0320  FIND NUMBER EMPLOYEES WITH PERSONNEL-ID =  #PERSONNEL-ID
0330  IF *NUMBER > 0
0340    REINPUT 'PERSON WITH SAME PERSONNEL-ID ALREADY EXISTS'
0350            MARK 1 AND SOUND ALARM
0360  MOVE 'N' TO #CONF
0370  /*
0380  /*  GET FURTHER INFORMATION
0390  /*
0400  INPUT
0410    'ADDITIONAL PERSONNEL DATA'                        ////
0420    'PERSONNEL-ID             :' #PERSONNEL-ID (AD=IO) /
0430    'NAME                     :' #NAME         (AD=IO) /
0440    'FIRST-NAME               :' #FIRST-NAME   (AD=IO) ///
0450    'MARITAL STATUS           :' #MAR-STAT             /
0460    'DATE OF BIRTH (YYYYMMDD) :' #BIRTH                /
0470    'CITY                     :' #CITY                 /
0480    'COUNTRY (3 CHARACTERS)   :' #COUNTRY              //
0490    'ADD THIS RECORD (Y/N)    :' #CONF          (AD=M)
0500  /*
0510  /*   ENSURE REQUIRED FIELDS CONTAIN VALID DATA
0520  /*
0530  IF NOT (#MAR-STAT = 'S'  OR = 'M' OR = 'D' OR = 'W')
0540    REINPUT TEXT 'ENTER VALID MARITAL STATUS  S=SINGLE ' -
0550                 'M=MARRIED D=DIVORCED W=WIDOWED' MARK 1
0560  IF NOT (#BIRTH = MASK(YYYYMMDD) AND #BIRTH = MASK(1582-2699))
0570    REINPUT TEXT 'ENTER CORRECT DATE' MARK 2
0580  IF #CITY  = ' '
0590    REINPUT TEXT 'ENTER A CITY NAME' MARK 3
0600  IF #COUNTRY = ' '
0610    REINPUT TEXT 'ENTER A COUNTRY CODE' MARK 4
0620  IF NOT (#CONF = 'N' OR= 'Y')
0630    REINPUT TEXT 'ENTER Y (YES) OR N (NO)' MARK 5
0640  IF #CONF = 'N'
0650    ESCAPE TOP
0660  /*
0670  /*  ADD THE RECORD
0680  /*
0690  MOVE EDITED #BIRTH TO #BIRTH-D (EM=YYYYMMDD)
0700  /*
0710  STORE RECORD IN EMPLOYEES
0720     WITH PERSONNEL-ID = #PERSONNEL-ID
0730          NAME         = #NAME
0740          FIRST-NAME   = #FIRST-NAME
0750          MAR-STAT     = #MAR-STAT
0760          BIRTH        = #BIRTH-D
0770          CITY         = #CITY
0780          COUNTRY      = #COUNTRY
0790  END OF TRANSACTION
0800  /*
0810  WRITE NOTITLE 'RECORD HAS BEEN ADDED'
0820  /*
0830LOOP
0840END
