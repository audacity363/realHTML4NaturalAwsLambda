0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3SCLIST
0050*
0060* DESCRIPTION   This Program generates a Scrolling List
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 H3SIZE          (N4)      BY VALUE /* i /m : Number of lines:
0240*                                    /*       =1 combo box
0250*                                    /*       >1 list box
02601 H3ARRAYCOUNT    (I4)        BY VALUE /* i /m : Number of list elements
02701 H3NAME          (A)     DYNAMIC BY VALUE /* i /m : Name of the group
0280*                                    /*        variable
02901 H3ARRAYVALUE    (A/1:V) DYNAMIC BY VALUE /* i /  : Default Value of the
0300*                                    /*        list values
03101 H3ARRAYLABEL    (A/1:V) DYNAMIC BY VALUE /* i /mH: Label of the list
0320*                                    /*        elements
03301 H3ARRAYSELECTED (L/1:V)   BY VALUE /* i /m : Elements selected by
0340*                                    /*        default
03501 H3MULTIPLE      (L)       BY VALUE /* i /m : Multiple selection
0360*                                    /*        allowed
0370*
0380LOCAL  USING W3PINFO
0390LOCAL  USING W3CONST
0400LOCAL
04101 II (I4)
04201 IK (I4)
04301 IJ (I4)
04401 REST (I4)
04501 #H3VALUE         (A) DYNAMIC
04601 #H3NAME          (A) DYNAMIC
0470END-DEFINE
0480*
0490DEFINE SUBROUTINE H3-SCROLLING-LIST
0500* ------ ERROR HANDLING ------------------------------------------
0510ON ERROR
0520  ##W3ERROR.SUBROUTINE := "H3-SCROLLING-LIST"
0530  PERFORM W3ERROR ##W3ERROR
0540  ESCAPE ROUTINE
0550END-ERROR
0560*
0570* ------ MAIN PROGRAM --------------------------------------------
0580RESET ##W3INFO
0590PERFORM W3INFO ##W3INFO
0600* --- Can only be used inside a FORM tag. ---
0610IF ##W3INFO.FORM EQ FALSE THEN
0620  ##W3ERROR.SUBROUTINE := "H3-SCROLLING-LIST"
0630  ##W3ERROR.SUBPROGRAM := *PROGRAM
0640  ##W3ERROR.NR         := 20003
0650  ##W3ERROR.LINE       := 0
0660  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0670  PERFORM W3ERROR ##W3ERROR
0680  ESCAPE ROUTINE
0690END-IF
0700* --- No elements defined. ---
0710IF H3ARRAYCOUNT LE 0 THEN
0720  ##W3ERROR.SUBROUTINE := "H3-SCROLLING-LIST"
0730  ##W3ERROR.SUBPROGRAM := *PROGRAM
0740  ##W3ERROR.NR         := 20002
0750  ##W3ERROR.LINE       := 0
0760  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0770  PERFORM W3ERROR ##W3ERROR
0780  ESCAPE ROUTINE
0790END-IF
0800*
0810IF H3SIZE LT 0 THEN
0820  H3SIZE := 1
0830END-IF
0840IF H3NAME NE " " THEN
0850  EXAMINE H3NAME TRANSLATE INTO UPPER
0860  COMPRESS 'NAME="' H3NAME '"'  INTO #H3NAME LEAVING NO
0870ELSE
0880  RESET H3NAME
0890END-IF
0900COMPRESS 'SIZE="' H3SIZE '">' INTO #H3VALUE LEAVING NO
0910IF H3MULTIPLE THEN
0920  COMPRESS '<SELECT MULTIPLE' #H3NAME #H3VALUE INTO #H3VALUE
0930ELSE
0940  COMPRESS '<SELECT' #H3NAME #H3VALUE INTO #H3VALUE
0950END-IF
0960PERFORM W3TEXTLINEDYNAMIC #H3VALUE
0970*
0980IK := 0
0990IJ := 0
1000*
1010FOR II = 1 TO H3ARRAYCOUNT
1020  IF H3ARRAYSELECTED(II) THEN
1030    COMPRESS '<OPTION SELECTED' INTO #H3VALUE
1040  ELSE
1050    COMPRESS '<OPTION' INTO #H3VALUE
1060  END-IF
1070*
1080  IF H3ARRAYVALUE(II) NE " " THEN
1090    COMPRESS #H3VALUE 'VALUE="' INTO #H3VALUE
1100    COMPRESS #H3VALUE H3ARRAYVALUE(II)'">'
1110    INTO #H3VALUE LEAVING NO
1120   ELSE
1130    COMPRESS #H3VALUE '">' INTO #H3VALUE LEAVING NO
1140  END-IF
1150  PERFORM W3TEXTDYNAMIC #H3VALUE
1160*
1170  IF H3ARRAYLABEL(II) NE " " THEN
1180    PERFORM W3HTMLLINE H3ARRAYLABEL(II)
1190  ELSE
1200    PERFORM W3HTMLLINE H3ARRAYVALUE(II)
1210  END-IF
1220*
1230END-FOR
1240PERFORM W3TEXTLINEDYNAMIC '</SELECT>'
1250*
1260END-SUBROUTINE
1270END
