0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Utilities
0030*
0040*        NAT-LIST
0050*
0060* DESCRIPTION
0070*               Lists the source of a Natural sources.
0080*
0090*               This program can handle even source greater
0100*               30000 bytes. If The generation exceed 25000 bytes the
0110*               output is stopped and the ammount of output will be
0120*               evaluated. Then at the end of the page,
0130*               a list of next and previous pages will be generated.
0140*               The blocksize will be re-evaluated if neccesary.
0150*
0160*               The output is text/html
0170*
0180* AUTHOR        SAG   28.07.97
0190*
0200* Version       8.3
0210*
0220* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0230*
0240* HISTORY
0250* USER    DATE       REASON
0260* ------+----------+-------------------------------------------------
0270* SAG   ! 02.03.98 ! Library to call NAT-LIST is SYSWEB not *Library
0280* SAG   ! 20.12.02 ! New Option LINE-NUMBERS to switch off line numbers
0290* ------+----------+-------------------------------------------------
0300*
0310DEFINE DATA
0320* ------------------------------------------- standard PDA for HTTP Api
0330PARAMETER USING W3PARM
0340* -------------------------------------------- definitions for HTTP Api
0350LOCAL  USING W3CONST
0360LOCAL  USING W3PINFO
0370* -------------------------------------------------- parameter USR1057N
0380LOCAL USING USR-MSG   /* Data for message exchange
0390  LOCAL USING USR-FLD   /* Description of the field in error
0400  LOCAL
0410  1 V                    (I002) CONST <100>
0420  1 USR1057L
0430    2 OBJECT-KEY
0440      3 LIBRARY          (A008)
0450      3 OBJECT-NAME      (A032)
0460      3 OBJECT-TYPE      (A002)
0470*
0480    2 INPUTS
0490      3 OPT-ACCESS       (A001)
0500      3 OPT-UNUSED-1     (   L)
0510      3 OPT-UNUSED-2     (A001)
0520      3 OPT-LINE-NUM     (A001)
0530      3 OPT-UNUSED-3     (   L)
0540      3 OPT-REDEF-DIR    (   L)
0550      3 OPT-UNUSED-4     (A001)
0560      3 OPT-AMOUNT       (I002)
0570      3 OPT-LINESIZE     (I002)
0580*
0590    2 INPUT-OUTPUTS
0600      3 INT-HANDLE       (I004)
0610      3 NEXT-SEQ         (I004)
0620      3 NEXT-NUM         (I002)
0630*
0640    2 OUTPUTS
0650      3 RETURNED         (I002)
0660      3 SRC-NUM          (I002/1:V)
0670      3 SRC-TEXT         (A001/1:V,1:092)
0680      3 REDEFINE SRC-TEXT
0690        4 SRC-GROUP      (1:V)
0700          5 SRC-LINE     (A092)
0710      3 REDEFINE SRC-TEXT
0720        4 DIR-OBJNAME    (A32)    /* Object Name
0730        4 DIR-LIBRARY    (A08)    /* Library ID
0740        4 DIR-OBJTYPE    (A02)    /* Object Type
0750        4 DIR-OBJKIND    (A01)    /* Source or Module
0760        4 DIR-DBID       (A05)    /* DBID of System File
0770        4 DIR-FNR        (A05)    /* FNR of System File
0780        4 DIR-DATN       (A08)    /* Date in Format (YYYYMMDD)
0790        4 DIR-TIMN       (A07)    /* Time in Format (HHIISST)
0800        4 DIR-USERID     (A08)    /* User ID
0810        4 DIR-PROGMODE   (A01)    /* Programming Mode
0820        4 DIR-SRCSIZE    (A10)    /* Source Area Size
0830        4 DIR-GPSIZE     (A10)    /* Size of Module
0840        4 DIR-UNIQUE-ID  (A32)    /* Unique ID
0850        4 DIR-DDM-DBID   (A05)    /* DBID the DDM is cataloged with
0860        4 DIR-DDM-FNR    (A05)    /* FNR the DDM is cataloged with
0870        4 DIR-NATVERS    (A04)    /* Natural Version
0880        4 DIR-NATSM      (A02)    /* Natural SM Level
0890        4 DIR-INIT-USER  (A008)    /* Init User ID
0900        4 DIR-TID        (A008)    /* Terminal ID
0910        4 DIR-TRANS-NAME (A008)    /* TP Transcation Name
0920        4 DIR-OPSYS      (A008)    /* Operating System
0930        4 DIR-TPSYS      (A008)    /* TP System
0940        4 DIR-USED-GDA   (A008)    /* Used GDA
0950*
0960      3 SRC-SEQ          (I004/1:V)
0970      3 SRC-LONG         (A001/1:V)
0980*
0990  1 USR1057N
1000    2 VERSION            (I001) INIT <0>
1010  1 REDEFINE USR1057N
1020    2 EXTENDED-PARMS
1030      3 EXTENDED-DATA    (A001/1:1)
1040* ----------------------------------------------
1050  1 LOCAL-MSG            (A079)
1060  1 IX                   (I002)
1070  1 INPUT-OK             (   L)
1080  1 OUTNUM               (A004)
1090  1 REDEFINE OUTNUM
1100    2  NUMOUT            (N004)
1110*
1120  1 II                   (N004)
1130  1 SOURCELINE           (N004) INIT <0>
1140  1 OUTLINE              (N004) INIT <0>
1150  1 LINENR               (I002/0:9999)
1160* --------------------------------------------------- parameter W3FREE
1170  1 #W3COUNT             (I004)
1180  1 #W3COUNT-MAX         (I004)
1190  1 #W3FREE              (I004)
1200* -------------------------------------------------- parmeter W3READENV
1210  1 #MAX                 (I004)
1220  1 #VALUE               (A) DYNAMIC
1230  1 #HTML                (A) DYNAMIC
1240*
1250  1 #FIRST               (N004)
1260  1 #COUNT               (N004)
1270  1 TEST-MF              (A001) INIT <"A">
1280* ---------------------------------------------------- set exprire
1290  1 #EXPIRES             (A029)
1300  1 REDEFINE #EXPIRES
1310    2 RDATE              (A017)
1320    2 RTIME              (A008)
1330    2 RZONE              (A004)
1340  1 ATIME                (   T)
1350  1 ADATE                (   D)
1360  1 #ADDDAY              (I004) INIT <0>
1370  1 LANG                 (I004)
1380  1 #LINENR              (A) DYNAMIC
1390  1 LASTLINE             (N4)
1400  END-DEFINE
1410* ---------------------------------------------------- error handling
1420  ON ERROR
1430    PERFORM W3ERROR ##W3ERROR
1440    PERFORM W3END ##RPC
1450    ESCAPE ROUTINE
1460  END-ERROR
1470* ======================================================== Main Program
1480* ------------------------------------------------------- init HTML API
1490  PERFORM W3INIT ##RPC
1500* ---------------------------------------- Start from this Line Number
1510  PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "FIRST" "P" #VALUE
1520  IF *LENGTH(#VALUE) > 0 AND #VALUE IS (N4) THEN
1530    #FIRST := VAL(#VALUE)
1540  ELSE
1550    #FIRST := 0
1560  END-IF
1570* ----------------------------------------- Read this ammount of Lines
1580  PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "COUNT" "P" #VALUE
1590  IF *LENGTH(#VALUE) > 0 AND #VALUE IS (N4) THEN
1600    #COUNT := VAL(#VALUE)
1610  ELSE
1620    #COUNT := 9999
1630  END-IF
1640* ------------------------------------------------- Name of the source
1650  PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SOURCE" "P" #VALUE
1660  IF *LENGTH(#VALUE) = 0 THEN
1670    OBJECT-NAME := *PROGRAM
1680  ELSE
1690    OBJECT-NAME := #VALUE
1700  END-IF
1710  EXAMINE OBJECT-NAME TRANSLATE INTO UPPER
1720* ------------------------------------------------------- Line Numbers
1730  PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "LINE-NUMBERS" "P" #VALUE
1740  EXAMINE #VALUE TRANSLATE INTO UPPER
1750  IF #VALUE = "OFF" THEN
1760    #LINENR := '&LINE-NUMBERS=OFF'
1770  END-IF
1780* ------------------------------------------------------------ Expire
1790  PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "EXPIRE" "P" #VALUE
1800  IF *LENGTH(#VALUE) > 0 THEN
1810    IF #VALUE IS (N4) THEN
1820      #ADDDAY := VAL(#VALUE)
1830    END-IF
1840    ADATE := *DATX + #ADDDAY
1850    ATIME := *TIMX
1860    LANG := *LANGUAGE
1870     *LANGUAGE := 1
1880    MOVE EDITED ADATE (EM=NNN', 'DD' 'LLL' 'YYYY) TO RDATE
1890    MOVE EDITED ATIME (EM=HH:II:SS) TO RTIME
1900    MOVE " GMT" TO RZONE
1910     *LANGUAGE := LANG
1920  END-IF
1930* ------------------------------------------------ Name of the library
1940  PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "LIB" "P" #VALUE
1950  IF *LENGTH(#VALUE) = 0 THEN
1960    LIBRARY := *LIBRARY-ID
1970  ELSE
1980    LIBRARY := #VALUE
1990  END-IF
2000  EXAMINE LIBRARY TRANSLATE INTO UPPER
2010* ----------------------------------------- Set Interface to 'USR1057N'
2020  OPT-LINE-NUM  := 'Y'
2030  OPT-REDEF-DIR := TRUE
2040  OPT-AMOUNT    := V
2050* ------------------------------ for MF only linesize of 92 is possible
2060* ------------------------- error at userexit, more then 92 may not run
2070* IF TEST-MF EQ H'41' THEN
2080*  OPT-LINESIZE  := 250
2090* ELSE
2100  OPT-LINESIZE  := 92
2110* END-IF
2120* --------------------------------------------- Set type of return page
2130  PERFORM W3CONTENT-TYPE 'text/html'
2140* ------------------------------------------- Set head of the HTML page
2150  #HTML := "<!DOCTYPE 'HTML PUBLIC -//W3C//DTD HTML 3.2//EN'>"
2160  IF #ADDDAY > 0 THEN
2170    COMPRESS #HTML ##HTTP_NEWLINE '<HTML><HEAD>' ##HTTP_NEWLINE
2180      '<META HTTP-EQUIV="Expires" CONTENT="' #EXPIRES'">'
2190      '<TITLE>List' INTO #HTML LEAVING NO
2200  ELSE
2210    COMPRESS #HTML ##HTTP_NEWLINE '<HTML><HEAD><TITLE>List' INTO #HTML LEAVING NO
2220  END-IF
2230*
2240  COMPRESS #HTML 'Natural Source</TITLE>'-
2250    '<META http-equiv="Content-Type" '-
2260    'content="text/html; charset=iso-8859-1"></HEAD>'
2270    '<BODY BGCOLOR=#FFFFFF><P>&nbsp;'-
2280    '<H2 ALIGN=CENTER><FONT COLOR=NAVY>List Natural Source</FONT></H2>'
2290* --------------------------------- Page heading and start output table
2300    '<BR><TABLE WIDTH=100% BORDER=0 CELLPADDING=5 CELLSPACING=3>'-
2310    '<TR BGCOLOR=#999999><TH>Source:' OBJECT-NAME '- at Library: '
2320    LIBRARY '</TH></TR><TR><TD><PRE>' INTO #HTML
2330  PERFORM W3TEXTDYNAMIC #HTML
2340* ----------------------------------------- Set Interface to 'USR1057N'
2350  OPT-LINE-NUM  := 'Y'
2360  OPT-REDEF-DIR := TRUE
2370  OPT-AMOUNT    := V
2380  OPT-ACCESS   := "O"
2390  LASTLINE     := -1
2400* ----------------------------------------------------- Read all lines
2410  COMPRESS " " INTO #HTML LEAVING NO
2420  REPEAT
2430    CALLNAT 'USR1057N' USR1057L  USR1057N.EXTENDED-PARMS NAD-MSG NAD-FLD
2440    OPT-ACCESS := "R"
2450    IF MSG-NR NE 0 AND MSG-NR NE 100 THEN
2460      IF MSG-NR EQ 1006 THEN
2470        COMPRESS '<FONT COLOR="RED">' MSG-NR MSG 'or is locked.</FONT>'
2480          INTO #HTML
2490      ELSE
2500        COMPRESS '<FONT COLOR="RED">' MSG-NR MSG'.</FONT>'
2510          INTO #HTML
2520      END-IF
2530      PERFORM W3TEXTLINEDYNAMIC #HTML
2540    END-IF
2550* ------------------------------------------------------- display lines
2560    FOR IX = 1 TO RETURNED
2570      IF SRC-NUM(IX) NE 0 THEN
2580        ADD 1 TO SOURCELINE
2590        LINENR(SOURCELINE) := SRC-NUM(IX)
2600        NUMOUT := SRC-NUM(IX)
2610* ----------------------------------------------------- lines of 'page'
2620        IF NUMOUT >= #FIRST AND OUTLINE <= #COUNT THEN
2630          IF #LINENR = " "
2640            COMPRESS OUTNUM SRC-LINE(IX) INTO #HTML
2650            PERFORM W3HTMLLINE #HTML
2660          ELSE
2670            IF LASTLINE NE NUMOUT
2680              PERFORM W3TEXTDYNAMIC ##HTTP_NEWLINE
2690              PERFORM W3HTML #HTML
2700              COMPRESS " " INTO #HTML LEAVING NO
2710            END-IF
2720            COMPRESS FULL #HTML SRC-LINE(IX) INTO #HTML LEAVING NO
2730          END-IF
2740          ADD 1 TO OUTLINE
2750          LASTLINE := NUMOUT
2760        END-IF
2770      END-IF
2780      PERFORM W3COUNTER #W3COUNT #W3COUNT-MAX #W3FREE
2790      IF #W3FREE < 2500 THEN
2800        #COUNT := OUTLINE - 1
2810        ESCAPE BOTTOM
2820      END-IF
2830* -------------------------------------------- Check the written Output
2840    END-FOR
2850* ------------------------------------------------- Error from USR1057N
2860    IF (#W3FREE < 1500) OR (MSG-NR NE 0) THEN
2870      ESCAPE BOTTOM
2880    END-IF
2890  END-REPEAT
2900*
2910  IF #LINENR NE " " AND NUMOUT >= #FIRST AND OUTLINE <= #COUNT THEN
2920    PERFORM W3TEXTDYNAMIC ##HTTP_NEWLINE
2930    PERFORM W3HTMLDYNAMIC #HTML
2940  END-IF
2950* ------------------------------------------------------ close USR1057N
2960  OPT-ACCESS := 'C'
2970  CALLNAT 'USR1057N' USR1057L USR1057N.EXTENDED-PARMS NAD-MSG NAD-FLD
2980* ---------------------------------------- close predefined output part
2990  PERFORM W3TEXTLINEDYNAMIC "</PRE></TD></TR></TABLE>"
3000* ------------------------ Output to large, create 'next pages' index
3010  IF #COUNT < OUTLINE OR #FIRST NE 0 THEN
3020    PERFORM W3TEXTDYNAMIC '<P ALIGN=CENTER><I>Next Pages:&nbsp;'
3030    FOR II = 1 TO SOURCELINE STEP #COUNT
3040      NUMOUT := LINENR(II)
3050      COMPRESS "<A HREF='NAT-LIST?SOURCE="
3060        OBJECT-NAME "&LIB=" LIBRARY "&FIRST=" LINENR(II)
3070        "&COUNT=" #COUNT #LINENR "'>" OUTNUM
3080        "</A>" ##HTTP_NEWLINE INTO #VALUE LEAVING NO
3090      PERFORM W3TEXTDYNAMIC #VALUE
3100    END-FOR
3110    PERFORM W3TEXTLINEDYNAMIC '</I><BR>&nbsp;'
3120  END-IF
3130* perform w3textline MSG
3140* -------------------------- Display current time/date and program name
3150* ------------------------------------------------- and close HTML page
3160  PERFORM W3INFO ##W3INFO
3170  COMPRESS "<TABLE BORDER=0 WIDTH=100% CELLSPACING=0 "-
3180    "CELLPADDING=5>"
3190    "<TR BGCOLOR='#00CC66'><TD>" *PROGRAM '&#32;-&#32;' ##W3INFO.LOG-TIME
3200    "</TD><TD ALIGN='RIGHT'>" 'Natural'
3210    "</TD></TR></TABLE></BODY></HTML>"
3220    INTO #HTML LEAVING NO
3230  PERFORM W3TEXTDYNAMIC #HTML
3240* ------------------------------------------------------ close HTTP Api
3250  PERFORM W3END ##RPC
3260* ================================================================= End
3270  END
3280
