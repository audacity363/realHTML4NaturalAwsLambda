0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3TXAREA
0050*
0060* DESCRIPTION   This Program generates a Text Area
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 H3ARRAYCOUNT (I4)            BY VALUE /* i /m : Number of text lines
02401 H3NAME       (A)     DYNAMIC BY VALUE /* i /m : Name of the text
0250*                                       /*        variable
02601 H3ARRAYVALUE (A/1:V) DYNAMIC BY VALUE /* i /  : Default value of the
0270*                                       /*        text variable
02801 H3ROW        (N4)            BY VALUE /* i /  : set number of rows
02901 H3COLUM      (N4)            BY VALUE /* i /  : set number of columns
0300*
0310LOCAL  USING W3PINFO
0320LOCAL  USING W3CONST
0330LOCAL
03401 II       (I4)
03501 IK       (I4)
03601 IJ       (I4)
03701 REST     (I4)
0380*
03901 #H3VALUE (A) DYNAMIC
0400END-DEFINE
0410*
0420DEFINE SUBROUTINE H3-TEXT-AREA
0430* ------ ERROR HANDLING ------------------------------------------
0440ON ERROR
0450##W3ERROR.SUBROUTINE := "H3-TEXT-AREA"
0460PERFORM W3ERROR ##W3ERROR
0470ESCAPE ROUTINE
0480END-ERROR
0490*
0500* ------ MAIN PROGRAM --------------------------------------------
0510RESET ##W3INFO
0520PERFORM W3INFO ##W3INFO
0530* --- Can only be used inside a FORM tag. ---
0540IF ##W3INFO.FORM EQ FALSE THEN
0550  ##W3ERROR.SUBROUTINE := "H3-TEXT-AREA"
0560  ##W3ERROR.SUBPROGRAM := *PROGRAM
0570  ##W3ERROR.NR         := 20003
0580  ##W3ERROR.LINE       := 0
0590  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0600  PERFORM W3ERROR ##W3ERROR
0610  ESCAPE ROUTINE
0620END-IF
0630* --- Textarea has no name. ---
0640IF H3NAME EQ " " THEN
0650  ##W3ERROR.SUBROUTINE := "H3-TEXT-AREA"
0660  ##W3ERROR.SUBPROGRAM := *PROGRAM
0670  ##W3ERROR.NR         := 20010
0680  ##W3ERROR.LINE       := 0
0690  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0700  PERFORM W3ERROR ##W3ERROR
0710  ESCAPE ROUTINE
0720END-IF
0730*
0740IF H3COLUM > 0 AND  H3ROW = 0 THEN
0750  DIVIDE H3COLUM INTO H3ARRAYCOUNT GIVING H3ROW REMAINDER REST
0760  ADD 1 TO H3ROW
0770END-IF
0780*
0790#H3VALUE := "<TEXTAREA"
0800*
0810IF H3ROW GT 0 THEN
0820  COMPRESS #H3VALUE 'ROWS="' INTO #H3VALUE
0830  COMPRESS #H3VALUE H3ROW '"' INTO #H3VALUE LEAVING NO
0840END-IF
0850*
0860IF H3COLUM GT 0 THEN
0870  COMPRESS #H3VALUE 'COLS="' INTO #H3VALUE
0880  COMPRESS #H3VALUE H3COLUM '"' INTO #H3VALUE LEAVING NO
0890END-IF
0900*
0910EXAMINE H3NAME TRANSLATE INTO UPPER
0920COMPRESS #H3VALUE 'NAME="' INTO #H3VALUE
0930COMPRESS #H3VALUE H3NAME '">' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
0940*
0950PERFORM W3TEXTDYNAMIC #H3VALUE
0960*
0970FOR II = 1 TO H3ARRAYCOUNT
0980  PERFORM W3TEXTDYNAMIC H3ARRAYVALUE(II)
0990  IF II < H3ARRAYCOUNT THEN
1000    PERFORM W3TEXTDYNAMIC ##HTTP_NEWLINE
1010  END-IF
1020END-FOR
1030*
1040PERFORM W3TEXTLINEDYNAMIC '</TEXTAREA>'
1050*
1060END-SUBROUTINE
1070END
