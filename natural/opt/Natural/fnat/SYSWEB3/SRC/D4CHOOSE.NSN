0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D4CHOOSE
0050*
0060* DESCRIPTION
0070*               Dispater Program for the demo
0080*
0090* AUTHOR        SAG   08.09.1998
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING W3PARM
0240LOCAL USING D4-PARM
0250LOCAL USING W3CONST
0260LOCAL
02701 P-NO         (A) DYNAMIC
02801 P-NAME       (A) DYNAMIC
02901 P-FIRST-NAME (A) DYNAMIC
03001 P-BIRTH      (A) DYNAMIC
03101 P-SEX        (A) DYNAMIC
03201 P-DEPT       (A) DYNAMIC
03301 P-CITY       (A) DYNAMIC
03401 P-AREA-CODE  (A) DYNAMIC
03501 P-ZIP        (A) DYNAMIC
03601 P-COUNTRY    (A) DYNAMIC
03701 P-PHONE      (A) DYNAMIC
0380*
03901 P-AGE        (A) DYNAMIC
04001 P-HOBBY      (A) DYNAMIC
0410*
04201 #PROG        (A8)
0430*
04401 W3URL        (A) DYNAMIC
04501 W3VALUE      (A) DYNAMIC
0460END-DEFINE
0470*
0480ON ERROR
0490  PERFORM W3ERROR ##W3ERROR
0500  PERFORM W3END ##RPC
0510  ESCAPE ROUTINE
0520END-ERROR
0530*
0540PERFORM W3INIT ##RPC
0550*
0560PERFORM D4-RGLOB #DEMO-MENU #DEMO-PARM
0570*
0580EXAMINE #DEMO-PARM.#TODO TRANSLATE INTO UPPER
0590EXAMINE #DEMO-PARM.#OT   TRANSLATE INTO UPPER
0600*
0610IF #DEMO-PARM.#TODO EQ "HOME"
0620  PERFORM W3CLEAR
0630  PERFORM W3LOCATION "/"
0640ELSE
0650*
0660  IF #DEMO-PARM.#OT NE "EM" AND #DEMO-PARM.#OT NE " " THEN
0670    PERFORM W3CLEAR
0680    PERFORM W3LOCATION "D4TEMPEL?SOURCE=D4NOTIMP"
0690  ELSE
0700*
0710    DECIDE ON FIRST OF #DEMO-PARM.#TODO
0720      VALUE "HOME"
0730        PERFORM W3CLEAR
0740        PERFORM W3LOCATION "/"
0750      VALUE "NEW"
0760        PERFORM D4NEW #DEMO-MENU #DEMO-PARM
0770      VALUE "DEL"
0780        PERFORM D4DELETE #DEMO-MENU #DEMO-PARM
0790      VALUE "SEL"
0800        PERFORM D4SELECT #DEMO-MENU #DEMO-PARM
0810      VALUE "LIST"
0820        PERFORM D4LIST #DEMO-MENU #DEMO-PARM
0830      VALUE "SHOW"
0840        PERFORM D4SHOW #DEMO-MENU #DEMO-PARM
0850      VALUE "BROW"
0860        PERFORM D4BROWSE #DEMO-MENU #DEMO-PARM
0870      VALUE "CHAN"
0880        PERFORM D4CHANGE #DEMO-MENU #DEMO-PARM
0890      VALUE "DELO"
0900        COMPRESS "D4" #DEMO-PARM.#OT "DEL" INTO #PROG LEAVING NO
0910        CALLNAT #PROG #DEMO-PARM.#PERSON #DEMO-PARM.#MSG
0920*
0930        PERFORM BACK
0940      VALUE "SAVC"
0950        PERFORM READ-ENV
0960*
0970        COMPRESS "D4" #DEMO-PARM.#OT "CHAN" INTO #PROG LEAVING NO
0980        CALLNAT #PROG P-NO P-NAME P-FIRST-NAME P-BIRTH P-SEX P-DEPT
0990          P-CITY P-AREA-CODE P-ZIP P-COUNTRY P-PHONE #DEMO-PARM.#MSG
1000*
1010        PERFORM D4CHANGE #DEMO-MENU #DEMO-PARM
1020      VALUE "CREN"
1030        PERFORM READ-ENV
1040*
1050        #DEMO-PARM.#OT := "EM"
1060        COMPRESS "D4" #DEMO-PARM.#OT "NEW" INTO #PROG LEAVING NO
1070        CALLNAT #PROG P-NO P-NAME P-FIRST-NAME P-BIRTH P-SEX P-DEPT
1080          P-CITY P-AREA-CODE P-ZIP P-COUNTRY P-PHONE #DEMO-PARM.#MSG
1090*
1100        PERFORM D4NEW #DEMO-MENU #DEMO-PARM
1110      NONE
1120        #DEMO-PARM.#STATUS := "M"
1130        INCLUDE D4-BACK
1140    END-DECIDE
1150  END-IF
1160END-IF
1170*
1180PERFORM W3END ##RPC
1190*
1200DEFINE SUBROUTINE BACK
1210INCLUDE D4-BACK
1220END-SUBROUTINE
1230*
1240DEFINE SUBROUTINE READ-ENV
1250PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "ID"    "P" P-NO
1260PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "NAME"  "P" P-NAME
1270PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "FIRST" "P" P-FIRST-NAME
1280PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "BIRTH" "P" P-BIRTH
1290PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SEX"   "P" P-SEX
1300PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "ZIP"   "P" P-ZIP
1310PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "DEPT"  "P" P-DEPT
1320PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "CITY"  "P" P-CITY
1330PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "AREA"  "P" P-AREA-CODE
1340PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "CNTRY" "P" P-COUNTRY
1350PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "PHONE" "P" P-PHONE
1360*
1370END-SUBROUTINE
1380END
