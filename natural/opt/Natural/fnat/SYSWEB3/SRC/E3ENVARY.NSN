0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Example Programs
0030*
0040*        E3ENVARY
0050*
0060* DESCRIPTION   Reads the first occurrence of a single variable.
0070*
0080* SEE           .../sysweb/t3envary
0090*
0100* AUTHOR        SAG   08.09.1998
0110*
0120* Version       8.3
0130*
0140* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0150*
0160* HISTORY
0170* USER    DATE       REASON
0180* ------+----------+----------------------------------------------
0190*       ! --.--.-- !
0200* ------+----------+----------------------------------------------
0210*
0220* ------ PARAMETER -----------------------------------------------
0230DEFINE DATA
0240PARAMETER USING W3PARM
0250LOCAL USING W3CONST
0260LOCAL
0270*
02801 W3START        (I4)
02901 W3ARRAYCOUNT   (I4) INIT <80>
03001 W3ARRAYVALUE   (A1/1:80)
03101 W3VALUELENGTH  (I4)
0320*
03301 W3VALUE        (A250)
03401 W3DYNAMIC      (A) DYNAMIC
03501 W3MAX          (I4)
0360END-DEFINE
0370* --- ERROR HANDLING ---
0380ON ERROR
0390  PERFORM W3ERROR ##W3ERROR
0400  PERFORM W3END ##RPC
0410  ESCAPE ROUTINE
0420END-ERROR
0430*
0440* --- INITIALIZE Web Interface ---
0450PERFORM W3INIT ##RPC
0460*
0470* --- SET TYPE OF RETURN-PAGE ---
0480PERFORM W3CONTENT-TYPE 'text/html'
0490* --- WRITE THE HEAD OF THE DOCUMENT ---
0500PERFORM W3TEXTLINE "<!DOCTYPE 'HTML PUBLIC -//W3C//DTD HTML 3.2//EN'>"
0510PERFORM W3TEXTLINE '<HTML><HEAD><META HTTP-EQUIV="Content-Type" '-
0520  'CONTENT="text/html; charset=iso-8859-1"><TITLE>'
0530* --- THE TITLE OF THE PAGE ---
0540PERFORM W3TEXTLINE 'Basic Routines - Read Environment Variable'
0550PERFORM W3TEXTLINE '</TITLE></HEAD><BODY><H2>'
0560* --- THE FIRST HEADERLINE ---
0570PERFORM W3HTMLLINE 'Read Environment Variable'
0580PERFORM W3TEXTLINE '</H2><HR>'
0590PERFORM W3TEXTDYNAMIC ##HTTP_NEWLINE
0600*
0610* --- READ ENVIRONMENT ---
0620PERFORM W3READ-ENVIRONMENT 'REMOTE_HOST' ' ' W3VALUE W3MAX
0630IF W3MAX LT 0 THEN
0640  W3VALUE := "(REMOTE_HOST)"
0650END-IF
0660*
0670COMPRESS '<P>Your remote host is:' "<FONT COLOR='GREEN'>"
0680  W3VALUE INTO W3VALUE
0690PERFORM W3TEXT W3VALUE
0700PERFORM W3TEXTLINE ".</FONT>"
0710*
0720* --- READ ENVIRONMENT ---
0730W3START         := 0
0740W3ARRAYCOUNT    := 3
0750PERFORM W3READ-ENVIRONMENT-ARRAY W3START 'REMOTE_ADDR' ' '
0760  W3ARRAYCOUNT W3ARRAYVALUE(*) W3VALUELENGTH
0770IF W3VALUELENGTH LE 0 THEN
0780  W3ARRAYVALUE(1) := "?"
0790  W3ARRAYVALUE(2) := "?"
0800  W3ARRAYVALUE(3) := "?"
0810END-IF
0820*
0830COMPRESS '<P>The first 3 digits of you remote addres:'
0840  "<FONT COLOR='GREEN'>" W3ARRAYVALUE(1:3) INTO W3VALUE
0850PERFORM W3TEXT W3VALUE
0860PERFORM W3TEXTLINE ".</FONT>"
0870*
0880PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC 'HTTP_USER_AGENT' ' ' W3DYNAMIC
0890IF *LENGTH(W3DYNAMIC) LE 0 THEN
0900  W3VALUE := "(HTTP_USER_AGENT)"
0910END-IF
0920*
0930COMPRESS '<P>Your are using' "<FONT COLOR='RED'>" W3VALUE
0940  '</FONT>as web browser.' ##HTTP_NEWLINE INTO W3VALUE
0950PERFORM W3TEXTLINEDYNAMIC W3VALUE
0960*
0970PERFORM W3READ-ENVIRONMENT 'SCRIPT_NAME' ' ' W3VALUE W3MAX
0980IF W3MAX LE 0 THEN
0990  W3VALUE := "(SCRIPT_NAME)"
1000END-IF
1010*
1020COMPRESS '<P>You habe called the following cgi script:'
1030  "<FONT COLOR='BLUE'>" W3VALUE  INTO W3VALUE
1040PERFORM W3TEXT W3VALUE
1050PERFORM W3TEXTLINE "</FONT>.<P>"
1060*
1070* --- END THE HTML PAGE ---
1080COMPRESS "<TABLE BORDER='0' WIDTH='100%' CELLSPACING='0' "-
1090  "CELLPADDING='5'>"
1100  '<TR BGCOLOR="#00cc66"><TD>' *PROGRAM '&#32;-&#32;' *DATE '&#32;' *TIME
1110  "</TD><TD ALIGN='RIGHT'>" 'Natural'
1120  "</TD></TR></TABLE>"
1130  '</BODY></HTML>' INTO W3VALUE LEAVING NO
1140PERFORM W3TEXTDYNAMIC W3VALUE
1150*
1160* --- END Web Interface ---
1170PERFORM W3END ##RPC
1180*
1190END
