0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D4EMCHAN
0050* DESCRIPTION
0060*               change a Person
0070*
0080* AUTHOR        SAG   08.09.1998
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+------------+--------------------------------------------
0170* SAG   ! 27.04.1999 ! Year2000 Employee File uses D instead of N6
0180* ------+------------+--------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 #P-NO         (A) DYNAMIC BY VALUE
02401 #P-NAME       (A) DYNAMIC BY VALUE
02501 #P-FIRST-NAME (A) DYNAMIC BY VALUE
02601 #P-BIRTH      (A) DYNAMIC BY VALUE
02701 #P-SEX        (A) DYNAMIC BY VALUE
02801 #P-DEPT       (A) DYNAMIC BY VALUE
02901 #P-CITY       (A) DYNAMIC BY VALUE
03001 #P-AREA-CODE  (A) DYNAMIC BY VALUE
03101 #P-ZIP        (A) DYNAMIC BY VALUE
03201 #P-COUNTRY    (A) DYNAMIC BY VALUE
03301 #P-PHONE      (A) DYNAMIC BY VALUE
03401 #INOUT-MSG    (A) DYNAMIC
0350*
0360LOCAL
03701 #BIRTH       (A10)
03801 REDEFINE #BIRTH
0390  2 #P-BIRTH-D (A2)
0400  2 FILLER 1X
0410  2 #P-BIRTH-M (A2)
0420  2 FILLER 1X
0430  2 #P-BIRTH-J (A4)
0440*
04501 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0460  2 PERSONNEL-ID
0470  2 FIRST-NAME
0480  2 NAME
0490  2 SEX
0500  2 BIRTH
0510  2 CITY
0520  2 ZIP
0530  2 COUNTRY
0540  2 AREA-CODE
0550  2 PHONE
0560  2 DEPT
0570*
05801 BIRTH-A (A8)
05901 REDEFINE BIRTH-A
0600  2 BIRTH-J (N4)
0610  2 BIRTH-M (N2)
0620  2 BIRTH-D (N2)
0630*
06401 #ERROR      (I4)
0650END-DEFINE
0660*
0670#BIRTH := #P-BIRTH
0680IF #P-BIRTH-J IS (N4) THEN
0690  BIRTH-J :=VAL(#P-BIRTH-J)
0700ELSE
0710  BIRTH-J := 0
0720END-IF
0730*
0740IF #P-BIRTH-M IS (N2) THEN
0750  BIRTH-M :=VAL(#P-BIRTH-M)
0760ELSE
0770  BIRTH-M := 0
0780END-IF
0790*
0800IF #P-BIRTH-D IS (N2) THEN
0810  BIRTH-D :=VAL(#P-BIRTH-D)
0820ELSE
0830  BIRTH-D := 0
0840END-IF
0850*
0860EXAMINE #P-NAME       TRANSLATE INTO UPPER
0870EXAMINE #P-FIRST-NAME TRANSLATE INTO UPPER
0880EXAMINE #P-BIRTH      TRANSLATE INTO UPPER
0890EXAMINE #P-SEX        TRANSLATE INTO UPPER
0900EXAMINE #P-DEPT       TRANSLATE INTO UPPER
0910EXAMINE #P-CITY       TRANSLATE INTO UPPER
0920EXAMINE #P-AREA-CODE  TRANSLATE INTO UPPER
0930EXAMINE #P-ZIP        TRANSLATE INTO UPPER
0940EXAMINE #P-COUNTRY    TRANSLATE INTO UPPER
0950EXAMINE #P-PHONE      TRANSLATE INTO UPPER
0960*
0970F. FIND EMPLOYEES-VIEW PERSONNEL-ID = #P-NO
0980  IF NO
0990    COMPRESS 'Sorry Object with personnel-id' #P-NO
1000      'does not exist:' INTO #INOUT-MSG
1010  END-NOREC
1020  IF *NUMBER > 0
1030    EMPLOYEES-VIEW.PERSONNEL-ID := #P-NO
1040    EMPLOYEES-VIEW.FIRST-NAME   := #P-FIRST-NAME
1050    EMPLOYEES-VIEW.NAME         := #P-NAME
1060    EMPLOYEES-VIEW.SEX          := #P-SEX
1070* Year2000 Employee File uses D instead of N6
1080*   EMPLOYEES-VIEW.BIRTH        := BIRTH-N
1090    MOVE EDITED BIRTH-A TO EMPLOYEES-VIEW.BIRTH (EM=YYYYMMDD)
1100    EMPLOYEES-VIEW.CITY          := #P-CITY
1110    EMPLOYEES-VIEW.ZIP          := #P-ZIP
1120    EMPLOYEES-VIEW.COUNTRY      := #P-COUNTRY
1130    EMPLOYEES-VIEW.AREA-CODE    := #P-AREA-CODE
1140    EMPLOYEES-VIEW.PHONE        := #P-PHONE
1150    EMPLOYEES-VIEW.DEPT         := #P-DEPT
1160    UPDATE
1170    END OF TRANSACTION
1180*
1190    COMPRESS
1200      'Update on Object' #P-NO '(old) performed' INTO #INOUT-MSG
1210  END-IF
1220END-FIND
1230*
1240END
