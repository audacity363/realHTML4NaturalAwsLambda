0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3EMBROW
0050*
0060* DESCRIPTION
0070*               Browse Persons
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING D3-PARM
0240LOCAL
0250*
02601 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0270  2 PERSONNEL-ID
0280  2 FIRST-NAME
0290  2 NAME
0300  2 PHONE
0310  2 AREA-CODE
0320  2 DEPT
0330*
03401 ##FROM           (A020)
03501 ##TO             (A020)
0360*
03701 #I               (N005)  INIT <0>
03801 #J               (N003)  INIT <0>
0390*
04001 W3VALUE          (A250)
04101 W3MAX            (I004)
04201 II               (N005)  INIT <1>
0430*
04401 V                (I004) CONST <20>
0450*
04601 H3ARRAYVALUE     (A072/1:V,1:6) /*  in: Background color
04701 H3ARRAYALIGN     (A001/1:V,1:6) /*  in: Align the Table
04801 H3ARRAYNOWRAP    (   L/1:V,1:6) /*  in: Align the Table
0490*
05001 #F-SELECTED      (   L) INIT <FALSE>
0510END-DEFINE
0520*
0530DEFINE SUBROUTINE D3EMBROW
0540*
0550*
0560##FROM := #DEMO-PARM.#FROM
0570##TO   := #DEMO-PARM.#TO
0580IF #TO EQ #FROM OR #TO EQ " " THEN
0590  COMPRESS ##FROM H'FF' INTO ##TO LEAVING NO
0600END-IF
0610IF #DEMO-PARM.#START LE 1 THEN
0620  #J := 1 + #DEMO-PARM.#END-MAX
0630ELSE
0640  #J := 2 + #DEMO-PARM.#END-MAX
0650END-IF
0660*
0670H3ARRAYVALUE(II,1) := 'S'
0680H3ARRAYVALUE(II,3) := 'First-Name'
0690H3ARRAYVALUE(II,2) := 'Name'
0700H3ARRAYVALUE(II,4) := 'Department'
0710H3ARRAYVALUE(II,5) := 'Area-Code'
0720H3ARRAYVALUE(II,6) := 'Phone'
0730*
0740F. FIND EMPLOYEES-VIEW WITH NAME = ##FROM THRU ##TO SORTED BY NAME
0750  IF NO
0760    COMPRESS ' Sorry nothing found for from:' ##FROM ' to:' ##TO
0770      INTO #DEMO-PARM.#MSG
0780  END-NOREC
0790  IF *NUMBER(F.) > 0
0800*
0810    IF *COUNTER(F.) GE #DEMO-PARM.#START THEN
0820      ADD 1 TO II
0830      COMPRESS EMPLOYEES-VIEW.PERSONNEL-ID INTO W3VALUE
0840      IF (NOT #F-SELECTED) AND (W3VALUE = #DEMO-PARM.#PERSON) THEN
0850        #F-SELECTED := TRUE
0860        COMPRESS 'VALUE="'EMPLOYEES-VIEW.PERSONNEL-ID'"'
0870          INTO W3VALUE LEAVING NO
0880        COMPRESS "<INPUT TYPE='RADIO' " W3VALUE
0890          "NAME='ID' CHECKED>" INTO H3ARRAYVALUE(II,1)
0900        H3ARRAYALIGN(II,1)  := "C"
0910        H3ARRAYNOWRAP(II,1) := FALSE
0920      ELSE
0930        COMPRESS 'VALUE="'EMPLOYEES-VIEW.PERSONNEL-ID'"'
0940          INTO W3VALUE LEAVING NO
0950        COMPRESS "<INPUT TYPE='RADIO' " W3VALUE "NAME='ID'>"
0960          INTO H3ARRAYVALUE(II,1)
0970        H3ARRAYALIGN(II,1)  := "C"
0980        H3ARRAYNOWRAP(II,1) := FALSE
0990      END-IF
1000*
1010      W3VALUE := EMPLOYEES-VIEW.NAME
1020      PERFORM H3-TEXT-TO-HTML W3MAX W3VALUE
1030      H3ARRAYVALUE(II,2)  := W3VALUE
1040      H3ARRAYALIGN(II,2)  := " "
1050      H3ARRAYNOWRAP(II,2) := TRUE
1060*
1070      W3VALUE := EMPLOYEES-VIEW.FIRST-NAME
1080      PERFORM H3-TEXT-TO-HTML W3MAX W3VALUE
1090      H3ARRAYVALUE(II,3)  := W3VALUE
1100      H3ARRAYALIGN(II,3)  := " "
1110      H3ARRAYNOWRAP(II,3) := TRUE
1120*
1130      W3VALUE := EMPLOYEES-VIEW.DEPT
1140      PERFORM H3-TEXT-TO-HTML W3MAX W3VALUE
1150      H3ARRAYVALUE(II,4)  := W3VALUE
1160      H3ARRAYALIGN(II,4)  := " "
1170      H3ARRAYNOWRAP(II,4) := FALSE
1180*
1190      COMPRESS EMPLOYEES-VIEW.AREA-CODE INTO H3ARRAYVALUE(II,5)
1200      H3ARRAYALIGN(II,5)  := "R"
1210      H3ARRAYNOWRAP(II,5) := FALSE
1220*
1230      COMPRESS EMPLOYEES-VIEW.PHONE INTO H3ARRAYVALUE(II,6)
1240      H3ARRAYALIGN(II,6)  := "L"
1250      H3ARRAYNOWRAP(II,6) := FALSE
1260*
1270    END-IF
1280    IF II GE #J THEN
1290      ESCAPE BOTTOM(F.)
1300    END-IF
1310  END-IF
1320END-FIND
1330*
1340IF NOT #F-SELECTED THEN
1350  EXAMINE H3ARRAYVALUE(*,1) FOR "NAME=" REPLACE FIRST
1360    WITH "CHECKED NAME="
1370END-IF
1380*
1390PERFORM H3-TABLE II 6 H3ARRAYVALUE(*,*) H3ARRAYALIGN(*,*)
1400  H3ARRAYNOWRAP(*,*) TRUE " " FALSE
1410  FALSE 1
1420*
1430#DEMO-PARM.#END := *NUMBER(F.)
1440*
1450IF *NUMBER(F.) > 0 AND #DEMO-PARM.#MSG = ' '
1460  II :=  #DEMO-PARM.#START + II -1
1470  COMPRESS 'List of'  *NUMBER(F.) 'objects .... from'
1480    #DEMO-PARM.#FROM 'to' #DEMO-PARM.#TO '; Object'
1490    #DEMO-PARM.#START "-" II INTO #DEMO-PARM.#MSG
1500END-IF
1510*
1520END-SUBROUTINE
1530*
1540END
