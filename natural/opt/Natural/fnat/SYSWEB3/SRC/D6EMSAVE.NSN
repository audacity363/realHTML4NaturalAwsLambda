0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D6EMCHAN
0050* DESCRIPTION
0060*               change a Person
0070*
0080* AUTHOR        SAG   08.09.1998
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+------------+--------------------------------------------
0170*       ! --.--.---- !
0180* ------+------------+--------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 #P-NO         (A) DYNAMIC BY VALUE
02401 #P-NAME       (A) DYNAMIC BY VALUE
02501 #P-FIRST-NAME (A) DYNAMIC BY VALUE
02601 #P-BIRTH      (A) DYNAMIC BY VALUE
02701 #P-SEX        (A) DYNAMIC BY VALUE
02801 #P-DEPT       (A) DYNAMIC BY VALUE
02901 #P-CITY       (A) DYNAMIC BY VALUE
03001 #P-AREA-CODE  (A) DYNAMIC BY VALUE
03101 #P-ZIP        (A) DYNAMIC BY VALUE
03201 #P-COUNTRY    (A) DYNAMIC BY VALUE
03301 #P-PHONE      (A) DYNAMIC BY VALUE
03401 #INOUT-MSG    (A) DYNAMIC
03501 #RESPONSE     (I4)
0360*
0370LOCAL
03801 #BIRTH       (A10)
03901 REDEFINE #BIRTH
0400  2 #P-BIRTH-D (A2)
0410  2 FILLER 1X
0420  2 #P-BIRTH-M (A2)
0430  2 FILLER 1X
0440  2 #P-BIRTH-J (A4)
0450*
04601 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0470  2 PERSONNEL-ID
0480  2 FIRST-NAME
0490  2 NAME
0500  2 SEX
0510  2 BIRTH
0520  2 CITY
0530  2 ZIP
0540  2 COUNTRY
0550  2 AREA-CODE
0560  2 PHONE
0570  2 DEPT
0580*
05901 BIRTH-A (A8)
06001 REDEFINE BIRTH-A
0610  2 BIRTH-J (N4)
0620  2 BIRTH-M (N2)
0630  2 BIRTH-D (N2)
0640*
06501 #ERROR      (I4)
0660END-DEFINE
0670*
0680RESET #RESPONSE #INOUT-MSG
0690*
0700#BIRTH := #P-BIRTH
0710IF #P-BIRTH-J IS (N4) THEN
0720  BIRTH-J :=VAL(#P-BIRTH-J)
0730ELSE
0740  BIRTH-J := 0
0750END-IF
0760*
0770IF #P-BIRTH-M IS (N2) THEN
0780  BIRTH-M :=VAL(#P-BIRTH-M)
0790ELSE
0800  BIRTH-M := 0
0810END-IF
0820*
0830IF #P-BIRTH-D IS (N2) THEN
0840  BIRTH-D :=VAL(#P-BIRTH-D)
0850ELSE
0860  BIRTH-D := 0
0870END-IF
0880*
0890EXAMINE #P-NAME       TRANSLATE INTO UPPER
0900EXAMINE #P-FIRST-NAME TRANSLATE INTO UPPER
0910EXAMINE #P-BIRTH      TRANSLATE INTO UPPER
0920EXAMINE #P-SEX        TRANSLATE INTO UPPER
0930EXAMINE #P-DEPT       TRANSLATE INTO UPPER
0940EXAMINE #P-CITY       TRANSLATE INTO UPPER
0950EXAMINE #P-AREA-CODE  TRANSLATE INTO UPPER
0960EXAMINE #P-ZIP        TRANSLATE INTO UPPER
0970EXAMINE #P-COUNTRY    TRANSLATE INTO UPPER
0980EXAMINE #P-PHONE      TRANSLATE INTO UPPER
0990*
1000F. FIND EMPLOYEES-VIEW PERSONNEL-ID = #P-NO
1010  IF NO
1020    COMPRESS 'Sorry Object with personnel-id' #P-NO
1030      'does not exist:' INTO #INOUT-MSG
1040      #RESPONSE := -1
1050  END-NOREC
1060  IF *NUMBER > 0
1070    EMPLOYEES-VIEW.PERSONNEL-ID := #P-NO
1080    EMPLOYEES-VIEW.FIRST-NAME   := #P-FIRST-NAME
1090    EMPLOYEES-VIEW.NAME         := #P-NAME
1100    EMPLOYEES-VIEW.SEX          := #P-SEX
1110*
1120    MOVE EDITED BIRTH-A TO EMPLOYEES-VIEW.BIRTH (EM=YYYYMMDD)
1130    EMPLOYEES-VIEW.CITY          := #P-CITY
1140    EMPLOYEES-VIEW.ZIP          := #P-ZIP
1150    EMPLOYEES-VIEW.COUNTRY      := #P-COUNTRY
1160    EMPLOYEES-VIEW.AREA-CODE    := #P-AREA-CODE
1170    EMPLOYEES-VIEW.PHONE        := #P-PHONE
1180    EMPLOYEES-VIEW.DEPT         := #P-DEPT
1190    UPDATE
1200    END OF TRANSACTION
1210*
1220    COMPRESS #INOUT-MSG
1230      'Update on Object' #P-NO '(old) performed' INTO #INOUT-MSG
1240  END-IF
1250END-FIND
1260*
1270END
