0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3RCGROUP
0050*
0060* DESCRIPTION   This Program generates a Radio Button Group
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 H3ARRAYCOUNT   (I004)     BY VALUE /* i /m : Number of group elements
02401 H3ARRAYNAME    (A/1:V) DYNAMIC BY VALUE /* i /m : Name of the group
0250*                                    /*        variable
02601 H3ARRAYVALUE   (A/1:V) DYNAMIC BY VALUE /* i /  : Default Value of the
0270*                                    /*        group variable
02801 H3ARRAYLABEL   (A/1:V) DYNAMIC BY VALUE /* i /mH: Label of the group
0290*                                    /*        element
03001 H3ARRAYCHECKED (L/1:V) BY VALUE /* i /m : Button selected by default
03101 H3LINEBREAK    (L)     BY VALUE /* i /m : Set line breaks
0320*                                    /*        between the elements
03301 H3ROW          (N4)     BY VALUE /* i /m : Set number of rows for
0340*                                    /*        tables
0350  1 H3COLUM        (N4)     BY VALUE /* i /m : Set number of columns
0360*                                    /*        for tables
0370*
0380  LOCAL  USING W3PINFO
0390  LOCAL  USING W3CONST
0400  LOCAL
0410  1 II             (I4)
0420  1 IK             (I4)
0430  1 IJ             (I4)
0440  1 REST           (I4)
0450  1 #H3VALUE       (A) DYNAMIC
0460  1 #NR            (A4)
0470  END-DEFINE
0480*
0490  DEFINE SUBROUTINE H3-CHECKBOX-GROUP
0500* ------ ERROR HANDLING ------------------------------------------
0510  ON ERROR
0520  ##W3ERROR.SUBROUTINE := "H3-CHECKBOX-GROUP"
0530  PERFORM W3ERROR ##W3ERROR
0540  ESCAPE ROUTINE
0550END-ERROR
0560*
0570* ------ MAIN PROGRAM --------------------------------------------
0580RESET ##W3INFO
0590PERFORM W3INFO ##W3INFO
0600* --- Can only be used inside a FORM tag. ---
0610IF ##W3INFO.FORM EQ FALSE THEN
0620  ##W3ERROR.SUBROUTINE := "H3-CHECKBOX-GROUP"
0630  ##W3ERROR.SUBPROGRAM := *PROGRAM
0640  ##W3ERROR.NR         := 20003
0650  ##W3ERROR.LINE       := 0
0660  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0670  PERFORM W3ERROR ##W3ERROR
0680  ESCAPE ROUTINE
0690END-IF
0700* --- No elements defined. ---
0710IF H3ARRAYCOUNT LE 0 THEN
0720  ##W3ERROR.SUBROUTINE := "H3-CHECKBOX-GROUP"
0730  ##W3ERROR.SUBPROGRAM := *PROGRAM
0740  ##W3ERROR.NR         := 20003
0750  ##W3ERROR.LINE       := 0
0760  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0770  PERFORM W3ERROR ##W3ERROR
0780  ESCAPE ROUTINE
0790END-IF
0800*
0810IF H3COLUM > 0 AND  H3ROW = 0 THEN
0820  DIVIDE H3COLUM INTO H3ARRAYCOUNT GIVING H3ROW REMAINDER REST
0830  ADD 1 TO H3ROW
0840END-IF
0850*
0860IF H3ROW GT 0 THEN
0870  PERFORM W3TEXTDYNAMIC '<TABLE>'
0880  IK := H3ROW + 1
0890  IJ := -1
0900END-IF
0910* --- Element' II 'has no name. ---
0920FOR II = 1 TO H3ARRAYCOUNT
0930  IF H3ARRAYNAME(II) EQ " " THEN
0940    ##W3ERROR.SUBROUTINE := "H3-CHECKBOX-GROUP"
0950    ##W3ERROR.SUBPROGRAM := *PROGRAM
0960    ##W3ERROR.NR         := 20009
0970    ##W3ERROR.LINE       := 0
0980    PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0990    #NR := II
1000    EXAMINE ##W3ERROR.TEXT FOR '%1%' REPLACE WITH #NR
1010    PERFORM W3ERROR ##W3ERROR
1020    ESCAPE ROUTINE
1030  END-IF
1040*
1050  IF H3ROW GT 0 OR H3COLUM GT 0 THEN
1060    IF IK GE H3ROW THEN
1070      COMPRESS '<TR><TD>' INTO #H3VALUE LEAVING NO
1080      PERFORM W3TEXTDYNAMIC #H3VALUE
1090      IK := 0
1100      ADD 1 TO IJ
1110    ELSE
1120      COMPRESS '<TD>' INTO #H3VALUE LEAVING NO
1130      PERFORM W3TEXTDYNAMIC #H3VALUE
1140    END-IF
1150  END-IF
1160*
1170  IF H3ARRAYNAME(II) NE " " THEN
1180    EXAMINE H3ARRAYNAME(II) TRANSLATE INTO UPPER
1190    COMPRESS 'NAME="' H3ARRAYNAME(II) '"' INTO #H3VALUE LEAVING NO
1200  ELSE
1210    RESET #H3VALUE
1220  END-IF
1230*
1240  COMPRESS '<INPUT TYPE="CHECKBOX"' #H3VALUE INTO #H3VALUE
1250  PERFORM W3TEXTDYNAMIC #H3VALUE
1260*
1270  IF H3ARRAYVALUE(II) NE " " THEN
1280    COMPRESS 'VALUE="' INTO #H3VALUE LEAVING NO
1290    PERFORM W3TEXTDYNAMIC #H3VALUE
1300    #H3VALUE := H3ARRAYVALUE(II)
1310    PERFORM W3TEXTDYNAMIC #H3VALUE
1320    COMPRESS  '"' INTO #H3VALUE LEAVING NO
1330    PERFORM W3TEXTDYNAMIC #H3VALUE
1340  END-IF
1350*
1360  IF H3ARRAYCHECKED(II) THEN
1370    COMPRESS 'CHECKED>' INTO #H3VALUE LEAVING NO
1380    PERFORM W3TEXTDYNAMIC #H3VALUE
1390  ELSE
1400    COMPRESS '>' INTO #H3VALUE LEAVING NO
1410    PERFORM W3TEXTDYNAMIC #H3VALUE
1420  END-IF
1430*
1440  IF H3ARRAYLABEL(II) EQ " " THEN
1450    IF H3ARRAYVALUE(II) EQ " " THEN
1460      #H3VALUE :=  H3ARRAYNAME(II)
1470    ELSE
1480      #H3VALUE := H3ARRAYVALUE(II)
1490    END-IF
1500  ELSE
1510    #H3VALUE := H3ARRAYLABEL(II)
1520  END-IF
1530  PERFORM W3HTML #H3VALUE
1540*
1550  IF H3ROW GT 0 OR H3COLUM GT 0 THEN
1560    ADD 1 TO IK
1570    IF IK GE H3ROW THEN
1580      COMPRESS '</TD></TR>' ##HTTP_NEWLINE
1590        INTO #H3VALUE LEAVING NO
1600      PERFORM W3TEXTDYNAMIC #H3VALUE
1610    ELSE
1620      COMPRESS '</TD>' INTO #H3VALUE LEAVING NO
1630      PERFORM W3TEXTDYNAMIC #H3VALUE
1640    END-IF
1650  ELSE
1660    IF H3LINEBREAK EQ TRUE THEN
1670      COMPRESS '<BR>' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
1680      PERFORM W3TEXTDYNAMIC #H3VALUE
1690    ELSE
1700      PERFORM W3TEXTDYNAMIC ##HTTP_NEWLINE
1710    END-IF
1720  END-IF
1730*
1740END-FOR
1750IF H3ROW GT 0 OR H3COLUM GT 0 THEN
1760  IF IK LT H3ROW THEN
1770    REPEAT
1780      IF IK GE H3ROW THEN
1790        IK := 0
1800        ADD 1 TO IJ
1810        ESCAPE BOTTOM
1820      ELSE
1830        ADD 1 TO IK
1840        IF IK GE H3ROW THEN
1850          COMPRESS '<TD></TD></TR>' ##HTTP_NEWLINE
1860            INTO #H3VALUE LEAVING NO
1870          PERFORM W3TEXTDYNAMIC #H3VALUE
1880        ELSE
1890          COMPRESS '<TD></TD>'
1900            INTO #H3VALUE LEAVING NO
1910          PERFORM W3TEXTDYNAMIC #H3VALUE
1920        END-IF
1930      END-IF
1940    END-REPEAT
1950  END-IF
1960  PERFORM W3TEXTLINEDYNAMIC '</TABLE>'
1970END-IF
1980*
1990END-SUBROUTINE
2000END
