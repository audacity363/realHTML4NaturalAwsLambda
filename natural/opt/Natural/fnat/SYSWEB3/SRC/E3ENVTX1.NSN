0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Example Programs
0030*
0040*        E3ENVTX1
0050*
0060* DESCRIPTION   Reads a variable set by a text area tag and separates
0070*               the text lines.
0080*
0090* SEE           .../sysweb/t3envtx
0100*
0110* AUTHOR        SAG   08.09.1998
0120*
0130* Version       8.3
0140*
0150* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0160*
0170* HISTORY
0180* USER    DATE       REASON
0190* ------+------------+----------------------------------------------
0200*       ! --.--.---- !
0210* ------+------------+----------------------------------------------
0220*
0230* ------ PARAMETER -----------------------------------------------
0240DEFINE DATA
0250PARAMETER USING W3PARM
0260LOCAL USING W3CONST
0270LOCAL
02801 V                  (I4) CONST <10>
02901 W3START            (I4)
03001 W3NAME             (A72)
03101 W3VALUESERVER      (A1)
03201 W3COUNT            (I4)
03301 W3ARRAYVALUE       (A250/1:V)
03401 W3ARRAYVALUELENGTH (I4/1:V)
03501 W3ARRAYMAXIMUM     (I4)
0360*
03701 W3VALUE          (A) DYNAMIC
03801 II               (I4)
0390END-DEFINE
0400* --- ERROR HANDLING ---
0410ON ERROR
0420  PERFORM W3ERROR ##W3ERROR
0430   PERFORM W3END ##RPC
0440  ESCAPE ROUTINE
0450END-ERROR
0460*
0470* --- INITIALIZE Web Interface ---
0480PERFORM W3INIT ##RPC
0490*
0500* --- SET TYPE OF RETURN-PAGE ---
0510PERFORM W3CONTENT-TYPE 'text/html'
0520* --- WRITE THE HEAD OF THE DOCUMENT ---
0530PERFORM W3TEXTLINE "<!DOCTYPE 'HTML PUBLIC -//W3C//DTD HTML 3.2//EN'>"
0540PERFORM W3TEXTLINE '<HTML><HEAD><META HTTP-EQUIV="Content-Type" '-
0550  'CONTENT="text/html; charset=iso-8859-1"><TITLE>'
0560* --- THE TITLE OF THE PAGE ---
0570PERFORM W3TEXTLINE 'Basic Routines - Read Environment Text Area Variables'
0580PERFORM W3TEXTLINE '</TITLE></HEAD><BODY>'
0590PERFORM W3TEXTLINE '<H2>Read Environment Text Area Variables</H2>'
0600*
0610W3COUNT       := 3
0620W3START       := 0
0630*
0640REPEAT
0650PERFORM W3READ-ENVIRONMENT-TEXTAREA W3START "TEXT" 'P'
0660  W3COUNT W3ARRAYVALUE(*) W3ARRAYVALUELENGTH(*) W3ARRAYMAXIMUM
0670*
0680PERFORM W3TEXTLINE '<TABLE BORDER="1"><TR><TH>Text</TH><TH>Number</TH>'-
0690  '</TR>'
0700*
0710FOR II = 1 TO W3COUNT
0720  COMPRESS "<TR><TD>"  INTO W3VALUE
0730  PERFORM W3TEXTDYNAMIC W3VALUE
0740*
0750  PERFORM W3TEXTDYNAMIC W3ARRAYVALUE(II)
0760*
0770  COMPRESS "</TD><TD>"
0780    W3ARRAYVALUELENGTH(II) "</TD></TR>" ##HTTP_NEWLINE_END
0790    INTO W3VALUE
0800  PERFORM W3TEXTDYNAMIC W3VALUE
0810END-FOR
0820*
0830PERFORM W3TEXTLINE "</TABLE>"
0840*
0850COMPRESS '<BR>Restart from:' W3START
0860  '<BR>Read lines:' W3COUNT
0870  '<BR>Maximum lines:' W3ARRAYMAXIMUM
0880  INTO W3VALUE
0890PERFORM W3TEXTLINEDYNAMIC W3VALUE
0900IF W3START = 0
0910ESCAPE BOTTOM
0920END-IF
0930END-REPEAT
0940*
0950* --- END THE HTML PAGE ---
0960COMPRESS "<TABLE BORDER='0' WIDTH='100%' CELLSPACING='0' "-
0970  "CELLPADDING='5'>"
0980  '<TR BGCOLOR="#00cc66"><TD>' *PROGRAM '&#32;-&#32;' *DATE '&#32;' *TIME
0990  "</TD><TD ALIGN='RIGHT'>" 'Natural'
1000  "</TD></TR></TABLE>"
1010  '</BODY></HTML>' INTO W3VALUE LEAVING NO
1020PERFORM W3TEXTDYNAMIC W3VALUE
1030*
1040* --- END Web Interface ---
1050PERFORM W3END ##RPC
1060*
1070END
1080
