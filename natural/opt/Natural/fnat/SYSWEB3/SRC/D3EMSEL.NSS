0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3EMSEL
0050*
0060* DESCRIPTION
0070*               Select Persons
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING D3-PARM
0240LOCAL
02501 ##FROM           (A020)
02601 ##TO             (A020)
0270*
02801 V                (I004) CONST <200>
0290*
03001 H3ARRAYVALUE     (A072/1:V) /*  in: Background color
03101 H3ARRAYLABEL     (A072/1:V) /*  in: Name of the label of the button
03201 H3ARRAYSELECTED  (   L/1:V) /*  in: Number of the pre-selected button
0330*
03401 W3SIZE           (N004)
0350*
0360LOCAL
03701 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0380  2 PERSONNEL-ID
0390  2 NAME
0400  2 FIRST-NAME
0410*
04201 #I (N7)  INIT <0>
04301 #J (N3)  INIT <0>
0440*
04501 W3VALUE (A250)
04601 #F-SELECTED (L) INIT <FALSE>
0470END-DEFINE
0480*
0490DEFINE SUBROUTINE D3EMSEL
0500* -------------------------------------------------------------
0510##FROM := #DEMO-PARM.#FROM
0520##TO   := #DEMO-PARM.#TO
0530IF #TO EQ #FROM OR #TO EQ " " THEN
0540  COMPRESS ##FROM H'FF' INTO ##TO LEAVING NO
0550END-IF
0560*
0570IF #DEMO-PARM.#START LE 1 THEN
0580  #J := 1 + #DEMO-PARM.#END-MAX
0590ELSE
0600  #J := 2 + #DEMO-PARM.#END-MAX
0610END-IF
0620*
0630F. FIND EMPLOYEES-VIEW WITH NAME = ##FROM THRU ##TO SORTED BY NAME
0640  IF NO
0650    COMPRESS ' Sorry nothing found for from:' ##FROM ' to:'
0660      ##TO INTO #DEMO-PARM.#MSG
0670  END-NOREC
0680  IF *NUMBER(F.) > 0
0690    IF *COUNTER(F.) GE #DEMO-PARM.#START THEN
0700      ADD 1 TO #I
0710      COMPRESS EMPLOYEES-VIEW.PERSONNEL-ID INTO H3ARRAYVALUE(#I)
0720      COMPRESS  EMPLOYEES-VIEW.NAME EMPLOYEES-VIEW.FIRST-NAME
0730        INTO H3ARRAYLABEL(#I)
0740      IF (NOT #F-SELECTED) AND (H3ARRAYVALUE(#I) = #DEMO-PARM.#PERSON)
0750        #F-SELECTED := TRUE
0760        H3ARRAYSELECTED(#I) := TRUE
0770      ELSE
0780        H3ARRAYSELECTED(#I) := FALSE
0790      END-IF
0800*
0810      IF #I GE #J THEN
0820        ESCAPE BOTTOM(F.)
0830      END-IF
0840    END-IF
0850  END-IF
0860END-FIND
0870*
0880IF *NUMBER(F.) > 10 THEN
0890  W3SIZE := 10
0900ELSE
0910  W3SIZE := *NUMBER(F.)
0920END-IF
0930*
0940IF NOT #F-SELECTED THEN
0950  H3ARRAYSELECTED(1) := TRUE
0960END-IF
0970*
0980IF #I = 0 THEN
0990  PERFORM W3TEXTLINEDYNAMIC "(NONE)"
1000ELSE
1010  PERFORM H3-SCROLLING-LIST W3SIZE #I "ID"
1020    H3ARRAYVALUE(*) H3ARRAYLABEL(*) H3ARRAYSELECTED(*) FALSE
1030END-IF
1040*
1050#DEMO-PARM.#END := *NUMBER(F.)
1060*
1070IF *NUMBER(F.) > 0 AND #DEMO-PARM.#MSG = ' '
1080  #I :=  #DEMO-PARM.#START + #I -1
1090  COMPRESS 'List of'  *NUMBER(F.) 'objects .... from'
1100    #DEMO-PARM.#FROM 'to' #DEMO-PARM.#TO '; Object'
1110    #DEMO-PARM.#START "-" #I INTO #DEMO-PARM.#MSG
1120END-IF
1130END-SUBROUTINE
1140END
