0010* ---------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Utilities
0030*
0040*        NAT-ENV
0050*
0060* DESCRIPTION
0070*               This Program returnes the environment send via
0080*               HTTP API
0090*
0100* AUTHOR        SAG   28.07.97
0110*
0120* Version       8.3
0130*
0140* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0150*
0160* HISTORY
0170* USER    DATE       REASON
0180* ------+----------+----------------------------------------------
0190*       ! --.--.-- !
0200* ------+----------+----------------------------------------------
0210DEFINE DATA
0220* ------------------------------------------- standard PDA for HTTP Api
0230PARAMETER USING W3PARM
0240* -------------------------------------------- definitions for HTTP Api
0250LOCAL  USING W3CONST
0260LOCAL  USING W3PINFO
0270LOCAL
02801 #HTML                  (A) DYNAMIC
0290* ---------------------------------------------- parameter W3READ-INPUT
03001 #BODY                  (A) DYNAMIC
03101 #BINARY                (B) DYNAMIC
03201 #HEADER                (A) DYNAMIC
03301 #DATA                  (A) DYNAMIC
0340* ---------------------------------------- parameter W3LIST-ENVIRONMENT
03501 V                    (I2)  CONST <25>
03601 ANAME                (A/1:V) DYNAMIC
03701 ANAMEVALUE           (A/1:V) DYNAMIC
03801 ANAMEANZ             (I4)
03901 ANAMESERVER          (L/1:V)
04001 ANAMESTART           (I4) INIT <1>
04101 ANAMEMAX             (I4)
0420* ---------------------------------------------------- display binary
04301 #A1 (A1)
0440*
04501 #II (I4)
04601 #IJ (I4)
04701 #IL (I4)
04801 #ILL (I4)
0490*
05001 #B24 (B24)
05101 #A24 (A) DYNAMIC
05201 #A48 (A48)
05301 REDEFINE #A48
0540  2 #A48-2 (A2/1:24)
05501 #RESP (I4)
0560*
0570
0580END-DEFINE
0590* ---------------------------------------------------- error handling
0600ON ERROR
0610  PERFORM W3ERROR ##W3ERROR
0620  PERFORM W3END ##RPC
0630*
0640  ESCAPE ROUTINE
0650END-ERROR
0660* ======================================================== Main Program
0670* ------------------------------------------------------- init HTML API
0680PERFORM W3INIT ##RPC TRUE TRUE
0690* --------------------------------------------- Set type of return Page
0700PERFORM W3CONTENT-TYPE 'text/html; charset=utf-8'
0710PERFORM W3INFO ##W3INFO
0720PERFORM W3READ-INPUT #BODY #BINARY #HEADER #DATA
0730* ------------------------------------------- Set head of the HTML page
0740COMPRESS
0750  '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">' ##HTTP_NEWLINE
0760  '<HTML>'
0770  '<HEAD>'
0780  '<TITLE>HTTP - All Input Data</TITLE>'-
0790  '<META HTTP-EQUIV="Content-Type" CONTENT="text/html; charset=utf-8">'
0800  '<STYLE><!--'
0810  ' pre  {background:#FFFFFF; font-family: "Courier New", monospace; font-size: 90%}'
0820  ' td.g {background:#CCCCCC; font-family: "Courier New", monospace; font-size: 90%}' ##HTTP_NEWLINE
0830  ' td.w {background:#FFFFFF; font-family: "Courier New", monospace; font-size: 90%}' ##HTTP_NEWLINE
0840  ' td.r {background:#CCFFCC; color:darkblue; font-family: "Courier New", monospace; font-size: 90% }' ##HTTP_NEWLINE
0850  '--></STYLE>'
0860  '</HEAD>'
0870  '<BODY BGCOLOR=#FFFFFF>'
0880  '<P>&nbsp;'
0890  '<H1 ALIGN="CENTER">'
0900  '<FONT COLOR="NAVY">All Input Data</FONT>'
0910  '</H1>' ##HTTP_NEWLINE
0920  '<TABLE BORDER="0" CELLPADDING="5" CELLSPACING="3">' ##HTTP_NEWLINE
0930  '<TR>'
0940  '<TH>&nbsp;</TH>'
0950  '<TH BGCOLOR="#999999" WIDTH="100%">Value</TH>'
0960  '<TH BGCOLOR="#999999">Length</TH>'
0970  '</TR>' ##HTTP_NEWLINE
0980  '<TR>'
0990  '<TH BGCOLOR="#999999">Mime-Type</TH>'
1000  '<TD><PRE>' INTO #HTML LEAVING NO
1010PERFORM W3TEXTDYNAMIC #HTML
1020PERFORM W3HTMLDYNAMIC ##W3INFO.MIME-TYPE
1030COMPRESS
1040  '</PRE></TD><TD valign="top">' *LENGTH(##W3INFO.MIME-TYPE) '</TD>'
1050  '</TR>' ##HTTP_NEWLINE
1060  '<TR>'
1070  '<TH BGCOLOR="#999999" valign="top">Header</TH>'
1080  '<TD><PRE>' INTO #HTML LEAVING NO
1090PERFORM W3TEXTDYNAMIC #HTML
1100PERFORM W3HTMLDYNAMIC #HEADER
1110COMPRESS
1120  '</PRE></TD><TD valign="top">' *LENGTH(#HEADER) '</TD>'
1130  '</TR>' ##HTTP_NEWLINE
1140  '<TR>'
1150  '<TH BGCOLOR="#999999" valign="top">Data</TH>'
1160  '<TD><PRE>' INTO #HTML LEAVING NO
1170PERFORM W3TEXTDYNAMIC #HTML
1180PERFORM W3HTMLDYNAMIC #DATA
1190COMPRESS
1200  '</PRE></TD>'
1210  '<TD valign="top">' *LENGTH(#DATA) '</TD>'
1220  '</TR>' ##HTTP_NEWLINE
1230  '<TR>'
1240  '<TH BGCOLOR="#999999" valign="top">Body</TH>'
1250  '<TD><PRE>' INTO #HTML LEAVING NO
1260PERFORM W3TEXTDYNAMIC #HTML
1270PERFORM W3HTMLDYNAMIC #BODY
1280COMPRESS
1290  '</PRE></TD>'
1300  '<TD valign="top">' *LENGTH(#BODY) '</TD>'
1310  '</TR>' ##HTTP_NEWLINE
1320  '<TR>'
1330  '<TH BGCOLOR="#999999" valign="top">Binary</TH>'
1340  '<TD><PRE>' INTO #HTML LEAVING NO
1350PERFORM W3TEXTDYNAMIC #HTML
1360*
1370PERFORM DISPLAY-BINARY
1380*
1390COMPRESS
1400  '</PRE></TD>'
1410  '<TD valign="top">' *LENGTH(#BINARY) '</TD>'
1420  '</TR>' ##HTTP_NEWLINE INTO #HTML LEAVING NO
1430* -------------------------- Display current time/date and program name
1440* ------------------------------------------------- and close HTML page
1450
1460COMPRESS #HTML
1470  '</TABLE>'  ##HTTP_NEWLINE
1480  '<BR>'
1490  '<TABLE BORDER="0" WIDTH="100%" CELLSPACING="0" CELLPADDING="5">'
1500  '<TR BGCOLOR="#00cc66">'
1510  '<TD>' *PROGRAM '&nbsp;-&nbsp;' ##W3INFO.LOG-TIME '</TD>'
1520  '<TD ALIGN="RIGHT">Natural</TD>'
1530  '</TR>'
1540  '</TABLE>'
1550  '</BODY>'
1560  '</HTML>' INTO #HTML LEAVING NO
1570PERFORM W3TEXTDYNAMIC #HTML
1580* ------------------------------------------------------ close HTTP Api
1590*
1600PERFORM W3END ##RPC
1610* ================================================================= End
1620*
1630DEFINE SUBROUTINE DISPLAY-BINARY
1640*
1650#HTML := '<table border="0" cellpadding="0" cellspacing="0">'
1660*
1670#ILL := 0
1680FOR #II = 1 TO *LENGTH(#BINARY) STEP 24
1690  #IL := *LENGTH(#BINARY) - #II
1700
1710  IF 0 < #IL AND #IL < 24
1720    RESET #B24
1730    ADD 1 TO #IL
1740    MOVE SUBSTR(#BINARY,#II,#IL) TO SUBSTR(#B24,1,#IL)
1750    MOVE EDITED #B24 (EM=H(48)) TO #A48
1760    #A24 := #B24
1770    #IJ := #IL * 2 +1
1780    IF #IJ < 24
1790      MOVE " " TO SUBSTR(#A48,#IJ)
1800    END-IF
1810    FOR #IJ = 1 TO #IL
1820      #A1 := SUBSTR(#A24,#IJ,1)
1830*
1840      IF #A1 LT H'24' OR #A1 GT H'FE'
1850        MOVE '.' TO SUBSTR(#A24,#IJ,1)
1860      END-IF
1870      IF #A1 GE H'7F' AND #A1 LE H'9F'
1880        MOVE '.' TO SUBSTR(#A24,#IJ,1)
1890      END-IF
1900    END-FOR
1910  ELSE
1920    IF #II LT *LENGTH(#BINARY)
1930      #B24 := SUBSTR(#BINARY,#II,24)
1940      MOVE EDITED #B24 (EM=H(48)) TO #A48
1950      #A24 := #B24
1960      FOR #IJ = 1 TO 24
1970        #A1 := SUBSTR(#A24,#IJ,1)
1980*
1990        IF #A1 LT H'20' OR #A1 GE H'A0'
2000          MOVE '.' TO SUBSTR(#A24,#IJ,1)
2010        END-IF
2020        IF #A1 GE H'7F' AND #A1 LE H'9F'
2030          MOVE '.' TO SUBSTR(#A24,#IJ,1)
2040        END-IF
2050      END-FOR
2060    END-IF
2070  END-IF
2080  #IL := 0
2090  COMPRESS #HTML '<tr><td align="right">' #II '&nbsp;<td>' INTO #HTML LEAVING NO
2100  FOR #IJ = 1 TO 24
2110    ADD 1 TO #ILL
2120    IF #ILL <= *LENGTH(#BINARY)
2130      IF #IL = 0
2140        COMPRESS #HTML '<td class="g">'
2150          #A48-2(#IJ)
2160          '</td>' INTO #HTML LEAVING NO
2170        ADD 1 TO #IL
2180      ELSE
2190        COMPRESS #HTML '<td class="w">'
2200          #A48-2(#IJ)
2210          '</td>' INTO #HTML LEAVING NO
2220        RESET #IL
2230      END-IF
2240    ELSE
2250      COMPRESS #HTML '<td class="w">'
2260        '&nbsp;'
2270        '</td>' INTO #HTML LEAVING NO
2280      RESET #IL
2290    END-IF
2300  END-FOR
2310
2320  PERFORM W3TEXT-TO-HTML #A24
2330  COMPRESS #HTML '<td>&nbsp;</td><td class="r">'
2340    #A24
2350    '</td></tr>' ##HTTP_NEWLINE INTO #HTML LEAVING NO
2360*
2370*  PERFORM W3TEXTDYNAMIC #HTML
2380* #HTML := ##NULL-STRING
2390*
2400  IF #II > 100000
2410    #II := *LENGTH(#BINARY) / 24
2420    COMPRESS #HTML '<tr><td colspan="20" align="center"><br><b>Output truncated, "' #II
2430      '" more lines.</b></td></tr>' ##HTTP_NEWLINE INTO #HTML LEAVING NO
2440    ESCAPE BOTTOM
2450  END-IF
2460END-FOR
2470*
2480COMPRESS #HTML '</table>' ##HTTP_NEWLINE INTO #HTML LEAVING NO
2490
2500PERFORM W3TEXTDYNAMIC #HTML
2510*
2520END-SUBROUTINE
2530END
