0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3BUTTON
0050*
0060* DESCRIPTION   This Program generates a submit or reset Button
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 #H3TYPE   (A1)        BY VALUE /*  i /m : 'R' reset-button
0240*                                /*         'S' submit-button
0250*                                /*         'I' submit-button with image
02601 #H3NAME   (A) DYNAMIC BY VALUE /*  i /m : Name of the button
02701 #H3VAL    (A) DYNAMIC BY VALUE /*  i /m : Value of the input variable
02801 #H3SRC    (A) DYNAMIC BY VALUE /*  i /m : URL of the picture to be used
0290LOCAL  USING W3CONST
0300LOCAL  USING W3PINFO
0310LOCAL
03201 #H3VALUE  (A) DYNAMIC
0330END-DEFINE
0340*
0350DEFINE SUBROUTINE H3-BUTTON
0360* ------ ERROR HANDLING ------------------------------------------
0370ON ERROR
0380  ##W3ERROR.SUBROUTINE := "H3-BUTTON"
0390  PERFORM W3ERROR ##W3ERROR
0400  ESCAPE ROUTINE
0410END-ERROR
0420*
0430PERFORM W3INFO ##W3INFO
0440* ------ MAIN PROGRAM --------------------------------------------
0450* 'Can only be used inside a FORM tag.'
0460IF ##W3INFO.FORM EQ FALSE THEN
0470  ##W3ERROR.SUBROUTINE := "H3-BUTTON"
0480  ##W3ERROR.SUBPROGRAM := *PROGRAM
0490  ##W3ERROR.NR         := 20003
0500  ##W3ERROR.LINE       := 0
0510  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0520  PERFORM W3ERROR ##W3ERROR
0530  ESCAPE ROUTINE
0540END-IF
0550*
0560EXAMINE #H3TYPE TRANSLATE INTO UPPER
0570*
0580IF #H3NAME NE " " THEN
0590  EXAMINE #H3NAME TRANSLATE INTO UPPER
0600  COMPRESS 'NAME="' #H3NAME '"' INTO #H3VALUE LEAVING NO
0610ELSE
0620  RESET #H3VALUE
0630END-IF
0640*
0650DECIDE ON FIRST VALUE OF #H3TYPE
0660  VALUE "R"
0670    COMPRESS '<INPUT TYPE="RESET"' #H3VALUE INTO #H3VALUE
0680*
0690    IF #H3VAL NE " " THEN
0700      COMPRESS #H3VALUE 'VALUE="' INTO #H3VALUE
0710      COMPRESS #H3VALUE #H3VAL '">' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
0720    ELSE
0730      COMPRESS #H3VALUE '>' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
0740    END-IF
0750    PERFORM W3TEXTDYNAMIC #H3VALUE
0760*
0770  VALUE "S", " "
0780    COMPRESS '<INPUT TYPE="SUBMIT"' #H3VALUE INTO #H3VALUE
0790*
0800    IF #H3VAL NE " " THEN
0810      COMPRESS #H3VALUE 'VALUE="' INTO #H3VALUE
0820      COMPRESS #H3VALUE #H3VAL '">' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
0830    ELSE
0840      COMPRESS #H3VALUE '>' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
0850    END-IF
0860    PERFORM W3TEXTDYNAMIC #H3VALUE
0870*
0880  VALUE "I"
0890    COMPRESS '<INPUT TYPE="IMAGE" BORDER="0"' #H3VALUE INTO #H3VALUE
0900*
0910    IF #H3SRC NE " " THEN
0920      COMPRESS #H3VALUE 'SRC="' INTO #H3VALUE
0930      COMPRESS #H3VALUE #H3SRC '"' INTO #H3VALUE LEAVING NO
0940    END-IF
0950*
0960    IF #H3VAL NE " " THEN
0970      COMPRESS #H3VALUE 'VALUE="' INTO #H3VALUE
0980      COMPRESS #H3VALUE #H3VAL '">' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
0990    ELSE
1000      COMPRESS #H3VALUE '>' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
1010    END-IF
1020    PERFORM W3TEXTDYNAMIC #H3VALUE
1030*
1040  NONE
1050    IGNORE
1060END-DECIDE
1070*
1080END-SUBROUTINE
1090END
