0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        T-MENU
0050*
0060* DESCRIPTION
0070*               GENERATE HTML-PAGE FOR THE MAIN MENU
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING W3PARM
0240LOCAL USING D3-PARM
0250LOCAL USING W3CONST
0260LOCAL
0270*
02801 H3ARRAYVALUE       (A072/1:2)
02901 H3ARRAYLABLE       (A072/1:2)
03001 H3ARRAYSELECTED    (   L/1:2)
03101 W3URL        (A250)
03201 W3VALUE      (A250)
0330END-DEFINE
0340*
0350ON ERROR
0360  PERFORM W3ERROR ##W3ERROR
0370  PERFORM W3END ##RPC
0380  ESCAPE ROUTINE
0390END-ERROR
0400*
0410PERFORM W3INIT ##RPC
0420*
0430PERFORM D3-RGLOB #DEMO-MENU #DEMO-PARM
0440*
0450#DEMO-PARM.#TODO := "MENU"
0460*
0470COMPRESS 'NATweb -' #DEMO-PARM.#TODO INTO W3VALUE
0480PERFORM H3-OPEN-HTML W3VALUE "FFFF80" " "
0490*
0500COMPRESS  #DEMO-PARM.#CGI "/D3CHOOSE" INTO W3URL LEAVING NO
0510PERFORM H3-OPEN-FORM "G" W3URL
0520*
0530PERFORM W3TEXTLINEDYNAMIC "<BR><H1 ALIGN='CENTER'>"-
0540  'Maintenance of File<BR>'
0550PERFORM W3TEXTLINEDYNAMIC "<FONT SIZE=5>MENU</FONT></H1>"
0560*
0570PERFORM W3TEXTLINEDYNAMIC "<HR><P ALIGN='CENTER'><TABLE BORDER=0>"
0580PERFORM W3TEXTLINEDYNAMIC "<TR><TD ROWSPAN='7'>"
0590*
0600COMPRESS #DEMO-PARM.#GIF '/natw_peo.gif' INTO W3URL LEAVING NO
0610PERFORM H3-IMAGE W3URL ' ' 153 220 "L"
0620*
0630PERFORM W3TEXTLINEDYNAMIC '</TD></TR><TR><TD>&nbsp;</TD><TD><B>File:</B><TD>'
0640*
0650H3ARRAYVALUE(1)    := "EM"
0660H3ARRAYVALUE(2)    := "VE"
0670H3ARRAYLABLE(1)    := " EMPLOYEES "
0680H3ARRAYLABLE(2)    := " VEHICLES  "
0690H3ARRAYSELECTED(1) := TRUE
0700H3ARRAYSELECTED(2) := FALSE
0710*
0720PERFORM H3-SCROLLING-LIST 1 2 "OT" H3ARRAYVALUE(*)
0730  H3ARRAYLABLE(*) H3ARRAYSELECTED(*) FALSE
0740*
0750PERFORM W3TEXTLINEDYNAMIC '</TD></TR>'
0760*
0770IF #DEMO-PARM.#FROM EQ " " THEN
0780  #DEMO-PARM.#FROM := "A"
0790END-IF
0800*
0810IF #DEMO-PARM.#TO EQ " " THEN
0820  #DEMO-PARM.#TO := "D"
0830END-IF
0840*
0850PERFORM W3TEXTLINEDYNAMIC '<TR><TD>&nbsp;'-
0860  '<TR><TD>&nbsp;</TD><TD><B>Selection by Name</B></TD>'-
0870  '<Tr><TR><TD>&nbsp;</TD><TD><B>From:</B><TD>'
0880PERFORM H3-INPUT "T" "FROM" #DEMO-PARM.#FROM 5 20
0890PERFORM W3TEXTLINEDYNAMIC '<TR><TD>&nbsp;</TD><TD><B>To:</B><TD>'
0900PERFORM H3-INPUT "T" "TO" #DEMO-PARM.#TO 5 20
0910PERFORM W3TEXTLINEDYNAMIC "</TABLE>"
0920PERFORM W3TEXTLINEDYNAMIC "<BR><BR>"
0930*
0940#DEMO-MENU.#SE := "T"
0950#DEMO-MENU.#BR := "T"
0960#DEMO-MENU.#LI := "T"
0970#DEMO-MENU.#AD := "T"
0980*
0990PERFORM D3-MENUBR #DEMO-MENU #DEMO-PARM
1000*
1010PERFORM H3-CLOSE-FORM
1020PERFORM H3-CLOSE-HTML
1030PERFORM W3END ##RPC
1040*
1050END
