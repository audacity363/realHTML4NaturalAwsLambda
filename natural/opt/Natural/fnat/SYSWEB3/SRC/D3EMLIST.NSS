0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3EMLIST
0050*
0060* DESCRIPTION
0070*               List Persons
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+------------+--------------------------------------------
0180* SAG   ! 27.04.1999 ! Year2000 Employee File uses D instead of N6
0190* ------+------------+--------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING D3-PARM
0240LOCAL USING W3CONST
0250LOCAL
02601 ##FROM (A20)
02701 ##TO (A20)
0280*
0290* Year2000 Employee File uses D instead of N6 this not needed
0300* BIRTH-N (N08)
0310* REDEFINE BIRTH-N
0320* 2 BIRTH-J (A4)
0330* 2 BIRTH-M (A2)
0340* 2 BIRTH-D (A2)
0350*
0360LOCAL
03701 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0380  2 PERSONNEL-ID
0390  2 FIRST-NAME
0400  2 NAME
0410  2 SEX
0420  2 BIRTH
0430  2 CITY
0440  2 ZIP
0450  2 POST-CODE
0460  2 COUNTRY
0470  2 AREA-CODE
0480  2 PHONE
0490  2 DEPT
0500  2 SALARY(1)
0510*
05201 #I (N7)  INIT <0>
05301 #J (N3)  INIT <0>
0540*
05501 W3VALUE (A250)
05601 II (N5)
0570END-DEFINE
0580*
0590DEFINE SUBROUTINE D3EMLIST
0600*
0610##FROM := #DEMO-PARM.#FROM
0620##TO   := #DEMO-PARM.#TO
0630IF #TO EQ #FROM OR #TO EQ " " THEN
0640  COMPRESS ##FROM H'FF' INTO ##TO LEAVING NO
0650END-IF
0660*
0670#J := #DEMO-PARM.#END-MAX
0680*
0690PERFORM W3TEXTLINEDYNAMIC "<CENTER><TABLE BORDER=1><TR>"-
0700  '<TH>Personel-ID</TH>'-
0710  '<TH>First-Name</TH>'-
0720  '<TH>Name</TH>'-
0730  '<TH>Sex</TH>'-
0740  '<TH>Birth</TH>'-
0750  '<TH>City</TH>'-
0760  '<TH>Country</TH>'-
0770  '<TH>Area-Code</TH>'-
0780  '<TH>Phone</TH>'-
0790  '<TH>Department</TH>'-
0800  '<TH>Salary</TH>'-
0810  '</TR>'
0820*
0830F. FIND EMPLOYEES-VIEW WITH NAME = ##FROM THRU ##TO SORTED BY NAME
0840  IF NO
0850    COMPRESS ' Sorry nothing found for from:' ##FROM ' to:'
0860      ##TO INTO #DEMO-PARM.#MSG
0870  END-NOREC
0880  IF *NUMBER(F.) > 0
0890*
0900    IF *COUNTER(F.) GE #DEMO-PARM.#START THEN
0910      ADD 1 TO #I
0920*
0930      PERFORM W3TEXTDYNAMIC "<TR><TD ALIGN=CENTER NOWRAP>"
0940      PERFORM W3HTMLLINE EMPLOYEES-VIEW.PERSONNEL-ID
0950*
0960      PERFORM W3TEXTDYNAMIC "</TD><TD NOWRAP>"
0970      PERFORM W3HTMLLINE EMPLOYEES-VIEW.FIRST-NAME
0980*
0990      PERFORM W3TEXTDYNAMIC "</TD><TD NOWRAP>"
1000      PERFORM W3HTMLLINE EMPLOYEES-VIEW.NAME
1010*
1020      PERFORM W3TEXTDYNAMIC "</TD><TD ALIGN=CENTER NOWRAP>"
1030      PERFORM W3HTMLLINE EMPLOYEES-VIEW.SEX
1040*
1050      PERFORM W3TEXTDYNAMIC "</TD><TD ALIGN=CENTER NOWRAP>"
1060*     Year2000 Employee File uses D instead of N6 This not needed
1070*     BIRTH-N := EMPLOYEES-VIEW.BIRTH
1080*     IF BIRTH-N < 200000 THEN
1090*       ADD 20000000 TO BIRTH-N
1100*     ELSE
1110*       ADD 19000000 TO BIRTH-N
1120*     END-IF
1130*     COMPRESS BIRTH-D "." BIRTH-M "." BIRTH-J
1140*               INTO W3VALUE LEAVING NO
1150      MOVE EDITED EMPLOYEES-VIEW.BIRTH (EM=DD.MM.YYYY) TO W3VALUE
1160      PERFORM W3HTMLLINE W3VALUE
1170*
1180      PERFORM W3TEXTDYNAMIC "</TD><TD NOWRAP>"
1190      PERFORM W3HTMLLINE EMPLOYEES-VIEW.CITY
1200*
1210      PERFORM W3TEXTDYNAMIC "</TD><TD ALIGN=CENTER NOWRAP>"
1220      PERFORM W3HTMLLINE EMPLOYEES-VIEW.COUNTRY
1230*
1240      PERFORM W3TEXTDYNAMIC "</TD><TD ALIGN=RIGHT NOWRAP>"
1250      PERFORM W3HTMLLINE EMPLOYEES-VIEW.AREA-CODE
1260*
1270      PERFORM W3TEXTDYNAMIC "</TD><TD NOWRAP>"
1280      PERFORM W3HTMLLINE EMPLOYEES-VIEW.PHONE
1290*
1300      PERFORM W3TEXTDYNAMIC "</TD><TD NOWRAP>"
1310      PERFORM W3HTMLLINE EMPLOYEES-VIEW.DEPT
1320*
1330      PERFORM W3TEXTDYNAMIC "</TD><TD ALIGN=RIGHT NOWRAP>"
1340      PERFORM W3HTMLLINE EMPLOYEES-VIEW.SALARY(1)
1350*
1360      PERFORM W3TEXTLINEDYNAMIC "</TD></TR>"
1370*
1380      IF #I GE #J THEN
1390        ESCAPE BOTTOM(F.)
1400      END-IF
1410    END-IF
1420  END-IF
1430END-FIND
1440*
1450PERFORM W3TEXTLINEDYNAMIC "</TABLE></CENTER>"
1460*
1470#DEMO-PARM.#END := *NUMBER(F.)
1480*
1490IF *NUMBER(F.) > 0 AND #DEMO-PARM.#MSG = ' '
1500  #I :=  #DEMO-PARM.#START + #I - 1
1510  COMPRESS 'List of'  *NUMBER(F.) 'objects .... from'
1520    #DEMO-PARM.#FROM 'to' #DEMO-PARM.#TO '; Object'
1530    #DEMO-PARM.#START "-" #I INTO #DEMO-PARM.#MSG
1540END-IF
1550*
1560END-SUBROUTINE
1570*
1580END
