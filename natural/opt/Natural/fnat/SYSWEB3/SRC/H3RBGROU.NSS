0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3RBGROUP
0050*
0060* DESCRIPTION   This Program generates a radio button Group
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 H3ARRAYCOUNT (I004)     BY VALUE /* i /m : Number of group elements
02401 H3NAME       (A072)     BY VALUE /* i /m : Name of the group variable
02501 H3ARRAYVALUE (A072/1:V) BY VALUE /* i /  : Default value of the group
0260*                                  /*        variable
02701 H3ARRAYLABEL (A072/1:V) BY VALUE /* i /mH: Label of the group element
02801 H3ISCHECKED  (I004)     BY VALUE /* i /m : Number of the default
0290*                                  /*        selected button
03001 H3LINEBREAK  (   L)     BY VALUE /* i /m : set line breaks between
0310*                                  /*        the elements
03201 H3ROW        (N004)     BY VALUE /* i /m : set number of rows for
0330*                                  /*        tables
03401 H3COLUM      (N004)     BY VALUE /* i /m : set number of columns for
0350*                                  /*        tables
0360*
0370LOCAL USING W3PINFO
0380LOCAL USING W3CONST
0390LOCAL
04001 II (I4)
04101 IK (I4)
04201 IJ (I4)
04301 REST (I4)
04401 #H3VALUE         (A) DYNAMIC
0450END-DEFINE
0460*
0470DEFINE SUBROUTINE H3-RADIO-GROUP
0480* ------ ERROR HANDLING ------------------------------------------
0490ON ERROR
0500  ##W3ERROR.SUBROUTINE := "H3-RADIO-GROUP"
0510  PERFORM W3ERROR ##W3ERROR
0520  ESCAPE ROUTINE
0530END-ERROR
0540*
0550* ------ MAIN PROGRAM --------------------------------------------
0560RESET ##W3INFO
0570PERFORM W3INFO ##W3INFO
0580* --- Can only be used inside a FORM tag. ---
0590IF ##W3INFO.FORM EQ FALSE THEN
0600  ##W3ERROR.SUBROUTINE := "H3-RADIO-GROUP"
0610  ##W3ERROR.SUBPROGRAM := *PROGRAM
0620  ##W3ERROR.NR         := 20003
0630  ##W3ERROR.LINE       := 0
0640  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0650  PERFORM W3ERROR ##W3ERROR
0660  ESCAPE ROUTINE
0670END-IF
0680* --- No elements defined. ---
0690IF H3ARRAYCOUNT LE 0 THEN
0700  ##W3ERROR.SUBROUTINE := "H3-RADIO-GROUP"
0710  ##W3ERROR.SUBPROGRAM := *PROGRAM
0720  ##W3ERROR.NR         := 20002
0730  ##W3ERROR.LINE       := 0
0740  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0750  PERFORM W3ERROR ##W3ERROR
0760  ESCAPE ROUTINE
0770END-IF
0780* --- Radio Button Group has no name. ---
0790IF H3NAME EQ " " THEN
0800  ##W3ERROR.SUBROUTINE := "H3-RADIO-GROUP"
0810  ##W3ERROR.SUBPROGRAM := *PROGRAM
0820  ##W3ERROR.NR         := 20007
0830  ##W3ERROR.LINE       := 0
0840  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0850  PERFORM W3ERROR ##W3ERROR
0860  ESCAPE ROUTINE
0870END-IF
0880*
0890IF H3COLUM > 0 AND  H3ROW = 0 THEN
0900  DIVIDE H3COLUM INTO H3ARRAYCOUNT GIVING H3ROW REMAINDER REST
0910  ADD 1 TO H3ROW
0920END-IF
0930*
0940IF H3ROW GT 0 THEN
0950  PERFORM W3TEXTDYNAMIC '<TABLE>'
0960  IK := H3ROW + 1
0970  IJ := -1
0980END-IF
0990*
1000EXAMINE H3NAME TRANSLATE INTO UPPER
1010FOR II = 1 TO H3ARRAYCOUNT
1020  IF H3ROW GT 0 OR H3COLUM GT 0 THEN
1030    IF IK GE H3ROW THEN
1040      PERFORM W3TEXTDYNAMIC '<TR><TD>'
1050      IK := 0
1060      ADD 1 TO IJ
1070    ELSE
1080      PERFORM W3TEXTDYNAMIC '<TD>'
1090    END-IF
1100  END-IF
1110*
1120  IF H3NAME NE " " THEN
1130    COMPRESS 'NAME="' H3NAME '"' INTO #H3VALUE LEAVING NO
1140  ELSE
1150    RESET #H3VALUE
1160  END-IF
1170*
1180  COMPRESS '<INPUT TYPE="RADIO"' #H3VALUE INTO #H3VALUE
1190  PERFORM W3TEXTDYNAMIC #H3VALUE
1200*
1210  IF H3ARRAYVALUE(II) NE " " THEN
1220    PERFORM W3TEXTDYNAMIC ' VALUE="'
1230    PERFORM W3HTML H3ARRAYVALUE(II)
1240  END-IF
1250*
1260  IF II EQ H3ISCHECKED THEN
1270    PERFORM W3TEXTDYNAMIC '" CHECKED>'
1280  ELSE
1290    PERFORM W3TEXTDYNAMIC '">'
1300  END-IF
1310*
1320  IF H3ARRAYLABEL(II) EQ " " THEN
1330    IF H3ARRAYVALUE(II) EQ " " THEN
1340      #H3VALUE := H3NAME
1350    ELSE
1360      #H3VALUE := H3ARRAYVALUE(II)
1370    END-IF
1380  ELSE
1390    #H3VALUE :=  H3ARRAYLABEL(II)
1400  END-IF
1410  PERFORM W3HTML #H3VALUE
1420*
1430  IF H3ROW GT 0 OR H3COLUM GT 0 THEN
1440    ADD 1 TO IK
1450    IF IK GE H3ROW THEN
1460      PERFORM W3TEXTLINEDYNAMIC '</TD></TR>'
1470    ELSE
1480      PERFORM W3TEXTDYNAMIC '</TD>'
1490    END-IF
1500  ELSE
1510    IF H3LINEBREAK EQ TRUE THEN
1520      PERFORM W3TEXTLINEDYNAMIC '<BR>'
1530    ELSE
1540      PERFORM W3TEXTDYNAMIC ##HTTP_NEWLINE
1550    END-IF
1560  END-IF
1570*
1580END-FOR
1590IF H3ROW GT 0 OR H3COLUM GT 0 THEN
1600  IF IK LT H3ROW THEN
1610    REPEAT
1620      IF IK GE H3ROW THEN
1630        IK := 0
1640        ADD 1 TO IJ
1650        ESCAPE BOTTOM
1660      ELSE
1670        ADD 1 TO IK
1680        IF IK GE H3ROW THEN
1690          PERFORM W3TEXTDYNAMIC '<TD></TD></TR>'
1700        ELSE
1710          PERFORM W3TEXTDYNAMIC '<TD></TD>'
1720        END-IF
1730      END-IF
1740    END-REPEAT
1750  END-IF
1760  PERFORM W3TEXTLINEDYNAMIC ##HTTP_NEWLINE
1770  PERFORM W3TEXTLINEDYNAMIC '</TABLE>'
1780END-IF
1790*
1800END-SUBROUTINE
1810END
