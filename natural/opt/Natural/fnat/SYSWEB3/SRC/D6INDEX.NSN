0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D6INDEX
0050*
0060* DESCRIPTION
0070*               Dispatcher Program for the Demo
0080*
0090* AUTHOR        SAG   30.01.2003
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+------------+----------------------------------------------
0180* SAG   ! 18.03.2004 ! change PERFORM D6EMXML to CALLNAT 'D6EM2XML'
0190*       !            ! because for 3tier configuration
0200* ------+------------+----------------------------------------------
0210*
0220* ------ PARAMETER -----------------------------------------------
0230DEFINE DATA
0240PARAMETER USING W3PARM
0250LOCAL USING D6-PARM
0260LOCAL USING W3CONST
0270LOCAL
02801 P-NO         (A) DYNAMIC /* data of the person
02901 P-NAME       (A) DYNAMIC
03001 P-FIRST-NAME (A) DYNAMIC
03101 P-BIRTH      (A) DYNAMIC
03201 P-SEX        (A) DYNAMIC
03301 P-DEPT       (A) DYNAMIC
03401 P-CITY       (A) DYNAMIC
03501 P-AREA-CODE  (A) DYNAMIC
03601 P-ZIP        (A) DYNAMIC
03701 P-COUNTRY    (A) DYNAMIC
03801 P-PHONE      (A) DYNAMIC
0390*
04001 #HTML        (A) DYNAMIC /* internally used variables
04101 #XML         (A) DYNAMIC
04201 #XSLT        (A) DYNAMIC
04301 #MSG_TEXT    (A) DYNAMIC
04401 #RESPONSE    (I4)
0450*
04601 #MENU        (A) DYNAMIC
04701 #GOTODO      (A) DYNAMIC
04801 #NAME        (A) DYNAMIC
0490*
05001 #PARM        (A) DYNAMIC
05101 #CURSOR      (A) DYNAMIC
05201 #URL         (A) DYNAMIC
05301 #TEMP        (A) DYNAMIC
05401 #II          (I4)
0550* change to different output style
05601 STYLE2       (L)  INIT <TRUE>
0570END-DEFINE
0580* --- ERROR HANDLING ---
0590ON ERROR
0600  PERFORM W3ERROR ##W3ERROR
0610  PERFORM W3END ##RPC
0620  ESCAPE ROUTINE
0630END-ERROR
0640*
0650* --- INITIALIZE Web Interface ---
0660PERFORM W3INIT ##RPC
0670*
0680* --- Read parameter given from the internet --
0690PERFORM READ-ENVRIONMENT
0700*
0710* Relocate to a the given page
0720IF #DEMO-PARM.#TODO EQ "HOME"
0730  PERFORM W3CLEAR
0740  PERFORM W3LOCATION "/"
0750ELSE
0760  /*[ --- SET TYPE OF RETURN-PAGE ---
0770  PERFORM W3CONTENT-TYPE  'text/html'
0780  IF STYLE2
0790    PERFORM W3LOAD-RESOURCE 1X 'd6-template2.html'
0800    PERFORM W3READ-RESOURCE 1X 'd6-css2.css' #HTML
0810  ELSE
0820    PERFORM W3LOAD-RESOURCE 1X 'd6-template.html'
0830    PERFORM W3READ-RESOURCE 1X 'd6-css.css' #HTML
0840  END-IF
0850  /*
0860  PERFORM W3REPLACE 1X "<!CSS>" #HTML
0870  /*]
0880* --- Make the requested action --
0890  DECIDE ON FIRST OF #DEMO-PARM.#TODO
0900    VALUE "BROW"
0910      IF STYLE2
0920        PERFORM W3READ-RESOURCE 1X 'd6-select-left2.html' #HTML
0930      ELSE
0940        PERFORM W3READ-RESOURCE 1X 'd6-select-left.html' #HTML
0950      END-IF
0960      PERFORM W3REPLACE       1X "$LEFT_MENU$" #HTML
0970      /* nothing selected
0980      IF #DEMO-PARM.#FROM = " " OR #DEMO-PARM.#TO = " "
0990        PERFORM W3READ-RESOURCE 1X 'd6-select-none.html' #HTML
1000        PERFORM W3REPLACE       1X "$MAIN$" #HTML
1010      ELSE
1020        #DEMO-PARM.#END-MAX := 15
1030        CALLNAT 'D6EM2XML' #XML #DEMO-MENU #DEMO-PARM
1040        /*
1050        PERFORM ADD-INDEXLINE
1060        PERFORM W3READ-RESOURCE 1X 'd6-browse.xsl' #XSLT
1070        PERFORM W3APPLY-XSLT-XML-TO-DYNAMIC #XSLT #XML #HTML
1080        PERFORM W3REPLACE 1X "$MAIN$" #HTML
1090      END-IF
1100      PERFORM W3REPLACE 1X "$TODO$" 'BROW'
1110      PERFORM W3REPLACE 1X "$SUBMIT$" 'Browse Person'
1120*
1130    VALUE "LIST"
1140      IF STYLE2
1150        PERFORM W3READ-RESOURCE 1X 'd6-select-left2.html' #HTML
1160      ELSE
1170        PERFORM W3READ-RESOURCE 1X 'd6-select-left.html' #HTML
1180      END-IF
1190      PERFORM W3REPLACE       1X "$LEFT_MENU$" #HTML
1200      /* nothing selected
1210      IF #DEMO-PARM.#FROM = " " OR #DEMO-PARM.#TO = " "
1220        PERFORM W3READ-RESOURCE 1X 'd6-select-none.html' #HTML
1230        PERFORM W3REPLACE       1X "$MAIN$"              #HTML
1240      ELSE
1250        #DEMO-PARM.#END-MAX := 15
1260        CALLNAT 'D6EM2XML' #XML #DEMO-MENU #DEMO-PARM
1270        /*
1280        PERFORM ADD-INDEXLINE
1290        PERFORM W3READ-RESOURCE 1X 'd6-list.xsl' #XSLT
1300        PERFORM W3APPLY-XSLT-XML-TO-DYNAMIC #XSLT #XML #HTML
1310        PERFORM W3REPLACE 1X "$MAIN$" #HTML
1320      END-IF
1330      PERFORM W3REPLACE 1X "$TODO$"   'LIST'
1340      PERFORM W3REPLACE 1X "$SUBMIT$" 'List Person'
1350*
1360    VALUE "SHOW"
1370      PERFORM W3REPLACE 1X "$LEFT_MENU$" 1X
1380      /* no person selected
1390      IF #DEMO-PARM.#PERSON = " "
1400        PERFORM W3READ-RESOURCE 1X 'd6-select-none.html' #HTML
1410        PERFORM W3REPLACE       1X "$MAIN$"              #HTML
1420      ELSE
1430        PERFORM W3READ-RESOURCE 1X 'd6-output.html' #HTML
1440        PERFORM W3REPLACE       1X "$MAIN$"         #HTML
1450        PERFORM W3REPLACE       1X "$SUBMIT$"       'Modify Person'
1460        /*
1470        CALLNAT "D6EMREAD" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME
1480          P-BIRTH P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY
1490          P-PHONE #DEMO-PARM.#MSG
1500        /*
1510        PERFORM REPLACE-PARM
1520        /*
1530        #DEMO-MENU.#SINGLE := TRUE
1540      END-IF
1550*
1560    VALUE "CHAN"
1570      PERFORM W3REPLACE 1X "$LEFT_MENU$" 1X
1580      /* no person selected
1590      IF #DEMO-PARM.#PERSON = " "
1600        PERFORM W3READ-RESOURCE 1X 'd6-select-none.html' #HTML
1610        PERFORM W3REPLACE       1X "$MAIN$"              #HTML
1620      ELSE
1630        PERFORM W3READ-RESOURCE 1X 'd6-input.html'  #HTML
1640        PERFORM W3REPLACE       1X "$MAIN$"         #HTML
1650        PERFORM W3READ-RESOURCE 1X 'd6-submit.html' #HTML
1660        PERFORM W3REPLACE       1X "<!SUBMIT>"      #HTML
1670        PERFORM W3REPLACE       1X "$TODO$"         'SAVE'
1680        PERFORM W3REPLACE       1X "$SUBMIT$"       'Change Person'
1690        /*
1700        CALLNAT "D6EMREAD" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME
1710          P-BIRTH P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY
1720          P-PHONE #DEMO-PARM.#MSG
1730        /*
1740        PERFORM REPLACE-PARM
1750        /*
1760        #DEMO-MENU.#SINGLE := TRUE
1770      END-IF
1780*
1790    VALUE "NEW"
1800      PERFORM W3READ-RESOURCE 1X 'd6-input.html'  #HTML
1810      PERFORM W3REPLACE       1X "$MAIN$"         #HTML
1820      PERFORM W3READ-RESOURCE 1X 'd6-submit.html' #HTML
1830      PERFORM W3REPLACE       1X "<!SUBMIT>"      #HTML
1840      PERFORM W3REPLACE       1X "$TODO$"         'CREN'
1850      PERFORM W3REPLACE       1X "$SUBMIT$"       'Add Person'
1860      PERFORM W3REPLACE       1X "$LEFT_MENU$"    1X
1870      /*
1880      RESET #DEMO-PARM.#PERSON P-NAME
1890        P-FIRST-NAME P-BIRTH P-SEX
1900        P-DEPT P-CITY P-AREA-CODE
1910        P-ZIP P-COUNTRY P-PHONE
1920        #DEMO-PARM.#MSG
1930      /*
1940      PERFORM REPLACE-PARM
1950      /*
1960      #DEMO-MENU.#SINGLE := TRUE
1970*
1980    VALUE "DEL"
1990      PERFORM W3REPLACE 1X "$LEFT_MENU$" 1X
2000      /* no person selected
2010      IF #DEMO-PARM.#PERSON = " "
2020        PERFORM W3READ-RESOURCE 1X 'd6-select-none.html' #HTML
2030        PERFORM W3REPLACE       1X "$MAIN$"              #HTML
2040      ELSE
2050        PERFORM W3READ-RESOURCE 1X 'd6-output.html' #HTML
2060        PERFORM W3REPLACE       1X "$MAIN$"         #HTML
2070        PERFORM W3READ-RESOURCE 1X 'd6-submit.html' #HTML
2080        PERFORM W3REPLACE       1X '<!SUBMIT>'      #HTML
2090        PERFORM W3REPLACE       1X "$TODO$"         'PURG'
2100        PERFORM W3REPLACE       1X "$SUBMIT$"       'Delete Person'
2110        /*
2120        CALLNAT "D6EMREAD" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME
2130          P-BIRTH P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY
2140          P-PHONE #DEMO-PARM.#MSG
2150        /*
2160        PERFORM REPLACE-PARM
2170        /*
2180        #DEMO-MENU.#SINGLE := TRUE
2190      END-IF
2200*
2210    VALUE "PURG"
2220      PERFORM W3REPLACE 1X "$LEFT_MENU$" 1X
2230      /*
2240      RESET P-SEX
2250      CALLNAT "D6EMPURG" #DEMO-PARM.#PERSON #DEMO-PARM.#MSG #RESPONSE
2260      /*
2270      PERFORM W3READ-RESOURCE 1X 'd6-output.html' #HTML
2280      PERFORM W3REPLACE       1X "$MAIN$"         #HTML
2290      PERFORM W3READ-RESOURCE 1X 'd6-submit.html' #HTML
2300      PERFORM W3REPLACE       1X "<!SUBMIT>"      #HTML
2310      PERFORM W3REPLACE       1X "$TODO$"         'PURG'
2320      PERFORM W3REPLACE       1X "$SUBMIT$"       'Delete Person'
2330      /*
2340      IF #RESPONSE NE 0
2350        CALLNAT "D6EMREAD" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME
2360          P-BIRTH P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY
2370          P-PHONE #DEMO-PARM.#MSG
2380      ELSE
2390        RESET #DEMO-PARM.#PERSON
2400      END-IF
2410      /*
2420      PERFORM REPLACE-PARM
2430      /*
2440      #DEMO-MENU.#SINGLE := TRUE
2450*
2460    VALUE "SAVE"
2470      PERFORM W3REPLACE 1X "$LEFT_MENU$" 1X
2480      /*
2490      PERFORM READ-ENVRIONMENT-INPUT
2500      /*
2510      CALLNAT "D6EMSAVE" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME P-BIRTH
2520        P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY P-PHONE
2530        #DEMO-PARM.#MSG #RESPONSE
2540      /*
2550      PERFORM W3READ-RESOURCE 1X 'd6-input.html'  #HTML
2560      PERFORM W3REPLACE       1X "$MAIN$"         #HTML
2570      PERFORM W3READ-RESOURCE 1X 'd6-submit.html' #HTML
2580      PERFORM W3REPLACE       1X "<!SUBMIT>"      #HTML
2590      PERFORM W3REPLACE       1X "$TODO$"         'SAVE'
2600      PERFORM W3REPLACE       1X "$SUBMIT$"       'Change Person'
2610      /*
2620      IF #RESPONSE = 0
2630        CALLNAT "D6EMREAD" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME
2640          P-BIRTH P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY
2650          P-PHONE #MSG_TEXT
2660      END-IF
2670      /*
2680      PERFORM REPLACE-PARM
2690      /*
2700      #DEMO-MENU.#SINGLE := TRUE
2710*
2720    VALUE "CREN"
2730      PERFORM W3REPLACE 1X "$LEFT_MENU$" 1X
2740      /*
2750      PERFORM READ-ENVRIONMENT-INPUT
2760      /*
2770      CALLNAT "D6EMNEW" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME P-BIRTH
2780        P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY P-PHONE
2790        #DEMO-PARM.#MSG #RESPONSE
2800      /*
2810      PERFORM W3READ-RESOURCE 1X 'd6-input.html'  #HTML
2820      PERFORM W3REPLACE       1X "$MAIN$"         #HTML
2830      PERFORM W3READ-RESOURCE 1X 'd6-submit.html' #HTML
2840      PERFORM W3REPLACE       1X "<!SUBMIT>"      #HTML
2850      PERFORM W3REPLACE       1X "$TODO$"         'CREN'
2860      PERFORM W3REPLACE       1X "$SUBMIT$"       'New Person'
2870      /*
2880      IF #RESPONSE = 0
2890        CALLNAT "D6EMREAD" #DEMO-PARM.#PERSON P-NAME P-FIRST-NAME
2900          P-BIRTH P-SEX P-DEPT P-CITY P-AREA-CODE P-ZIP P-COUNTRY
2910          P-PHONE #MSG_TEXT
2920      END-IF
2930      /*
2940      PERFORM REPLACE-PARM
2950      /*
2960      #DEMO-MENU.#SINGLE := TRUE
2970*
2980    VALUE "OVER"
2990      PERFORM W3READ-RESOURCE 1X 'd6-overview.html' #HTML
3000      PERFORM W3REPLACE       1X "$MAIN$"           #HTML
3010      PERFORM W3REPLACE       1X "$LEFT_MENU$"      1X
3020      /*
3030      PERFORM REPLACE-PARM
3040*
3050    NONE
3060      IGNORE
3070      PERFORM W3READ-RESOURCE 1X 'd6-welcome.html' #HTML
3080      PERFORM W3REPLACE       1X "$MAIN$"          #HTML
3090      PERFORM W3REPLACE       1X "$LEFT_MENU$"     1X
3100      /*
3110  END-DECIDE
3120*
3130* --- now replace all other stuff...
3140  PERFORM W3REPLACE 1X "$TITLE$"  "NATURAL DEMO APPLICATION"
3150*
3160  /*[ create Navigation
3170  PERFORM MENUBAR
3180*
3190  PERFORM W3READ-RESOURCE 1X 'd6-topmenu.html' #HTML
3200  PERFORM W3REPLACE       1X "$TOP_MENU$"      #HTML
3210  /*]
3220  IF #DEMO-PARM.#FROM = " " OR #DEMO-PARM.#TO = " "
3230    PERFORM W3REPLACE 1X "$FROM$"  1X
3240    PERFORM W3REPLACE 1X "$TO$"    1X
3250    PERFORM W3REPLACE 1X "$START$" #DEMO-PARM.#FROM
3260  ELSE
3270    PERFORM W3REPLACE 1X "$FROM$"  #DEMO-PARM.#FROM
3280    PERFORM W3REPLACE 1X "$TO$"    #DEMO-PARM.#TO
3290    PERFORM W3REPLACE 1X "$START$" #DEMO-PARM.#START
3300  END-IF
3310  /*
3320  PERFORM W3REPLACE 1X "$ID$"       #DEMO-PARM.#PERSON
3330  PERFORM W3REPLACE 1X "$PICTURES$" #DEMO-PARM.#GIF
3340  PERFORM W3REPLACE 1X "$MSG$"      #DEMO-PARM.#MSG
3350END-IF
3360*
3370PERFORM W3END ##RPC
3380*
3390* ======================================================================
3400DEFINE SUBROUTINE READ-ENVRIONMENT-INPUT
3410PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "ID"    "P" P-NO
3420PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "NAME"  "P" P-NAME
3430PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "FIRST" "P" P-FIRST-NAME
3440PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "BIRTH" "P" P-BIRTH
3450PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SEX"   "P" P-SEX
3460PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "ZIP"   "P" P-ZIP
3470PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "DEPT"  "P" P-DEPT
3480PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "CITY"  "P" P-CITY
3490PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "AREA"  "P" P-AREA-CODE
3500PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "CNTRY" "P" P-COUNTRY
3510PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "PHONE" "P" P-PHONE
3520END-SUBROUTINE
3530*
3540* ======================================================================
3550DEFINE SUBROUTINE REPLACE-PARM
3560PERFORM W3REPLACE 1X "$ID$"    #DEMO-PARM.#PERSON
3570PERFORM W3REPLACE 1X "$NAME$"  P-NAME
3580PERFORM W3REPLACE 1X "$FIRST$" P-FIRST-NAME
3590PERFORM W3REPLACE 1X "$BIRTH$" P-BIRTH
3600PERFORM W3REPLACE 1X "$SEX$"   P-SEX
3610*
3620IF P-SEX NE " " THEN
3630  IF P-SEX = "M" THEN
3640    PERFORM W3REPLACE 1X "$SELECTED_M$" 'SELECTED'
3650    PERFORM W3REPLACE 1X "$SELECTED_F$" 1X
3660    PERFORM W3REPLACE 1X "$GIF_ALT$"    'male'
3670    PERFORM W3REPLACE 1X "$GIF$"        "$PICTURES$/"-'d6_man.gif'
3680  ELSE
3690    PERFORM W3REPLACE 1X "$SELECTED_M$" 1X
3700    PERFORM W3REPLACE 1X "$SELECTED_F$" 'SELECTED'
3710    PERFORM W3REPLACE 1X "$GIF_ALT$"    'female'
3720    PERFORM W3REPLACE 1X "$GIF$"        "$PICTURES$/"-'d6_wom.gif'
3730  END-IF
3740ELSE
3750  PERFORM W3REPLACE 1X "$GIF_ALT$" 'Person'
3760  PERFORM W3REPLACE 1X "$GIF$"     "$PICTURES$/"-'d6_new.gif'
3770END-IF
3780*
3790PERFORM W3REPLACE 1X "$ZIP$"   P-ZIP
3800PERFORM W3REPLACE 1X "$DEPT$"  P-DEPT
3810PERFORM W3REPLACE 1X "$CITY$"  P-CITY
3820PERFORM W3REPLACE 1X "$AREA$"  P-AREA-CODE
3830PERFORM W3REPLACE 1X "$CNTRY$" P-COUNTRY
3840PERFORM W3REPLACE 1X "$PHONE$" P-PHONE
3850*
3860END-SUBROUTINE
3870*
3880* ======================================================================
3890DEFINE SUBROUTINE MENUBAR
3900*
3910IF STYLE2
3920  COMPRESS
3930    '<!-- generic menubar - start -->'
3940    '<table border="0" cellspacing="0" width="135">'
3950*    '<table cellSpacing="0" cellPadding="0" width="180" border="0">'
3960    INTO #MENU LEAVING NO
3970ELSE
3980  COMPRESS
3990    '<table class="orange" width="100%" border="1"><TR>'
4000    INTO #MENU LEAVING NO
4010END-IF
4020*
4030#GOTODO := " "
4040#NAME   := 'Main'
4050IF #GOTODO = #TODO THEN
4060  PERFORM MENUBAR-ACTIVE
4070ELSE
4080  PERFORM MENUBAR-INACTIVE-LINK
4090END-IF
4100*
4110#GOTODO := "BROW"
4120#NAME   := 'Browse'
4130IF #GOTODO = #TODO THEN
4140  PERFORM MENUBAR-ACTIVE
4150ELSE
4160  PERFORM MENUBAR-INACTIVE-LINK
4170END-IF
4180*
4190#GOTODO := "LIST"
4200#NAME   := 'Listing'
4210IF #GOTODO = #TODO THEN
4220  PERFORM MENUBAR-ACTIVE
4230ELSE
4240  PERFORM MENUBAR-INACTIVE-LINK
4250END-IF
4260*
4270#GOTODO := "SHOW"
4280#NAME   := 'Show'
4290IF #GOTODO = #TODO THEN
4300  PERFORM MENUBAR-ACTIVE
4310ELSE
4320  IF #DEMO-MENU.#SINGLE AND #TODO NE "NEW" THEN
4330    PERFORM MENUBAR-INACTIVE-LINK
4340  ELSE
4350    PERFORM MENUBAR-INACTIVE-NOLINK
4360  END-IF
4370END-IF
4380*
4390#GOTODO := "CHAN"
4400#NAME   := 'Change'
4410IF #GOTODO = #TODO OR #TODO ="SAVE" THEN
4420  PERFORM MENUBAR-ACTIVE
4430ELSE
4440  IF #DEMO-MENU.#SINGLE AND #TODO NE "NEW" THEN
4450    PERFORM MENUBAR-INACTIVE-LINK
4460  ELSE
4470    PERFORM MENUBAR-INACTIVE-NOLINK
4480  END-IF
4490END-IF
4500*
4510#GOTODO := "DEL"
4520#NAME   := 'Delete'
4530IF #GOTODO = #TODO OR #TODO ="PURG" THEN
4540  PERFORM MENUBAR-ACTIVE
4550ELSE
4560  IF #DEMO-MENU.#SINGLE AND #TODO NE "NEW" THEN
4570    PERFORM MENUBAR-INACTIVE-LINK
4580  ELSE
4590    PERFORM MENUBAR-INACTIVE-NOLINK
4600  END-IF
4610END-IF
4620*
4630#GOTODO := "NEW"
4640#NAME   := 'New'
4650IF #GOTODO = #TODO OR #TODO ="CREN" THEN
4660  PERFORM MENUBAR-ACTIVE
4670ELSE
4680  PERFORM MENUBAR-INACTIVE-LINK
4690END-IF
4700*
4710COMPRESS #MENU
4720  '</table>'
4730  '<!-- generic menubar - end -->'
4740  INTO #MENU LEAVING NO
4750*
4760PERFORM W3REPLACE 1X "$MIDDLE_MENU$" #MENU
4770END-SUBROUTINE
4780*
4790* ======================================================================
4800DEFINE SUBROUTINE MENUBAR-INACTIVE-LINK
4810IF STYLE2
4820*   COMPRESS #MENU
4830*     '<tr valign="center">'-
4840*     '<td class="blue" bgcolor="#e9eef5">'-
4850*     '<a class="blue" href="d6index?TODO=' #GOTODO
4860*     '&FROM=$FROM$&TO=$TO$&START=$START$&ID=$ID$"'-
4870*     ' onMouseOver="imgAct('-H'22' #NAME
4880*     H'22'-');window.status='-H'22' #NAME
4890*     H'22'-'; return true;" onMouseOut="imgInact('-H'22' #NAME
4900*     H'22'-')">'
4910*     '<img width="14" height="17" border="0" name="' #NAME
4920*     '" SRC="$PICTURES$/d6_dot_clear.gif">'
4930*     #NAME '</a></td></tr>'
4940*     '<tr height=1% bgcolor="white">'
4950*     '<td>'
4960*     '<img height="1" alt="" SRC="$PICTURES$/d6_dot_clear.gif" '-
4970*     'width="1" border="0">'
4980*     '</td></tr>'
4990*     ##HTTP_NEWLINE
5000*     INTO #MENU LEAVING NO
5010  COMPRESS #MENU
5020    '<tr>'
5030    '<td class="submenue" valign="bottom" '-
5040    'onmouseover="this.style.background='-H'22'-'#ffffff'-H'22'-';" '-
5050    'onmouseout="javascript:this.style.background='-H'22'-'#e9eef5'-H'22'-';">'
5060    '<img src="$PICTURES$/d6_dot_clear.gif" alt="" '-
5070    'width="5" height="2">'
5080    '<a class="noline" href="d6index?TODO=' #GOTODO
5090    '&FROM=$FROM$&TO=$TO$&START=$START$&ID=$ID$">' #NAME
5100    '</a></td>'
5110    '</tr>'
5120    ##HTTP_NEWLINE
5130    INTO #MENU LEAVING NO
5140ELSE
5150  COMPRESS #MENU
5160    '<td class="orange" align="center">'-
5170    '<a class="orange" href="d6index?TODO=' #GOTODO
5180    "&FROM=$FROM$&TO=$TO$&START=$START$&ID=$ID$'>"
5190    #NAME '</a></td>'
5200    ##HTTP_NEWLINE
5210    INTO #MENU LEAVING NO
5220END-IF
5230END-SUBROUTINE
5240*
5250* ======================================================================
5260DEFINE SUBROUTINE MENUBAR-INACTIVE-NOLINK
5270IF STYLE2
5280*   COMPRESS #MENU
5290*     '<tr>'
5300*     '<td class="blue"><IMG name="' #GOTODO
5310*     '" SRC="$PICTURES$/d6_dot_clear.gif" WIDTH="14" HEIGHT="17" '-
5320*     'BORDER="0">'
5330*     #NAME '</td>'
5340*     '</tr>'
5350*     '<tr height=1% bgcolor="white">'
5360*     '<td>'
5370*     '<img height="1" alt="" SRC="$PICTURES$/d6_dot_clear.gif" '-
5380*     'width="1" border="0">'
5390*     '</td></tr>'
5400*     ##HTTP_NEWLINE
5410*     INTO #MENU LEAVING NO
5420  COMPRESS #MENU
5430    '<tr>'
5440    '<td class="submenue-inactive" valign="bottom">'-
5450    '<img src="$PICTURES$/d6_dot_clear.gif" alt="" '-
5460    'width="5" height="2">'
5470    #NAME
5480    '</td></tr>'
5490    ##HTTP_NEWLINE
5500    INTO #MENU LEAVING NO
5510ELSE
5520  COMPRESS #MENU
5530    '<td class="orange" align="center">'
5540    #NAME '</td>'
5550    ##HTTP_NEWLINE INTO
5560    #MENU LEAVING NO
5570END-IF
5580
5590END-SUBROUTINE
5600*
5610* ======================================================================
5620DEFINE SUBROUTINE MENUBAR-ACTIVE
5630IF STYLE2
5640*   COMPRESS #MENU
5650*     '<tr>'
5660*     '<td class="white">'
5670*     '<img width="14" height="17" border="0" name="' #GOTODO
5680*     '" src="$PICTURES$/d6_dot_clear.gif" >' #NAME
5690*     '</td>'
5700*     '</tr>'
5710*     '<tr height=1% bgcolor="white">'
5720*     '<td>'
5730*     '<img height="1" alt="" SRC="$PICTURES$/d6_dot_clear.gif" '-
5740*     'width="1" border="0">'
5750*     '</td>'
5760*     '</tr>'
5770*     ##HTTP_NEWLINE
5780*     INTO #MENU LEAVING NO
5790  COMPRESS #MENU
5800    '<tr>'
5810    '<td class="submenue-selected" valign="bottom" >'
5820    '<img src="$PICTURES$/d6_dot_clear.gif" alt="" '-
5830    'width="5" height="2">'
5840    #NAME
5850    '</td></tr>'
5860    ##HTTP_NEWLINE
5870    INTO #MENU LEAVING NO
5880ELSE
5890  COMPRESS #MENU
5900    '<th class="orange">' #NAME
5910    '</th>'
5920    ##HTTP_NEWLINE
5930    INTO #MENU LEAVING NO
5940END-IF
5950
5960END-SUBROUTINE
5970*
5980* ======================================================================
5990DEFINE SUBROUTINE READ-ENVRIONMENT
6000*
6010IF H'41' EQ "A" THEN
6020  #DEMO-PARM.#QUOTE  := H'27'
6030  #DEMO-PARM.#DQUOTE := H'22'
6040ELSE
6050  #DEMO-PARM.#QUOTE  := H'7D'
6060  #DEMO-PARM.#DQUOTE := H'7F'
6070END-IF
6080*
6090#DEMO-MENU.#SINGLE := FALSE
6100*
6110PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "PICTURES" ' ' #PARM
6120IF *LENGTH(#PARM) = 0
6130  #DEMO-PARM.#GIF  := '/pictures'
6140ELSE
6150  #DEMO-PARM.#GIF  := #PARM
6160END-IF
6170*
6180PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "START" "P" #PARM
6190IF *LENGTH(#PARM) > 0 AND *LENGTH(#PARM) < 5 THEN
6200  IF #PARM IS (N5) THEN
6210    #DEMO-PARM.#START:= VAL(#PARM)
6220  ELSE
6230    #DEMO-PARM.#START:= 1
6240  END-IF
6250ELSE
6260  #DEMO-PARM.#START:= 1
6270END-IF
6280*
6290PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "TODO" "P" #DEMO-PARM.#TODO
6300PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "FROM" "P" #DEMO-PARM.#FROM
6310PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "TO"   "P" #DEMO-PARM.#TO
6320PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "ID"   "P" #DEMO-PARM.#PERSON
6330*
6340EXAMINE #DEMO-PARM.#TODO TRANSLATE INTO UPPER
6350*
6360EXAMINE #DEMO-PARM.#FROM TRANSLATE INTO UPPER
6370EXAMINE #DEMO-PARM.#TO   TRANSLATE INTO UPPER
6380*
6390END-SUBROUTINE
6400*
6410* ======================================================================
6420DEFINE SUBROUTINE ADD-INDEXLINE
6430*
6440IF #DEMO-PARM.#END > #DEMO-PARM.#END-MAX THEN
6450*
6460  #CURSOR := '<div class="index" align="center">P.:&nbsp;'
6470  FOR #II = 1 TO #DEMO-PARM.#END STEP #DEMO-PARM.#END-MAX
6480*
6490    IF #DEMO-PARM.#TODO EQ "BROW" THEN
6500      #URL := "D6INDEX?TODO=BROW"
6510    END-IF
6520*
6530    IF #DEMO-PARM.#TODO EQ "LIST" THEN
6540      #URL := "D6INDEX?TODO=LIST"
6550    END-IF
6560*
6570    #TEMP := #DEMO-PARM.#FROM
6580    PERFORM W3TEXT-TO-URL #TEMP
6590    COMPRESS #URL "&FROM=" #TEMP INTO #URL LEAVING NO
6600*
6610    #TEMP := #DEMO-PARM.#TO
6620    PERFORM W3TEXT-TO-URL #TEMP
6630    COMPRESS #URL "&TO=" #TEMP INTO #URL LEAVING NO
6640*
6650    #TEMP := #DEMO-PARM.#OT
6660    PERFORM W3TEXT-TO-URL #TEMP
6670    COMPRESS #URL "&OT=" #TEMP INTO #URL LEAVING NO
6680*
6690    IF #II = 0 THEN
6700      COMPRESS #URL "&START=1" INTO #URL LEAVING NO
6710      COMPRESS '<a class="index" href="' #URL '">1</a>' '&nbsp;' INTO
6720        #URL LEAVING NO
6730    ELSE
6740      COMPRESS #URL "&START=" #II INTO #URL LEAVING NO
6750      COMPRESS  '<a class="index" href="' #URL '">' #II '</a>' INTO
6760        #URL LEAVING NO
6770    END-IF
6780    COMPRESS #CURSOR #URL INTO #CURSOR
6790  END-FOR
6800  /*
6810  COMPRESS "$MAIN$" ##HTTP_NEWLINE #CURSOR '</div>' ##HTTP_NEWLINE
6820    INTO #HTML LEAVING NO
6830  /*
6840  PERFORM W3REPLACE 1X "$MAIN$" #HTML
6850END-IF
6860*
6870END-SUBROUTINE
6880*
6890END
