0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3FORMS
0050*
0060* DESCRIPTION   This Program generates a strarting FORM tag
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200DEFINE DATA
0210PARAMETER
02201 #H3METHOD  (A1)        BY VALUE /* i /m : "G" GET    (default)
0230*                                 /*        "P" POST
02401 #H3ACTION  (A) DYNAMIC BY VALUE /* i /m : URL to be called
02501 #H3NAME    (A) DYNAMIC BY VALUE /* i /m : NAME of Form
0260*
0270LOCAL  USING W3PINFO
0280LOCAL  USING W3CONST
0290LOCAL
03001 #H3VALUE   (A) DYNAMIC
03101 #H3VALUE2  (A) DYNAMIC
0320END-DEFINE
0330*
0340DEFINE SUBROUTINE H3-OPEN-FORM-JAVASCRIPT
0350*
0360ON ERROR
0370  ##W3ERROR.SUBROUTINE := "H3-OPEN-FORM-JAVASCRIPT"
0380  PERFORM W3ERROR ##W3ERROR
0390  ESCAPE ROUTINE
0400END-ERROR
0410*
0420IF #H3ACTION NE " " THEN
0430  COMPRESS 'ACTION="' #H3ACTION '"' INTO #H3VALUE LEAVING NO
0440ELSE
0450* --- A FORM tag without ACTION is not allowed. ---
0460  ##W3ERROR.SUBROUTINE := "H3-OPEN-FORM-JAVASCRIPT"
0470  ##W3ERROR.SUBPROGRAM := *PROGRAM
0480  ##W3ERROR.NR         := 20004
0490  ##W3ERROR.LINE       := 0
0500  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0510  PERFORM W3ERROR ##W3ERROR
0520  ESCAPE ROUTINE
0530END-IF
0540*
0550IF #H3NAME NE " " THEN
0560  COMPRESS 'NAME="' #H3NAME '"' INTO #H3VALUE2 LEAVING NO
0570  COMPRESS #H3VALUE #H3VALUE2 INTO #H3VALUE
0580END-IF
0590*
0600##W3INFO.FORM := TRUE
0610##W3INFO.ACTION := "F"
0620PERFORM W3INFO ##W3INFO
0630*
0640EXAMINE #H3METHOD TRANSLATE INTO UPPER
0650*
0660DECIDE ON FIRST VALUE OF #H3METHOD
0670  VALUE "P"
0680    COMPRESS '<FORM METHOD="POST"' #H3VALUE ">" ##HTTP_NEWLINE
0690      INTO #H3VALUE
0700  VALUE "G", " "
0710    COMPRESS '<FORM METHOD="GET"' #H3VALUE ">" ##HTTP_NEWLINE
0720      INTO #H3VALUE
0730  NONE
0740    IGNORE
0750END-DECIDE
0760*
0770PERFORM W3TEXTDYNAMIC #H3VALUE
0780*
0790END-SUBROUTINE
0800END
