0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3CLIST
0050*
0060* DESCRIPTION   This Program generates the end of a list
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200DEFINE DATA
0210PARAMETER
02201 #H3LEVEL    (N2) BY VALUE /* i /m : levels to close
0230*
0240LOCAL USING W3CONST
0250LOCAL USING W3PINFO
0260LOCAL
02701 #HTTP_VALUE (A) DYNAMIC
02801 #II         (N2)
02901 #MAX        (N2)
0300END-DEFINE
0310*
0320DEFINE SUBROUTINE H3-CLOSE-LIST
0330*
0340ON ERROR
0350  ##W3ERROR.SUBROUTINE := "H3-CLOSE-LIST"
0360  PERFORM W3ERROR ##W3ERROR
0370  ESCAPE ROUTINE
0380END-ERROR
0390*
0400RESET ##W3INFO
0410PERFORM W3INFO ##W3INFO
0420*
0430IF ##W3INFO.LIST_MAX GT 0 THEN
0440  FOR #II = 1 TO #H3LEVEL
0450*
0460    #MAX := ##W3INFO.LIST_MAX
0470    DECIDE ON FIRST VALUE OF ##W3INFO.LIST(#MAX)
0480      VALUE "O"
0490        COMPRESS '</OL>' ##HTTP_NEWLINE INTO #HTTP_VALUE LEAVING NO
0500      VALUE "U"
0510        COMPRESS '</UL>' ##HTTP_NEWLINE INTO #HTTP_VALUE LEAVING NO
0520      VALUE "D"
0530        COMPRESS '</DIR>' ##HTTP_NEWLINE INTO #HTTP_VALUE LEAVING NO
0540      VALUE "M"
0550        COMPRESS '</MENU>' ##HTTP_NEWLINE INTO #HTTP_VALUE
0560          LEAVING NO
0570      NONE
0580        IGNORE
0590    END-DECIDE
0600*
0610    PERFORM W3TEXTDYNAMIC #HTTP_VALUE
0620*
0630    ##W3INFO.LIST(#MAX) := " "
0640    SUBTRACT 1 FROM ##W3INFO.LIST_MAX
0650*
0660    IF ##W3INFO.LIST_MAX LE 0 THEN
0670      ##W3INFO.LIST_MAX := 0
0680      ESCAPE BOTTOM
0690    END-IF
0700  END-FOR
0710ELSE
0720  ##W3INFO.LIST(*)  := " "
0730  ##W3INFO.LIST_MAX := 0
0740END-IF
0750*
0760##W3INFO.ACTION := "L"
0770PERFORM W3INFO ##W3INFO
0780END-SUBROUTINE
0790END
