0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3OFORM
0050*
0060* DESCRIPTION   This Program generates a strarting FORM tag
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200DEFINE DATA
0210PARAMETER
02201 #H3METHOD  (A1)        BY VALUE /* i /m : "G" GET    (default)
0230*                                 /*        "P" POST
02401 #H3ACTION  (A) DYNAMIC BY VALUE /* i /m : URL to be called
0250*
0260LOCAL  USING W3PINFO
0270LOCAL  USING W3CONST
0280LOCAL
02901 #H3VALUE        (A) DYNAMIC
0300END-DEFINE
0310*
0320DEFINE SUBROUTINE H3-OPEN-FORM
0330*
0340ON ERROR
0350  ##W3ERROR.SUBROUTINE := "H3-OPEN-FORM"
0360  PERFORM W3ERROR ##W3ERROR
0370  ESCAPE ROUTINE
0380END-ERROR
0390*
0400IF #H3ACTION NE " " THEN
0410  COMPRESS 'ACTION="' #H3ACTION '"' INTO #H3VALUE LEAVING NO
0420ELSE
0430* --- A FORM tag without ACTION is not allowed. ---
0440  ##W3ERROR.SUBROUTINE := "H3-OPEN-FORM"
0450  ##W3ERROR.SUBPROGRAM := *PROGRAM
0460  ##W3ERROR.NR         := 20004
0470  ##W3ERROR.LINE       := 0
0480  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0490  PERFORM W3ERROR ##W3ERROR
0500  ESCAPE ROUTINE
0510END-IF
0520*
0530##W3INFO.FORM := TRUE
0540##W3INFO.ACTION := "F"
0550PERFORM W3INFO ##W3INFO
0560*
0570EXAMINE #H3METHOD TRANSLATE INTO UPPER
0580*
0590DECIDE ON FIRST VALUE OF #H3METHOD
0600  VALUE "P"
0610    COMPRESS '<FORM METHOD="POST"' #H3VALUE ">" ##HTTP_NEWLINE
0620      INTO #H3VALUE
0630  VALUE "G", " "
0640    COMPRESS '<FORM METHOD="GET"' #H3VALUE ">" ##HTTP_NEWLINE
0650      INTO #H3VALUE
0660  NONE
0670    IGNORE
0680END-DECIDE
0690*
0700PERFORM W3TEXTDYNAMIC #H3VALUE
0710*
0720END-SUBROUTINE
0730END
