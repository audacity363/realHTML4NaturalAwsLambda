0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D4EMNEW
0050*
0060* DESCRIPTION
0070*               Add a Person
0080*
0090* AUTHOR        SAG   08.09.1998
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+------------+--------------------------------------------
0180* SAG   ! 27.04.1999 ! Year2000 Employee File uses D instead of N6
0190* ------+------------+--------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER
02401 #P-NO         (A) DYNAMIC BY VALUE
02501 #P-NAME       (A) DYNAMIC BY VALUE
02601 #P-FIRST-NAME (A) DYNAMIC BY VALUE
02701 #P-BIRTH      (A) DYNAMIC BY VALUE
02801 #P-SEX        (A) DYNAMIC BY VALUE
02901 #P-DEPT       (A) DYNAMIC BY VALUE
03001 #P-CITY       (A) DYNAMIC BY VALUE
03101 #P-AREA-CODE  (A) DYNAMIC BY VALUE
03201 #P-ZIP        (A) DYNAMIC BY VALUE
03301 #P-COUNTRY    (A) DYNAMIC BY VALUE
03401 #P-PHONE      (A) DYNAMIC BY VALUE
03501 #INOUT-MSG    (A) DYNAMIC
0360*
0370LOCAL
03801 #BIRTH       (A10)
03901 REDEFINE #BIRTH
0400  2 #P-BIRTH-D (A2)
0410  2 FILLER 1X
0420  2 #P-BIRTH-M (A2)
0430  2 FILLER 1X
0440  2 #P-BIRTH-J (A4)
0450*
04601 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0470  2 PERSONNEL-ID
0480  2 FIRST-NAME
0490  2 NAME
0500  2 SEX
0510  2 BIRTH
0520  2 CITY
0530  2 ZIP
0540  2 COUNTRY
0550  2 AREA-CODE
0560  2 PHONE
0570  2 DEPT
0580*
05901 BIRTH-A (A8)
06001 REDEFINE BIRTH-A
0610  2 BIRTH-J (N4)
0620  2 BIRTH-M (N2)
0630  2 BIRTH-D (N2)
0640*
06501 #ERROR      (I4)
0660END-DEFINE
0670*
0680IF #P-NO NE " "
0690  #BIRTH := #P-BIRTH
0700  IF #P-BIRTH-J IS (N4) THEN
0710    BIRTH-J :=VAL(#P-BIRTH-J)
0720  ELSE
0730    BIRTH-J := 0
0740  END-IF
0750  IF #P-BIRTH-M IS (N2) THEN
0760    BIRTH-M :=VAL(#P-BIRTH-M)
0770  ELSE
0780    BIRTH-M := 0
0790  END-IF
0800  IF #P-BIRTH-D IS (N2) THEN
0810    BIRTH-D :=VAL(#P-BIRTH-D)
0820  ELSE
0830    BIRTH-D := 0
0840  END-IF
0850*
0860  EXAMINE #P-NAME       TRANSLATE INTO UPPER
0870  EXAMINE #P-FIRST-NAME TRANSLATE INTO UPPER
0880  EXAMINE #P-BIRTH      TRANSLATE INTO UPPER
0890  EXAMINE #P-SEX        TRANSLATE INTO UPPER
0900  EXAMINE #P-DEPT       TRANSLATE INTO UPPER
0910  EXAMINE #P-CITY       TRANSLATE INTO UPPER
0920  EXAMINE #P-AREA-CODE  TRANSLATE INTO UPPER
0930  EXAMINE #P-ZIP        TRANSLATE INTO UPPER
0940  EXAMINE #P-COUNTRY    TRANSLATE INTO UPPER
0950  EXAMINE #P-PHONE      TRANSLATE INTO UPPER
0960*
0970  F. FIND NUMBER EMPLOYEES-VIEW PERSONNEL-ID = #P-NO
0980  IF *NUMBER > 0
0990    BACKOUT TRANSACTION
1000    COMPRESS 'Sorry Object with personnel-number ' #P-NO
1010      'already exist:' INTO #INOUT-MSG
1020  ELSE
1030    EMPLOYEES-VIEW.PERSONNEL-ID := #P-NO
1040    EMPLOYEES-VIEW.FIRST-NAME   := #P-FIRST-NAME
1050    EMPLOYEES-VIEW.NAME         := #P-NAME
1060    EMPLOYEES-VIEW.SEX          := #P-SEX
1070* Year2000 Employee File uses D instead of N6
1080* EMPLOYEES-VIEW.BIRTH        := BIRTH-N
1090    MOVE EDITED BIRTH-A TO EMPLOYEES-VIEW.BIRTH (EM=YYYYMMDD)
1100    EMPLOYEES-VIEW.CITY         := #P-CITY
1110    EMPLOYEES-VIEW.ZIP          := #P-ZIP
1120    EMPLOYEES-VIEW.COUNTRY      := #P-COUNTRY
1130    EMPLOYEES-VIEW.AREA-CODE    := #P-AREA-CODE
1140    EMPLOYEES-VIEW.PHONE        := #P-PHONE
1150    EMPLOYEES-VIEW.DEPT         := #P-DEPT
1160*
1170    STORE EMPLOYEES-VIEW
1180    END OF TRANSACTION
1190*
1200    COMPRESS 'Object added:' #P-NO #P-FIRST-NAME #P-NAME INTO #INOUT-MSG
1210  END-IF
1220ELSE
1230  COMPRESS 'Sorry no Personel-ID given!' INTO #INOUT-MSG
1240END-IF
1250*
1260END
