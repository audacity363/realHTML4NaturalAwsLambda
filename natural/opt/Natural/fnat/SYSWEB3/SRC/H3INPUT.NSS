0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3INPUT
0050*
0060* DESCRIPTION   This Program generates a Input Field
0070*
0080* AUTHOR        SAG   28.07.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+----------+----------------------------------------------
0170*       ! --.--.-- !
0180* ------+----------+----------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 #H3TYPE   (A1) BY VALUE        /* i /m : Type of the input field:
0240*                                /*        'T' Text (default)
0250*                                /*        'P' Password
0260*                                /*        'H' Hidden
02701 #H3NAME   (A) DYNAMIC BY VALUE /* i /  : Name of the input variable
02801 #H3VAL    (A) DYNAMIC BY VALUE /* i /  : Default value of the input
0290*                                /*        variable
03001 #H3SIZE   (N4)        BY VALUE /* i /  : Size of the input box
03101 #H3MAX    (N4)        BY VALUE /* i /  : Maximum length of the input text
0320*
0330LOCAL  USING W3PINFO
0340LOCAL  USING W3CONST
0350LOCAL
03601 #H3VALUE  (A) DYNAMIC
03701 #H3VALUE2 (A) DYNAMIC
0380END-DEFINE
0390*
0400DEFINE SUBROUTINE H3-INPUT
0410* ------ ERROR HANDLING ------------------------------------------
0420ON ERROR
0430  ##W3ERROR.SUBROUTINE := "H3-INPUT"
0440  PERFORM W3ERROR ##W3ERROR
0450  ESCAPE ROUTINE
0460END-ERROR
0470*
0480* ------ MAIN PROGRAM --------------------------------------------
0490RESET ##W3INFO
0500PERFORM W3INFO ##W3INFO
0510* --- Can only be used inside a FORM tag. ---
0520IF ##W3INFO.FORM EQ FALSE THEN
0530  ##W3ERROR.SUBROUTINE := "H3-INPUT"
0540  ##W3ERROR.SUBPROGRAM := *PROGRAM
0550  ##W3ERROR.NR         := 20003
0560  ##W3ERROR.LINE       := 0
0570  PERFORM H3-ERROR-TEXT ##W3ERROR.NR ##W3ERROR.TEXT
0580  COMPRESS ##W3ERROR.TEXT "(" #H3NAME ")" INTO ##W3ERROR.TEXT
0590  PERFORM W3ERROR ##W3ERROR
0600  ESCAPE ROUTINE
0610END-IF
0620*
0630IF #H3NAME NE " " THEN
0640  EXAMINE #H3NAME TRANSLATE INTO UPPER
0650  COMPRESS 'NAME="' #H3NAME '"' INTO #H3VALUE LEAVING NO
0660ELSE
0670  RESET #H3VALUE
0680END-IF
0690*
0700EXAMINE #H3TYPE TRANSLATE INTO UPPER
0710*
0720DECIDE ON FIRST VALUE OF #H3TYPE
0730  VALUE "T", " "
0740    COMPRESS '<INPUT TYPE="TEXT"' #H3VALUE INTO #H3VALUE
0750*
0760  VALUE "H"
0770    COMPRESS '<INPUT TYPE="HIDDEN"' #H3VALUE INTO #H3VALUE
0780*
0790  VALUE "P"
0800    COMPRESS '<INPUT TYPE="PASSWORD"' #H3VALUE INTO #H3VALUE
0810  NONE
0820    COMPRESS '<INPUT' #H3VALUE INTO #H3VALUE
0830END-DECIDE
0840*
0850IF #H3MAX GT 0 THEN
0860  COMPRESS #H3VALUE 'MAXLENGTH="' INTO #H3VALUE
0870  COMPRESS #H3VALUE #H3MAX '"' INTO #H3VALUE LEAVING NO
0880END-IF
0890*
0900IF #H3SIZE GT 0 THEN
0910  COMPRESS #H3VALUE 'SIZE="' INTO #H3VALUE
0920  COMPRESS #H3VALUE #H3SIZE '"' INTO #H3VALUE LEAVING NO
0930END-IF
0940*
0950IF #H3VAL NE " " THEN
0960  COMPRESS #H3VALUE 'VALUE="' INTO #H3VALUE
0970  COMPRESS #H3VALUE #H3VAL '">' ##HTTP_NEWLINE
0980  INTO #H3VALUE LEAVING NO
0990ELSE
1000  COMPRESS #H3VALUE '>' ##HTTP_NEWLINE INTO #H3VALUE LEAVING NO
1010END-IF
1020PERFORM W3TEXTDYNAMIC #H3VALUE
1030*
1040END-SUBROUTINE
1050END
