0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3EMREAD
0050*
0060* DESCRIPTION
0070*               Get a Person
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+------------+--------------------------------------------
0180* SAG   ! 27.04.1999 ! Year2000 Employee File uses D instead of N6
0190* ------+------------+--------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER
02401 #P-NO         (A008)
02501 #P-NAME       (A020)
02601 #P-FIRST-NAME (A020)
02701 #P-BIRTH      (A010)
02801 #P-SEX        (A001)
02901 #P-DEPT       (A006)
03001 #P-CITY       (A020)
03101 #P-AREA-CODE  (A006)
03201 #P-ZIP        (A010)
03301 #P-COUNTRY    (A003)
03401 #P-PHONE      (A015)
03501 #P-PICTURE    (A250)
03601 #INOUT-MSG    (A250)
0370*
0380LOCAL
03901 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0400  2 PERSONNEL-ID
0410  2 FIRST-NAME
0420  2 NAME
0430  2 SEX
0440  2 BIRTH
0450  2 CITY
0460  2 ZIP
0470  2 POST-CODE
0480  2 COUNTRY
0490  2 AREA-CODE
0500  2 PHONE
0510  2 DEPT
0520*
05301 #I (N7)  INIT <0>
0540* Year2000 Employee File uses D instead of N6 this not needed
0550* BIRTH-N (N08)
0560* REDEFINE BIRTH-N
0570* 2 BIRTH-J (A4)
0580* 2 BIRTH-M (A2)
0590* 2 BIRTH-D (A2)
0600*
06101 #ERROR      (I4)
0620END-DEFINE
0630*
0640IF #P-NO NE ' '
0650*
0660  F. FIND EMPLOYEES-VIEW WITH PERSONNEL-ID = #P-NO
0670    IF NO
0680      COMPRESS #INOUT-MSG
0690        'Sorry nothing found for personnel-id:' #P-NO
0700        INTO #INOUT-MSG
0710    END-NOREC
0720*
0730    #P-SEX:=EMPLOYEES-VIEW.SEX
0740    IF #P-SEX ='M'
0750      #P-PICTURE := '/natw_man.gif'
0760    ELSE
0770      IF #P-SEX ='F'
0780        #P-PICTURE := '/natw_wom.gif'
0790      ELSE
0800        #P-PICTURE := '/natw_no.gif'
0810      END-IF
0820    END-IF
0830*
0840    #P-NO         :=EMPLOYEES-VIEW.PERSONNEL-ID
0850    #P-NAME       :=EMPLOYEES-VIEW.NAME
0860    #P-FIRST-NAME :=EMPLOYEES-VIEW.FIRST-NAME
0870*   Year2000 Employee File uses D instead of N6 ADD not needed
0880*   BIRTH-N      :=EMPLOYEES-VIEW.BIRTH
0890*   IF BIRTH-N < 200000 THEN
0900*     ADD 20000000 TO BIRTH-N
0910*   ELSE
0920*     ADD 19000000 TO BIRTH-N
0930*   END-IF
0940*   COMPRESS BIRTH-D "." BIRTH-M "." BIRTH-J INTO #P-BIRTH LEAVING NO
0950    MOVE EDITED EMPLOYEES-VIEW.BIRTH (EM=DD.MM.YYYY) TO #P-BIRTH
0960    #P-DEPT       :=EMPLOYEES-VIEW.DEPT
0970    #P-CITY       :=EMPLOYEES-VIEW.CITY
0980    #P-AREA-CODE  :=EMPLOYEES-VIEW.AREA-CODE
0990    #P-ZIP        :=EMPLOYEES-VIEW.ZIP
1000    #P-COUNTRY    :=EMPLOYEES-VIEW.COUNTRY
1010    #P-PHONE      :=EMPLOYEES-VIEW.PHONE
1020*
1030  END-FIND
1040*
1050  IF *NUMBER(F.) > 1
1060    COMPRESS #INOUT-MSG 'Sorry' *NUMBER(F.)
1070      'persons with personnel-id:' #P-NO 'found' INTO #INOUT-MSG
1080  END-IF
1090ELSE
1100  COMPRESS 'Please select a person' INTO #INOUT-MSG
1110END-IF
1120*
1130END
