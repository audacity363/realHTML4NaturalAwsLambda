0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Example Programs
0030*
0040*        E3ENVTX1
0050*
0060* DESCRIPTION   Reads a variable set by a text area tag and separates
0070*               the text lines.
0080*
0090* SEE           .../sysweb/t3envtx
0100*
0110* AUTHOR        SAG   08.09.1998
0120*
0130* Version       8.3
0140*
0150* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0160*
0170* HISTORY
0180* USER    DATE       REASON
0190* ------+------------+----------------------------------------------
0200*       ! --.--.---- !
0210* ------+------------+----------------------------------------------
0220*
0230* ------ PARAMETER -----------------------------------------------
0240DEFINE DATA
0250PARAMETER USING W3PARM
0260LOCAL USING W3CONST
0270LOCAL
02801 V                  (I4) CONST <10>
02901 W3START            (I4)
03001 W3NAME             (A) DYNAMIC
03101 W3VALUESERVER      (A1)
03201 W3COUNT            (I4)
03301 W3ARRAYVALUE       (A/1:V) DYNAMIC
03401 W3ARRAYVALUELENGTH (I4/1:V)
03501 W3ARRAYMAXIMUM     (I4)
0360*
03701 W3VALUE            (A) DYNAMIC
03801 II                 (I4)
0390END-DEFINE
0400* --- ERROR HANDLING ---
0410ON ERROR
0420  PERFORM W3ERROR ##W3ERROR
0430  PERFORM W3END ##RPC
0440  ESCAPE ROUTINE
0450END-ERROR
0460*
0470* --- INITIALIZE Web Interface ---
0480PERFORM W3INIT ##RPC
0490*
0500* --- SET TYPE OF RETURN-PAGE ---
0510PERFORM W3CONTENT-TYPE 'text/html'
0520* --- WRITE THE HEAD OF THE DOCUMENT ---
0530PERFORM W3TEXTLINE "<!DOCTYPE 'HTML PUBLIC -//W3C//DTD HTML 3.2//EN'>"
0540PERFORM W3TEXTLINE '<HTML><HEAD><META HTTP-EQUIV="Content-Type" '-
0550  'CONTENT="text/html; charset=iso-8859-1"><TITLE>'
0560* --- THE TITLE OF THE PAGE ---
0570PERFORM W3TEXTLINE 'Basic Routines - Read Environment Text Area Variables'
0580PERFORM W3TEXTLINE '</TITLE></HEAD><BODY>'
0590PERFORM W3TEXTLINE '<H2>Read Environment Text Area Variables '-
0600'(dynamic)</H2>'
0610*
0620W3COUNT       := 3
0630W3START       := 0
0640*
0650REPEAT
0660  PERFORM W3READ-ENVIRONMENT-TEXTAREA-DYNAMIC  W3START "TEXT" 'P'
0670    W3COUNT W3ARRAYVALUE(*) W3ARRAYVALUELENGTH(*) W3ARRAYMAXIMUM
0680*
0690  PERFORM W3TEXTLINE '<TABLE BORDER="1"><TR><TH>Text</TH><TH>Number</TH>'-
0700    '</TR>'
0710*
0720  FOR II = 1 TO W3COUNT
0730    COMPRESS "<TR><TD>"  INTO W3VALUE
0740    PERFORM W3TEXTDYNAMIC W3VALUE
0750*
0760    PERFORM W3TEXTDYNAMIC W3ARRAYVALUE(II)
0770*
0780    COMPRESS "</TD><TD>"
0790      W3ARRAYVALUELENGTH(II) "</TD></TR>" ##HTTP_NEWLINE_END
0800      INTO W3VALUE
0810    PERFORM W3TEXTDYNAMIC W3VALUE
0820  END-FOR
0830*
0840  PERFORM W3TEXTLINE "</TABLE>"
0850*
0860  COMPRESS '<BR>Restart from:' W3START
0870    '<BR>Read lines:' W3COUNT
0880    '<BR>Maximum lines:' W3ARRAYMAXIMUM
0890    INTO W3VALUE
0900  PERFORM W3TEXTLINEDYNAMIC W3VALUE
0910  IF W3START = 0
0920    ESCAPE BOTTOM
0930  END-IF
0940END-REPEAT
0950*
0960* --- END THE HTML PAGE ---
0970COMPRESS "<TABLE BORDER='0' WIDTH='100%' CELLSPACING='0' "-
0980  "CELLPADDING='5'>"
0990  '<TR BGCOLOR="#00cc66"><TD>' *PROGRAM '&#32;-&#32;' *DATE '&#32;' *TIME
1000  "</TD><TD ALIGN='RIGHT'>" 'Natural'
1010  "</TD></TR></TABLE>"
1020  '</BODY></HTML>' INTO W3VALUE LEAVING NO
1030PERFORM W3TEXTDYNAMIC W3VALUE
1040*
1050* --- END Web Interface ---
1060PERFORM W3END ##RPC
1070*
1080END
1090
