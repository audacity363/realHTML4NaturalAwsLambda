0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3EMNEW
0050*
0060* DESCRIPTION
0070*               Add a Person
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+------------+--------------------------------------------
0180* SAG   ! 27.04.1999 ! Year2000 Employee File uses D instead of N6
0190* SAG   ! 03.01.2003 ! Change N2 to N4 for Year check
0200* ------+------------+--------------------------------------------
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER
02401 #P-NO         (A008) BY VALUE
02501 #P-NAME       (A020) BY VALUE
02601 #P-FIRST-NAME (A020) BY VALUE
02701 #P-BIRTH      (A010) BY VALUE
02801 REDEFINE #P-BIRTH
0290  2 #P-BIRTH-D (A2)
0300  2 FILLER 1X
0310  2 #P-BIRTH-M (A2)
0320  2 FILLER 1X
0330  2 #P-BIRTH-J (A4)
03401 #P-SEX        (A001) BY VALUE
03501 #P-DEPT       (A006) BY VALUE
03601 #P-CITY       (A020) BY VALUE
03701 #P-AREA-CODE  (A006) BY VALUE
03801 #P-ZIP        (A010) BY VALUE
03901 #P-COUNTRY    (A003) BY VALUE
04001 #P-PHONE      (A015) BY VALUE
04101 #INOUT-MSG    (A250)
0420*
0430LOCAL
04401 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0450  2 PERSONNEL-ID
0460  2 FIRST-NAME
0470  2 NAME
0480  2 SEX
0490  2 BIRTH
0500  2 CITY
0510  2 ZIP
0520  2 COUNTRY
0530  2 AREA-CODE
0540  2 PHONE
0550  2 DEPT
0560*
05701 BIRTH-A (A8)
05801 REDEFINE BIRTH-A
0590  2 BIRTH-J (N4)
0600  2 BIRTH-M (N2)
0610  2 BIRTH-D (N2)
0620*
0630*
06401 #ERROR      (I4)
0650END-DEFINE
0660*
0670IF #P-BIRTH-J IS (N4) THEN /* change N2 to N4  SAG 03.01.2002
0680  BIRTH-J :=VAL(#P-BIRTH-J)
0690ELSE
0700  BIRTH-J := 0
0710END-IF
0720IF #P-BIRTH-M IS (N2) THEN
0730  BIRTH-M :=VAL(#P-BIRTH-M)
0740ELSE
0750  BIRTH-M := 0
0760END-IF
0770IF #P-BIRTH-D IS (N2) THEN
0780  BIRTH-D :=VAL(#P-BIRTH-D)
0790ELSE
0800  BIRTH-D := 0
0810END-IF
0820*
0830EXAMINE #P-NAME       TRANSLATE INTO UPPER
0840EXAMINE #P-FIRST-NAME TRANSLATE INTO UPPER
0850EXAMINE #P-BIRTH      TRANSLATE INTO UPPER
0860EXAMINE #P-SEX        TRANSLATE INTO UPPER
0870EXAMINE #P-DEPT       TRANSLATE INTO UPPER
0880EXAMINE #P-CITY       TRANSLATE INTO UPPER
0890EXAMINE #P-AREA-CODE  TRANSLATE INTO UPPER
0900EXAMINE #P-ZIP        TRANSLATE INTO UPPER
0910EXAMINE #P-COUNTRY    TRANSLATE INTO UPPER
0920EXAMINE #P-PHONE      TRANSLATE INTO UPPER
0930*
0940F. FIND NUMBER EMPLOYEES-VIEW PERSONNEL-ID = #P-NO
0950IF *NUMBER > 0
0960  BACKOUT TRANSACTION
0970  COMPRESS 'Sorry Object with personnel-number ' #P-NO
0980    'already exist:' INTO #INOUT-MSG
0990ELSE
1000  EMPLOYEES-VIEW.PERSONNEL-ID := #P-NO
1010  EMPLOYEES-VIEW.FIRST-NAME   := #P-FIRST-NAME
1020  EMPLOYEES-VIEW.NAME         := #P-NAME
1030  EMPLOYEES-VIEW.SEX          := #P-SEX
1040* Year2000 Employee File uses D instead of N6
1050* EMPLOYEES-VIEW.BIRTH        := BIRTH-N
1060  MOVE EDITED BIRTH-A TO EMPLOYEES-VIEW.BIRTH (EM=YYYYMMDD)
1070  EMPLOYEES-VIEW.CITY         := #P-CITY
1080  EMPLOYEES-VIEW.ZIP          := #P-ZIP
1090  EMPLOYEES-VIEW.COUNTRY      := #P-COUNTRY
1100  EMPLOYEES-VIEW.AREA-CODE    := #P-AREA-CODE
1110  EMPLOYEES-VIEW.PHONE        := #P-PHONE
1120  EMPLOYEES-VIEW.DEPT         := #P-DEPT
1130*
1140  STORE EMPLOYEES-VIEW
1150  END OF TRANSACTION
1160*
1170  COMPRESS 'Object added:' #P-NO #P-FIRST-NAME #P-NAME INTO #INOUT-MSG
1180END-IF
1190*
1200END
