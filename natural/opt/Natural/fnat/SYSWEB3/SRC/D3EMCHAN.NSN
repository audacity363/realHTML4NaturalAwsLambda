0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3EMCHAN
0050* DESCRIPTION
0060*               change a Person
0070*
0080* AUTHOR        SAG   11.04.97
0090*
0100* Version       8.3
0110*
0120* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0130*
0140* HISTORY
0150* USER    DATE       REASON
0160* ------+------------+--------------------------------------------
0170* SAG   ! 27.04.1999 ! Year2000 Employee File uses D instead of N6
0180* ------+------------+--------------------------------------------
0190*
0200* ------ PARAMETER -----------------------------------------------
0210DEFINE DATA
0220PARAMETER
02301 #P-NO         (A008) BY VALUE
02401 #P-NAME       (A020) BY VALUE
02501 #P-FIRST-NAME (A020) BY VALUE
02601 #P-BIRTH      (A010) BY VALUE
02701 REDEFINE #P-BIRTH
0280  2 #P-BIRTH-D (A2)
0290  2 FILLER 1X
0300  2 #P-BIRTH-M (A2)
0310  2 FILLER 1X
0320  2 #P-BIRTH-J (A4)
03301 #P-SEX        (A001) BY VALUE
03401 #P-DEPT       (A006) BY VALUE
03501 #P-CITY       (A020) BY VALUE
03601 #P-AREA-CODE  (A006) BY VALUE
03701 #P-ZIP        (A010) BY VALUE
03801 #P-COUNTRY    (A003) BY VALUE
03901 #P-PHONE      (A015) BY VALUE
04001 #INOUT-MSG    (A250)
0410*
0420LOCAL
04301 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0440  2 PERSONNEL-ID
0450  2 FIRST-NAME
0460  2 NAME
0470  2 SEX
0480  2 BIRTH
0490  2 CITY
0500  2 ZIP
0510  2 COUNTRY
0520  2 AREA-CODE
0530  2 PHONE
0540  2 DEPT
0550*
05601 BIRTH-A (A8)
05701 REDEFINE BIRTH-A
0580  2 BIRTH-J (N4)
0590  2 BIRTH-M (N2)
0600  2 BIRTH-D (N2)
0610*
06201 #ERROR      (I4)
0630END-DEFINE
0640*
0650IF #P-BIRTH-J IS (N4) THEN
0660  BIRTH-J :=VAL(#P-BIRTH-J)
0670ELSE
0680  BIRTH-J := 0
0690END-IF
0700*
0710IF #P-BIRTH-M IS (N2) THEN
0720  BIRTH-M :=VAL(#P-BIRTH-M)
0730ELSE
0740  BIRTH-M := 0
0750END-IF
0760*
0770IF #P-BIRTH-D IS (N2) THEN
0780  BIRTH-D :=VAL(#P-BIRTH-D)
0790ELSE
0800  BIRTH-D := 0
0810END-IF
0820*
0830EXAMINE #P-NAME       TRANSLATE INTO UPPER
0840EXAMINE #P-FIRST-NAME TRANSLATE INTO UPPER
0850EXAMINE #P-BIRTH      TRANSLATE INTO UPPER
0860EXAMINE #P-SEX        TRANSLATE INTO UPPER
0870EXAMINE #P-DEPT       TRANSLATE INTO UPPER
0880EXAMINE #P-CITY       TRANSLATE INTO UPPER
0890EXAMINE #P-AREA-CODE  TRANSLATE INTO UPPER
0900EXAMINE #P-ZIP        TRANSLATE INTO UPPER
0910EXAMINE #P-COUNTRY    TRANSLATE INTO UPPER
0920EXAMINE #P-PHONE      TRANSLATE INTO UPPER
0930*
0940F. FIND EMPLOYEES-VIEW PERSONNEL-ID = #P-NO
0950  IF NO
0960    COMPRESS 'Sorry Object with personnel-id' #P-NO
0970      'does not exist:' INTO #INOUT-MSG
0980  END-NOREC
0990  IF *NUMBER > 0
1000    EMPLOYEES-VIEW.PERSONNEL-ID := #P-NO
1010    EMPLOYEES-VIEW.FIRST-NAME   := #P-FIRST-NAME
1020    EMPLOYEES-VIEW.NAME         := #P-NAME
1030    EMPLOYEES-VIEW.SEX          := #P-SEX
1040* Year2000 Employee File uses D instead of N6
1050*   EMPLOYEES-VIEW.BIRTH        := BIRTH-N
1060    MOVE EDITED BIRTH-A TO EMPLOYEES-VIEW.BIRTH (EM=YYYYMMDD)
1070    EMPLOYEES-VIEW.CITY          := #P-CITY
1080    EMPLOYEES-VIEW.ZIP          := #P-ZIP
1090    EMPLOYEES-VIEW.COUNTRY      := #P-COUNTRY
1100    EMPLOYEES-VIEW.AREA-CODE    := #P-AREA-CODE
1110    EMPLOYEES-VIEW.PHONE        := #P-PHONE
1120    EMPLOYEES-VIEW.DEPT         := #P-DEPT
1130    UPDATE
1140    END OF TRANSACTION
1150*
1160    COMPRESS
1170      'Update on Object' #P-NO '(old) performed' INTO #INOUT-MSG
1180  END-IF
1190END-FIND
1200*
1210END
