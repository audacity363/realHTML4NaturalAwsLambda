0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Example Programs
0030*
0040*        NAT-RES
0050*
0060* DESCRIPTION   Read a resource from a Natural Library
0070*
0080* SEE
0090*
0100* AUTHOR        SAG   03.06.2002
0110*
0120* Version       8.3
0130*
0140* Copyright (c) 2002-2016 Software AG, Germany.  All rights reserved.
0150*
0160* HISTORY
0170* USER    DATE       REASON
0180* ------+----------+----------------------------------------------
0190*       ! --.--.-- !
0200* ------+----------+----------------------------------------------
0210*
0220* ------ PARAMETER -----------------------------------------------
0230DEFINE DATA
0240PARAMETER USING W3PARM
0250LOCAL USING W3CONST
0260LOCAL USING W3PINFO
0270LOCAL
0280*
02901 #II             (I4)
03001 #VALUE          (A) DYNAMIC
03101 #MIME-TYPE      (A) DYNAMIC
0320*
03301 #LIBRARY        (A) DYNAMIC
03401 #SOURCE         (A) DYNAMIC
03501 #FILE-EXTENTION (A) DYNAMIC
0360*
03701 #REQUEST_METHOD (A) DYNAMIC
03801 #PATH_INFO      (A) DYNAMIC
03901 #BODY           (A) DYNAMIC
04001 #BINARY         (B) DYNAMIC
04101 #PATH           (A) DYNAMIC
0420*
04301 F_PUT           (L) INIT <FALSE> /* no save back via http put possible
0440* F_PUT           (L) INIT <TRUE>  /* save back via http put possible
0450END-DEFINE
0460* --- ERROR HANDLING ---
0470ON ERROR
0480  PERFORM W3ERROR ##W3ERROR
0490  PERFORM W3END ##RPC
0500  ESCAPE ROUTINE
0510END-ERROR
0520*
0530* --- INITIALIZE W3 API PROCESSING ---
0540IF F_PUT
0550  PERFORM W3INIT ##RPC TRUE TRUE
0560ELSE
0570  PERFORM W3INIT ##RPC
0580END-IF
0590*
0600* ------------------------------------------------ Name of the library
0610PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "LIB" " " #LIBRARY
0620PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SOURCE" " " #SOURCE
0630PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "PATH_INFO" " " #PATH_INFO
0640*
0650IF #PATH_INFO NE " " AND #LIBRARY = " " AND #SOURCE = " "
0660  SEPARATE #PATH_INFO INTO #PATH REMAINDER #PATH_INFO WITH DELIMITER '/'
0670  IF #PATH_INFO NE " "
0680    SEPARATE #PATH_INFO INTO #PATH REMAINDER #PATH_INFO WITH DELIMITER '/'
0690    IF #PATH_INFO NE " "
0700      SEPARATE #PATH_INFO INTO #PATH REMAINDER #PATH_INFO WITH DELIMITER '/'
0710      IF #PATH_INFO NE " "
0720        SEPARATE #PATH_INFO INTO #LIBRARY REMAINDER #PATH_INFO WITH DELIMITER '/'
0730        IF #PATH_INFO NE " "
0740          SEPARATE #PATH_INFO INTO #SOURCE REMAINDER #PATH_INFO WITH DELIMITER '/'
0750        END-IF
0760      END-IF
0770    END-IF
0780  END-IF
0790END-IF
0800*
0810IF #LIBRARY = " " THEN
0820  #LIBRARY  := *LIBRARY-ID
0830END-IF
0840*
0850IF #SOURCE NE " "
0860  EXAMINE DIRECTION BACKWARD #SOURCE FOR '.' GIVING POSITION #II
0870  IF #II > 0 THEN
0880    #FILE-EXTENTION := SUBSTR(#SOURCE,#II)
0890    EXAMINE #FILE-EXTENTION FOR '.' DELETE
0900*
0910    PERFORM W3MIME-TYPE #MIME-TYPE #FILE-EXTENTION
0920  END-IF
0930END-IF
0940*
0950PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "REQUEST_METHOD" " " #REQUEST_METHOD
0960IF #REQUEST_METHOD = "PUT"
0970  IF F_PUT
0980    PERFORM W3READ-INPUT #BODY #BINARY
0990    IF #LIBRARY NE " " AND #SOURCE NE " "
1000        AND (*LENGTH(#BODY) > 0 OR *LENGTH(#BINARY) > 0 )
1010      PERFORM W3CHECK-RESOURCE #LIBRARY #SOURCE FALSE #PATH
1020      IF *LENGTH(#BODY) > 0
1030        PERFORM W3WRITE-RESOURCE #LIBRARY #SOURCE #BODY
1040      ELSE
1050        PERFORM W3WRITE-RESOURCE #LIBRARY #SOURCE #BINARY
1060      END-IF
1070      IF #PATH = " "
1080        PERFORM W3HTTP-HEADER 'Status' '201'
1090      ELSE
1100        PERFORM W3HTTP-HEADER 'Status' '200'
1110      END-IF
1120*
1130      PERFORM W3TEXTDYNAMIC  'File "'
1140      PERFORM W3TEXTDYNAMIC  #LIBRARY
1150      PERFORM W3TEXTDYNAMIC  '/'
1160      PERFORM W3TEXTDYNAMIC  #SOURCE
1170      PERFORM W3TEXTLINEDYNAMIC  '" saved.'
1180    ELSE
1190      PERFORM W3HTTP-HEADER 'Status' '202'
1200
1210      PERFORM W3TEXTDYNAMIC  'Library/file "'
1220      PERFORM W3TEXTDYNAMIC  #PATH_INFO
1230      PERFORM W3TEXTLINEDYNAMIC  '" not specified or empty.'
1240    END-IF
1250  ELSE
1260    PERFORM W3HTTP-HEADER 'Status' '406'
1270    PERFORM W3TEXTDYNAMIC  'File "'
1280    PERFORM W3TEXTDYNAMIC  #LIBRARY
1290    PERFORM W3TEXTDYNAMIC  '/'
1300    PERFORM W3TEXTDYNAMIC  #SOURCE
1310    PERFORM W3TEXTLINEDYNAMIC  '" not saved.'
1320  END-IF
1330ELSE
1340  IF #SOURCE NE " "
1350    EXAMINE DIRECTION BACKWARD #SOURCE FOR '.' GIVING POSITION #II
1360    IF #II > 0 THEN
1370      #FILE-EXTENTION := SUBSTR(#SOURCE,#II)
1380      EXAMINE #FILE-EXTENTION FOR '.' DELETE
1390*
1400      PERFORM W3MIME-TYPE #MIME-TYPE #FILE-EXTENTION
1410    END-IF
1420  END-IF
1430*
1440  IF #MIME-TYPE = " "
1450    #MIME-TYPE := 'text/plain'
1460  END-IF
1470*
1480* set mimetype
1490  PERFORM W3CONTENT-TYPE #MIME-TYPE
1500*
1510* load the html template
1520  IF #SOURCE NE " "
1530    PERFORM W3LOAD-RESOURCE #LIBRARY #SOURCE
1540  ELSE
1550    ##W3ERROR.SUBPROGRAM := *PROGRAM
1560    ##W3ERROR.NR         := 20999
1570    ##W3ERROR.LINE       := 0
1580    ##W3ERROR.TEXT       := 'No resource name given, use: "NAT-RES?SOURCE=...".'
1590    PERFORM W3ERROR ##W3ERROR
1600  END-IF
1610END-IF
1620*
1630* --- END W3 API PROCESSING ---
1640PERFORM W3END ##RPC
1650*
1660END
