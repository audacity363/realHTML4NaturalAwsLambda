0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - HTML Extension
0030*
0040*        H3JHTMLS
0050*
0060* DESCRIPTION   This Program generates a HTML document, containing
0070*               java script coding
0080*
0090* AUTHOR        SAG   28.07.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210DEFINE DATA
0220PARAMETER
02301 #H3TITLE           (A) DYNAMIC BY VALUE
02401 #H3BGCOLOR         (A) DYNAMIC BY VALUE
02501 #H3BGPICTURE       (A) DYNAMIC BY VALUE
02601 #H3JAVASRC         (A) DYNAMIC BY VALUE
02701 #H3JAVA            (A/1:V) DYNAMIC BY VALUE
02801 #H3JAVACOUNTER     (I4)   BY VALUE
02901 #H3ONLOAD          (A) DYNAMIC BY VALUE
03001 #H3ONUNLOAD        (A) DYNAMIC BY VALUE
0310LOCAL  USING W3CONST
0320LOCAL
03301 #HTTP_VALUE        (A) DYNAMIC
03401 II                 (I4)
0350END-DEFINE
0360*
0370DEFINE SUBROUTINE H3-OPEN-HTML-JAVASCRIPT
0380*
0390ON ERROR
0400##W3ERROR.SUBROUTINE := "H3-OPEN-HTML-JAVASCRIPT"
0410PERFORM W3ERROR ##W3ERROR
0420ESCAPE ROUTINE
0430END-ERROR
0440*
0450PERFORM W3CONTENT-TYPE 'text/html'
0460*
0470COMPRESS '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0//EN">'
0480  ##HTTP_NEWLINE INTO #HTTP_VALUE
0490* PERFORM W3TEXTDYNAMIC #HTTP_VALUE
0500*
0510COMPRESS '<HTML><HEAD>' ##HTTP_NEWLINE
0520  '<META HTTP-EQUIV="Content-Type" CONTENT="text/html; CHARSET='-
0530  'iso-8859-1">' ##HTTP_NEWLINE INTO #HTTP_VALUE LEAVING NO
0540PERFORM W3TEXTDYNAMIC #HTTP_VALUE
0550*
0560IF #H3JAVASRC NE " " THEN
0570  COMPRESS '<SCRIPT LANGUAGE="JavaScript" SRC="' #H3JAVASRC
0580    '"></SCRIPT>' ##HTTP_NEWLINE INTO #HTTP_VALUE LEAVING NO
0590  PERFORM W3TEXTDYNAMIC #HTTP_VALUE
0600END-IF
0610IF #H3JAVACOUNTER > 0 THEN
0620  COMPRESS '<SCRIPT LANGUAGE="JavaScript">'
0630    -'<!--- hide script from old browsers'
0640    ##HTTP_NEWLINE INTO #HTTP_VALUE
0650  PERFORM W3TEXTDYNAMIC #HTTP_VALUE
0660  FOR II = 1 TO #H3JAVACOUNTER
0670    PERFORM W3TEXTDYNAMIC #H3JAVA(II)
0680  END-FOR
0690  COMPRESS '// end hiding from old browsers -->' ##HTTP_NEWLINE
0700    '</SCRIPT>' ##HTTP_NEWLINE INTO #HTTP_VALUE
0710  PERFORM W3TEXTDYNAMIC #HTTP_VALUE
0720END-IF
0730*
0740PERFORM W3TEXTDYNAMIC '<TITLE>'
0750*
0760PERFORM W3HTML #H3TITLE
0770*
0780COMPRESS '</TITLE>' ##HTTP_NEWLINE '</HEAD><BODY'
0790  INTO #HTTP_VALUE LEAVING NO
0800*
0810IF #H3BGCOLOR NE " " THEN
0820  COMPRESS #HTTP_VALUE 'BGCOLOR="' INTO #HTTP_VALUE
0830  COMPRESS #HTTP_VALUE #H3BGCOLOR '"' INTO #HTTP_VALUE LEAVING NO
0840END-IF
0850*
0860IF #H3BGPICTURE NE " " THEN
0870  COMPRESS #HTTP_VALUE 'BACKGROUND="' INTO #HTTP_VALUE
0880  COMPRESS #HTTP_VALUE #H3BGPICTURE '"' INTO #HTTP_VALUE LEAVING NO
0890END-IF
0900*
0910IF #H3ONUNLOAD NE " " THEN
0920  COMPRESS #HTTP_VALUE 'ONUNLOAD="' INTO #HTTP_VALUE
0930  COMPRESS #HTTP_VALUE #H3ONUNLOAD '"' INTO #HTTP_VALUE LEAVING NO
0940END-IF
0950*
0960IF #H3ONLOAD NE " " THEN
0970  COMPRESS #HTTP_VALUE 'ONLOAD="' INTO #HTTP_VALUE
0980  COMPRESS #HTTP_VALUE #H3ONLOAD '"' INTO #HTTP_VALUE LEAVING NO
0990END-IF
1000COMPRESS #HTTP_VALUE '>'  ##HTTP_NEWLINE INTO #HTTP_VALUE LEAVING NO
1010PERFORM W3TEXTDYNAMIC #HTTP_VALUE
1020*
1030END-SUBROUTINE
1040END
1050
