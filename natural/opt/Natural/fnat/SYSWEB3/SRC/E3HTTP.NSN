0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Example Programs
0030*
0040*        E3HTTP
0050*
0060* DESCRIPTION   Writes a text line to the HEAD of a document.
0070*
0080* SEE           .../sysweb/t3nawiz
0090*
0100* AUTHOR        SAG   08.09.1998
0110*
0120* Version       8.3
0130*
0140* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0150*
0160* HISTORY
0170* USER    DATE       REASON
0180* ------+----------+----------------------------------------------
0190*       ! --.--.-- !
0200* ------+----------+----------------------------------------------
0210*
0220* ------ PARAMETER -----------------------------------------------
0230DEFINE DATA
0240PARAMETER USING W3PARM
0250LOCAL  USING W3CONST
0260LOCAL
02701 W3VALUE               (A) DYNAMIC
0280*
02901 #COOKIE               (A32)
03001 REDEFINE #COOKIE
0310  2 #VAL-COOKIE-NAME    (A12)
0320  2 #VAL-COOKIE-NUMBER  (A10)
0330  2 REDEFINE #VAL-COOKIE-NUMBER
0340    3 #VAL-COOKIE-NUMBER-A (A1/1:10)
0350*
03601 #NUMBER               (N9) INIT <0>
0370*
03801 COOKIE-NAME           (A) DYNAMIC
03901 #II                   (I4)
0400END-DEFINE
0410*
0420ON ERROR
0430  PERFORM W3ERROR ##W3ERROR
0440  PERFORM W3END ##RPC
0450  ESCAPE ROUTINE
0460END-ERROR
0470*
0480PERFORM W3INIT ##RPC
0490*
0500* --- SET TYPE OF RETURN-PAGE ---
0510PERFORM W3CONTENT-TYPE 'text/html'
0520* --- WRITE THE HEAD OF THE DOCUMENT ---
0530PERFORM W3TEXTLINE '<HTML><HEAD><META HTTP-EQUIV="Content-Type" '-
0540  'CONTENT="text/html; charset=iso-8859-1"><TITLE>'
0550* --- THE TITLE OF THE PAGE ---
0560PERFORM W3TEXTLINE 'Basic Routines -'-
0570  ' Write HTTP Settings to The Document'
0580PERFORM W3TEXTLINE  "</TITLE><BODY>"
0590*
0600PERFORM W3TEXTLINE '<H2>Write HTTP Settings to The Document</H2><HR>'
0610*
0620PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "HTTP_COOKIE" 'S' W3VALUE
0630IF *LENGTH(W3VALUE) > 0 THEN
0640  #COOKIE := W3VALUE
0650  IF #VAL-COOKIE-NAME = "CGI-COUNTER=" THEN
0660    FOR #II = 1 TO 10
0670      IF NOT (#VAL-COOKIE-NUMBER-A(#II) IS (N1) ) THEN
0680        #VAL-COOKIE-NUMBER-A(#II) := " "
0690      END-IF
0700    END-FOR
0710    IF #VAL-COOKIE-NUMBER IS (N9)
0720      COMPUTE #NUMBER = VAL(#VAL-COOKIE-NUMBER)
0730    END-IF
0740  END-IF
0750  COMPRESS 'The counter is now:<FONT COLOR="RED">' #NUMBER "<BR></FONT>"
0760    ##HTTP_NEWLINE_END INTO W3VALUE
0770  PERFORM W3TEXTLINE W3VALUE
0780ELSE
0790  PERFORM W3TEXTLINE '<FONT COLOR="GREEN">The counter will be '-
0800    'initialized if you allow COOKIES to be saved.'-
0810    "</FONT><BR>"
0820END-IF
0830PERFORM W3TEXTLINE '<P>The following HTTP-COOKIE has been set with '-
0840  'this page:<BR>'
0850* ----------------------------------------- Set cookie with next number
0860ADD 1 TO #NUMBER
0870COMPRESS "CGI-COUNTER=" #NUMBER ";" INTO COOKIE-NAME LEAVING NO
0880COMPRESS 'Set-Cookie:' COOKIE-NAME
0890  'expires=Thu, 01 JAN 2010 00:00:00 GMT' ##HTTP_NEWLINE
0900  INTO W3VALUE
0910PERFORM W3HTTP W3VALUE
0920PERFORM W3TEXTLINE W3VALUE
0930*
0940PERFORM W3TEXTLINE '<P>Press <FONT COLOR="BLUE">RELOAD</FONT> '-
0950  'to see the next count of the cookie.'
0960*
0970* --- END THE HTML PAGE ---
0980COMPRESS "<TABLE BORDER='0' WIDTH='100%' CELLSPACING='0' "-
0990  "CELLPADDING='5'>"
1000  '<TR BGCOLOR="#00cc66"><TD>' *PROGRAM '&#32;-&#32;' *DATE '&#32;' *TIME
1010  "</TD><TD ALIGN='RIGHT'>" 'Natural'
1020  "</TD></TR></TABLE>"
1030  '</BODY></HTML>' INTO W3VALUE LEAVING NO
1040PERFORM W3TEXTDYNAMIC W3VALUE
1050*
1060* --- now with HTTPDYNAMIC
1070COMPRESS  'Date: Thu, 28 AUG 1994 00:55:00 GMT'
1080  ##HTTP_NEWLINE INTO W3VALUE
1090PERFORM W3HTTPDYNAMIC W3VALUE
1100*
1110* --- END Web Interface ---
1120PERFORM W3END ##RPC
1130*
1140END
