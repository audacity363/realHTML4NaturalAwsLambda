0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D4EMSEL
0050*
0060* DESCRIPTION
0070*               Select Persons
0080*
0090* AUTHOR        SAG   08.09.1998
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING D4-PARM
0240LOCAL
02501 ##FROM           (A) DYNAMIC
02601 ##TO             (A) DYNAMIC
0270*
02801 V                (I4) CONST <201>
0290*
03001 H3ARRAYVALUE     (A/1:V) DYNAMIC /*  in: Background color
03101 H3ARRAYLABEL     (A/1:V) DYNAMIC /*  in: Name of the label of the button
03201 H3ARRAYSELECTED  (L/1:V)         /*  in: Number of the pre-selected button
0330*
03401 W3SIZE           (N4)
0350*
0360LOCAL
03701 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0380  2 PERSONNEL-ID
0390  2 NAME
0400  2 FIRST-NAME
0410*
04201 #I (N7)  INIT <0>
04301 #J (N3)  INIT <0>
0440*
04501 W3VALUE (A) DYNAMIC
0460*
04701 #F-SELECTED (L) INIT <FALSE>
0480END-DEFINE
0490*
0500DEFINE SUBROUTINE D4EMSEL
0510* -------------------------------------------------------------
0520##FROM := #DEMO-PARM.#FROM
0530##TO   := #DEMO-PARM.#TO
0540IF #TO EQ #FROM OR #TO EQ " " THEN
0550  COMPRESS ##FROM H'FF' INTO ##TO LEAVING NO
0560END-IF
0570*
0580IF #DEMO-PARM.#START LE 1 THEN
0590  #J := 1 + #DEMO-PARM.#END-MAX
0600ELSE
0610  #J := 2 + #DEMO-PARM.#END-MAX
0620END-IF
0630*
0640F. FIND EMPLOYEES-VIEW WITH NAME = ##FROM THRU ##TO SORTED BY NAME
0650  IF NO
0660    COMPRESS ' Sorry nothing found for from:' ##FROM ' to:'
0670      ##TO INTO #DEMO-PARM.#MSG
0680  END-NOREC
0690  IF *NUMBER(F.) > 0
0700    IF *COUNTER(F.) GE #DEMO-PARM.#START THEN
0710      ADD 1 TO #I
0720      COMPRESS EMPLOYEES-VIEW.PERSONNEL-ID INTO H3ARRAYVALUE(#I)
0730      COMPRESS  EMPLOYEES-VIEW.NAME EMPLOYEES-VIEW.FIRST-NAME
0740        INTO H3ARRAYLABEL(#I)
0750      IF (NOT #F-SELECTED) AND (H3ARRAYVALUE(#I) = #DEMO-PARM.#PERSON)
0760        #F-SELECTED := TRUE
0770        H3ARRAYSELECTED(#I) := TRUE
0780      ELSE
0790        H3ARRAYSELECTED(#I) := FALSE
0800      END-IF
0810*
0820      IF #I GE #J THEN
0830        ESCAPE BOTTOM(F.)
0840      END-IF
0850    END-IF
0860  END-IF
0870END-FIND
0880*
0890IF *NUMBER(F.) > 10 THEN
0900  W3SIZE := 10
0910ELSE
0920  W3SIZE := *NUMBER(F.)
0930END-IF
0940*
0950IF NOT #F-SELECTED THEN
0960  H3ARRAYSELECTED(1) := TRUE
0970END-IF
0980*
0990IF #I = 0 THEN
1000  PERFORM W3TEXTLINEDYNAMIC "(NONE)"
1010ELSE
1020  PERFORM H3-SCROLLING-LIST W3SIZE #I "ID"
1030    H3ARRAYVALUE(*) H3ARRAYLABEL(*) H3ARRAYSELECTED(*) FALSE
1040END-IF
1050*
1060#DEMO-PARM.#END := *NUMBER(F.)
1070*
1080IF *NUMBER(F.) > 0 AND #DEMO-PARM.#MSG = ' '
1090  #I :=  #DEMO-PARM.#START + #I -1
1100  COMPRESS 'List of'  *NUMBER(F.) 'objects .... from'
1110    #DEMO-PARM.#FROM 'to' #DEMO-PARM.#TO '; Object'
1120    #DEMO-PARM.#START "-" #I INTO #DEMO-PARM.#MSG
1130END-IF
1140END-SUBROUTINE
1150END
