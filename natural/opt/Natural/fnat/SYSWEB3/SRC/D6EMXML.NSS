0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - New Demo Application
0030*
0040*        D6EMXML
0050*
0060* DESCRIPTION
0070*               List Persons
0080*
0090* AUTHOR        SAG   08.09.1998
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+------------+--------------------------------------------
0180* SAG   ! 18.03.2004 ! Obsolet -> D6EM2XML used instead
0190* ------+------------+--------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER
02401 #XML   (A) DYNAMIC
0250PARAMETER USING D6-PARM
0260LOCAL USING W3CONST
0270LOCAL
02801 ##FROM (A) DYNAMIC
02901 ##TO   (A) DYNAMIC
0300*
03101 #BIRTH (A10)
0320LOCAL
03301 EMPLOYEES-VIEW VIEW OF EMPLOYEES
0340  2 PERSONNEL-ID
0350  2 FIRST-NAME
0360  2 NAME
0370  2 MIDDLE-NAME
0380* 2 MIDDLE-I    /* known problem:
0390*               /* if adabas nucleus parameter options=(Trucation)
0400*               /* is not set, NAT3055 occurres
0410*               /* -> MIDDLE-I is not used for this example
0420  2 ADDRESS-LINE(6)
0430  2 C*ADDRESS-LINE
0440  2 SEX
0450  2 BIRTH
0460  2 CITY
0470  2 ZIP
0480  2 POST-CODE
0490  2 COUNTRY
0500  2 AREA-CODE
0510  2 PHONE
0520  2 MAR-STAT
0530  2 DEPT
0540  2 JOB-TITLE
0550  2 INCOME(6)
0560    3 BONUS(4)
0570    3 C*BONUS
0580    3 SALARY
0590    3 CURR-CODE
0600  2 C*INCOME
0610  2 LEAVE-DATA
0620    3 LEAVE-DUE
0630    3 LEAVE-TAKEN
0640  2 LEAVE-BOOKED(4)
0650    3 LEAVE-START
0660    3 LEAVE-END
0670  2 C*LEAVE-BOOKED
0680  2 LANG(4)
0690  2 C*LANG
0700*
07101 #I (N7)  INIT <0>
07201 #J (N3)  INIT <0>
0730*
07401 #IK (I4)
07501 #IL (I4)
0760END-DEFINE
0770*
0780DEFINE SUBROUTINE D6EMXML
0790*
0800##FROM := #DEMO-PARM.#FROM
0810##TO   := #DEMO-PARM.#TO
0820IF #TO EQ #FROM OR #TO EQ " " THEN
0830  COMPRESS ##FROM H'FF' INTO ##TO LEAVING NO
0840END-IF
0850*
0860#J := #DEMO-PARM.#END-MAX
0870*
0880IF #DEMO-PARM.#START = 0
0890  ADD 1 TO #I
0900END-IF
0910*
0920#XML := '<?xml version="1.0" encoding="ISO-8859-1" ?>' -H'0D0A' -'<Result>'
0930*
0940F. FIND EMPLOYEES-VIEW WITH NAME = ##FROM THRU ##TO SORTED BY NAME
0950  IF NO
0960    COMPRESS 'Sorry nothing found for from:' ##FROM ' to:'
0970      ##TO INTO #DEMO-PARM.#MSG
0980  END-NOREC
0990  IF *NUMBER(F.) > 0
1000*
1010    IF *COUNTER(F.) GE #DEMO-PARM.#START THEN
1020      ADD 1 TO #I
1030      COMPRESS #XML '<Employee Personnel-ID="'
1040        EMPLOYEES-VIEW.PERSONNEL-ID '">' INTO #XML LEAVING NO
1050      /* now the children
1060      COMPRESS #XML '<Full-Name>' INTO #XML LEAVING NO
1070      COMPRESS #XML '<First-Name>' EMPLOYEES-VIEW.FIRST-NAME
1080        '</First-Name>' INTO #XML LEAVING NO
1090      COMPRESS #XML '<Middle-Name>' EMPLOYEES-VIEW.MIDDLE-NAME
1100        '</Middle-Name>' INTO #XML LEAVING NO
1110*     /* see declaration of MIDDLE-I for known problem
1120*     COMPRESS #XML '<Middle-I>' EMPLOYEES-VIEW.MIDDLE-I
1130*       '</Middle-I>' INTO #XML LEAVING NO
1140      COMPRESS #XML '<Name>' EMPLOYEES-VIEW.NAME
1150        '</Name>' INTO #XML LEAVING NO
1160      COMPRESS #XML '</Full-Name>' INTO #XML LEAVING NO
1170      /*
1180      COMPRESS #XML '<Mar-Stat>' EMPLOYEES-VIEW.MAR-STAT
1190        '</Mar-Stat>' INTO #XML LEAVING NO
1200      COMPRESS #XML '<Sex>' EMPLOYEES-VIEW.SEX
1210        '</Sex>' INTO #XML LEAVING NO
1220      /*
1230      MOVE EDITED EMPLOYEES-VIEW.BIRTH (EM=YYYY-MM-DD) TO #BIRTH
1240      COMPRESS #XML '<Birth>' #BIRTH '</Birth>' INTO #XML LEAVING NO
1250      /*
1260      /* now the children
1270      COMPRESS #XML '<Full-Address>' INTO #XML LEAVING NO
1280      IF EMPLOYEES-VIEW.C*ADDRESS-LINE GT 0 THEN
1290        FOR #IK = 1 TO EMPLOYEES-VIEW.C*ADDRESS-LINE
1300          COMPRESS #XML '<Address-Line>' EMPLOYEES-VIEW.ADDRESS-LINE(#IK)
1310            '</Address-Line>' INTO #XML LEAVING NO
1320        END-FOR
1330      END-IF
1340      IF EMPLOYEES-VIEW.CITY NE " " OR
1350          EMPLOYEES-VIEW.ZIP  NE " " OR
1360          EMPLOYEES-VIEW.ZIP NE " " OR
1370          EMPLOYEES-VIEW.COUNTRY  NE " " THEN
1380        COMPRESS #XML '<City>' EMPLOYEES-VIEW.CITY
1390          '</City>' INTO #XML LEAVING NO
1400        COMPRESS #XML '<Zip>' EMPLOYEES-VIEW.ZIP
1410          '</Zip>' INTO #XML LEAVING NO
1420        COMPRESS #XML '<Post-Code>' EMPLOYEES-VIEW.POST-CODE
1430          '</Post-Code>' INTO #XML LEAVING NO
1440        COMPRESS #XML '<Country>' EMPLOYEES-VIEW.COUNTRY
1450          '</Country>' INTO #XML LEAVING NO
1460      END-IF
1470      COMPRESS #XML '</Full-Address>' INTO #XML LEAVING NO
1480      /*
1490      IF EMPLOYEES-VIEW.PHONE NE " " OR
1500      EMPLOYEES-VIEW.AREA-CODE NE " " THEN
1510        COMPRESS #XML '<Telephone>' INTO #XML LEAVING NO
1520        /* now the children
1530        COMPRESS #XML '<Phone>' EMPLOYEES-VIEW.PHONE
1540          '</Phone>' INTO #XML LEAVING NO
1550        COMPRESS #XML '<Area-Code>' EMPLOYEES-VIEW.AREA-CODE
1560          '</Area-Code>' INTO #XML LEAVING NO
1570        COMPRESS #XML '</Telephone>' INTO #XML LEAVING NO
1580      END-IF
1590      /*
1600      IF EMPLOYEES-VIEW.DEPT NE " " THEN
1610        COMPRESS #XML '<Dept>' EMPLOYEES-VIEW.DEPT
1620          '</Dept>' INTO #XML LEAVING NO
1630      END-IF
1640      /*
1650      IF  EMPLOYEES-VIEW.JOB-TITLE NE " " THEN
1660        COMPRESS #XML '<Job-Title>' EMPLOYEES-VIEW.JOB-TITLE
1670          '</Job-Title>' INTO #XML LEAVING NO
1680      END-IF
1690      /*
1700      FOR #IK = 1 TO EMPLOYEES-VIEW.C*INCOME
1710        COMPRESS #XML '<Income>' INTO #XML LEAVING NO
1720        /* now the children
1730        COMPRESS #XML '<Curr-Code>' EMPLOYEES-VIEW.CURR-CODE(#IK)
1740          '</Curr-Code>' INTO #XML LEAVING NO
1750        COMPRESS #XML '<Salary>' EMPLOYEES-VIEW.SALARY(#IK)
1760          '</Salary>' INTO #XML LEAVING NO
1770        /*
1780        FOR #IL = 1 TO EMPLOYEES-VIEW.C*BONUS(#IK)
1790          COMPRESS #XML '<Bonus>' EMPLOYEES-VIEW.BONUS(#IK,#IL)
1800            '</Bonus>' INTO #XML LEAVING NO
1810        END-FOR
1820        /*
1830        COMPRESS #XML '</Income>' INTO #XML LEAVING NO
1840      END-FOR
1850      /*
1860      IF EMPLOYEES-VIEW.LEAVE-DUE NE 0 OR
1870      EMPLOYEES-VIEW.LEAVE-TAKEN  NE 0 THEN
1880        COMPRESS #XML '<Leave-Data'
1890          '>' INTO #XML LEAVING NO
1900        IF EMPLOYEES-VIEW.LEAVE-DUE NE 0 THEN
1910          COMPRESS #XML '<Leave-Due>' EMPLOYEES-VIEW.LEAVE-DUE
1920            '</Leave-Due>' INTO #XML LEAVING NO
1930        END-IF
1940        IF EMPLOYEES-VIEW.LEAVE-TAKEN NE 0 THEN
1950          COMPRESS #XML '<Leave-Taken>' EMPLOYEES-VIEW.LEAVE-TAKEN
1960            '</Leave-Taken>' INTO #XML LEAVING NO
1970        END-IF
1980        /*
1990        COMPRESS #XML '</Leave-Data>' INTO #XML LEAVING NO
2000      END-IF
2010      /*
2020      FOR #IK = 1 TO EMPLOYEES-VIEW.C*LEAVE-BOOKED
2030        COMPRESS #XML '<Leave-Booked>' INTO #XML LEAVING NO
2040        /* now the children
2050        COMPRESS #XML '<Leave-Start>' EMPLOYEES-VIEW.LEAVE-START(#IK)
2060          '</Leave-Start>' INTO #XML LEAVING NO
2070        COMPRESS #XML '<Leave-End>' EMPLOYEES-VIEW.LEAVE-END(#IK)
2080          '</Leave-End>' INTO #XML LEAVING NO
2090        /*
2100        COMPRESS #XML '</Leave-Booked>' INTO #XML LEAVING NO
2110      END-FOR
2120
2130      FOR #IK = 1 TO EMPLOYEES-VIEW.C*LANG
2140        COMPRESS #XML '<Lang>' EMPLOYEES-VIEW.LANG(#IK)
2150          '</Lang>' INTO #XML LEAVING NO
2160      END-FOR
2170      /*
2180      COMPRESS #XML '</Employee>' INTO #XML LEAVING NO
2190*
2200      IF #I GE #J THEN
2210        ESCAPE BOTTOM(F.)
2220      END-IF
2230    END-IF
2240  END-IF
2250END-FIND
2260*
2270COMPRESS #XML '</Result>' INTO #XML LEAVING NO
2280*
2290#DEMO-PARM.#END := *NUMBER(F.)
2300*
2310IF *NUMBER(F.) > 0 AND #DEMO-PARM.#MSG = ' '
2320  #I :=  #DEMO-PARM.#START + #I - 1
2330  COMPRESS 'List of'  *NUMBER(F.) 'objects .... from'
2340    #DEMO-PARM.#FROM 'to' #DEMO-PARM.#TO '; Object'
2350    #DEMO-PARM.#START "-" #I INTO #DEMO-PARM.#MSG
2360END-IF
2370*
2380END-SUBROUTINE
2390*
2400END
