0010* ----------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Demo Application
0030*
0040*        D3CHOOSE
0050*
0060* DESCRIPTION
0070*               Dispatcher Program for the demo
0080*
0090* AUTHOR        SAG   11.04.97
0100*
0110* Version       8.3
0120*
0130* Copyright (c) 1997-2016 Software AG, Germany.  All rights reserved.
0140*
0150* HISTORY
0160* USER    DATE       REASON
0170* ------+----------+----------------------------------------------
0180*       ! --.--.-- !
0190* ------+----------+----------------------------------------------
0200*
0210* ------ PARAMETER -----------------------------------------------
0220DEFINE DATA
0230PARAMETER USING W3PARM
0240LOCAL USING D3-PARM
0250LOCAL USING W3CONST
0260LOCAL
02701 P-NO (A250)
02801 P-NAME       (A250)
02901 P-FIRST-NAME (A250)
03001 P-BIRTH      (A250)
03101 P-SEX        (A250)
03201 P-DEPT       (A250)
03301 P-CITY       (A250)
03401 P-AREA-CODE  (A250)
03501 P-ZIP        (A250)
03601 P-COUNTRY    (A250)
03701 P-PHONE      (A250)
0380*
03901 P-AGE        (A250)
04001 P-HOBBY      (A250)
0410*
04201 #PROG        (A008)
0430*
04401 W3URL        (A250)
04501 W3VALUE      (A250)
04601 W3MAX        (I004)
0470END-DEFINE
0480*
0490ON ERROR
0500  PERFORM W3ERROR ##W3ERROR
0510  PERFORM W3END ##RPC
0520  ESCAPE ROUTINE
0530END-ERROR
0540*
0550PERFORM W3INIT ##RPC
0560*
0570PERFORM D3-RGLOB #DEMO-MENU #DEMO-PARM
0580*
0590EXAMINE #DEMO-PARM.#TODO TRANSLATE INTO UPPER
0600EXAMINE #DEMO-PARM.#OT   TRANSLATE INTO UPPER
0610*
0620IF #DEMO-PARM.#TODO EQ "HOME"
0630  PERFORM W3READ-ENVIRONMENT "SERVER_NAME" ' ' W3VALUE W3MAX
0640  IF W3MAX EQ 0 THEN
0650    W3URL  := 'http://www.softwareag.com/'
0660  ELSE
0670    COMPRESS 'http://' W3VALUE INTO W3URL LEAVING NO
0680    PERFORM W3READ-ENVIRONMENT "SERVER_PORT" ' ' W3VALUE W3MAX
0690    IF W3MAX NE 0 THEN
0700      COMPRESS W3URL ':' W3VALUE '/' INTO W3URL LEAVING NO
0710    END-IF
0720  END-IF
0730*
0740  PERFORM W3CLEAR
0750  PERFORM W3LOCATION W3URL
0760ELSE
0770*
0780  IF #DEMO-PARM.#OT NE "EM" THEN
0790    PERFORM W3CLEAR
0800    COMPRESS #DEMO-PARM.#CGI "NAT-HTML?LIB=SYSWEB&SOURCE=D3NOTIMP"
0810      INTO W3VALUE LEAVING NO
0820    PERFORM W3LOCATION W3VALUE
0830  ELSE
0840*
0850    DECIDE ON FIRST OF #DEMO-PARM.#TODO
0860      VALUE "HOME"
0870        PERFORM W3READ-ENVIRONMENT "SERVER_NAME" ' ' W3VALUE W3MAX
0880        IF W3MAX EQ 0 THEN
0890          W3VALUE  := 'http://www.softwareag.de'
0900        ELSE
0910          COMPRESS 'http://' W3VALUE INTO W3VALUE LEAVING NO
0920        END-IF
0930*
0940        PERFORM W3CLEAR
0950        PERFORM W3LOCATION W3VALUE
0960      VALUE "NEW"
0970        PERFORM D3NEW #DEMO-MENU #DEMO-PARM
0980      VALUE "DELETE"
0990        PERFORM D3DELETE #DEMO-MENU #DEMO-PARM
1000      VALUE "SELECT"
1010        PERFORM D3SELECT #DEMO-MENU #DEMO-PARM
1020      VALUE "LISTING"
1030        PERFORM D3LIST #DEMO-MENU #DEMO-PARM
1040      VALUE "SHOW"
1050        PERFORM D3SHOW #DEMO-MENU #DEMO-PARM
1060      VALUE "BROWSE"
1070        PERFORM D3BROWSE #DEMO-MENU #DEMO-PARM
1080      VALUE "CHANGE"
1090        PERFORM D3CHANGE #DEMO-MENU #DEMO-PARM
1100      VALUE "DELETE OBJECT"
1110        COMPRESS "D3" #DEMO-PARM.#OT "DEL" INTO #PROG LEAVING NO
1120        CALLNAT #PROG #DEMO-PARM.#PERSON #DEMO-PARM.#MSG
1130*
1140        PERFORM BACK
1150      VALUE "SAVE CHANGE"
1160        PERFORM READ-ENV
1170*
1180        COMPRESS "D3" #DEMO-PARM.#OT "CHAN" INTO #PROG LEAVING NO
1190        CALLNAT #PROG P-NO P-NAME P-FIRST-NAME P-BIRTH P-SEX P-DEPT
1200          P-CITY P-AREA-CODE P-ZIP P-COUNTRY P-PHONE #DEMO-PARM.#MSG
1210*
1220        PERFORM D3CHANGE #DEMO-MENU #DEMO-PARM
1230      VALUE "CREATE NEW"
1240        PERFORM READ-ENV
1250*
1260        #DEMO-PARM.#OT := "EM"
1270        COMPRESS "D3" #DEMO-PARM.#OT "NEW" INTO #PROG LEAVING NO
1280        CALLNAT #PROG P-NO P-NAME P-FIRST-NAME P-BIRTH P-SEX P-DEPT
1290          P-CITY P-AREA-CODE P-ZIP P-COUNTRY P-PHONE #DEMO-PARM.#MSG
1300*
1310        PERFORM D3NEW #DEMO-MENU #DEMO-PARM
1320      NONE
1330        #DEMO-PARM.#STATUS := "M"
1340        INCLUDE D3-BACK
1350    END-DECIDE
1360  END-IF
1370END-IF
1380*
1390PERFORM W3END ##RPC
1400*
1410DEFINE SUBROUTINE BACK
1420INCLUDE D3-BACK
1430END-SUBROUTINE
1440*
1450DEFINE SUBROUTINE READ-ENV
1460PERFORM W3READ-ENVIRONMENT "ID"    "P" P-NO         W3MAX
1470PERFORM W3READ-ENVIRONMENT "NAME"  "P" P-NAME       W3MAX
1480PERFORM W3READ-ENVIRONMENT "FIRST" "P" P-FIRST-NAME W3MAX
1490PERFORM W3READ-ENVIRONMENT "BIRTH" "P" P-BIRTH      W3MAX
1500PERFORM W3READ-ENVIRONMENT "SEX"   "P" P-SEX        W3MAX
1510PERFORM W3READ-ENVIRONMENT "ZIP"   "P" P-ZIP        W3MAX
1520PERFORM W3READ-ENVIRONMENT "DEPT"  "P" P-DEPT       W3MAX
1530PERFORM W3READ-ENVIRONMENT "CITY"  "P" P-CITY       W3MAX
1540PERFORM W3READ-ENVIRONMENT "AREA"  "P" P-AREA-CODE  W3MAX
1550PERFORM W3READ-ENVIRONMENT "CNTRY" "P" P-COUNTRY    W3MAX
1560PERFORM W3READ-ENVIRONMENT "PHONE" "P" P-PHONE      W3MAX
1570*
1580END-SUBROUTINE
1590END
