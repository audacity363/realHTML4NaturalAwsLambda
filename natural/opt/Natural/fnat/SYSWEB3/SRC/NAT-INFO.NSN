0010* ---------------------------------------------------------------------
0020* CLASS  Natural Web Interface - Utilities
0030*
0040*        NAT-INFO
0050*
0060* DESCRIPTION
0070*               This Program returnes the current configuration
0080*               of Natual(a)Web
0090*               Following settings have to be done at your NWW
0100*               configuration file to see all values:
0110*
0120*               SET=HTTP_USER_AGENT
0130*               SET=REMOTE_HOST
0140*               SET=REMOTE_ADDR
0150*               SET=SERVER_PORT
0160*               SET=SERVER_SOFTWARE
0170*               SET=GATEWAY_INTERFACE
0180*               SET=SERVER_NAME
0190*
0200* AUTHOR        SAG   14.01.98
0210*
0220* Version       8.3
0230*
0240* COPYRIGHT (c) 1997-2006 SOFTWARE AG, ALL Rights Reserved.
0250*
0260* HISTORY
0270* USER    DATE       REASON
0280* ------+------------+----------------------------------------------
0290* SAG   ! 06.04.1998 ! loop, usr0050p
0300* SAG   ! 06.04.1998 ! check for large FNRs
0310* SAG   ! 07.12.2001 ! change usr2013 to usr3013
0320* SAG   ! 30.10.2002 ! use USR3025L
0330* SAG   ! 28.02.2006 ! add *parm-user
0340* ------+------------+----------------------------------------------
0350DEFINE DATA
0360* ------------------------------------------- standard PDA for HTTP Api
0370PARAMETER USING W3PARM
0380* -------------------------------------------- definitions for HTTP Api
0390LOCAL USING W3CONST
0400LOCAL USING W3PINFO
0410LOCAL
04201 W3VALUE              (A) DYNAMIC
0430* ---------------------------------------------------- USR0050 SYSPROD
04401 USR0050P          /*  Output Elements
0450  2 PN (A20/1:12)   /*  Product Name
0460  2 VS (A04/1:12)   /*  Product Version and Release
0470  2 SM (A02/1:12)   /*  SM Level
0480  2 DT (A08/1:12)   /*  SM Release Date
0490  2 IU (A02/1:12)   /*  Update Level
0500  2 RC (P01)        /*  Response Code
0510  2 TX (A48)        /*  Explanation to RC
0520* ---------------------------------------------------- USR2013 SYSPROD
05301 USR3013P                        /* SAG 07122001
0540  2 FILENAME                      (A16/1:50)
0550  2 DBID                          (P05/1:50)
0560  2 FNR                           (P05/1:50)
0570  2 DBNAME                        (A20/1:50)
0580  2 AMOUNT                        (P04)
0590* -------------------------------------------------- USR3025L Steplibs
060001 USR3025L
0610  02 INPUTS
0620    03 ACTION        (A01)    INIT <'D'>
0630  02 INPUT-OUTPUTS
0640    03 STEPLIB-ID    (A08/1:8) /* ID of steplib
0650    03 DBID          (I04/1:8) /* DBID of steplib
0660    03 FNR           (I04/1:8) /* FNR of steplib
0670    03 PASSWORD      (A08)     /* Input for ACTION 'A'
0680    03 CIPHERCODE    (N08)     /* Input for ACTION 'A'
0690    03 POSITION      (N01)     /* Pos. of lib to be added/deleted
0700    03 AST-STEPLIB   (A08)     /* Value of *STEPLIB
0710    03 AST-DBID      (I04)     /* DBID of *STEPLIB
0720    03 AST-FNR       (I04)     /* FNR of *STEPLIB
0730    03 NUMBER-ACTIVE (N01)     /* Number of active steplibs
0740  02 OUTPUTS
0750    03 RESPONSE      (I04)     /* Error code
0760    03 INFOTEXT      (A65)     /* Text for RESPONSE field
0770*
078001 USR3025N
0790  02 VERSION          (I01)     INIT <0>
080001 REDEFINE USR3025N
0810  02 EXTENDED-PARMS
0820    03 EXTENDED-DATA (A01/1:1)
0830* ---------------------------------------------------- private Variable
08401 II                              (I002)
08501 #HTML                           (A) DYNAMIC
0860*
0870END-DEFINE
0880* ------------------------------------------------------ error handling
0890ON ERROR
0900  PERFORM W3ERROR ##W3ERROR
0910  PERFORM W3END ##RPC
0920  ESCAPE ROUTINE
0930END-ERROR
0940* ======================================================== Main Program
0950* ------------------------------------------------------- init HTML API
0960PERFORM W3INIT ##RPC
0970* --------------------------------------------- Set type of return Page
0980PERFORM W3CONTENT-TYPE 'text/html'
0990* ----------------------------------------------- Set head of HTML Page
1000COMPRESS "<!DOCTYPE 'HTML PUBLIC -//W3C//DTD HTML 3.2//EN'>"
1010  ##HTTP_NEWLINE
1020  '<HTML><HEAD><META http-equiv="Content-Type" '-
1030  'content="text/html; charset=iso-8859-1">'-
1040  '<TITLE>Natural Web Interface Configuration'
1050  '</TITLE></HEAD><BODY BGCOLOR="#FFFFFF"><P>&nbsp;'-
1060  '<H1 ALIGN="CENTER"><FONT COLOR="NAVY">Natural Web Interface '-
1070  'Configuration</FONT></H1><TABLE BORDER="0" CELLspacing="10" '
1080  'width="100%"><TR><TH bgcolor="#CCCCCC" colspan="2">'-
1090  'Browser</TH></TR><TR><TH ALIGN="RIGHT">Software</TH><TD>'
1100  INTO #HTML LEAVING NO
1110PERFORM W3TEXTDYNAMIC #HTML
1120* ------------------------------------------ Read Browser configuration
1130PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "HTTP_USER_AGENT" "S" W3VALUE
1140IF *LENGTH(W3VALUE) LE 0 THEN
1150  W3VALUE := "(? HTTP_USER_AGENT"
1160END-IF
1170PERFORM W3HTMLDYNAMIC W3VALUE
1180*
1190PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">TCP/IP Alias</TH><TD>'
1200PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "REMOTE_HOST" "S" W3VALUE
1210IF *LENGTH(W3VALUE) LE 0 THEN
1220  W3VALUE := "(? REMOTE_HOST"
1230END-IF
1240PERFORM W3HTMLDYNAMIC W3VALUE
1250*
1260PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">TCP/IP Address</TH><TD>'
1270PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "REMOTE_ADDR" "S" W3VALUE
1280IF *LENGTH(W3VALUE) LE 0 THEN
1290  W3VALUE := "(? REMOTE_ADDR"
1300END-IF
1310PERFORM W3HTMLDYNAMIC W3VALUE
1320*
1330PERFORM W3INFO ##W3INFO
1340*
1350PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH bgcolor=#CCCCCC colspan=2>'-
1360  'HTTP Server</TH></TR><TR><TH ALIGN="RIGHT">Port</TH><TD>'
1370PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SERVER_PORT" "S" W3VALUE
1380IF *LENGTH(W3VALUE) LE 0 THEN
1390  W3VALUE := "(? SERVER_PORT"
1400END-IF
1410PERFORM W3HTMLDYNAMIC W3VALUE
1420*
1430PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Software</TH><TD>'
1440PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SERVER_SOFTWARE" "S" W3VALUE
1450IF *LENGTH(W3VALUE) LE 0 THEN
1460  W3VALUE := "(? SERVER_SOFTWARE"
1470END-IF
1480PERFORM W3HTMLDYNAMIC W3VALUE
1490*
1500PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Interface</TH><TD>'
1510PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "GATEWAY_INTERFACE" "S" W3VALUE
1520IF *LENGTH(W3VALUE) LE 0 THEN
1530  W3VALUE := "(? GATEWAY_INTERFACE"
1540END-IF
1550PERFORM W3HTMLDYNAMIC W3VALUE
1560*
1570PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">TCP/IP Alias</TH><TD>'
1580PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SERVER_NAME" "S" W3VALUE
1590IF *LENGTH(W3VALUE) LE 0 THEN
1600  W3VALUE := "(? SERVER_NAME"
1610END-IF
1620PERFORM W3HTMLDYNAMIC W3VALUE
1630*
1640PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH bgcolor=#CCCCCC colspan=2>'-
1650  'HTTP Server Extension</TH></TR>'-
1660  '<TR><TH ALIGN="RIGHT">Version</TH><TD>'
1670PERFORM W3HTMLDYNAMIC ##W3INFO.VERSION
1680*
1690PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Log Time</TH><TD>'
1700PERFORM W3HTMLDYNAMIC ##W3INFO.LOG-TIME
1710*
1720PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Script Name</TH><TD>'
1730PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "SCRIPT_NAME" "S" W3VALUE
1740IF *LENGTH(W3VALUE) LE 0 THEN
1750  W3VALUE := "(? SCRIPT_NAME"
1760END-IF
1770PERFORM W3HTMLDYNAMIC W3VALUE
1780*
1790PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Tranfer Length</TH><TD>'
1800PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "NWW_INOUT_LENGTH" "S" W3VALUE
1810IF *LENGTH(W3VALUE) LE 0 THEN
1820  W3VALUE := "(* NWW_INOUT_LENGTH"
1830END-IF
1840PERFORM W3HTMLDYNAMIC W3VALUE
1850*
1860PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "RPC_ETB_ID_NAME" "S" W3VALUE
1870IF *LENGTH(W3VALUE) GT 0 THEN
1880  PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH bgcolor=#CCCCCC colspan=2>'-
1890    'Broker</TH></TR>'-
1900    '<TR><TH ALIGN="RIGHT">Broker ID'-
1910    '</TH><TD>'
1920  PERFORM W3HTMLDYNAMIC W3VALUE
1930END-IF
1940*
1950PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "RPC_SERVER_NAME" "S" W3VALUE
1960IF *LENGTH(W3VALUE) GT 0 THEN
1970  PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">RPC Server Name</TH><TD>'
1980  PERFORM W3HTMLDYNAMIC W3VALUE
1990END-IF
2000*
2010PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "DCOM_SERVER_NAME" "S" W3VALUE
2020IF *LENGTH(W3VALUE) GT 0 THEN
2030  PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">DCOM Server Name</TH><TD>'
2040  PERFORM W3HTMLDYNAMIC W3VALUE
2050END-IF
2060* ------------------------------------------------- NATURAL Environment
2070*
2080PERFORM W3TEXTDYNAMIC "</TD></TR><TR><TH COLSPAN=2 BGCOLOR=#CCCCCC>"-
2090  'NATURAL Environment</TH></TR>'-
2100  '<TR><TH ALIGN="RIGHT">Operating System</TH><TD>'
2110W3VALUE := *OPSYS
2120PERFORM W3HTMLDYNAMIC W3VALUE
2130*
2140PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Parameter File</TH><TD>'
2150COMPRESS *PARM-USER INTO W3VALUE
2160PERFORM W3HTMLDYNAMIC W3VALUE
2170*
2180PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">RPC User</TH><TD>'
2190PERFORM W3READ-ENVIRONMENT-TO-DYNAMIC "NWW_USER_ID" "S" W3VALUE
2200IF *LENGTH(W3VALUE) LE 0 THEN
2210  W3VALUE := "(* NWW_USER_ID"
2220END-IF
2230PERFORM W3HTMLDYNAMIC W3VALUE
2240*
2250PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">NATURAL User</TH><TD>'
2260COMPRESS *USER INTO W3VALUE
2270PERFORM W3HTMLDYNAMIC W3VALUE
2280*
2290PERFORM W3TEXTDYNAMIC '</TD></TR><TR><TH ALIGN="RIGHT">Logon Library</TH><TD>'
2300COMPRESS *LIBRARY-ID INTO W3VALUE
2310PERFORM W3HTMLDYNAMIC W3VALUE
2320*
2330* -------------------------------------------- Read Product Information
2340COMPRESS '</TD></TR>'-
2350  '<TR><TH ALIGN="RIGHT" VALIGN="TOP">Installed Products</TH>'-
2360  '<TD><TABLE BORDER="0">'-
2370  '<TR><TH>&nbsp;</TH><TH>'-
2380  '&nbsp;V/R&nbsp;</TH><TH>&nbsp;SM&nbsp;</TH>'-
2390  '<TH>&nbsp;Date&nbsp;</TH><TH>&nbsp;Iupd&nbsp;</TH><TR>'
2400  INTO #HTML LEAVING NO
2410REPEAT
2420  RESET USR0050P.PN(*) USR0050P.VS(*) USR0050P.SM(*)
2430    USR0050P.DT(*) USR0050P.IU(*)
2440  CALLNAT 'USR0050N' USR0050P
2450*
2460  FOR II = 1 TO 12
2470    IF USR0050P.PN(II) NE " "
2480      COMPRESS #HTML '<TR><TD>' USR0050P.PN(II)
2490        '</TD><TD ALIGN="CENTER">' USR0050P.VS(II)
2500        '</TD><TD ALIGN="CENTER">' USR0050P.SM(II)
2510        '</TD><TD>' USR0050P.DT(II)
2520        '</TD><TD ALIGN="CENTER">' USR0050P.IU(II)
2530        '</TD></TR>' INTO #HTML LEAVING NO
2540    ELSE
2550      ESCAPE BOTTOM
2560    END-IF
2570  END-FOR
2580*
2590  IF RC NE 1
2600    ESCAPE BOTTOM
2610  ELSE
2620    MOVE '+10' TO USR0050P.TX
2630  END-IF
2640END-REPEAT
2650COMPRESS #HTML '</TABLE></TD></TR>' INTO #HTML LEAVING NO
2660PERFORM W3TEXTDYNAMIC #HTML
2670*
2680* -------------------------------------------------------- System Files
2690** BZ001 CALLNAT 'USR0010N'  USR0010P
2700CALLNAT 'USR3013N'  USR3013P
2710*
2720IF AMOUNT > 0
2730  COMPRESS '</TD></TR>'-
2740    '<TR><TH ALIGN="RIGHT" VALIGN="TOP">System Files</TH>'-
2750    '<TD><TABLE BORDER="0">'-
2760    '<TR><TH>&nbsp;</TH><TH>'-
2770    '&nbsp;DBID&nbsp;</TH><TH>&nbsp;FNR&nbsp;</TH>'-
2780    '<TH>&nbsp;Type&nbsp;</TH><TR>' INTO #HTML LEAVING NO
2790  FOR II = 1 TO AMOUNT
2800    COMPRESS #HTML '<TR><TD>' USR3013P.FILENAME(II)
2810      '</TD><TD ALIGN="CENTER">' USR3013P.DBID(II)
2820      '</TD><TD ALIGN="CENTER">' USR3013P.FNR (II)
2830      '</TD><TD>' USR3013P.DBNAME (II)
2840      '</TD></TR>' INTO #HTML LEAVING NO
2850  END-FOR
2860  COMPRESS #HTML '</TABLE></TD></TR>' INTO #HTML LEAVING NO
2870  PERFORM W3TEXTDYNAMIC #HTML
2880END-IF
2890* ---------------------------------------------------- NATURAL steplibs
2900COMPRESS '<TR><TH ALIGN="RIGHT" VALIGN="TOP">Steplibs</TH>'-
2910  '<TD><TABLE BORDER="0">'-
2920  '<TR><TH>&nbsp;</TH><TH>'-
2930  '&nbsp;DBID&nbsp;</TH><TH>&nbsp;FNR&nbsp;</TH><TR>'
2940  INTO #HTML LEAVING NO
2950*
2960CALLNAT 'USR3025N' USR3025L USR3025N.EXTENDED-PARMS
2970*
2980FOR II = 1 TO 8
2990  IF USR3025L.STEPLIB-ID(II) NE " "
3000    COMPRESS #HTML "<TR><TD>" USR3025L.STEPLIB-ID(II)
3010      '</TD><TD ALIGN="CENTER">' USR3025L.DBID(II)
3020      '</TD><TD ALIGN="CENTER">' USR3025L.FNR(II)
3030      '</TD></TR>' INTO #HTML LEAVING NO
3040  END-IF
3050END-FOR
3060* --------------------------------------------------- NWW Configuration
3070COMPRESS #HTML '</TABLE></TD></TR><TR><TH colspan="2" bgcolor="#CCCCCC">'-
3080  'NATURAL Web Interface</TH></TR>'-
3090  '<TR><TH ALIGN="RIGHT">Version</TH><TD>'
3100  ##W3INFO.WEBAPI
3110  '</TD></TR><TR><TH ALIGN="RIGHT">Enviroment</TH><TD>'
3120  ##W3INFO.ENVIRONMENT_MAX
3130  ' bytes</TD></TR><TR><TH ALIGN="RIGHT">Maximum Page Size</TH><TD>'
3140  ##W3INFO.RETURN_MAX
3150  ' bytes</TD></TR><TR><TH ALIGN="RIGHT">Transfer Unit</TH><TD>'
3160  ##W3INFO.TRANSFER_MAX
3170  ' bytes</TD></TR></TABLE><P><I>'-
3180  'If you see with (? marked values, variables are '-
3190  'not passed by NWW. Please check your NWW '-
3200  'installation file and specify the missing '-
3210  'variables with SET.</I>'
3220  '<P><I>If you see with (* marked values,'-
3230  'variables are '-
3240  'not set by NWW. Please check your NWW '-
3250  'installation file and set the missing variables '-
3260  'with SETENV and the specific Value.</I><BR>&nbsp;'
3270* ------------------------- display current time/date and program name
3280* ------------------------------------------------ and close HTML page
3290  "<TABLE BORDER=0 WIDTH=100% CELLSPACING=0 "-
3300  "CELLPADDING=5>"
3310  '<TR BGCOLOR=#00cc66><TD>' *PROGRAM '&#32;-&#32;'
3320  ##W3INFO.LOG-TIME
3330  "</TD><TD ALIGN=RIGHT>" 'Natural'
3340  "</TD></TR></TABLE></BODY></HTML>"
3350  INTO #HTML LEAVING NO
3360PERFORM W3TEXTDYNAMIC #HTML
3370* ------------------------------------------------------ Close HTTP Api
3380PERFORM W3END ##RPC
3390* ================================================================= End
3400END
