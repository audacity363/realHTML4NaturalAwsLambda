0010**  Example program for USR3005N
0020**  This program shows, how data can be purged from Predict.
0030**  It purges a single object.
0040**
0050DEFINE DATA
0060PARAMETER
007001 OBJECT-TYPE(A32)
008001 OBJECT-ID  (A32)
0090LOCAL
01001 REQUEST-RESULT(A) DYNAMIC
0110*
0120LOCAL USING PARSER-X                /* Local Data Area is located
0130                                    /* in Library SYSEXXT
0140LOCAL
01501 XML_PARSER_XPATH             (A) DYNAMIC
01601 XML_PARSER_XPATH_TYPE        (A1)
01701 XML_PARSER_CONTENT           (A) DYNAMIC
01801 XML_PARSER_CONTENT_IS_EMPTY  (L)
01901 XML_PARSER_ERROR_TEXT        (A253)
02001 XML_PARSER_RESPONSE          (I2)
02101 #XNAME (A) DYNAMIC
02201 #XVALUE (A) DYNAMIC
02301 #MSG-NR(I4)
02401 #MSG-TEXT(A79)
02501 #MSG-TYPE(A8)
02601 #OBJ-TYPE(A34)
02701 #OBJ-ID(A34)
0280END-DEFINE
0290PRINT 'Example program' *PROGRAM 'for USR3005N'
0300/     'This program shows, how data can be purged from Predict.'
0310/     'It purges a single object.'
0320/
0330COMPRESS '"' OBJECT-TYPE '"' TO #OBJ-TYPE LEAVING NO
0340COMPRESS '"' OBJECT-ID '"' TO #OBJ-ID LEAVING NO
0350**
0360** Now purge the object
0370**
0380COMPRESS
0390'<Predict>'
0400  '<Request>'
0410    '<Purge>'
0420      '<Search>'
0430        '<Object-type value=' #OBJ-TYPE '/>'
0440        '<Attribute name=''ID'' value=' #OBJ-ID '/>'
0450      '</Search>'
0460    '</Purge>'
0470  '</Request>'
0480'</Predict>'
0490*
0500TO REQUEST-RESULT
0510*
0520PERFORM CALL-USR3005N
0530*
0540DEFINE SUBROUTINE CALL-USR3005N
0550*
0560PRINT 'Request passed to USR3005N' /
0570PRINT REQUEST-RESULT
0580*
0590CALLNAT 'USR3005N' REQUEST-RESULT
0600*
0610PRINT / 'Result returned from USR3005N' /
0620*
0630INCLUDE PARSER_X                    /* Copy Code is located
0640                                    /* in Library SYSEXXT
0650                                    /*
0660  'REQUEST-RESULT'                  /* XML FILE TO BE PARSED
0670  'XML_PARSER_XPATH'                /* XPATH TO REPESENT ELEMENT...
0680  'XML_PARSER_XPATH_TYPE'           /* TYPE OF CALLBACK
0690  'XML_PARSER_CONTENT'              /* CONTENT OF FOUND ELEMENT
0700  'XML_PARSER_CONTENT_IS_EMPTY'     /* IS TRUE IF ELEMENT IS EMPTY
0710  'XML_PARSER_ERROR_TEXT'           /* ERROR MESSAGE
0720  'XML_PARSER_RESPONSE'             /* ERROR NR; 0 = OK
0730END-SUBROUTINE
0740*
0750DEFINE SUBROUTINE CALLBACK
0760*
0770EXAMINE XML_PARSER_XPATH TRANSLATE INTO UPPER
0780DECIDE ON FIRST VALUE OF XML_PARSER_XPATH
0790*
0800  VALUE 'PREDICT/RESULT/MESSAGE/?NUMBER'
0810    #MSG-NR := VAL(XML_PARSER_CONTENT)
0820*
0830  VALUE 'PREDICT/RESULT/MESSAGE/?TEXT'
0840    INCLUDE USR3005Y '''2''' 'XML_PARSER_CONTENT' '#MSG-TEXT'
0850*
0860  VALUE 'PREDICT/RESULT/MESSAGE/?TYPE'
0870    #MSG-TYPE := XML_PARSER_CONTENT
0880    EXAMINE #MSG-TYPE TRANSLATE INTO UPPER
0890    IF #MSG-TYPE NE 'SUCCESS'
0900**
0910** Possible Values of ?TYPE are SUCCESS, ERROR and WARNING
0920**
0930      PRINT #MSG-NR
0940      PRINT #MSG-TEXT
0950    ELSE
0960      PRINT OBJECT-TYPE OBJECT-ID 'deleted successfully'
0970    END-IF
0980  NONE IGNORE
0990*
1000END-DECIDE
1010END-SUBROUTINE /* CALLBACK
1020*
1030DEFINE SUBROUTINE PARSER_ERROR
1040   PRINT 'Parser Error'
1050   PRINT XML_PARSER_ERROR_TEXT
1060 END-SUBROUTINE
1070*
1080END
