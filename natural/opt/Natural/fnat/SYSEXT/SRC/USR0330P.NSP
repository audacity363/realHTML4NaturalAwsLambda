0010/* USR0330P - Get NATURAL Object Directory Information
0020/*******************************************************************
0030/*  This program serves as example how to design a user-defined
0040/*  program to call 'USR0330N'
0050/*******************************************************************
0060DEFINE DATA LOCAL
00701 #CBDIR               (B94)
00801 REDEFINE #CBDIR
0090  2 FILLER             1X
0100  2 #CBDIR-OPT2        (A01)
0110  2 #CBDIR-RSP1        (I02)
0120  2 #CBDIR-RSP2        (I02)
0130  2 FILLER             20X
0140  2 #CBDIR-NAME        (A64)
0150  2 REDEFINE #CBDIR-NAME
0160    3 #CBDIR-NLIB      (A08)
0170    3 #CBDIR-NNAM      (A08)
0180  2 #CBDIR-DBID        (B02)
0190  2 REDEFINE #CBDIR-DBID
0200    3 #CBDIR-DBID-H    (B01)
0210    3 #CBDIR-DBID-L    (B01)
0220  2 #CBDIR-FNR         (B02)
0230  2 REDEFINE #CBDIR-FNR
0240    3 #CBDIR-FNR-H     (B01)
0250    3 #CBDIR-FNR-L     (B01)
0260/*
02701 #DIR-OB1             (B100/1:3)    /* Saved object directory
02801 REDEFINE #DIR-OB1
0290  2 NATVERSM           (A4)              /* not available in OS
0300  2 NATVER             (A4)              /* not available in OS
0310  2 NATSM              (A4)              /* not available in OS
0320  2 REDEFINE NATSM                       /* not available in OS
0330    3 NATSMN           (N4)              /* not available in OS
0340  2 USERID             (A8)
0350  2 TID                (A8)              /* not available in OS
0360  2 TIME               (A8)
0370  2 DATE               (A8)
0380  2 OBJ-TYPE           (A1)     /* Internal object type (e.g. 'F')
0390  2 OBJ-LONGNAME       (A11)    /* Object type name (e.g. 'Program')
0400  2 DIR-STATE          (A12)              /* not available in OS
0410  2 CREATE-LIB         (A8)     /* Current library name in OS
0420  2 CREATE-OBJ-NAME    (A8)              /* not available in OS
0430  2 TERM-USER          (A8)              /* not available in OS
0440  2 SA-SIZE            (I4)
0450  2 REC-SIZE           (I2)              /* not available in OS
0460  2 DEV-MODE           (A15)
0470  2 SAV-CAT-DATE-TIME  (T)
0480  2 OP-SYSTEM          (A8)              /* not available in OS
0490  2 TP-SYSTEM          (A8)              /* not available in OS
0500  2 TA-NAME            (A8)              /* not available in OS
0510  2 VCS-IAPPLID        (A8)              /* not available in OS
0520  2 REDEFINE VCS-IAPPLID                 /* not available in OS
0530    3 APPL-ID          (B2)              /* not available in OS
0540    3 APPL-VER         (B2)              /* not available in OS
0550    3 CNTL-VER         (B2)              /* not available in OS
0560    3 OBJ-VER          (B2)              /* not available in OS
0570  2 AIV-LENGTH         (I4)              /* not available in OS
0580  2 BP-SIZE            (I4)              /* not available in OS
0590  2 U-SIZE             (I2)              /* not available in OS
0600  2 INV-SUBTAB         (I2)              /* not available in OS
0610  2 DEF-SUBTAB         (I2)              /* not available in OS
0620  2 TURBO-CODE         (A3)              /* not available in OS
0630  2 RECAT-POSS         (A3)              /* not available in OS
0640  2 OBJ-RELINKED       (A3)              /* not available in OS
0650  2 TIMESTMP           (B8)              /* not available in OS
0660  2 TURBO-CODE-LEN     (I4)              /* not available in OS
0670  2 GDA-SIZE           (I2)              /* not available in OS
0680  2 NUM-SUBR           (I2)              /* not available in OS
0690  2 NUM-GDA            (I2)              /* not available in OS
0700  2 NUM-REP            (I2)              /* not available in OS
0710  2 NUM-WFILE          (I2)              /* not available in OS
0720/*
07301 #DIR-OB2             (B100/1:3)    /* Cataloged object directory
07401 REDEFINE #DIR-OB2
0750  2 NATVERSM           (A4)              /* not available in OS
0760  2 NATVER             (A4)              /* not available in OS
0770  2 NATSM              (A4)              /* not available in OS
0780  2 REDEFINE NATSM                       /* not available in OS
0790    3 NATSMN           (N4)              /* not available in OS
0800  2 USERID             (A8)
0810  2 TID                (A8)              /* not available in OS
0820  2 TIME               (A8)
0830  2 DATE               (A8)
0840  2 OBJ-TYPE           (A1)     /* Internal object type (e.g. 'F')
0850  2 OBJ-LONGNAME       (A11)    /* Object type name (e.g. 'Program')
0860  2 DIR-STATE          (A12)              /* not available in OS
0870  2 CREATE-LIB         (A8)     /* Current library name in OS
0880  2 CREATE-OBJ-NAME    (A8)              /* not available in OS
0890  2 TERM-USER          (A8)              /* not available in OS
0900  2 SA-SIZE            (I4)
0910  2 REC-SIZE           (I2)              /* not available in OS
0920  2 DEV-MODE           (A15)
0930  2 SAV-CAT-DATE-TIME  (T)
0940  2 OP-SYSTEM          (A8)              /* not available in OS
0950  2 TP-SYSTEM          (A8)              /* not available in OS
0960  2 TA-NAME            (A8)              /* not available in OS
0970  2 VCS-IAPPLID        (A8)              /* not available in OS
0980  2 REDEFINE VCS-IAPPLID                 /* not available in OS
0990    3 APPL-ID          (B2)              /* not available in OS
1000    3 APPL-VER         (B2)              /* not available in OS
1010    3 CNTL-VER         (B2)              /* not available in OS
1020    3 OBJ-VER          (B2)              /* not available in OS
1030  2 AIV-LENGTH         (I4)              /* not available in OS
1040  2 BP-SIZE            (I4)
1050  2 U-SIZE             (I2)              /* not available in OS
1060  2 INV-SUBTAB         (I2)              /* not available in OS
1070  2 DEF-SUBTAB         (I2)              /* not available in OS
1080  2 TURBO-CODE         (A3)              /* not available in OS
1090  2 RECAT-POSS         (A3)              /* not available in OS
1100  2 OBJ-RELINKED       (A3)              /* not available in OS
1110  2 TIMESTMP           (B8)              /* not available in OS
1120  2 TURBO-CODE-LEN     (I4)              /* not available in OS
1130  2 GDA-SIZE           (I2)              /* not available in OS
1140  2 NUM-SUBR           (I2)              /* not available in OS
1150  2 NUM-GDA            (I2)              /* not available in OS
1160  2 NUM-REP            (I2)              /* not available in OS
1170  2 NUM-WFILE          (I2)              /* not available in OS
1180  2 DUMMY              (A10)             /* not available in OS
1190  2 GDA-NAME           (A8)              /* not available in OS
1200LOCAL
12101 OBJ-DATE-TIME        (A19)
12201 SRC-DATE-TIME        (A19)
12301 #TIME                (A8)
12401 #DATE                (A10)
12501 #LIB-NAME            (A8)
12601 IX                   (I1)
12701 DB                   (N5)
12801 FNR                  (N4)
1290END-DEFINE
1300/*******************************************************************
1310SET KEY ALL
1320/*
1330ASSIGN #CBDIR-NNAM = *PROGRAM
1340ASSIGN #CBDIR-NLIB = *LIBRARY-ID
1350/*
1360INPUT  (AD=MILT)
1370    'Read NATURAL Object Directory:' (I)
1380  / '-' (30) (I) /
1390  / 'Object Name ..' #CBDIR-NNAM
1400  / 'Library ......' #CBDIR-NLIB
1410  / 'DBID .........' DB
1420  / 'FNR ..........' FNR
1430  /// 'Press any PF-key to stop.'
1440/*
1450IF *PF-KEY NE 'ENTR'
1460  ESCAPE ROUTINE
1470END-IF
1480/*
1490IF DB = 0 THRU 65536
1500  COMPUTE #CBDIR-DBID = DB
1510END-IF
1520/*
1530IF FNR = 0 THRU 5000
1540  COMPUTE #CBDIR-FNR = FNR
1550END-IF
1560/*
1570ASSIGN #CBDIR-OPT2 = 'E'   /* Read exactly specified object
1580CALLNAT 'USR0330N' #CBDIR #DIR-OB1 (*) #DIR-OB2 (*)
1590/*
1600IF #CBDIR-RSP1 NE 0 AND #CBDIR-RSP2 NE 0
1610  WRITE 'Object does not exist.'
1620ELSE
1630  IF #CBDIR-RSP1 EQ 0
1640    #LIB-NAME := #DIR-OB1.CREATE-LIB
1650  ELSE
1660    #LIB-NAME := #DIR-OB2.CREATE-LIB
1670  END-IF
1680  WRITE (AD=LI)
1690      'Directory of' #CBDIR-NNAM 'in Library' #LIB-NAME
1700END-IF
1710/*
1720IF #CBDIR-RSP1 EQ 0
1730  MOVE EDITED #DIR-OB1.SAV-CAT-DATE-TIME(EM=HH:II:SS) TO #TIME
1740  MOVE EDITED #DIR-OB1.SAV-CAT-DATE-TIME(EM=YYYY-MM-DD) TO #DATE
1750  COMPRESS #DATE #TIME TO SRC-DATE-TIME
1760  WRITE (AD=LI)
1770    / 'Information about Object Source'
1780    / 3T 'Saved on ..............' SRC-DATE-TIME
1790    / 3T 'User ID ...............' #DIR-OB1.USERID
1800    / 3T 'Object Type ...........' #DIR-OB1.OBJ-TYPE
1810                                   #DIR-OB1.OBJ-LONGNAME
1820    / 3T 'Programming Mode ......' #DIR-OB1.DEV-MODE
1830    / 3T 'Object size (Bytes) ...' #DIR-OB1.SA-SIZE
1840END-IF
1850/*
1860IF #CBDIR-RSP2 EQ 0
1870  MOVE EDITED #DIR-OB2.SAV-CAT-DATE-TIME(EM=HH:II:SS) TO #TIME
1880  MOVE EDITED #DIR-OB2.SAV-CAT-DATE-TIME(EM=YYYY-MM-DD) TO #DATE
1890  COMPRESS #DATE #TIME TO OBJ-DATE-TIME
1900  WRITE (AD=LI)
1910    / 'Information about Object Module'
1920    / 3T 'Cataloged on ..........' OBJ-DATE-TIME
1930    / 3T 'User ID ...............' #DIR-OB2.USERID
1940    / 3T 'Object Type ...........' #DIR-OB2.OBJ-TYPE
1950                                   #DIR-OB2.OBJ-LONGNAME
1960    / 3T 'Object size (Bytes) ...' #DIR-OB2.BP-SIZE
1970END-IF
1980/* ---------------------------------------------------------------------
1990ASSIGN #CBDIR-OPT2 = 'N'   /* Search objects starting with #CBDIR-NNAM
2000NEWPAGE
2010WRITE // 'List of ten objects starting with:' #CBDIR-NNAM /
2020FOR IX = 1 TO 10
2030  CALLNAT 'USR0330N' #CBDIR #DIR-OB1 (*) #DIR-OB2 (*)
2040  /*
2050  IF #CBDIR-RSP1 NE 0 AND #CBDIR-RSP2 NE 0
2060    ESCAPE BOTTOM
2070  END-IF
2080  /*
2090  IF #CBDIR-RSP1 NE 0
2100    WRITE #CBDIR-NNAM #DIR-OB2.OBJ-LONGNAME
2110  ELSE
2120    WRITE #CBDIR-NNAM #DIR-OB1.OBJ-LONGNAME
2130  END-IF
2140END-FOR
2150/*
2160END
