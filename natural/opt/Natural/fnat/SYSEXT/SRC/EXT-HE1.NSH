0010*
0020*  Member ......... EXT-HE1
0030*  Type ........... Helproutine
0040*  Library ........ SYSEXTC
0050*
0060*  Function ....... SYSEXT Online Help: Line Commands in SYSEXT
0070*                   Get selected line command
0080*
0090************************************************************************
0100*
0110DEFINE DATA
0120PARAMETER
0130  01 M-INTERFACE  (A08/1:32)
0140  01 VARNAME      (A65)        /* name of variable
0150  01 CMD          (A1)         /* content of variable
0160  01 CMD-INDEX    (I2)         /* index of variable
0170                               /* (N2) not working on OS
0180LOCAL
0190  01 #NAME        (A08)
0200  01 REDEFINE #NAME
0210     02 #NAME-A7  (A07)
0220  01 TEXT-DOCU    (A08)
0230  01 TEXT-EXA     (A08)
0240  01 TEXT-INTER   (A08)
0250  01 #EDITOR-FLAG (L)
0260END-DEFINE
0270*
0280DEFINE WINDOW HELP-MAP
0290  SIZE 13 * 52
0300  BASE 05 / 16
0310  TITLE ' Select Line Command '
0320  CONTROL SCREEN
0330*
0340*
0350SET KEY
0360  TREQ PF1 PF2 PF4 PF5 PF6 PF7 PF8 PF9 PF10 PF11
0370  PF3  NAMED 'Exit'
0380  PF12 NAMED 'Canc'
0390*
0400SET CONTROL 'K00'
0410FORMAT KD=ON
0420*
0430RESET CMD
0440*
0450MOVE M-INTERFACE(CMD-INDEX) TO #NAME
0460COMPRESS #NAME-A7 'T' INTO TEXT-DOCU LEAVING NO
0470COMPRESS #NAME-A7 'P' INTO TEXT-EXA LEAVING NO
0480COMPRESS #NAME-A7 'N' INTO TEXT-INTER LEAVING NO
0490*
0500/* Check if EDIT command is enabled
0510CALLNAT 'EXTXMENE' #EDITOR-FLAG
0520*
0530REPEAT
0540  IF #EDITOR-FLAG
0550    INPUT WINDOW = 'HELP-MAP' USING MAP 'EXT-MAPL'
0560  ELSE
0570    INPUT WINDOW = 'HELP-MAP' USING MAP 'EXT-MAPM'
0580  END-IF
0590  /*
0600  IF *PF-KEY = 'PF3' OR= 'PF12'
0610    RESET CMD
0620    ESCAPE BOTTOM
0630  END-IF
0640  IF #EDITOR-FLAG
0650    IF CMD = 'K' OR= 'T' OR= 'E' OR= 'L' OR= 'R' OR= 'X' OR= '.' OR= ' '
0660      ESCAPE BOTTOM
0670    ELSE
0680      REINPUT 'Enter valid line command.'
0690    END-IF
0700  ELSE
0710    IF CMD = 'K' OR= 'T' OR= 'L' OR= 'R' OR= 'X' OR= '.' OR= ' '
0720      ESCAPE BOTTOM
0730    ELSE
0740      REINPUT 'Enter valid line command.'
0750    END-IF
0760  END-IF
0770END-REPEAT
0780*
0790SET WINDOW OFF
0800*
0810END
