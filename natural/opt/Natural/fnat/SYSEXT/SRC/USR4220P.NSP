0010/*  This program serves as example how to design a user-defined
0020/*  program to call 'USR4220N'.
0030/***********************************************************************
0040DEFINE DATA
0050LOCAL
00601 PARM-FUNCTION              (A2)
00701 PARM-RC                    (I4)
00801 PARM-ERRTXT                (A72)
00901 PARM-A                     (A) DYNAMIC
01001 PARM-B                     (B) DYNAMIC
01101 PARM-RFC                   (B1)
0120/*
01301 #BACKUP-A                  (A) DYNAMIC
01401 #BACKUP-B                  (B) DYNAMIC
0150END-DEFINE
0160/*
0170/*
0180SET KEY ALL
0190PARM-RFC := H'00'
0200PARM-FUNCTION := 'BA'
0210RESET PARM-A PARM-B
0220REDUCE DYNAMIC PARM-A TO 0
0230REDUCE DYNAMIC PARM-B TO 0
0240PARM-B := H'F1F2F3F4F5F6F7F8F9F0'
0250REPEAT
0260  INPUT (AD=MIL IP=OFF CD=NE) WITH TEXT PARM-ERRTXT
0270   // 10T 'Base64 Conversion:' (YEI)
0280    / 10T '-' (19) (YEI) /
0290    / 10T 'Function (BA,AB) ..' (TU) PARM-FUNCTION (AD=T)
0300    / 10T 'Alpha In/Output ...' (TU) PARM-A (AL=30)
0310    / 10T 'Binary In/Output ..' (TU) PARM-B (EM=HHHHHHHHHH)
0320    / 10T 'Convesrion Mode ...' (TU) PARM-RFC '(0=RFC3548,3=RFC2045,4=NATRPC)'
0330    / 10T 'Response ..........' (TU) PARM-RC (AD=OD CD=TU)
0340    / PARM-ERRTXT (AD=OD CD=TU)
0350  RESET PARM-ERRTXT
0360  IF *PF-KEY NE 'ENTR'
0370    ESCAPE BOTTOM
0380  END-IF
0390  /*
0400  RESET #BACKUP-A #BACKUP-B
0410  REDUCE DYNAMIC #BACKUP-A TO 0
0420  REDUCE DYNAMIC #BACKUP-B TO 0
0430  #BACKUP-A := PARM-A
0440  #BACKUP-B := PARM-B
0450  /*
0460  CALLNAT 'USR4220N' PARM-FUNCTION PARM-RC PARM-ERRTXT PARM-B PARM-A
0470    PARM-RFC
0480  IF PARM-RC NE 0 THEN
0490    WRITE 'Conversion' PARM-FUNCTION
0500    WRITE NOTITLE PARM-ERRTXT
0510  ELSE
0520    IF PARM-FUNCTION = 'BA' THEN
0530      WRITE 'Binary -> Alpha'
0540      WRITE '=' PARM-B (EM=HHHHHHHHHHHHHHHHHHHHHHHHH)
0550        / '=' PARM-A (AL=50)
0560      RESET PARM-B
0570      REDUCE DYNAMIC PARM-B TO 0
0580      PARM-FUNCTION := 'AB'
0590    ELSE
0600      WRITE 'Alpha -> Binary'
0610      WRITE '=' PARM-A (AL=50) /
0620        '=' PARM-B (EM=HHHHHHHHHHHHHHHHHHHHHHHHH)
0630      RESET PARM-A
0640      REDUCE DYNAMIC PARM-A TO 0
0650      PARM-FUNCTION := 'BA'
0660    END-IF
0670    /*
0680    CALLNAT 'USR4220N' PARM-FUNCTION PARM-RC PARM-ERRTXT PARM-B PARM-A
0690      PARM-RFC
0700    IF PARM-RC NE 0 THEN
0710      WRITE 'Conversion' PARM-FUNCTION
0720      WRITE NOTITLE PARM-ERRTXT
0730    ELSE
0740      IF PARM-FUNCTION = 'BA' THEN
0750        WRITE 'Binary -> Alpha'
0760        WRITE '=' PARM-B (EM=HHHHHHHHHHHHHHHHHHHHHHHHH)
0770          / '=' PARM-A (AL=50)
0780        IF PARM-A = #BACKUP-A THEN
0790          WRITE '******** Conversion successful ********'
0800        ELSE
0810          WRITE '******** Value changed by conversion ********'
0820        END-IF
0830        PARM-FUNCTION := 'AB'
0840      ELSE
0850        WRITE 'Alpha -> Binary'
0860        WRITE '=' PARM-A (AL=50) /
0870          '=' PARM-B (EM=HHHHHHHHHHHHHHHHHHHHHHHHH)
0880        IF PARM-B = #BACKUP-B THEN
0890          WRITE '******** Conversion successful ********'
0900        ELSE
0910          WRITE '******** Value changed by conversion ********'
0920        END-IF
0930        PARM-FUNCTION := 'BA'
0940      END-IF
0950    END-IF
0960  END-IF
0970END-REPEAT
0980END
