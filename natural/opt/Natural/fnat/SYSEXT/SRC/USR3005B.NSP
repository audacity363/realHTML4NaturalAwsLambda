0010**  Example program for USR3005N
0020**  This program shows, how data can be purged from Predict.
0030**  It purges all subprograms located in library SYSDIC.
0040**
0050DEFINE DATA LOCAL
00601 REQUEST-RESULT(A) DYNAMIC
0070*
0080LOCAL USING PARSER-X                /* Local Data Area is located
0090                                    /* in Library SYSEXXT
0100LOCAL
01101 XML_PARSER_XPATH             (A) DYNAMIC
01201 XML_PARSER_XPATH_TYPE        (A1)
01301 XML_PARSER_CONTENT           (A) DYNAMIC
01401 XML_PARSER_CONTENT_IS_EMPTY  (L)
01501 XML_PARSER_ERROR_TEXT        (A253)
01601 XML_PARSER_RESPONSE          (I2)
01701 #XNAME (A) DYNAMIC
01801 #XVALUE (A) DYNAMIC
01901 #MSG-NR(I4)
02001 #MSG-TEXT(A79)
02101 #MSG-TYPE(A8)
02201 OBJECT-TYPE(A32)
02301 OBJECT-ID (A32)
0240END-DEFINE
0250PRINT 'Example program' *PROGRAM 'for USR3005N'
0260/     'This program shows, how data can be purged from Predict.'
0270/     'It purges all subprograms located in library SYSDIC.'
0280/
0290**
0300** Search the objects to be deleted
0310**
0320COMPRESS
0330'<Predict>'
0340  '<Request>'
0350    '<Select>'
0360      '<Search>'
0370        '<Object-type value=''PROGRAM-N''/>'
0380        '<Attribute name=''LIBRARY'' value=''SYSDIC''/>'
0390        '<Attribute name=''ID'' value=''SMR*''/>'
0400      '</Search>'
0410      '<Return>'
0420        '<Field name=''#object-type''/>'
0430        '<Field name=''ID''/>'
0440      '</Return>'
0450    '</Select>'
0460  '</Request>'
0470'</Predict>'
0480**
0490TO REQUEST-RESULT
0500*
0510PERFORM CALL-USR3005N
0520*
0530*
0540DEFINE SUBROUTINE CALL-USR3005N
0550*
0560PRINT 'Request passed to USR3005N' /
0570PRINT REQUEST-RESULT
0580*
0590CALLNAT 'USR3005N' REQUEST-RESULT
0600*
0610PRINT / 'Result returned from USR3005N' /
0620*
0630INCLUDE PARSER_X                    /* Copy Code is located
0640                                    /* in Library SYSEXXT
0650                                    /*
0660  'REQUEST-RESULT'                  /* XML FILE TO BE PARSED
0670  'XML_PARSER_XPATH'                /* XPATH TO REPESENT ELEMENT...
0680  'XML_PARSER_XPATH_TYPE'           /* TYPE OF CALLBACK
0690  'XML_PARSER_CONTENT'              /* CONTENT OF FOUND ELEMENT
0700  'XML_PARSER_CONTENT_IS_EMPTY'     /* IS TRUE IF ELEMENT IS EMPTY
0710  'XML_PARSER_ERROR_TEXT'           /* ERROR MESSAGE
0720  'XML_PARSER_RESPONSE'             /* ERROR NR; 0 = OK
0730END-SUBROUTINE
0740*
0750DEFINE SUBROUTINE CALLBACK
0760*
0770EXAMINE XML_PARSER_XPATH TRANSLATE INTO UPPER
0780DECIDE ON FIRST VALUE OF XML_PARSER_XPATH
0790*
0800  VALUE 'PREDICT/RESULT/MESSAGE/?NUMBER'
0810    #MSG-NR := VAL(XML_PARSER_CONTENT)
0820*
0830  VALUE 'PREDICT/RESULT/MESSAGE/?TEXT'
0840    INCLUDE USR3005Y '''2''' 'XML_PARSER_CONTENT' '#MSG-TEXT'
0850*
0860  VALUE 'PREDICT/RESULT/MESSAGE/?TYPE'
0870    #MSG-TYPE := XML_PARSER_CONTENT
0880    EXAMINE #MSG-TYPE TRANSLATE INTO UPPER
0890    IF #MSG-TYPE NE 'SUCCESS'
0900**
0910** Possible Values of ?TYPE are SUCCESS, ERROR and WARNING
0920**
0930      PRINT #MSG-NR
0940      PRINT #MSG-TEXT
0950    END-IF
0960*
0970*
0980  VALUE 'PREDICT/RESULT/ROW//'
0990**
1000** Last row is complete, now delete the object
1010**
1020   PRINT 'Object to be deleted:'
1030   PRINT 'Object type:' OBJECT-TYPE 'ID:' OBJECT-ID
1040   CALLNAT 'EXPRDB-N' OBJECT-TYPE OBJECT-ID
1050*
1060  VALUE 'PREDICT/RESULT/ROW/ATTRIBUTE/?NAME'
1070**
1080** Next attribute of result
1090**
1100    #XNAME:= XML_PARSER_CONTENT
1110    EXAMINE #XNAME TRANSLATE INTO UPPER
1120*
1130  VALUE 'PREDICT/RESULT/ROW/ATTRIBUTE/?VALUE'
1140    EXAMINE XML_PARSER_CONTENT TRANSLATE INTO UPPER
1150    IF #XNAME = '#OBJECT-TYPE'
1160      OBJECT-TYPE := XML_PARSER_CONTENT
1170    END-IF
1180    IF #XNAME = 'ID'
1190      OBJECT-ID := XML_PARSER_CONTENT
1200    END-IF
1210*
1220  NONE IGNORE
1230*
1240END-DECIDE
1250END-SUBROUTINE /* CALLBACK
1260*
1270DEFINE SUBROUTINE PARSER_ERROR
1280   PRINT 'Parser Error'
1290   PRINT XML_PARSER_ERROR_TEXT
1300 END-SUBROUTINE
1310*
1320END
